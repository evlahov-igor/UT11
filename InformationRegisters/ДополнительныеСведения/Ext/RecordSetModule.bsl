///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// << 18.05.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-1112
	КР_ПередЗаписью(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	// >> 18.05.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-1112
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВерсионированиеОбъектов") Тогда
		ЭлементОтбора = Отбор.Найти("Объект");
		Если ЭлементОтбора <> Неопределено Тогда
			МодульВерсионированиеОбъектов = ОбщегоНазначения.ОбщийМодуль("ВерсионированиеОбъектов");
			УстановитьПривилегированныйРежим(Истина);
			МодульВерсионированиеОбъектов.ЗаписатьВерсиюОбъекта(ЭлементОтбора.Значение);
			УстановитьПривилегированныйРежим(Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КР_ДобавленныеПроцедурыИФункции

// << 18.05.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-1112
Процедура КР_ПередЗаписью(Отказ)
	
	КР_ПроверкаПраваНаЗаписьСвойстваХодовойРазмер(Отказ);
	
КонецПроцедуры // >> 18.05.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-1112

// << 18.05.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-1112
Процедура КР_ПроверкаПраваНаЗаписьСвойстваХодовойРазмер(Отказ)
	
	ОбъектДопСведения = Отбор.Объект.Значение;
	
	Если ТипЗнч(ОбъектДопСведения) <> Тип("СправочникСсылка.ХарактеристикиНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьСвойствоХодовойРазмер = Ложь;
	Для Каждого ТекущаяЗапись Из ЭтотОбъект Цикл
		Если ТекущаяЗапись.Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.КР_ХодовойРазмер Тогда
			ЕстьСвойствоХодовойРазмер = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если ЕстьСвойствоХодовойРазмер Тогда
		ТекущееЗначение = ТекущаяЗапись.Значение;
	Иначе
		ТекущееЗначение = Ложь;
	КонецЕсли;
	
	ПредыдущееЗначение = КР_ПредыдущееЗначениеСвойстваХодовойРазмер(ОбъектДопСведения);
	Если ТекущееЗначение = ПредыдущееЗначение Тогда
		Возврат;
	КонецЕсли;
	
	ПравоЕсть = Пользователи.РолиДоступны("КР_ИзменениеПризнакаХодовогоРазмера", , Истина);
	Если Не ПравоЕсть Тогда
		ТекстСообщения = НСтр("ru='Недостаточно прав для изменения свойства ""Ходовой размер""'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры // >> 18.05.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-1112

// << 18.05.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-1112
Функция КР_ПредыдущееЗначениеСвойстваХодовойРазмер(Объект)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДополнительныеСведения.Объект КАК Объект,
	|	ДополнительныеСведения.Свойство КАК Свойство,
	|	ДополнительныеСведения.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|ГДЕ
	|	ДополнительныеСведения.Объект = &Объект
	|	И ДополнительныеСведения.Свойство = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения.КР_ХодовойРазмер)";
	
	Запрос.УстановитьПараметр("Объект", Объект);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	Возврат ВыборкаДетальныеЗаписи.Значение;
	
КонецФункции // >> 18.05.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-1112

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли