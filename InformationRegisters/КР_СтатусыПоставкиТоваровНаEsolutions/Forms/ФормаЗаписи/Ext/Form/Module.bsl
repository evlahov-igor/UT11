
//////////////////////////////////////////////////////////////
//// Модуль формы записи регистра сведений "КР_СтатусыПоставкиТоваровНаEsolutions"
//// Создан: 12.12.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-772
//// Разработка по ФДР С14.002 Интеграция с агентом фулфилмент аутсорса

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаОтправленныйФайл Тогда
		ПрочитатьОтправленныйФайл();
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаПолученныйФайл Тогда
		ПрочитатьПолученныйФайл();
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПрочитатьОтправленныйФайл()
	
	Если ОтправленныйФайлПрочитан Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьФайлСервер("ОтправленныйФайл");
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьПолученныйФайл()
	
	Если ПолученныйФайлПрочитан Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьФайлСервер("ПолученныйФайл");
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьФайлСервер(НаправлениеФайла)
	
	ЭтотОбъект[НаправлениеФайла + "Прочитан"] = Истина;
	
	ФайлДанные = РеквизитФормыВЗначение("Запись")[НаправлениеФайла].Получить();
	Если Не ТипЗнч(ФайлДанные) = Тип("ДвоичныеДанные") Тогда
		Возврат;
	КонецЕсли;
	
	// На случай, если в двоичных данных прячется не ZIP-архив
	Попытка
		ZipФайл = Новый ЧтениеZipФайла(ФайлДанные.ОткрытьПотокДляЧтения());
	Исключение
		Возврат;
	КонецПопытки;
	
	ВремКаталог = ФайловаяСистема.СоздатьВременныйКаталог();
	
	ZipФайл.ИзвлечьВсе(ВремКаталог);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	
	ФайлыАрхива = НайтиФайлы(ВремКаталог, "*.xml", Ложь);
	Для Каждого ФайлАрхива Из ФайлыАрхива Цикл
		ТекстовыйДокумент.Прочитать(ФайлАрхива.ПолноеИмя);
		
		ЭтотОбъект[НаправлениеФайла + "Строка"] = ТекстовыйДокумент.ПолучитьТекст();
	КонецЦикла;
	
	ФайловаяСистема.УдалитьВременныйКаталог(ВремКаталог);
	
КонецПроцедуры

#КонецОбласти
