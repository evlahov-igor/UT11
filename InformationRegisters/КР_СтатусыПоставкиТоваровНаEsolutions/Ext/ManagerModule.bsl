//////////////////////////////////////////////////////////////
//// Модуль менеджера регистра сведений "КР_СтатусыПоставкиТоваровНаEsolutions"
//// Создан: 02.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-772
//// Разработка по ФДР С14.002 Интеграция с агентом фулфилмент аутсорса

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает статус заказа.
//
// Параметры:
//  Заказ - ДокументСсылка.ЗаказКлиента - Заказ.
//  ДатаАктуальности - Дата - Дата актуальности данных.
//
// Возвращаемое значение:
//  ПеречислениеСсылка.КР_СтатусыАнонсаПоставкиДляСистемыEsolutions - Текущий статус заказа
//
Функция ПолучитьСтатусЗаказа(Заказ, ДатаАктуальности = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Запрос.УстановитьПараметр("Период", ?(ДатаАктуальности = Неопределено, ТекущаяДатаСеанса(), ДатаАктуальности));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтатусыПоставки.Статус КАК Статус
	|ИЗ
	|	РегистрСведений.КР_СтатусыПоставкиТоваровНаEsolutions.СрезПоследних(&Период, Объект = &Заказ) КАК СтатусыПоставки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Статус;
	Иначе
		Возврат Перечисления.КР_СтатусыАнонсаПоставкиДляСистемыEsolutions.ТребуетсяРегистрация;
	КонецЕсли;
	
КонецФункции

// Устанавливает статус переданному заказу.
// 
// Параметры:
// 	Заказ - ДокументСсылка.ЗаказКлиента - 
// 	Статус - ПеречислениеСсылка.КР_СтатусыАнонсаПоставкиДляСистемыEsolutions
//  ДатаАктуальности - Дата - Дата актуальности данных.
//  ДополнительныеДанные - Структура - Данные ресурсов и реквизитов записи
//   - Комментарий - Строка
//   - ПринятыйФайл - Произвольный
//   - ОтправленныйФайл - Произвольный
//
Процедура ЗаписатьСтатусЗаказа(
							Заказ,
							Статус,
							ДатаАктуальности = Неопределено,
							ДополнительныеДанные = Неопределено) Экспорт
	
	ЗаписьРегистра = РегистрыСведений.КР_СтатусыПоставкиТоваровНаEsolutions.СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Период = ?(ДатаАктуальности = Неопределено, ТекущаяДатаСеанса(), ДатаАктуальности);
	ЗаписьРегистра.Объект = Заказ;
	ЗаписьРегистра.Статус = Статус;
	
	Если ТипЗнч(ДополнительныеДанные) = Тип("Структура") Тогда
		ДополнительныеДанные.Свойство("Комментарий", ЗаписьРегистра.Комментарий);
		
		Если ДополнительныеДанные.Свойство("ПолученныйФайл") Тогда
			ЗаписьРегистра.ПолученныйФайл = Новый ХранилищеЗначения(ДополнительныеДанные.ПолученныйФайл);
		КонецЕсли;
		
		Если ДополнительныеДанные.Свойство("ОтправленныйФайл") Тогда
			ЗаписьРегистра.ОтправленныйФайл = Новый ХранилищеЗначения(ДополнительныеДанные.ОтправленныйФайл);
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		ЗаписьРегистра.Записать(Истина);
	Исключение
		ТекстОшибки = НСтр("ru = 'Не удалось записать статус.'");
		ТекстОшибки = ТекстОшибки + Символы.ПС + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
