////////////////////////////////////////////////////
//// Модуль набора записей регистра сведений "КР_ПриемкаПеремещенийТоваров"
//// Создан: 18.10.2022 Марченко С.Н., КРОК, JIRA№A2105505-702

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если РольДоступна("КР_ДобавлениеПриемкиПеремещенийТоваров")
		И Не Пользователи.РолиДоступны("КР_ДобавлениеИзменениеПриемкиПеремещенийТоваров") Тогда 
		
		// A2105505-2447 Исправление ошибки
		Запись = РегистрыСведений.КР_ПриемкаПеремещенийТоваров.СоздатьМенеджерЗаписи();
		Запись.Накладная = Отбор.Накладная.Значение;
		Запись.Прочитать();
		
		Если Запись.Выбран() Тогда 
			ТекстСообщения = НСтр(
				"ru = 'Недостаточно прав для изменения записей РС ""Приемка перемещений товаров""'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецЕсли;	
	КонецЕсли;	

	// A2105505-2046 
	ОтложеннаяОбработка = Неопределено;
	Если Не ДополнительныеСвойства.Свойство("ОтложеннаяОбработка", ОтложеннаяОбработка) Тогда 
		ОтложеннаяОбработка = КР_ДополнительныеНастройкиПовтИсп.БазаЯвляетсяЦентральной();  
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл 
		Запись.ОтложеннаяОбработка = ОтложеннаяОбработка
			И ЗначениеЗаполнено(Запись.ДатаПотоварнойПриемки)
			И Не Запись.НеФормироватьАктОрасхождениях;
	КонецЦикла;
	//
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)   
	
	// Не выполняем специальную логику если запись добавлена обменом с магазинами
	Если ДополнительныеСвойства.Свойство(КР_УТ11_ЦБ_Магазин_ЗагрузкаДанныхСервер.СвойствоОбъектБылЗагружен())
		Тогда 
		Возврат;
	КонецЕсли;	

	МереджерРегистра = РегистрыСведений.КР_ПриемкаПеремещенийТоваров;
	Накладная = Отбор.Накладная.Значение;  
	
	// A2105505-1947
	ТребуетсяАктОРасхождениях = Ложь;  
	//
	
	// Если есть запись (а она только одна может быть)
	//	то создаем акт о расхожнении 
	Если Количество() Тогда 
		
		ИндексПервойЗаписи = 0;
		ПерваяЗапись = ЭтотОбъект[ИндексПервойЗаписи];
		
		ДатаПотоварнойПриемки = ПерваяЗапись.ДатаПотоварнойПриемки;	
		
		ТребуетсяАктОРасхождениях = ЗначениеЗаполнено(ДатаПотоварнойПриемки)
			// A2105505-1769
			И Не ПерваяЗапись.НеФормироватьАктОрасхождениях
			//     
		;
		
	КонецЕсли;
	
	// A2105505-1947
	Если ТребуетсяАктОРасхождениях Тогда
		
		// A2105505-2046
		Если Не ПерваяЗапись.ОтложеннаяОбработка Тогда 	
			МереджерРегистра.СоздатьПерезаполнитьАктОРасхожденияхПослеПеремещения(
				Накладная, ДатаПотоварнойПриемки, Отказ);
		КонецЕсли;	
		
	Иначе	        
		
		МереджерРегистра.УдалитьАктОРасхожденияхПослеПеремещения(Накладная, Отказ);
		
	КонецЕсли;	
	//

КонецПроцедуры

#КонецОбласти

#КонецЕсли
