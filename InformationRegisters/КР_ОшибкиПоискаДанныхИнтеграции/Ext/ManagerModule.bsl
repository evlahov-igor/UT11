////////////////////////////////////////////////////
//// Модуль менеджера регистра сведений "КР_ОшибкиПоискаДанныхИнтеграции"
//// Создан: 13.03.2023 Марченко С.Н., КРОК, JIRA№A2105505-1402

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДобавитьЗапись(ОшибкаПоиска, СообщениеОбмена) Экспорт 
	
	// Инициализация переменных
	ОбъектМетаданных = ОшибкаПоиска.ОбъектМетаданных;
 	ТекстСообщения = ОшибкаПоиска.ТекстСообщения;   
	ИсходныеДанные = Неопределено;
	ОшибкаПоиска.Свойство("ИсходныеДанные", ИсходныеДанные);

	Отправитель = СообщениеОбмена.Отправитель; 
	КлючСообщения = СообщениеОбмена.КлючСообщения;
    //
	
	// Исходные данные надо сериализовать исходя из их типа
	Если ИсходныеДанные = Неопределено Тогда 
		УточнениеУникальности = ТекстСообщения;	
	ИначеЕсли ТипЗнч(ИсходныеДанные) = Тип("ОбъектXDTO") Тогда 
		СериализованныеИсходныеДанные = КР_ФункцииРаботыJSON.СериализоватьXDTOВJSON(ИсходныеДанные);   
		УточнениеУникальности = СериализованныеИсходныеДанные;	
	Иначе                          
		ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON;
		СериализованныеИсходныеДанные = КР_ОбщегоНазначениеСервер.ДанныеВJSON(ИсходныеДанные, ПараметрыЗаписиJSON);   
		УточнениеУникальности = СериализованныеИсходныеДанные;	
	КонецЕсли;		
	
	ОбъектМетаданныхПолноеИмя = ОбъектМетаданных.ПолноеИмя();
	ДанныеКлючаУникальности = ОбъектМетаданныхПолноеИмя + XMLСтрока(Отправитель) + УточнениеУникальности;
		
	ХэшИдентификаторЗаписи = КР_ОбщегоНазначениеСервер.MD5(ДанныеКлючаУникальности);	
	
	Запись = РегистрыСведений.КР_ОшибкиПоискаДанныхИнтеграции.СоздатьМенеджерЗаписи();
	Запись.ХэшИдентификаторЗаписи = ХэшИдентификаторЗаписи;  
	Запись.Прочитать();

	Если Запись.Выбран() Тогда 
		Возврат;
	КонецЕсли;
	
	Запись.ХэшИдентификаторЗаписи = ХэшИдентификаторЗаписи;  
	Запись.Отправитель = Отправитель;
	Запись.ОбъектМетаданных = ОбъектМетаданныхПолноеИмя;
	Запись.ТекстСообщения = ТекстСообщения;    
	
	Запись.ПериодЗаписи = ТекущаяДатаСеанса();
	Запись.КлючСообщения = КлючСообщения;
	Запись.ИсходныеДанные = СериализованныеИсходныеДанные;   
	
	Запись.Записать();
		
КонецПроцедуры	

#КонецОбласти

#Область СервисныеМетоды

#КонецОбласти

#КонецЕсли
