// 26.05.2023 Марченко С.Н., КРОК, JIRA№A2105505-991

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьЗапись(
	ТипДанных, Данные, ХэшИдетрификаторОбъекта, 
	Ссылка = Неопределено, Перезаписывать = Истина
	) Экспорт 

	Если Ссылка = Неопределено Тогда 
		Состояние = Перечисления.КР_СостоянияОбработкиОтложенногоВыполнения.КЗаписиВБазуДанных;  
	Иначе
		Состояние = Перечисления.КР_СостоянияОбработкиОтложенногоВыполнения.КОбработке;  
		GUID = XMLСтрока(Ссылка);
	КонецЕсли;
	
	Запись = СоздатьМенеджерЗаписи();
	Если Не Перезаписывать Тогда 
		Запись.ХэшИдетрификаторОбъекта = ХэшИдетрификаторОбъекта; 
		Запись.Прочитать();
		Если Запись.Выбран() Тогда 
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Запись.ХэшИдетрификаторОбъекта = ХэшИдетрификаторОбъекта; 
	Запись.Данные = Данные;                                         
	Запись.ТипДанных = ТипДанных;
	Запись.Состояние = Состояние;  
	Запись.GUID = GUID;

	МассивСообщений = Новый Массив;
	МассивСообщенийСеанса = ПолучитьСообщенияПользователю(Истина);
	Для Каждого СообщениеПользователю Из МассивСообщенийСеанса Цикл   
		Если СтрНачинаетсяС(СообщениеПользователю.Текст, "{") Тогда 
			Продолжить;
		КонецЕсли;	
		МассивСообщений.Добавить(СообщениеПользователю.Текст);
	КонецЦикла;

	Запись.СообщенияПользователю = СтрСоединить(МассивСообщений, Символы.ПС);    
	Запись.Записать();
	
	Возврат Истина;
	
КонецФункции

Функция УстановитьСостояниеЗаписи(ХэшИдетрификаторОбъекта, Состояние) Экспорт 
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.ХэшИдетрификаторОбъекта = ХэшИдетрификаторОбъекта;   
	Запись.Прочитать();
	Если Не Запись.Выбран() Тогда 
		Возврат Ложь
	КонецЕсли;	
	
	//// При установке состояния Обработано сразу удаляем запись из РС
	//Если Состояние = Перечисления.КР_СостоянияОбработкиОтложенногоВыполнения.Обработано Тогда 
	//	Запись.Удалить();
	//	Возврат Истина;
	//КонецЕсли;	
	
	МассивСообщений = Новый Массив;

	МассивСообщенийСеанса = ПолучитьСообщенияПользователю(Истина);
	Для Каждого СообщениеПользователю Из МассивСообщенийСеанса Цикл
		Если СтрНачинаетсяС(СообщениеПользователю.Текст, "{") Тогда 
			Продолжить;
		КонецЕсли;	
		МассивСообщений.Добавить(СообщениеПользователю.Текст);
	КонецЦикла;

	Запись.Состояние = Состояние;  
	Запись.СообщенияПользователю = СтрСоединить(МассивСообщений, Символы.ПС);    
	Запись.КоличествоПопытокОбработки = Запись.КоличествоПопытокОбработки + 1;
	Запись.Записать();
	
	Возврат Истина;
	
КонецФункции	

Функция УдалитьЗапись(ХэшИдетрификаторОбъекта) Экспорт 
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.ХэшИдетрификаторОбъекта = ХэшИдетрификаторОбъекта;   
	Запись.Прочитать();
	Если Не Запись.Выбран() Тогда 
		Возврат Ложь
	КонецЕсли;	
	
	Запись.Удалить(); 
	Возврат Истина;
	
КонецФункции	

#КонецОбласти

#КонецЕсли
