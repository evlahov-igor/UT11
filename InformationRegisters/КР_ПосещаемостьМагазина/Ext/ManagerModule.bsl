
////////////////////////////////////////////////////
//// Модуль менеджера регистра сведений "КР_ПосещаемостьМагазина"
//// Создан: 13.02.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-1166
//// Разработка по ФДР С11.035, Отчет о розничных продажах. Кассовые документы

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет данные на предмет наличия в регистре записи о подчете посетителей
//
// Параметры:
//  Магазин - СправочникСсылка.Склады - магазин, для которого выполняется проверка
//  НаДату - Дата - Дата проверки
//  КоличествоПосетителей - Число - Устанавливает количество посетителей, если есть данные в регистре
//
// Возвращаемое значение:
//  Булево - флаг наличия данных подсчета посетителей
//
Функция ЕстьПодсчетПосетителейНаДату(Магазин, НаДату, КоличествоПосетителей = 0) Экспорт
	
	ЕстьПодсчет = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период", НачалоДня(НаДату));
	Запрос.УстановитьПараметр("Магазин", Магазин);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КР_ПосещаемостьМагазина.КоличествоПосетителей КАК КоличествоПосетителей
	|ИЗ
	|	РегистрСведений.КР_ПосещаемостьМагазина КАК КР_ПосещаемостьМагазина
	|ГДЕ
	|	КР_ПосещаемостьМагазина.Период = &Период
	|	И КР_ПосещаемостьМагазина.Магазин = &Магазин";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЕстьПодсчет = Истина;
		КоличествоПосетителей = Выборка.КоличествоПосетителей;
	Иначе
		КоличествоПосетителей = 0;
	КонецЕсли;
	
	Возврат ЕстьПодсчет;
	
КонецФункции

// Создает запись в регистре сведений о количестве посетителей для магазина на указанную дату
//
// Параметры:
//  Магазин - СправочникСсылка.Склады - По какому магазину вводятся данные
//  КоличествоПосетителей - Число - Данные посещений магазина
//  НаДату - Дата - На какую дату вносятся данные
//
Процедура ЗаписатьПосещаемостьМагазина(Магазин, КоличествоПосетителей, Знач НаДату = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	НаДату = НачалоДня(НаДату);
	
	НаборЗаписей = РегистрыСведений.КР_ПосещаемостьМагазина.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(НаДату);
	НаборЗаписей.Отбор.Магазин.Установить(Магазин);
	
	Запись = НаборЗаписей.Добавить();
	Запись.Период = НаДату;
	Запись.Магазин = Магазин;
	Запись.КоличествоПосетителей = КоличествоПосетителей;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
