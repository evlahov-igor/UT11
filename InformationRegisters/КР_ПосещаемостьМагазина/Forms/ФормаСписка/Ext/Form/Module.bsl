
////////////////////////////////////////////////////
//// Форма списка регистра сведений "КР_ПосещаемостьМагазина"
//// Создан: 03.03.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-1166
//// Разработка по ФДР С11.035, Отчет о розничных продажах. Кассовые документы

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Отбор.Свойство("Магазин", ТекущийМагазин);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьКоличествоПосетителей(Команда)
	
	Если Не ЗначениеЗаполнено(КоличествоПосетителей) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не заполнено количество посетителей'"), , ,
			"КоличествоПосетителей");
		Возврат;
	КонецЕсли;
	
	УстановитьКоличествоПосетителейНаСервере();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура УстановитьКоличествоПосетителейНаСервере()
	
	ПериодЗаписи = ТекущаяДатаСеанса();
	
	Если Не ЗначениеЗаполнено(ТекущийМагазин) Тогда
		ТекущийПользователь = Пользователи.ТекущийПользователь();
		ТекущееПодразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийПользователь, "Подразделение");
		ТекущийМагазин = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущееПодразделение, "КР_Склад");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущийМагазин) Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'У текущего пользователя не заполнен Магазин'"));
		Возврат;
	КонецЕсли;
	
	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("Период", ПериодЗаписи);
	СтруктураЗаписи.Вставить("Магазин", ТекущийМагазин);
	
	Если Не ОбменДаннымиВызовСервера.НаборЗаписейРегистраПустой(
			СтруктураЗаписи, "КР_ПосещаемостьМагазина") Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'На текущю дату уже установлено количество посетителей'"));
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.КР_ПосещаемостьМагазина.ЗаписатьПосещаемостьМагазина(
		ТекущийМагазин, КоличествоПосетителей, ПериодЗаписи);
	
КонецПроцедуры

#КонецОбласти
