////////////////////////////////////////////////////
//// Модуль менеджера регистра сведений "КР_ПрефиксыИнформационныхБаз"
//// Создан: 21.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-991
//// Разработка по ФДР С11.027, Развертывание базы нового магазина (сложный вариант)

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьПрефиксИнформационнойБазы(СкладСсылка) Экспорт

	// Проверяем на наличие префикса 
	Запрос = Новый Запрос(ТекстЗапросаПрефиксИнформационнойБазы());     
	Запрос.УстановитьПараметр("Склад", СкладСсылка);
	РезультатЗапроса = Запрос.Выполнить(); 
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Возврат ВыборкаДетальныеЗаписи.Префикс;
	КонецЕсли;
	
	// Если префикса нет тогда генерируем
	ДопустимыеСимволы = СтрРазделить("A.B.C.D.E.F.G.H.I.J.K.L.M.N.O.P.Q.R.S.T.U.V.W.X.Y.Z", ".");

	// Выберем все префиксы в соответствие
	Запрос = Новый Запрос(ТекстЗапросаПрефиксыИнформационныхБазы());     
	РезультатЗапроса = Запрос.Выполнить(); 
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	СуществующиеПрефиксы = Новый Соответствие;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		
		СуществующиеПрефиксы[ВыборкаДетальныеЗаписи.Префикс] = Истина;
		
	КонецЦикла;      
	
	// Далее перебираем варанты символов и проверяем на совпадение
	Для ПозицияПервогоСимвола = 0 По ДопустимыеСимволы.ВГраница() Цикл 

		Для ПозицияВторогоСимвола = 0 По ДопустимыеСимволы.ВГраница() Цикл 
			
			НовыйПрефикс = ДопустимыеСимволы[ПозицияПервогоСимвола] + ДопустимыеСимволы[ПозицияВторогоСимвола];
			Если СуществующиеПрефиксы[НовыйПрефикс] = Неопределено Тогда 
				
				Запись = РегистрыСведений.КР_ПрефиксыИнформационныхБаз.СоздатьМенеджерЗаписи();
				Запись.Склад = СкладСсылка;
				Запись.Префикс = НовыйПрефикс;  
				Запись.Записать();
				
				Возврат НовыйПрефикс; 
				
			КонецЕсли;
			
		КонецЦикла;	
		
	КонецЦикла;	
	
КонецФункции

#КонецОбласти  

#Область ПрочиеПроцедурыИФункции

#КонецОбласти

#Область ТекстыЗапросов

Функция ТекстЗапросаПрефиксИнформационнойБазы()
	
	Возврат
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.Префикс КАК Префикс
	|ИЗ
	|	РегистрСведений.КР_ПрефиксыИнформационныхБаз КАК Т
	|ГДЕ
	|	Т.Склад = &Склад";
	
КонецФункции

Функция ТекстЗапросаПрефиксыИнформационныхБазы()
	
	Возврат
	"ВЫБРАТЬ
	|	Т.Префикс КАК Префикс
	|ИЗ
	|	РегистрСведений.КР_ПрефиксыИнформационныхБаз КАК Т";
	
КонецФункции

#КонецОбласти

#КонецЕсли
