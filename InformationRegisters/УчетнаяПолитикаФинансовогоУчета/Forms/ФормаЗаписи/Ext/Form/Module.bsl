
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПартионныйУчетВключен = РасчетСебестоимостиПовтИсп.ПартионныйУчетВключен();
	ПартионныйУчетВерсии22 = РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22();
	БазоваяВерсия = ПолучитьФункциональнуюОпцию("БазоваяВерсия");
	ИспользоватьРеглУчет = ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет");
	ИспользоватьМФУ = Ложь;
	
	КлючЗаписи = Неопределено;
	ИмяРегистра = Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.Имя;
	ТолькоПросмотр = НЕ ПравоДоступа("Изменение", Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета);
	
	Организация = Параметры.Организация;
	Элементы.Организация.Видимость = НЕ ЗначениеЗаполнено(Организация);
	Заголовок = Заголовок + " " + Строка(Организация);
	
	ЭтоФормаЗаписи = Параметры.Свойство("Ключ", КлючЗаписи);
	Если Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		СкопироватьЗаписьРегистра(Параметры.ЗначениеКопирования);
	Иначе
		ПрочитатьЗаписьРегистра(КлючЗаписи);
	КонецЕсли;
	
	НомерМесяца = 1;
	Пока НомерМесяца <= 12 Цикл
		Элементы.МесяцНачалаФинансовогоГода.СписокВыбора.Добавить(
			НомерМесяца,
			РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПредставлениеНачалаФинансовогоГода(НомерМесяца));
		НомерМесяца = НомерМесяца + 1;
	КонецЦикла;
	Элементы.МесяцНачалаФинансовогоГода.Видимость = Не ПолучитьФункциональнуюОпцию("ЛокализацияРФ");

	ПараметрыВыбораСтатейИАналитик = РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПараметрыВыбораСтатейИАналитик(
		Запись.ПризнаватьРасходыПоИсследованиям, 
		Запись.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам);
	ДоходыИРасходыСервер.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьТМЦВЭксплуатации") Тогда
		Элементы.ЗаписьВидЦеныТМЦВЭксплуатации.Видимость = Ложь;
	КонецЕсли;

	ДоступнаНастройкаВНА = Ложь;
	
	
	Если НЕ ДоступнаНастройкаВНА Тогда
		Элементы.ДекорацияПорядокУчетаВНА.Видимость = Ложь;
		Элементы.ЗаписьПорядокУчетаВНАПоСтандартамМУ.Видимость = Ложь;
		Элементы.ЗаписьПорядокУчетаВНАПоСтандартамРегл.Видимость = Ложь;
		Элементы.ВариантПроводокПоОбесценению.Видимость = Ложь;
		Элементы.ЗаписьУчетАрендыПоФСБУ25_2018.Видимость = Ложь;
	КонецЕсли;
	
	ВключеноОбесценение = Ложь;
	
	
	Если НЕ ВключеноОбесценение Тогда
		Элементы.ВариантПроводокПоОбесценению.Видимость = Ложь;
	КонецЕсли;
	
	НоваяАрхитектураВзаиморасчетов = ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов");
	ПрочитатьСтавкиДисконтирования();
	УправлениеНастройкойДетализацииДопРасходов();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	СписокПроверяемыхПолейРегистра = ПроверяемыеПоляРегистра();
	МассивПроверяемыхПолей = СписокПроверяемыхПолейРегистра.ВыгрузитьЗначения();
	
	ПараметрыВыбораСтатейИАналитик = РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПараметрыВыбораСтатейИАналитик(
		Запись.ПризнаватьРасходыПоИсследованиям,
		Запись.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам);
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(Запись, Отказ, МассивПроверяемыхПолей, ПараметрыВыбораСтатейИАналитик);
	
	Для Каждого ПроверяемыйРеквизит Из МассивПроверяемыхПолей Цикл
		Если Не ЗначениеЗаполнено(Запись[ПроверяемыйРеквизит]) Тогда
			ЭлементСписка = СписокПроверяемыхПолейРегистра.НайтиПоЗначению(ПроверяемыйРеквизит);
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено'"), ЭлементСписка.Представление);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Запись." + ПроверяемыйРеквизит, , Отказ);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	
	УстановитьДоступностьРеквизитовДисконтирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("РегистрСведений.СтавкиДисконтирования.Форма.РедактированиеСтавокДисконтирования") Тогда
		ЗначенияСтавокДисконтирования.Очистить();
		Для Каждого СтрокаСтавки Из ВыбранноеЗначение Цикл
			НоваяСтрока = ЗначенияСтавокДисконтирования.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСтавки);
		КонецЦикла;
		СформироватьНадписьСтавкаДисконтирования();
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МетодОценкиСтоимостиТоваровПриИзменении(Элемент)
	УстановитьВидимостьЭлементов(ЭтотОбъект);
	УправлениеНастройкойДетализацииДопРасходов();
КонецПроцедуры

&НаКлиенте
Процедура ВариантУчетаСтоимостиПродукцииПриИзменении(Элемент)
	Запись.УчетГотовойПродукцииПоПлановойСтоимости = ВариантУчетаСтоимостиПродукции = "ПоПлановой";
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Организация = Запись.Организация;
	ПрочитатьЗаписьРегистраПриИзмененииРеквизита(,ДатаИзменения);
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияПриИзменении(Элемент)
	ТекстВопроса = НСтр("ru = 'Создать новую учетную политику на %1?'");
	ТекстВопроса = СтрШаблон(ТекстВопроса, Формат(НачалоМесяца(ДатаИзменения),"ДЛФ=D"));
	ПоказатьВопрос(Новый ОписаниеОповещения("ДатаИзмененияПриИзмененииЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияПриИзмененииЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		ПрочитатьЗаписьРегистраПриИзмененииРеквизита(,ДатаИзменения);
	ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
		ПрочитатьЗаписьРегистраПриИзмененииРеквизита(,ДатаИзменения, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеИсторииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "ИсторияИзменений" Тогда
		Если Модифицированность Тогда
			ЗадатьВопросФормаМодифицирована("ОткрытьИсториюИзмененийПродолжение");
		Иначе
			ОткрытьИсториюИзменений();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьСтатьяРасходовПоИсследованиямПриИзменении(Элемент)

	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ЗаписьСтатьяРасходовПоИсследованиямНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораСтатьи(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьАналитикаРасходовПоИсследованиямАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьАналитикаРасходовПоИсследованиямОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьАналитикаРасходовПоИсследованиямНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьПризнаватьРасходыПоИсследованиямПриИзменении(Элемент)
	
	ЗаписьПризнаватьРасходыПоИсследованиямПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьВыделениеДолгосрочныхАктивовОбязательствПриИзменении(Элемент)

	ИспользоватьВыделениеДолгосрочныхАктивовОбязательствПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ИспользоватьВыделениеДолгосрочныхАктивовОбязательствПриИзмененииНаСервере()
	
	Если Запись.ИспользоватьВыделениеДолгосрочныхАктивовОбязательств
		И Не ЗначениеЗаполнено(Запись.ДлительностьОперационногоЦикла) Тогда
			Запись.ДлительностьОперационногоЦикла = 12;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьПорядокУчетаВНАПоСтандартамМУПриИзменении(Элемент)
	
	
	УстановитьВидимостьЭлементов(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ЗаписьПорядокУчетаВНАПоСтандартамРеглПриИзменении(Элемент)
	

	УстановитьВидимостьЭлементов(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СтавкаДисконтированияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьСтавкиДисконтирования();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьУчетДисконтированнойКредиторскойЗадолженностиПоставщикамПриИзменении(Элемент)
	
	УстановитьДоступностьРеквизитовДисконтирования();
	ЗаписьУчетДисконтированнойКредиторскойЗадолженностиПоставщикамПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяСписанияПроцентныхРасходовПриИзменении(Элемент)

	ДоходыИРасходыКлиентСервер.СтатьяПриИзменении(ЭтотОбъект, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура СтатьяСписанияПроцентныхРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораСтатьи(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаСписанияПроцентныхРасходовАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.АвтоПодборАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаСписанияПроцентныхРасходовОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.ОкончаниеВводаТекстаАналитикиРасходов(ЭтотОбъект, Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаСписанияПроцентныхРасходовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДоходыИРасходыКлиент.НачалоВыбораАналитикиРасходов(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если ЗаписатьИзменения(Истина) Тогда
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	ЗаписатьИзменения();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьЗаписьРегистра(КлючЗаписи = Неопределено, ПериодЗаписи = Неопределено, СоздатьНовую = Ложь)
	НастройкиНалоговУчетныхПолитик.ПрочитатьЗаписьРегистра(ЭтотОбъект, 
		ИмяРегистра,
		Организация,
		СоздатьНовую,
		КлючЗаписи,
		ПериодЗаписи);
		
	ВариантУчетаСтоимостиПродукции = ?(Запись.УчетГотовойПродукцииПоПлановойСтоимости, "ПоПлановой", "ПоФактической");
	НастройкиНалоговУчетныхПолитикЛокализация.ПрочитатьЗаписьРегистра_УчетнаяПолитикаФинУчета(ЭтотОбъект);
	
	УстановитьВидимостьЭлементов(ЭтотОбъект);
	
	ПараметрыВыбораСтатейИАналитик = РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПараметрыВыбораСтатейИАналитик(
		Запись.ПризнаватьРасходыПоИсследованиям,
		Запись.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам);
	ДоходыИРасходыСервер.ПриИзмененииПараметровВыбораСтатей(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	Если Не СоздатьНовую Тогда
		ПрочитатьСтавкиДисконтирования();
	КонецЕсли;
	УправлениеНастройкойДетализацииДопРасходов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьЗаписьРегистраПриИзмененииРеквизита(КлючЗаписи = Неопределено, ПериодЗаписи = Неопределено, СоздатьНовую = Ложь)
	Если НЕ Копирование Тогда
		ПрочитатьЗаписьРегистра(КлючЗаписи, ПериодЗаписи, СоздатьНовую);
	КонецЕсли;
	УстановитьДоступностьРеквизитовДисконтирования();
КонецПроцедуры

&НаСервере
Процедура СкопироватьЗаписьРегистра(ЗначениеКопирования)
	НастройкиНалоговУчетныхПолитик.СкопироватьУчетнуюПолитику(ЭтотОбъект, ЗначениеКопирования, ИмяРегистра);
	УстановитьВидимостьЭлементов(ЭтотОбъект);
	Копирование = Истина;
	Модифицированность = Истина;
	
	ПараметрыВыбораСтатейИАналитик = РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПараметрыВыбораСтатейИАналитик(
		Запись.ПризнаватьРасходыПоИсследованиям,
		Запись.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам);
	ДоходыИРасходыСервер.ПриИзмененииПараметровВыбораСтатей(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
	СформироватьНадписьСтавкаДисконтирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПослеЗаписи()
	
	ПараметрыОповещения = Новый Структура("Организация, Период", Запись.Организация, Запись.Период);
	ИмяСобытия = "Запись_" + ИмяРегистра;
	Оповестить(ИмяСобытия, ПараметрыОповещения);
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьИзмененияНаСервере(Закрытие = Ложь)
	ЗаписьУспешна = НастройкиНалоговУчетныхПолитик.ЗаписатьИзменениеЗаписейРегистра(ЭтотОбъект, Закрытие);
	Если ЗаписьУспешна Тогда
		ЗаписатьСтавкиДисконтирования();
	КонецЕсли;
	Возврат ЗаписьУспешна;
КонецФункции

&НаКлиенте
Функция ЗаписатьИзменения(Закрытие = Ложь)
	ОчиститьСообщения();
	ЗаписьУспешна = ЗаписатьИзмененияНаСервере(Закрытие);
	Если ЗаписьУспешна Тогда
		ОповеститьПослеЗаписи();
	КонецЕсли;
	Возврат ЗаписьУспешна;
КонецФункции

&НаКлиенте
Процедура ЗадатьВопросФормаМодифицирована(ИмяОповещения)
	
	ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
	Оповещение = Новый ОписаниеОповещения(ИмяОповещения, ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		
		Модифицированность = Ложь;
		Закрыть();
		
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		Если НЕ ЗаписатьИзменения(Истина) Тогда
			Возврат;
		КонецЕсли;
		Модифицированность = Ложь;
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюИзменений()
	ОткрытьФорму("РегистрСведений.УчетнаяПолитикаФинансовогоУчета.Форма.РедактированиеИстории",
		Новый Структура("ТолькоПросмотр, Организация", ТолькоПросмотр, Организация),
		ЭтаФорма,,,,
		Новый ОписаниеОповещения("ОткрытьИсториюИзмененийЗавершение", ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюИзмененийПродолжение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ОткрытьИсториюИзменений();
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		Если НЕ ЗаписатьИзменения(Ложь) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюИзмененийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если ЗначениеЗаполнено(Результат) Тогда
		ПрочитатьЗаписьРегистра(Результат);
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьЭлементов(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.БазоваяВерсия Тогда
		Элементы.МетодОценкиСтоимостиТоваров.Вид = ВидПоляФормы.ПолеНадписи;
	ИначеЕсли НЕ Форма.ПартионныйУчетВключен Тогда
		ЭлементСписка = Элементы.МетодОценкиСтоимостиТоваров.СписокВыбора.НайтиПоЗначению(
						ПредопределенноеЗначение("Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка"));
		Если ЭлементСписка <> Неопределено Тогда
			Элементы.МетодОценкиСтоимостиТоваров.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
	// При использовании партионного учета версии 2.2 будет не доступен выбор метода оценки "ФИФО (взвешенная оценка)".
	// Метод оценки "ФИФО (взвешенная оценка)" является устаревшим. Его использование не рекомендуется.
	ИначеЕсли Форма.ПартионныйУчетВерсии22
	 И Форма.Запись.МетодОценкиСтоимостиТоваров <> ПредопределенноеЗначение("Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОВзвешеннаяОценка") Тогда
		ЭлементФИФОВзвешеннаяОценка = Элементы.МетодОценкиСтоимостиТоваров.СписокВыбора.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОВзвешеннаяОценка"));
		Если ЭлементФИФОВзвешеннаяОценка <> Неопределено Тогда
			Элементы.МетодОценкиСтоимостиТоваров.СписокВыбора.Удалить(ЭлементФИФОВзвешеннаяОценка);
		КонецЕсли;
		ЭлементФИФОСкользящаяОценка = Элементы.МетодОценкиСтоимостиТоваров.СписокВыбора.НайтиПоЗначению(
			ПредопределенноеЗначение("Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка"));
		ЭлементФИФОСкользящаяОценка.Представление = НСтр("ru = 'ФИФО'");
	КонецЕсли;
	
	Элементы.ГруппаПредупреждениеМетодОценки.Видимость = Форма.ПрименяетсяУСН
		И Форма.ПрименяетсяУСНДоходыМинусРасходы 
		И Форма.ПартионныйУчетВключен
		И Форма.Запись.МетодОценкиСтоимостиТоваров 
			<> ПредопределенноеЗначение("Перечисление.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка");
	
	Форма.ТолькоПросмотр = Форма.ТолькоПросмотр Или Форма.ОбособленноеПодразделение;
	
	Элементы.ГруппаДлительностьОперационногоЦикла.Видимость = Форма.ИспользоватьРеглУчет ИЛИ Форма.ИспользоватьМФУ;
	

КонецПроцедуры

&НаСервере
Процедура ЗаписьПризнаватьРасходыПоИсследованиямПриИзмененииНаСервере()
		
	ПараметрыВыбораСтатейИАналитик = РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПараметрыВыбораСтатейИАналитик(
		Запись.ПризнаватьРасходыПоИсследованиям,
		Запись.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам);
	ДоходыИРасходыСервер.ПриИзмененииПараметровВыбораСтатей(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

&НаСервере
Функция ПроверяемыеПоляРегистра()
	
	СписокПроверяемыхПолей = Новый СписокЗначений;
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.УчетнаяПолитикаФинансовогоУчета;

	СписокПроверяемыхПолей.Добавить(
		МетаданныеРегистра.Ресурсы.СтатьяРасходовПоИсследованиям.Имя,
		МетаданныеРегистра.Ресурсы.СтатьяРасходовПоИсследованиям.Синоним);

	СписокПроверяемыхПолей.Добавить(
		МетаданныеРегистра.Ресурсы.АналитикаРасходовПоИсследованиям.Имя,
		МетаданныеРегистра.Ресурсы.АналитикаРасходовПоИсследованиям.Синоним);
	
	Если Запись.ИспользоватьВыделениеДолгосрочныхАктивовОбязательств Тогда
		СписокПроверяемыхПолей.Добавить(
			МетаданныеРегистра.Ресурсы.ДлительностьОперационногоЦикла.Имя,
			МетаданныеРегистра.Ресурсы.ДлительностьОперационногоЦикла.Синоним);
	КонецЕсли;
	
	СписокПроверяемыхПолей.Добавить(
		МетаданныеРегистра.Ресурсы.АналитикаСписанияПроцентныхРасходов.Имя,
		МетаданныеРегистра.Ресурсы.АналитикаСписанияПроцентныхРасходов.Синоним);
	
	СписокПроверяемыхПолей.Добавить(
		МетаданныеРегистра.Ресурсы.СтатьяСписанияПроцентныхРасходов.Имя,
		МетаданныеРегистра.Ресурсы.СтатьяСписанияПроцентныхРасходов.Синоним);
	
	СписокПроверяемыхПолей.Добавить(
		МетаданныеРегистра.Ресурсы.МесяцНачалаФинансовогоГода.Имя,
		МетаданныеРегистра.Ресурсы.МесяцНачалаФинансовогоГода.Синоним);
	
	Возврат СписокПроверяемыхПолей;
	
КонецФункции

&НаСервере
Процедура ЗаписьУчетДисконтированнойКредиторскойЗадолженностиПоставщикамПриИзмененииНаСервере()
		
	ПараметрыВыбораСтатейИАналитик = РегистрыСведений.УчетнаяПолитикаФинансовогоУчета.ПараметрыВыбораСтатейИАналитик(
		Запись.ПризнаватьРасходыПоИсследованиям,
		Запись.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам);
	ДоходыИРасходыСервер.ПриИзмененииПараметровВыбораСтатей(ЭтотОбъект, ПараметрыВыбораСтатейИАналитик);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНадписьСтавкаДисконтирования()
	
	Если ЗначенияСтавокДисконтирования.Количество() Тогда 
		СтавкаДисконтирования = НСтр("ru='Ставки дисконтирования'");
	Иначе
		СтавкаДисконтирования = НСтр("ru='Ставки не указаны'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтавкиДисконтирования()
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Период", ДатаИзменения);
	ПараметрыОткрытия.Вставить("Организация", Запись.Организация);
	ПараметрыОткрытия.Вставить("ЗначенияСтавокДисконтирования", Новый Массив);
	Для Каждого СтрокаСтавки Из ЗначенияСтавокДисконтирования Цикл
		НоваяСтрока = Новый Структура("Валюта, СтавкаДисконтирования");
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСтавки);
		ПараметрыОткрытия.ЗначенияСтавокДисконтирования.Добавить(НоваяСтрока);
	КонецЦикла;
	ОткрытьФорму("РегистрСведений.СтавкиДисконтирования.Форма.РедактированиеСтавокДисконтирования", 
		ПараметрыОткрытия, 
		ЭтаФорма,,,,, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьРеквизитовДисконтирования()
	
	Элементы.ЗаписьУчетДисконтированнойКредиторскойЗадолженностиПоставщикам.Доступность = НоваяАрхитектураВзаиморасчетов;
	ДоступныРеквизитыДисконтирования = Запись.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам И НоваяАрхитектураВзаиморасчетов;
	Элементы.СтавкаДисконтирования.Доступность = ДоступныРеквизитыДисконтирования;
	Если Не ДоступныРеквизитыДисконтирования Тогда
		Элементы.СтавкаДисконтирования.Видимость = ЗначенияСтавокДисконтирования.Количество() > 0;
	Иначе
		Элементы.СтавкаДисконтирования.Видимость = Истина;
	КонецЕсли;
	Если НоваяАрхитектураВзаиморасчетов Тогда 
		Элементы.ГруппаДисконтированиеВертикальная.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	Иначе 
		Элементы.ГруппаДисконтированиеВертикальная.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	КонецЕсли;
	Элементы.ЗаписьСрокДляПримененияДисконтирования.Доступность = ДоступныРеквизитыДисконтирования;
	Элементы.ДекорацияДней.Доступность = ДоступныРеквизитыДисконтирования;
	
КонецПроцедуры 

&НаСервере
Процедура ПрочитатьСтавкиДисконтирования()
	
	ЗначенияСтавокДисконтирования.Очистить();
	
	Выборка = РегистрыСведений.СтавкиДисконтирования.Выбрать(
		Запись.Период, 
		Запись.Период, 
		Новый Структура("Организация", Запись.Организация));
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ЗначенияСтавокДисконтирования.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
	Попытка
		Для Каждого СтрокаСтавки Из ЗначенияСтавокДисконтирования Цикл
			Отбор = Новый Структура;
			Отбор.Вставить("Период", Запись.Период);
			Отбор.Вставить("Организация", Запись.Организация);
			Отбор.Вставить("Валюта", СтрокаСтавки.Валюта);
			КлючЗаписи = РегистрыСведений.СтавкиДисконтирования.СоздатьКлючЗаписи(Отбор);
			ЗаблокироватьДанныеДляРедактирования(КлючЗаписи,, УникальныйИдентификатор);
		КонецЦикла;
	Исключение
		
		ТекстОшибки = НСтр("ru='Не удалось заблокировать ставки дисконтирования. %ОписаниеОшибки%'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
	СформироватьНадписьСтавкаДисконтирования();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСтавкиДисконтирования()
	
	Если Запись.Период <> ДатаИзменения Тогда
		НаборЗаписей = РегистрыСведений.СтавкиДисконтирования.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Запись.Период);
		НаборЗаписей.Отбор.Организация.Установить(Запись.Организация);
		НаборЗаписей.Записать();
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СтавкиДисконтирования.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДатаИзменения); 
	НаборЗаписей.Отбор.Организация.Установить(Запись.Организация);
	
	Для Каждого СтрокаСтавки Из ЗначенияСтавокДисконтирования Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период 					= ДатаИзменения;
		НоваяЗапись.Организация 			= Запись.Организация;
		НоваяЗапись.Валюта 					= СтрокаСтавки.Валюта;
		НоваяЗапись.СтавкаДисконтирования 	= СтрокаСтавки.СтавкаДисконтирования;
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаСервере	
Процедура УправлениеНастройкойДетализацииДопРасходов()
	
	Если Запись.МетодОценкиСтоимостиТоваров <> Перечисления.МетодыОценкиСтоимостиТоваров.ФИФОСкользящаяОценка Тогда
		
		ДетализацияДополнительныхРасходовВключена = Ложь;
		Элементы.ГруппаДетализироватьДополнительныеРасходы.ТекущаяСтраница = Элементы.ГруппаДетализироватьДополнительныеРасходыНастройкаНевозможна;
		
		Если Запись.ДетализироватьДополнительныеРасходыВСебестоимостиТоваров Тогда
			Запись.ДетализироватьДополнительныеРасходыВСебестоимостиТоваров = Ложь;
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Т.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВтГоловныеОрганизации
	|ИЗ
	|	Справочник.Организации КАК Т
	|ГДЕ
	|	Т.Ссылка = &Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГоловнаяОрганизация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.РаздельныйУчетТоваровПоНалогообложениюНДС
	|ИЗ
	|	РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Период, Организация В
	|		(ВЫБРАТЬ
	|			ГоловнаяОрганизация
	|		ИЗ
	|			ВтГоловныеОрганизации)) КАК Т
	|";
	
	Запрос.УстановитьПараметр("Организация", Запись.Организация);
	Запрос.УстановитьПараметр("Период", 	 Запись.Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ДетализацияВключена = Выборка.Следующий() И Выборка.РаздельныйУчетТоваровПоНалогообложениюНДС;
	
	Если ДетализацияВключена Тогда
		
		ДетализацияДополнительныхРасходовВключена = Истина;
		Элементы.ГруппаДетализироватьДополнительныеРасходы.ТекущаяСтраница = Элементы.ГруппаДетализироватьДополнительныеРасходыНастройкаНевозможна;
		
	Иначе
		
		Элементы.ГруппаДетализироватьДополнительныеРасходы.ТекущаяСтраница = Элементы.ГруппаДетализироватьДополнительныеРасходыНастройкаВозможна;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
