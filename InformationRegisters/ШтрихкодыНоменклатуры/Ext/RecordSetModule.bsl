#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	  
	// << 24.08.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-324
	КР_ПередЗаписьюШтрихкодовНоменклатуры(Отказ, Замещение);
	// >> 24.08.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-324      

	// << 09.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-1802     
	ДополнительныеСвойства.Вставить("КР_МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	Если Не ДополнительныеСвойства.Свойство(КР_УТ11_ЦБ_Магазин_ЗагрузкаДанныхСервер.СвойствоОбъектБылЗагружен()) 
		Тогда 
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.Штрихкод КАК Штрихкод,         
		|	Т.Характеристика КАК Характеристика         
		|ПОМЕСТИТЬ ХарактеристикиКОчисткеШтрихкода
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК Т
		|ГДЕ
		|	(Т.Штрихкод = &Штрихкод 
		|		ИЛИ &ШтрихкодПустой)
		|	И Т.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|	И Т.Номенклатура.ИспользованиеХарактеристик 
		|		= ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
		|	И Т.Характеристика.Владелец ССЫЛКА Справочник.Номенклатура
		|	И Т.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)"
		);   
		
		Штрихкод = Отбор.Штрихкод.Значение;
		Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
		Запрос.УстановитьПараметр("ШтрихкодПустой", Не ЗначениеЗаполнено(Штрихкод));   
	    Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.КР_МенеджерВременныхТаблиц;

		Запрос.Выполнить();   
		
	КонецЕсли;	
	// >> 09.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-1802

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("Характеристика");
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ СтрокиСОшибками
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
	|			ИЛИ ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
	|			ИЛИ ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеЗаполненаХарактеристика
	|ИЗ
	|	СтрокиСОшибками КАК СтрокиСОшибками
	|ГДЕ
	|	ВЫРАЗИТЬ(СтрокиСОшибками.Номенклатура КАК Справочник.Номенклатура).ИспользованиеХарактеристик <> ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)";

	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",  ЭтотОбъект.Выгрузить(,"НомерСтроки,Номенклатура,Характеристика"));
	
	ШаблонСообщения = НСтр("ru='Поле ""%Характеристика%"" не заполнено'");
	ТекстСообщения = СтрЗаменить(ШаблонСообщения, "%Характеристика%", НСтр("ru = 'Характеристика'"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НеЗаполненаХарактеристика Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,"Характеристика","Запись",Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

// << 09.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-1802
Процедура ПриЗаписи(Отказ, Замещение)

	Если Не ДополнительныеСвойства.Свойство(КР_УТ11_ЦБ_Магазин_ЗагрузкаДанныхСервер.СвойствоОбъектБылЗагружен()) 
		Тогда 

		// Сравниваем то что было с тем что стало по характеристикам.
		// Заполняем/очищаем штрихкоды
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.Штрихкод КАК Штрихкод,
		|	Т.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ ХарактеристикиКЗаполнениюШтрихкода
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК Т
		|ГДЕ
		|	(Т.Штрихкод = &Штрихкод
		|			ИЛИ &ШтрихкодПустой)
		|	И Т.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|	И Т.Номенклатура.ИспользованиеХарактеристик 
		|		= ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
		|	И Т.Характеристика.Владелец ССЫЛКА Справочник.Номенклатура
		|	И Т.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(Стало.Штрихкод, """") КАК Штрихкод,
		|	ЕСТЬNULL(Стало.Характеристика, Было.Характеристика) КАК Характеристика
		|ИЗ
		|	ХарактеристикиКЗаполнениюШтрихкода КАК Стало
		|		ПОЛНОЕ СОЕДИНЕНИЕ ХарактеристикиКОчисткеШтрихкода КАК Было
		|		ПО Стало.Штрихкод = Было.Штрихкод
		|ГДЕ
		|	ВЫРАЗИТЬ(ЕСТЬNULL(Стало.Характеристика, Было.Характеристика) КАК Справочник.ХарактеристикиНоменклатуры).КР_Штрихкод 
		|		<> ЕСТЬNULL(Стало.Штрихкод, """")"
		);   
		
		Штрихкод = Отбор.Штрихкод.Значение;
		Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
		Запрос.УстановитьПараметр("ШтрихкодПустой", Не ЗначениеЗаполнено(Штрихкод));   
	    Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.КР_МенеджерВременныхТаблиц;
		
		РезультатЗапроса = Запрос.Выполнить(); 
	    ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
			КР_ЗаписатьШтрихкод(ВыборкаДетальныеЗаписи.Характеристика, ВыборкаДетальныеЗаписи.Штрихкод, Отказ); 
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры // >> 09.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-1802

#КонецОбласти

#Область КР_ДобавленныеПроцедурыИФункции

// << 24.08.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-324
Процедура КР_ПередЗаписьюШтрихкодовНоменклатуры(Отказ, Замещение)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаШтрихкоды", ЭтотОбъект.Выгрузить(, "Номенклатура, Характеристика"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаШтрихкоды.Номенклатура КАК Номенклатура,
	|	ТаблицаШтрихкоды.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ врт_ТаблицаШтрихкоды
	|ИЗ
	|	&ТаблицаШтрихкоды КАК ТаблицаШтрихкоды
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод,
	|	врт_ТаблицаШтрихкоды.Номенклатура КАК Номенклатура,
	|	врт_ТаблицаШтрихкоды.Характеристика КАК Характеристика
	|ИЗ
	|	врт_ТаблицаШтрихкоды КАК врт_ТаблицаШтрихкоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО врт_ТаблицаШтрихкоды.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И врт_ТаблицаШтрихкоды.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|ГДЕ
	|	НЕ ШтрихкодыНоменклатуры.Штрихкод ЕСТЬ NULL";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		ШаблонСообщения = НСтр("ru='%1 (%2). Для данной номенклатуры штрихкод существует.'");
		
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ШаблонСообщения, Выборка.Номенклатура, Выборка.Характеристика), , , , Отказ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // >> 24.08.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-324

// << 09.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-1802
Процедура КР_ЗаписатьШтрихкод(Характеристика, Штрихкод, Отказ)
	
	СправочникОбъект = Характеристика.ПолучитьОбъект();
	СправочникОбъект.КР_Штрихкод = Штрихкод;
	СправочникОбъект.ОбменДанными.Загрузка = Истина;   
	
	// Копируем дополнительные свойства набора запсей чтоб исключить ошибки..
	Для Каждого КлючЗначение Из ДополнительныеСвойства Цикл 
		СправочникОбъект.ДополнительныеСвойства.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;	                                      
	
	СправочникОбъект.ДополнительныеСвойства.Вставить("КР_РазрешитьУстановкуШтрихкода", Истина);
	
	СправочникОбъект.Записать();

КонецПроцедуры // >> 09.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-1802

#КонецОбласти

#КонецЕсли