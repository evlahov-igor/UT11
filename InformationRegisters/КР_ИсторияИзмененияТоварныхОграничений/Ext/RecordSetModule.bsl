////////////////////////////////////////////////////
//// Модуль набора записей регистра сведений "КР_ИсторияИзмененияТоварныхОграничений"
//// Создан: 22.03.2023 Марченко С.Н., КРОК, JIRA№A2105505-1148
//// Разработка по ФДР С31.010, Обработка для формирования заказов по потребностям

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;	
	
	МенеджерВременныхТаблиц = Неопределено; 
	Если Не ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц) Тогда 
	 	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
        ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	КонецЕсли;
	
	// Получаем уникальные значения измерений до и при записи по регистраторы
	Регистратор = Отбор.Регистратор.Значение;
	Запрос = Новый Запрос(ТекстЗапросаПередЗаписью());
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	// Помещаем во временную таблицу
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;	                                                       
	
	МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	// << 14.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2384
	// В рамках оптимизации обновление товарных ограницений вынесено в отложенные действия системы
	МенеджерОтложенныхДействийСистемы = РегистрыСведений.КР_ОтложенныеДействияСистемы;
	Регистратор = Отбор.Регистратор.Значение;
	
	// Готовим параметры для регистрации отложенного действия
	Параметры = Новый Массив;
	Параметры.Добавить(Регистратор);
	Параметры.Добавить(МенеджерВременныхТаблиц.Таблицы["ДоИзменения"].ПолучитьДанные().Выгрузить());
	
	Заголовок = НСтр("ru = 'Заполнение товарных ограничений на основании Истории изменений товарных ограничений (%1)'");
	Заголовок = СтрШаблон(Заголовок, Регистратор);  
	
	// Если набор будет обновляться несколько раз до того как выполнится отложенное действие,
	//	запись в обновлять не будем так как в нем сохранено изначальное состояние "контекста"
	//  В конечном итоге мы сразу отразим конечное состояние набора
	ДействиеПриНаличииЗаписи = МенеджерОтложенныхДействийСистемы.ДействиеПриНаличииЗаписи_ОставитьБезИзменений();
	
	// Регистрируем отложенное действие к выполнению
	МенеджерОтложенныхДействийСистемы.ЗапланироватьДействиеСистемы(
		"РегистрыСведений.КР_ИсторияИзмененияТоварныхОграничений.ОбновитьТоварныеОграничения",
		Параметры, Заголовок, ТекущаяДатаСеанса(), Регистратор, ДействиеПриНаличииЗаписи);
	
	// >> 14.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2384

КонецПроцедуры

#КонецОбласти	

#Область ТекстыЗапросов

Функция ТекстЗапросаПередЗаписью()
	
	Возврат
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Склад КАК Склад,
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ДоИзменения
	|ИЗ
	|	РегистрСведений.КР_ИсторияИзмененияТоварныхОграничений КАК Т
	|ГДЕ
	|	Т.Регистратор = &Регистратор";

КонецФункции	

#КонецОбласти

#КонецЕсли
