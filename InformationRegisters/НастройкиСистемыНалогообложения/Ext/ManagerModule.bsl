#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Параметры:
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
Функция ЗначенияПоУмолчанию() Экспорт
	СтруктураЗначений = Новый Структура;
	СтруктураЗначений.Вставить("СистемаНалогообложения", Перечисления.СистемыНалогообложения.Общая);
	СтруктураЗначений.Вставить("ПрименяетсяЕНВД", Ложь);
	СтруктураЗначений.Вставить("ЯвляетсяПлательщикомНДПИ", Ложь);
	СтруктураЗначений.Вставить("ПрименяетсяУСН", Ложь);
	СтруктураЗначений.Вставить("ПлательщикНалогаНаПрибыль", Ложь);
	СтруктураЗначений.Вставить("ДатаПереходаНаУСН", Неопределено);
	СтруктураЗначений.Вставить("УведомлениеНомерУСН", Неопределено);
	СтруктураЗначений.Вставить("УведомлениеДатаУСН", Неопределено);
	СтруктураЗначений.Вставить("ПрименяетсяПСН", Ложь);
	СтруктураЗначений.Вставить("ПрименяетсяАУСН", Ложь);
	СтруктураЗначений.Вставить("ПлательщикЕНП", Ложь);
	СтруктураЗначений.Вставить("УчетнаяПолитикаСуществует", Ложь);
	
	Возврат СтруктураЗначений
КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
// 	Строка - Текст запроса.
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР КОГДА ТаблицаСрезПоследних.Период ЕСТЬ NULL Тогда
	|		ЛОЖЬ
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ КАК УчетнаяПолитикаСуществует,
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Организация,
	|	ТаблицаСрезПоследних.СистемаНалогообложения КАК СистемаНалогообложения,
	|	ТаблицаСрезПоследних.ПрименяетсяЕНВД КАК ПрименяетсяЕНВД,
	|	ТаблицаСрезПоследних.ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	ТаблицаСрезПоследних.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	ТаблицаСрезПоследних.ЯвляетсяПлательщикомНДПИ КАК ЯвляетсяПлательщикомНДПИ,
	|	ТаблицаСрезПоследних.ДатаПереходаНаУСН КАК ДатаПереходаНаУСН,
	|	ТаблицаСрезПоследних.УведомлениеНомерУСН КАК УведомлениеНомерУСН,
	|	ТаблицаСрезПоследних.УведомлениеДатаУСН КАК УведомлениеДатаУСН,
	|	(ВЫБОР
	|		КОГДА ТаблицаСрезПоследних.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС) КОНЕЦ) КАК НалогообложениеНДСПоУмолчанию,
	|	ТаблицаСрезПоследних.ПрименяетсяПСН КАК ПрименяетсяПСН,
	|	ТаблицаСрезПоследних.ПрименяетсяАУСН КАК ПрименяетсяАУСН,
	|	ТаблицаСрезПоследних.ПлательщикЕНП КАК ПлательщикЕНП
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ТаблицаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = ТаблицаСрезПоследних.Организация
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеНастройки - Структура - действующие параметры учетной политики.
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Организация, ДатаДействия = Неопределено, ДействующиеНастройки = Неопределено) Экспорт
	
	Если ДействующиеНастройки = Неопределено Тогда
		ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик(
			"НастройкиСистемыНалогообложения",
			Организация,
			ДатаДействия,
			Ложь);
	КонецЕсли;
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;
	Если НЕ ЗначениеЗаполнено(ДействующиеНастройки) Тогда
		СтрокаОписанияНастроек = НСтр("ru='Не заданы параметры.'");
		Возврат СтрокаОписанияНастроек;
	КонецЕсли;
	
	СтрокаОписанияНастроек = СтрШаблон(СтрокаШаблон, 
		НСтр("ru='Система налогообложения'"),
		ДействующиеНастройки.СистемаНалогообложения);
	
	Если ДействующиеНастройки.ПрименяетсяЕНВД Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Организация является плательщиком ЕНВД'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ЯвляетсяПлательщикомНДПИ Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Является плательщиком НДПИ по процентной ставке'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ПрименяетсяПСН Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Организация применяет патентную систему налогообложения (ПСН)'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ПрименяетсяАУСН Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru='Организация применяет автоматизированную упрощенную систему налогообложения (АУСН)'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
		Если ЗначениеЗаполнено(ДействующиеНастройки.ДатаПереходаНаУСН) Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru='Дата перехода на УСН'"),
					Формат(ДействующиеНастройки.ДатаПереходаНаУСН,"ДЛФ=DD;"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДействующиеНастройки.УведомлениеНомерУСН) Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru='Уведомление о переходе на УСН №'"),
					ДействующиеНастройки.УведомлениеНомерУСН);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДействующиеНастройки.УведомлениеДатаУСН) Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru='Дата уведомления о переходе на УСН'"),
					Формат(ДействующиеНастройки.УведомлениеДатаУСН,"ДЛФ=DD;"));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДействующиеНастройки.ПлательщикЕНП) Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru='Организация использует единый налоговый платеж при уплате налогов'"));
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрокаОписанияНастроек
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы
//++ Локализация

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.НастройкиСистемыНалогообложения.ОбработатьДанныеДляПереходаНаНовуюВерсиюМонопольно";
	Обработчик.Версия = "2.5.3.5";
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("4c97907e-382c-47c2-8399-54fd776648ae4");
	Обработчик.Комментарий = НСтр("ru = 'Заполняет регистр ""Настройки системы налогообложения"" из настроек учетных политик организаций.'");
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсиюМонопольно() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	ДанныеРегистра.ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	ДанныеРегистра.ПрименяетсяПСН КАК ПрименяетсяПСН,
	|	Политики.СистемаНалогообложения КАК СистемаНалогообложения,
	|	Политики.ПрименяетсяЕНВД КАК ПрименяетсяЕНВД,
	|	Политики.ДатаПереходаНаУСН КАК ДатаПереходаНаУСН,
	|	Политики.УведомлениеНомерУСН КАК УведомлениеНомерУСН,
	|	Политики.УведомлениеДатаУСН КАК УведомлениеДатаУСН,
	|	Политики.ЯвляетсяПлательщикомНДПИ КАК ЯвляетсяПлательщикомНДПИ
	|ИЗ
	|	РегистрСведений.УдалитьУчетнаяПолитикаОрганизаций КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиСистемыНалогообложения КАК Настройки
	|		ПО (Настройки.Организация = ДанныеРегистра.Организация)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УдалитьУчетныеПолитикиОрганизаций КАК Политики
	|		ПО (ДанныеРегистра.УчетнаяПолитика = Политики.Ссылка)
	|ГДЕ
	|	Настройки.Организация ЕСТЬ NULL
	|";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.Прямой);
	
	Пока Выборка.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.НастройкиСистемыНалогообложения.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	
	КонецЦикла;
	
КонецПроцедуры
//-- Локализация

#КонецОбласти

#КонецЕсли
