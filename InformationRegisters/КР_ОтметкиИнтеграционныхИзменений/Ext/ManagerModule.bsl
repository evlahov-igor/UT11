////////////////////////////////////////////////////
//// Модуль менеджера регистра сведений "КР_ОтметкиИнтеграционныхИзменений"
//// Создан: 03.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1515
//// Оптимизация общего механизма интеграции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция Записать(Объект, СообщениеОбмена) Экспорт 
	
	СсылкаНаОбъект = КР_ОбщегоНазначениеСервер.ПолучитьСсылкуСУчетомСсылкиНового(Объект);
	
	ОтметкиИнтеграционныхИзменений = РегистрыСведений.КР_ОтметкиИнтеграционныхИзменений;
	
	Запись = ОтметкиИнтеграционныхИзменений.СоздатьМенеджерЗаписи();
	Запись.СсылкаНаОбъект = СсылкаНаОбъект;
	
	// Сначала поищем уже существующую запись
	// Если запись существует 
	//	и момент ее регистрации соврадает, то ничего не делаем - повторная загрузку
	//	и момент времени меньше момента времени записи, 
	// 		то запись не обновляем. Формируем пояснительное вообщение с изменением статуса    
	// Иначе добавляем новую запись
	
	Запись.Прочитать();
	Если Не Запись.Выбран() Тогда   
		
		Запись.СсылкаНаОбъект = СсылкаНаОбъект;  
		Запись.КлючСообщения = СообщениеОбмена.КлючСообщения;
		Запись.МоментРегистрации = СообщениеОбмена.МоментРегистрации;
		Запись.ДатаРегистрации = СообщениеОбмена.ДатаРегистрации;
		
		Запись.Записать();
		
		Возврат Истина;
	КонецЕсли;	
	
	Если Запись.ДатаРегистрации > СообщениеОбмена.ДатаРегистрации Тогда 
		ОбъектМетаданные = Объект.Метаданные();   
		
		ТекстСообщения = НСтр("ru = 'Объект ""%1"" был изменен более поздним входящим интеграционным сообщением %2 от %3'");   
		ТекстСообщения = СтрШаблон(ТекстСообщения,
			СсылкаНаОбъект, Запись.КлючСообщения, Запись.ДатаРегистрации);
			
		// A2105505-2346
		Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка.КР_Размеры") Тогда 
			ТипЗаписиВЛог = КР_ОбменRabbitОбработкаСообщенийОбмена.ТипЗаписиВЛог_Информация(ОбъектМетаданные, СсылкаНаОбъект);
			КР_ОбменRabbitОбработкаСообщенийОбмена.ДобавитьЗаписьВЛог(СообщениеОбмена, ТекстСообщения, ТипЗаписиВЛог);
		Иначе
		//	
		
		ТипЗаписиВЛог = КР_ОбменRabbitОбработкаСообщенийОбмена.ТипЗаписиВЛог_ИнформацияУстаревшиеДанные(ОбъектМетаданные, СсылкаНаОбъект);
		КР_ОбменRabbitОбработкаСообщенийОбмена.ДобавитьЗаписьВЛог(СообщениеОбмена, ТекстСообщения, ТипЗаписиВЛог);
		
		Возврат Ложь;
		
		// A2105505-2346
		КонецЕсли;
		//

	КонецЕсли;
		
	Если Запись.ДатаРегистрации = СообщениеОбмена.ДатаРегистрации Тогда 
		Возврат Истина;    	
	КонецЕсли;
	
	Запись.КлючСообщения = СообщениеОбмена.КлючСообщения;
	Запись.МоментРегистрации = СообщениеОбмена.МоментРегистрации;
	Запись.ДатаРегистрации = СообщениеОбмена.ДатаРегистрации;
	
	Запись.Записать();
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецЕсли
