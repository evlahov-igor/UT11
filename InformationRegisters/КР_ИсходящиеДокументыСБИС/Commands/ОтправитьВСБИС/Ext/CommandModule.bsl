// 23.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-1153

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Результат = Неопределено; 
	
	// --> Евлахов Игорь Николаевич (Начало) 02.05.2024
	// Задача #3985	
	СообщенияПользователю = ПроверитьЗаполнениеНаСервереИПолучитьСообщения(ПараметрКоманды);
	
	Если ЗначениеЗаполнено(СообщенияПользователю) Тогда
		ПодробноеОписаниеОшибки = СтрШаблон("Документ не проходит проверку заполнения:
		                                    |%1",
		                                    СообщенияПользователю);          
											
		ОбщегоНазначенияКлиент.СообщитьПользователю(ПодробноеОписаниеОшибки, ПараметрКоманды);
		Возврат;
	КонецЕсли;										
	// <-- Евлахов Игорь Николаевич (Конец) 02.05.2024
	
	// --> Евлахов Игорь Николаевич (Начало) 29.02.2024
	// Задача #3831
	ТекстСообщения = "";
	ДоступнаОтправка = ДоступнаОтправкаВСБИС(ПараметрКоманды, ТекстСообщения);
	
	Если Не ДоступнаОтправка Тогда
		Если ЗначениеЗаполнено(ТекстСообщения) Тогда
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("ДокументСсылка", ПараметрКоманды);
			ДополнительныеПараметры.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
			
			Режим = РежимДиалогаВопрос.ДаНет;
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, ДополнительныеПараметры);
			ТекстВопроса = НСтр("ru = 'Присоединить файл?'");
			ТекстСообщения = ТекстСообщения + Символы.ПС + ТекстВопроса;
			
			ПоказатьВопрос(Оповещение, ТекстСообщения, Режим, 0);
		КонецЕсли;
		
		Возврат;	
	КонецЕсли;
	// <-- Евлахов Игорь Николаевич (Конец) 29.02.2024
			
	Если Не ПроверитьОтправитьДокументВСБИС(ПараметрКоманды, Результат)
		Или ТипЗнч(Результат) <> Тип("Структура") Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат);
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Выгрузка в СБИС'"), 
            Результат.Гиперссылка, 
            Результат.ТекстСообщения
		); 
	КонецЕсли;	
		
КонецПроцедуры  

&НаСервере
Функция ПроверитьОтправитьДокументВСБИС(ДокументСсылка, Результат)
	
	// A2105505-2421
	// Удалена проверка по причине
	//	- Лист кассовой книги не участвует в механизме
	//	- Заказчик планирует кординально поменять проверку
	//Если ДатыЗапретаИзменения.ИзменениеЗапрещено(
	//		ДокументСсылка.Метаданные().ПолноеИмя(), ДокументСсылка, Результат) Тогда
	//	Возврат Ложь;
	//КонецЕсли; 
	//
	
	Возврат РегистрыСведений.КР_ИсходящиеДокументыСБИС.ПроверитьОтправитьДокументВСБИС(ДокументСсылка, Результат);
	
КонецФункции

// --> Евлахов Игорь Николаевич (Начало) 29.02.2024
// Задача #3831  
&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполненияКоманды = ДополнительныеПараметры.ПараметрыВыполненияКоманды;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВладелецФайла",  ДополнительныеПараметры.ДокументСсылка);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ПараметрыВыполненияКоманды.Источник.ТолькоПросмотр);
			
	ОткрытьФорму("Обработка.РаботаСФайлами.Форма.ПрисоединенныеФайлы",
	             ПараметрыФормы,
				 ПараметрыВыполненияКоманды.Источник,
	             Истина,
	             ПараметрыВыполненияКоманды.Источник.Окно);
				 
КонецПроцедуры

&НаСервере
Функция ДоступнаОтправкаВСБИС(ДокументСсылка, ТекстСообщения)
	
	ДоступнаОтправка = Истина;
	
	ТипДокумента = ТипЗнч(ДокументСсылка);
	
	Если 	ТипДокумента = Тип("ДокументСсылка.РасходныйКассовыйОрдер")
		ИЛИ ТипДокумента = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
		
		ОтправлятьБезПрисоединенныхФайлов = КР_ДополнительныеНастройкиПовтИсп.Значение("ОтправкаВСБИСБезПрисоединенныхФайлов");
		
		Если Не ОтправлятьБезПрисоединенныхФайлов Тогда
			Запрос = Новый Запрос;
			
			#Область ТекстЗапроса
			
			ТекстЗапроса = "ВЫБРАТЬ
			               |	НаличиеФайлов.ОбъектСФайлами КАК ОбъектСФайлами,
			               |	НаличиеФайлов.ЕстьФайлы КАК ЕстьФайлы
			               |ИЗ
			               |	РегистрСведений.НаличиеФайлов КАК НаличиеФайлов
			               |ГДЕ
			               |	НаличиеФайлов.ОбъектСФайлами = &ОбъектСФайлами";
			
			#КонецОбласти
			
			Запрос.Текст = ТекстЗапроса;
			
			Запрос.УстановитьПараметр("ОбъектСФайлами", ДокументСсылка);
			
			Результат = Запрос.Выполнить();
			Выборка = Результат.Выбрать();
			
			ЕстьФайлы = Ложь;
			
			Если Выборка.Следующий() Тогда
				ЕстьФайлы = Выборка.ЕстьФайлы;		
			КонецЕсли;
			
			ДоступнаОтправка = ЕстьФайлы;
			
			Если Не ДоступнаОтправка Тогда 
				ТекстСообщения = НСтр("ru = 'Необходимо присоединить скан-копию документа.'");
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	Возврат(ДоступнаОтправка);
	
КонецФункции 
// <-- Евлахов Игорь Николаевич (Конец) 29.02.2024  

// --> Евлахов Игорь Николаевич (Начало) 02.05.2024
// Задача #3985
&НаСервере
Функция ПроверитьЗаполнениеНаСервереИПолучитьСообщения(ДокументСсылка)
	
	ТестируемыйОбъект = ДокументСсылка.ПолучитьОбъект();
	ТестируемыйОбъект.ПроверитьЗаполнение();
	
	Возврат ПолучитьСообщения();
	
КонецФункции

&НаСервере
Функция ПолучитьСообщения()
	
	СообщенияПользователю = ПолучитьСообщенияПользователю(Истина);
	
	Ответ = "";
	
	Для Каждого СтрокаСообщения Из СообщенияПользователю Цикл
		Ответ = Ответ 
		      + ?(ЗначениеЗаполнено(СтрокаСообщения.Поле), СтрШаблон(НСтр("ru = 'Поле: %1'"), СтрокаСообщения.Поле) + Символы.ПС, "")
		      + ?(ЗначениеЗаполнено(СтрокаСообщения.Текст), СтрокаСообщения.Текст + Символы.ПС, "");
	КонецЦикла;
	
	Возврат Ответ;
	
КонецФункции
// <-- Евлахов Игорь Николаевич (Конец) 02.05.2024
