#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Записывает ставки НДС для всей номенклатуры. Используется в длительной операции "Рабочего места"
//
// Параметры:
//  Схема - СхемаКомпоновкиДанных
//  Настройки - НастройкиКомпоновкиДанных
//  Параметры - Структура - дополнительные параметры для записи
//
Процедура УстановитьСтавкуНДСНоменклатурыПоТекущемуОтбору(Схема, Настройки, Параметры) Экспорт
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Настройки, "ЭтоГруппа", Ложь);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, , ,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = МакетКомпоновки.НаборыДанных.НаборДанныхДинамическогоСписка.Запрос;	
	Для Каждого ЗначениеПараметра Из МакетКомпоновки.ЗначенияПараметров Цикл
		Запрос.УстановитьПараметр(ЗначениеПараметра.Имя,ЗначениеПараметра.Значение);
	КонецЦикла;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДобавитьЗаписьВРегистр(Выборка, Параметры);
	КонецЦикла;
	
КонецПроцедуры

// Записывает ставки НДС для списка номенклатуры. Используется в длительной операции "Рабочего места"
//
// Параметры:
//  СписокНоменклатуры - СписокЗначений - Список номенклатуры
//  Параметры - Структура - дополнительные параметры для записи
//
Процедура УстановитьСтавкуНДСНоменклатурыПоВыделеннымСтрокам(СписокНоменклатуры, Параметры) Экспорт
	
	ТаблицаЗначений = Новый ТаблицаЗначений();
	ТаблицаЗначений.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	
	Для Каждого Строка Из СписокНоменклатуры Цикл
		Если Строка.Значение.ЭтоГруппа Тогда
			ОбработатьГруппуНоменклатуры(ТаблицаЗначений, Строка.Значение);
		Иначе
			НоваяСтрока = ТаблицаЗначений.Добавить();
			НоваяСтрока.Номенклатура = Строка.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаЗначений Цикл
		ДобавитьЗаписьВРегистр(Строка, Параметры);
	КонецЦикла;
	
КонецПроцедуры

// Записывает актуальную ставку НДС для номенклатуры по основной стране в РС.СтавкиНДСНоменклатуры.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура
//  СтавкаНДС - СправочникСсылка.СтавкиНДС
//  ЗаписьНового - Булево
//  Дата - Дата - Необязательный, если не указана, то происходит запись на дату 01.01.1980
//
Процедура УстановитьАктуальнуюСтавкуНДСНоменклатурыПоОсновнойСтране(Номенклатура, СтавкаНДС, ЗаписьНового, Дата = '00010101') Экспорт
	
	ОсновнаяСтрана = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	
	Если ЗначениеЗаполнено(Дата) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	СтавкиНДСНоменклатуры.Период КАК Период,
			|	СтавкиНДСНоменклатуры.Номенклатура КАК Номенклатура,
			|	СтавкиНДСНоменклатуры.Страна КАК Страна
			|ИЗ
			|	РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
			|ГДЕ
			|	СтавкиНДСНоменклатуры.Номенклатура = &Номенклатура
			|	И СтавкиНДСНоменклатуры.Страна = &Страна
			|	И СтавкиНДСНоменклатуры.Период > &Период";
		
		Запрос.УстановитьПараметр("Страна", ОсновнаяСтрана);
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("Период", Дата);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			
			Пока Выборка.Следующий() Цикл
				НаборЗаписей = РегистрыСведений.СтавкиНДСНоменклатуры.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
				НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
				НаборЗаписей.Отбор.Страна.Установить(ОсновнаяСтрана);
				
				НаборЗаписей.Записать();
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗаписьНового Тогда
		Дата = Дата("19800101");
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.СтавкиНДСНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(НачалоМесяца(Дата));
	НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
	НаборЗаписей.Отбор.Страна.Установить(ОсновнаяСтрана);

	НоваяЗапись = НаборЗаписей.Добавить();
	НоваяЗапись.Период = НачалоМесяца(Дата);
	НоваяЗапись.Номенклатура = Номенклатура;
	НоваяЗапись.Страна = ОсновнаяСтрана;
	НоваяЗапись.СтавкаНДС = СтавкаНДС;
	
	НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьАктуализациюСтавкиНДСНоменклатуры");
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Получает актуальную ставку НДС для номенклатуры по основной стране из РС.СтавкиНДСНоменклатуры.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура
//
// Возвращаемое значение:
//  СправочникСсылка.СтавкиНДС
//
Функция АктуальнаяСтавкаНДСНоменклатурыПоОсновнойСтране(Номенклатура) Экспорт
	
	СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
	ОсновнаяСтрана = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СтавкиНДСНоменклатурыСрезПоследних.СтавкаНДС КАК СтавкаНДС
		|ИЗ
		|	РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(
		|			,
		|			(Страна = &Страна
		|				ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
		|				И Номенклатура = &Номенклатура) КАК СтавкиНДСНоменклатурыСрезПоследних";
	
	Запрос.УстановитьПараметр("Страна", ОсновнаяСтрана);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		СтавкаНДС = Выборка.СтавкаНДС;
	КонецЕсли;
	
	Возврат СтавкаНДС;
	
КонецФункции

// Получает таблицу ставок НДС для номенклатуры.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - 
//  ОтборПоСтране - СправочникСсылка.СтраныМира - Если без отбора, то Неопределено
//  ОтборПоПериоду - Дата - Если без отбора, то Неопределено
//
// Возвращаемое значение:
//  ТаблицаЗначений - Содержит колонки:
//  	* Период - Дата -
//  	* Страна - СправочникСсылка.СтраныМира -
//  	* СтавкаНДС - СправочникСсылка.СтавкиНДС -
//  	* ОсновнаяСтавка - СправочникСсылка.СтавкиНДС -
Функция ТаблицаСтавокНДСНоменклатуры(Номенклатура, ОтборПоСтране = Неопределено, ОтборПоПериоду = Неопределено)  Экспорт
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СтавкиНДСНоменклатуры.Период КАК Период,
	|	СтавкиНДСНоменклатуры.Страна КАК Страна,
	|	СтавкиНДСНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА СтавкиНДСНоменклатуры.СтавкаНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|		ИНАЧЕ СтавкиНДСНоменклатуры.СтавкаНДС.КонецПериода
	|	КОНЕЦ КАК СтавкаНДСДействуетДо,
	|	ВЫБОР
	|		КОГДА СтавкиНДСНоменклатуры.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачальноеЗаполнение
	|ПОМЕСТИТЬ СтавкиНДСНоменклатуры
	|ИЗ
	|	РегистрСведений.СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
	|ГДЕ
	|	СтавкиНДСНоменклатуры.Номенклатура = &Номенклатура
	|	И (&ФильтрПоСтране1
	|	ИЛИ СтавкиНДСНоменклатуры.Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Страна,
	|	НачальноеЗаполнение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтавкиНДСНоменклатуры.Страна КАК Страна
	|ПОМЕСТИТЬ СтраныСтавокНДСНоменклатуры
	|ИЗ
	|	СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
	|ГДЕ
	|	НЕ СтавкиНДСНоменклатуры.НачальноеЗаполнение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНДСНоменклатуры.Период КАК Период,
	|	СтраныСтавокНДСНоменклатуры.Страна КАК Страна,
	|	СтавкиНДСНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	СтавкиНДСНоменклатуры.СтавкаНДСДействуетДо КАК СтавкаНДСДействуетДо
	|ПОМЕСТИТЬ СтавкиНДСНоменклатурыПоСтранам
	|ИЗ
	|	СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтраныСтавокНДСНоменклатуры КАК СтраныСтавокНДСНоменклатуры
	|		ПО (ИСТИНА)
	|ГДЕ
	|	СтавкиНДСНоменклатуры.НачальноеЗаполнение
	|	И НЕ СтраныСтавокНДСНоменклатуры.Страна ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтавкиНДСНоменклатуры.Период,
	|	СтавкиНДСНоменклатуры.Страна,
	|	СтавкиНДСНоменклатуры.СтавкаНДС,
	|	СтавкиНДСНоменклатуры.СтавкаНДСДействуетДо
	|ИЗ
	|	СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
	|ГДЕ
	|	НЕ СтавкиНДСНоменклатуры.НачальноеЗаполнение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНДСНоменклатурыПоСтранам.Период КАК Начало,
	|	ВЫБОР
	|		КОГДА СтавкиНДСНоменклатурыПоСтранам.СтавкаНДСДействуетДо > МИНИМУМ(ЕСТЬNULL(СтавкиНДСНоменклатурыПоСтранам1.Период,
	|			СтавкиНДСНоменклатурыПоСтранам.СтавкаНДСДействуетДо))
	|			ТОГДА МИНИМУМ(ЕСТЬNULL(СтавкиНДСНоменклатурыПоСтранам1.Период,
	|				СтавкиНДСНоменклатурыПоСтранам.СтавкаНДСДействуетДо))
	|		ИНАЧЕ СтавкиНДСНоменклатурыПоСтранам.СтавкаНДСДействуетДо
	|	КОНЕЦ КАК Окончание,
	|	СтавкиНДСНоменклатурыПоСтранам.Страна КАК Страна,
	|	СтавкиНДСНоменклатурыПоСтранам.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ СтавкиНДСНоменклатурыПоПериодам
	|ИЗ
	|	СтавкиНДСНоменклатурыПоСтранам КАК СтавкиНДСНоменклатурыПоСтранам
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНДСНоменклатурыПоСтранам КАК СтавкиНДСНоменклатурыПоСтранам1
	|		ПО СтавкиНДСНоменклатурыПоСтранам.Период < СтавкиНДСНоменклатурыПоСтранам1.Период
	|		И СтавкиНДСНоменклатурыПоСтранам.Страна = СтавкиНДСНоменклатурыПоСтранам1.Страна
	|СГРУППИРОВАТЬ ПО
	|	СтавкиНДСНоменклатурыПоСтранам.Период,
	|	СтавкиНДСНоменклатурыПоСтранам.Страна,
	|	СтавкиНДСНоменклатурыПоСтранам.СтавкаНДС,
	|	СтавкиНДСНоменклатурыПоСтранам.СтавкаНДСДействуетДо
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Страна,
	|	Начало
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНДСНоменклатурыПоПериодам.Страна КАК Страна,
	|	МАКСИМУМ(СтавкиНДСНоменклатурыПоПериодам.Окончание) КАК Окончание
	|ПОМЕСТИТЬ СтавкиНДСНоменклатурыМаксДата
	|ИЗ
	|	СтавкиНДСНоменклатурыПоПериодам КАК СтавкиНДСНоменклатурыПоПериодам
	|СГРУППИРОВАТЬ ПО
	|	СтавкиНДСНоменклатурыПоПериодам.Страна
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНДСНоменклатурыПоПериодам.Страна КАК Страна,
	|	МАКСИМУМ(ЕСТЬNULL(СтавкиНДСНоменклатурыПоПериодам1.Окончание, СтавкиНДСНоменклатурыПоПериодам.Начало)) КАК
	|		ПустойИнтервалНачало,
	|	СтавкиНДСНоменклатурыПоПериодам.Начало КАК ПустойИнтервалОкончание
	|ПОМЕСТИТЬ ПустыеИнтервалы
	|ИЗ
	|	СтавкиНДСНоменклатурыПоПериодам КАК СтавкиНДСНоменклатурыПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтавкиНДСНоменклатурыПоПериодам КАК СтавкиНДСНоменклатурыПоПериодам1
	|		ПО СтавкиНДСНоменклатурыПоПериодам.Страна = СтавкиНДСНоменклатурыПоПериодам1.Страна
	|		И СтавкиНДСНоменклатурыПоПериодам.Начало > СтавкиНДСНоменклатурыПоПериодам1.Начало
	|СГРУППИРОВАТЬ ПО
	|	СтавкиНДСНоменклатурыПоПериодам.Страна,
	|	СтавкиНДСНоменклатурыПоПериодам.Начало
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Страна,
	|	ПустойИнтервалНачало,
	|	ПустойИнтервалОкончание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеСтавкиНДС.Период КАК Период,
	|	ОсновныеСтавкиНДС.Страна КАК Страна,
	|	ОсновныеСтавкиНДС.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ОсновныеСтавкиНДС.СтавкаНДС.КонецПериода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|		ИНАЧЕ ОсновныеСтавкиНДС.СтавкаНДС.КонецПериода
	|	КОНЕЦ КАК СтавкаНДСДействуетДо
	|ПОМЕСТИТЬ ОсновныеСтавкиНДС
	|ИЗ
	|	РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
	|ГДЕ
	|	&ФильтрПоСтране2
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеСтавкиНДС.Страна КАК Страна
	|ПОМЕСТИТЬ СтраныДополнительные
	|ИЗ
	|	ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
	|ГДЕ
	|	НЕ ОсновныеСтавкиНДС.Страна В
	|		(ВЫБРАТЬ
	|			Т.Страна
	|		ИЗ
	|			СтраныСтавокНДСНоменклатуры КАК Т)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновныеСтавкиНДС.Период КАК Начало,
	|	МИНИМУМ(ЕСТЬNULL(ОсновныеСтавкиНДС1.Период, ОсновныеСтавкиНДС.СтавкаНДСДействуетДо)) КАК Окончание,
	|	ОсновныеСтавкиНДС.Страна КАК Страна,
	|	ОсновныеСтавкиНДС.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ОсновныеСтавкиНДСПоПериодам
	|ИЗ
	|	ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС1
	|		ПО ОсновныеСтавкиНДС.Страна = ОсновныеСтавкиНДС1.Страна
	|		И ОсновныеСтавкиНДС.Период < ОсновныеСтавкиНДС1.Период
	|СГРУППИРОВАТЬ ПО
	|	ОсновныеСтавкиНДС.Период,
	|	ОсновныеСтавкиНДС.Страна,
	|	ОсновныеСтавкиНДС.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Страна,
	|	Начало,
	|	Окончание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНДСНоменклатуры.Период КАК Начало,
	|	СтраныДополнительные.Страна КАК Страна,
	|	СтавкиНДСНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА СтавкиНДСНоменклатуры.СтавкаНДСДействуетДо > ОсновныеСтавкиНДССрезПервых.Период
	|			ТОГДА ОсновныеСтавкиНДССрезПервых.Период
	|		ИНАЧЕ СтавкиНДСНоменклатуры.СтавкаНДСДействуетДо
	|	КОНЕЦ КАК Окончание,
	|	ЛОЖЬ КАК ОсновнаяСтавка
	|ПОМЕСТИТЬ ОсновныеСтавкиНДСДополнение
	|ИЗ
	|	СтавкиНДСНоменклатуры КАК СтавкиНДСНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтраныДополнительные КАК СтраныДополнительные
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПервых КАК ОсновныеСтавкиНДССрезПервых
	|			ПО СтраныДополнительные.Страна = ОсновныеСтавкиНДССрезПервых.Страна
	|		ПО (ИСТИНА)
	|ГДЕ
	|	СтавкиНДСНоменклатуры.НачальноеЗаполнение
	|	И НЕ СтраныДополнительные.Страна ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновныеСтавкиНДСПоПериодам.Начало,
	|	ОсновныеСтавкиНДСПоПериодам.Страна,
	|	ОсновныеСтавкиНДСПоПериодам.СтавкаНДС,
	|	ОсновныеСтавкиНДСПоПериодам.Окончание,
	|	ИСТИНА
	|ИЗ
	|	ОсновныеСтавкиНДСПоПериодам КАК ОсновныеСтавкиНДСПоПериодам
	|ГДЕ
	|	ОсновныеСтавкиНДСПоПериодам.Страна В
	|		(ВЫБРАТЬ
	|			Т.Страна
	|		ИЗ
	|			СтраныДополнительные КАК Т)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(СтавкиНДСНоменклатурыМаксДата.Окончание, ДЕНЬ, 1),
	|	СтавкиНДСНоменклатурыМаксДата.Страна,
	|	ЕСТЬNULL(ОсновныеСтавкиНДССрезПоследних.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА (ЕСТЬNULL(ОсновныеСтавкиНДССрезПоследних.СтавкаНДС,
	|			ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))).КонецПериода = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|		ИНАЧЕ (ЕСТЬNULL(ОсновныеСтавкиНДССрезПоследних.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка))).КонецПериода
	|	КОНЕЦ,
	|	ИСТИНА
	|ИЗ
	|	СтавкиНДСНоменклатурыМаксДата КАК СтавкиНДСНоменклатурыМаксДата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних КАК ОсновныеСтавкиНДССрезПоследних
	|		ПО СтавкиНДСНоменклатурыМаксДата.Страна = ОсновныеСтавкиНДССрезПоследних.Страна
	|		И СтавкиНДСНоменклатурыМаксДата.Окончание >= ОсновныеСтавкиНДССрезПоследних.Период
	|ГДЕ
	|	СтавкиНДСНоменклатурыМаксДата.Окончание < &ТекущаяДата
	|ИНДЕКСИРОВАТЬ ПО
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПустыеИнтервалы.Страна КАК Страна,
	|	ВЫБОР
	|		КОГДА ДОБАВИТЬКДАТЕ(ПустыеИнтервалы.ПустойИнтервалНачало, ДЕНЬ, 1) > ОсновныеСтавкиНДСПоПериодам.Начало
	|			ТОГДА ДОБАВИТЬКДАТЕ(ПустыеИнтервалы.ПустойИнтервалНачало, ДЕНЬ, 1)
	|		ИНАЧЕ ОсновныеСтавкиНДСПоПериодам.Начало
	|	КОНЕЦ КАК Начало,
	|	ОсновныеСтавкиНДСПоПериодам.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ ОсновныеСтавкиНДСКПустымПериодам
	|ИЗ
	|	ПустыеИнтервалы КАК ПустыеИнтервалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОсновныеСтавкиНДСПоПериодам КАК ОсновныеСтавкиНДСПоПериодам
	|		ПО ПустыеИнтервалы.ПустойИнтервалНачало <= ОсновныеСтавкиНДСПоПериодам.Окончание
	|			И ПустыеИнтервалы.ПустойИнтервалОкончание >= ОсновныеСтавкиНДСПоПериодам.Начало
	|			И ПустыеИнтервалы.Страна = ОсновныеСтавкиНДСПоПериодам.Страна
	|ГДЕ
	|	ПустыеИнтервалы.ПустойИнтервалНачало <> ПустыеИнтервалы.ПустойИнтервалОкончание
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Страна
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиНДСНоменклатурыПоПериодам.Страна КАК Страна,
	|	СтавкиНДСНоменклатурыПоПериодам.Начало КАК Период,
	|	СтавкиНДСНоменклатурыПоПериодам.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ОсновнаяСтавка
	|ИЗ
	|	СтавкиНДСНоменклатурыПоПериодам КАК СтавкиНДСНоменклатурыПоПериодам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновныеСтавкиНДСДополнение.Страна,
	|	ОсновныеСтавкиНДСДополнение.Начало,
	|	ОсновныеСтавкиНДСДополнение.СтавкаНДС,
	|	ОсновныеСтавкиНДСДополнение.ОсновнаяСтавка
	|ИЗ
	|	ОсновныеСтавкиНДСДополнение КАК ОсновныеСтавкиНДСДополнение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОсновныеСтавкиНДСКПустымПериодам.Страна,
	|	ОсновныеСтавкиНДСКПустымПериодам.Начало,
	|	ОсновныеСтавкиНДСКПустымПериодам.СтавкаНДС,
	|	ИСТИНА
	|ИЗ
	|	ОсновныеСтавкиНДСКПустымПериодам КАК ОсновныеСтавкиНДСКПустымПериодам
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ
	|ПО
	|	Страна";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ФильтрПоСтране1",
			?(ЗначениеЗаполнено(ОтборПоСтране), "СтавкиНДСНоменклатуры.Страна = &Страна", "ИСТИНА"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ФильтрПоСтране2",
			?(ЗначениеЗаполнено(ОтборПоСтране), "ОсновныеСтавкиНДС.Страна = &Страна", "ИСТИНА"));
			
	Запрос.Текст = ТекстЗапроса;

	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("ОсновнаяСтрана", ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана());
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Если ЗначениеЗаполнено(ОтборПоСтране) Тогда
		Запрос.УстановитьПараметр("Страна", ОтборПоСтране);	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаСтавок = Новый ТаблицаЗначений;
	ТаблицаСтавок.Колонки.Добавить("Период");
	ТаблицаСтавок.Колонки.Добавить("Страна");
	ТаблицаСтавок.Колонки.Добавить("СтавкаНДС");
	ТаблицаСтавок.Колонки.Добавить("ОсновнаяСтавка");

	ВыборкаСтрана = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаСтрана.Следующий() Цикл
	
		ВыборкаДетальныеЗаписи = ВыборкаСтрана.Выбрать();
	    СтавкаВПериоде = Неопределено;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ВыборкаДетальныеЗаписи.СтавкаНДС <> СтавкаВПериоде Тогда
				Если ЗначениеЗаполнено(ОтборПоПериоду) И ВыборкаДетальныеЗаписи.Период > ОтборПоПериоду Тогда
					Продолжить;
				КонецЕсли;					
				НоваяСтрока = ТаблицаСтавок.Добавить();
				НоваяСтрока.Страна = ВыборкаСтрана.Страна;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
				СтавкаВПериоде = ВыборкаДетальныеЗаписи.СтавкаНДС;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаСтавок;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьГруппуНоменклатуры(ТаблицаЗначений, Группа)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В ИЕРАРХИИ(&Группа)
		|	И НЕ Номенклатура.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Группа", Группа);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаЗначений.Добавить();
			НоваяСтрока.Номенклатура = Выборка.Ссылка;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЗаписьВРегистр(Данные, Параметры)
	
	НаборЗаписей = РегистрыСведений.СтавкиНДСНоменклатуры.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(Параметры.Период);
	НаборЗаписей.Отбор.Номенклатура.Установить(Данные.Номенклатура);
	НаборЗаписей.Отбор.Страна.Установить(Параметры.Страна);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяЗапись, Данные);
	ЗаполнитьЗначенияСвойств(НоваяЗапись, Параметры);
	
	НаборЗаписей.Записать();
	
КонецПроцедуры


#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.СтавкиНДСНоменклатуры.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.8.48";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("cc6a902b-d891-4a04-9bc2-2a60ea719b17");
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.СтавкиНДСНоменклатуры.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Заполняет регистр ""Ставки НДС номенклатуры"".'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ОсновныеСтавкиНДС.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.Номенклатура.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.СтавкиНДСНоменклатуры.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ОсновныеСтавкиНДС.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Справочники.Номенклатура.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Номенклатура.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = Метаданные.РегистрыСведений.СтавкиНДСНоменклатуры.ПолноеИмя();
	
	ОсновнаяСтрана = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеСтавкиНДССрезПоследних.СтавкаНДС КАК ОсновнаяСтавкаНДС,
	|	ОсновныеСтавкиНДССрезПоследних.СтавкаНДС.ПеречислениеСтавкаНДС КАК ОсновнаяСтавкаНДСПеречисление
	|ИЗ
	|	РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &Страна) КАК ОсновныеСтавкиНДССрезПоследних";

	Запрос.УстановитьПараметр("Страна", ОсновнаяСтрана);
	Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());

	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ОсновнаяСтавкаНДС = Выборка.ОсновнаяСтавкаНДС;
		ОсновнаяСтавкаНДСПеречисление = Выборка.ОсновнаяСтавкаНДСПеречисление;
	Иначе
		ОсновнаяСтавкаНДС = Неопределено;
		ОсновнаяСтавкаНДСПеречисление = Неопределено;
	КонецЕсли;

	ВыполнитьМонопольнуюЗапись = Истина;
	Пока ВыполнитьМонопольнуюЗапись Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 50000
		|	СправочникНоменклатура.Ссылка КАК Номенклатура,
		|	&ОсновнаяСтрана КАК Страна,
		|	СправочникНоменклатура.СтавкаНДС КАК СтавкаНДС,
		|	СправочникНоменклатура.УдалитьСтавкаНДС КАК СтавкаНДСПеречисление
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период, Страна = &ОсновнаяСтрана) КАК
		|			СтавкиНДСНоменклатурыСрезПоследних
		|		ПО (СтавкиНДСНоменклатурыСрезПоследних.Номенклатура = СправочникНоменклатура.Ссылка)
		|ГДЕ
		|	НЕ СправочникНоменклатура.ПометкаУдаления
		|	И НЕ СправочникНоменклатура.ЭтоГруппа
		|	И &УсловиеСтавкиНДС
		|	И СтавкиНДСНоменклатурыСрезПоследних.Номенклатура ЕСТЬ NULL
		|	И (СправочникНоменклатура.СтавкаНДС <> ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
		//++ Локализация
		|	ИЛИ СправочникНоменклатура.УдалитьСтавкаНДС <> ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.ПустаяСсылка)
		//-- Локализация
		|)
		|";
		
		Запрос.УстановитьПараметр("ОсновнаяСтрана", ОсновнаяСтрана);
		Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
		
		Если ЗначениеЗаполнено(ОсновнаяСтавкаНДС) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"&УсловиеСтавкиНДС",
			"ВЫБОР
			|	КОГДА СправочникНоменклатура.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
			|		ТОГДА СправочникНоменклатура.УдалитьСтавкаНДС <> &СтавкаНДСПеречисление
			|	ИНАЧЕ СправочникНоменклатура.СтавкаНДС <> &СтавкаНДС
			|КОНЕЦ");
			Запрос.УстановитьПараметр("СтавкаНДС", ОсновнаяСтавкаНДС);
			Запрос.УстановитьПараметр("СтавкаНДСПеречисление", ОсновнаяСтавкаНДСПеречисление);
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст,"&УсловиеСтавкиНДС", "Истина");
		КонецЕсли;
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			ВыполнитьМонопольнуюЗапись = Ложь;
		Иначе
			Выборка = Результат.Выбрать();
			НаборЗаписей = РегистрыСведений.СтавкиНДСНоменклатуры.СоздатьНаборЗаписей();

			Пока Выборка.Следующий() Цикл
				
				Если ЗначениеЗаполнено(Выборка.СтавкаНДС) Тогда
					СтавкаНДСНоменклатуры = Выборка.СтавкаНДС;
				Иначе
					СтавкаНДСНоменклатуры = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Выборка.СтавкаНДСПеречисление);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтавкаНДСНоменклатуры) Тогда
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
					НоваяЗапись.СтавкаНДС = СтавкаНДСНоменклатуры;
					НоваяЗапись.Период = Дата("19800101");
				КонецЕсли;
			
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Ложь);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОсновнаяСтавкаНДС) Тогда	
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СправочникНоменклатура.Ссылка КАК Номенклатура,
		|	&ОсновнаяСтрана КАК Страна
		|ИЗ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период, Страна = &ОсновнаяСтрана) КАК СтавкиНДСНоменклатурыСрезПоследних
		|		ПО (СтавкиНДСНоменклатурыСрезПоследних.Номенклатура = СправочникНоменклатура.Ссылка)
		|ГДЕ
		|	НЕ СправочникНоменклатура.ПометкаУдаления
		|	И НЕ СправочникНоменклатура.ЭтоГруппа
		|	И СтавкиНДСНоменклатурыСрезПоследних.Номенклатура ЕСТЬ NULL";
		
		Запрос.УстановитьПараметр("ОсновнаяСтрана", ОсновнаяСтрана);
		Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
		
		Данные = Запрос.Выполнить().Выгрузить();
		Данные.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
		Данные.ЗаполнитьЗначения(Дата("19800101"), "Период");
		
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.СтавкиНДСНоменклатуры; 
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТДанныеДляОбработки";
	ДополнительныеПараметры.ВыбиратьПорциями = Ложь;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта,
		МенеджерВременныхТаблиц,
		ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДляОбработки.Период КАК Период,
	|	ДанныеДляОбработки.Страна КАК Страна,
	|	ДанныеДляОбработки.Номенклатура КАК Номенклатура
	|ИЗ
	|	ВТДанныеДляОбработки КАК ДанныеДляОбработки";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			НачатьТранзакцию();
			
			Попытка
				
				БлокировкаДанных = Новый БлокировкаДанных;

				ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.Номенклатура");
				ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", Выборка.Номенклатура);
				ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Разделяемый;
				
				ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтавкиНДСНоменклатуры");
				ЭлементБлокировкиДанных.УстановитьЗначение("Период", Выборка.Период);
				ЭлементБлокировкиДанных.УстановитьЗначение("Номенклатура", Выборка.Номенклатура);
				ЭлементБлокировкиДанных.УстановитьЗначение("Страна", Выборка.Страна);
				ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
				
				БлокировкаДанных.Заблокировать();
				
				НаборЗаписей = РегистрыСведений.СтавкиНДСНоменклатуры.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
				НаборЗаписей.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
				НаборЗаписей.Отбор.Страна.Установить(Выборка.Страна);
				
				СтавкаНДСНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Номенклатура, "СтавкаНДС");
				
				Если ЗначениеЗаполнено(СтавкаНДСНоменклатуры) Тогда
					
					НоваяЗапись = НаборЗаписей.Добавить();
					НоваяЗапись.Период = Выборка.Период;
					НоваяЗапись.Номенклатура = Выборка.Номенклатура;
					НоваяЗапись.Страна = Выборка.Страна;
					НоваяЗапись.СтавкаНДС = СтавкаНДСНоменклатуры;
					
				КонецЕсли;
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				
				ТекстСообщения = НСтр("ru = 'Не удалось записать данные в регистр %ИмяРегистра% по причине: %Причина%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяОбъекта);
				
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта, Неопределено, ТекстСообщения);
				
				ВызватьИсключение;
				
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
