#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика справочника
//
// Параметры:
//  Объект - СправочникОбъект.Склады - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	Если НЕ Объект.ЭтоГруппа Тогда
		
		Если ПолучитьФункциональнуюОпцию("ВестиУчетПодконтрольныхТоваровВЕТИС")
			И ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры")
			И (Объект.ИспользоватьОрдернуюСхемуПриОтгрузке
			Или Объект.ИспользоватьОрдернуюСхемуПриПоступлении) Тогда
			
			Если НоменклатураСервер.ЕстьВидНоменклатурыТребующийОтключенияОрдернойСхемыПриПоступленииИОтгрузки(Объект.Ссылка) Тогда
				
				Если Объект.ИспользоватьОрдернуюСхемуПриОтгрузке Тогда
					ТекстСообщения = НСтр("ru='Нельзя включить использование ордерной схемы при отгрузке, т.к. есть вид номенклатуры продукции, подконтрольной ВетИС, для которой настроена политика, не предусматривающая учет себестоимости по сериям.'");
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "ИспользоватьОрдернуюСхемуПриОтгрузке","Объект",Отказ);
					
				КонецЕсли;
				
				Если Объект.ИспользоватьОрдернуюСхемуПриПоступлении Тогда
					ТекстСообщения = НСтр("ru='Нельзя включить использование ордерной схемы при поступлении, т.к. есть вид номенклатуры продукции, подконтрольной ВетИС, для которой настроена политика, не предусматривающая учет себестоимости по сериям.'");
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, "ИспользоватьОрдернуюСхемуПриПоступлении","Объект",Отказ);
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;
	//-- Локализация
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура УправлениеЭлементами_ПанельСправочниковСклад(Элементы, ЕстьДанныеДляОтображения) Экспорт

	//++ Локализация

	ПравоДоступаКатегорииЭксплуатации = 
		ПравоДоступа("Просмотр", Метаданные.Справочники.КатегорииЭксплуатации)
		И ПолучитьФункциональнуюОпцию("УправлениеТорговлей");
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ОткрытьКатегорииЭксплуатации",
		"Видимость",
		ПравоДоступаКатегорииЭксплуатации);
		
	ЕстьДанныеДляОтображения = ЕстьДанныеДляОтображения ИЛИ ПравоДоступаКатегорииЭксплуатации;
	
	//-- Локализация
		
КонецПроцедуры

#КонецОбласти
