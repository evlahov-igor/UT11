// Универсальные механизмы интеграции ИС (ЕГАИС, ГИСМ, ВЕТИС, ...)

#Область ПрограммныйИнтерфейс

//Определяет соответствие переданного документа проверяемому типу.
//
// Параметры:
//  Контекст - ФормаКлиентскогоПриложения, ДокументСсылка - Контекст для определения типа документа.
//  Имя      - Строка - имя объекта метаданного документа.
//
// Возвращаемое значение:
// 	Булево - это документ.
Функция ЭтоДокументПоНаименованию(Контекст, Имя) Экспорт

	Результат = Ложь;

	ТипКонтекста = ТипЗнч(Контекст);
	ТипДокумента = Тип(СтрШаблон("ДокументСсылка.%1", Имя));

	Если ТипКонтекста = Тип("ФормаКлиентскогоПриложения") Тогда
		Если СтрНачинаетсяС(Контекст.ИмяФормы, СтрШаблон("Документ.%1", Имя)) Тогда
			Результат = Истина;
		КонецЕсли;
	ИначеЕсли ТипКонтекста = ТипДокумента Тогда
		Результат = Истина;
	КонецЕсли;

	Возврат Результат;

КонецФункции

//Возвращает значение особености учета номенклатуры по переданному виду продукции ИС
//или вид продукции ИС по переданной особенности учета номенклатуры
// Параметры:
//  ОсобенностьУчетаИлиВидПродукции - ПеречислениеСсылка.ОсобенностиУчетаНоменклатуры, ПеречислениеСсылка.ВидыПродукцииИС - особеннсоть учета номенклатуры или вид продукции ИС
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыПродукцииИС, ПеречислениеСсылка.ОсобенностиУчетаНоменклатуры - вид продукции ИС или особенность учета номенклатуры
//
Функция ОсобенностьУчетаПоВидуПродукции(ОсобенностьУчетаИлиВидПродукции) Экспорт
	
	Если ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияБезВЕТИС") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Шины") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Фотоаппараты") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Фотоаппараты");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Велосипеды") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Велосипеды");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.КреслаКоляски") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КреслаКоляски");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Духи") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.АльтернативныйТабак") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.УпакованнаяВода") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.УпакованнаяВода");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Антисептики") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Антисептики");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.БАДы") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.НикотиносодержащаяПродукция") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Пиво") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Алкогольная") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.АлкогольнаяПродукция");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ОбувнаяПродукция");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.ЛегкаяПромышленность");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияПодконтрольнаяВЕТИС");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.МолочнаяПродукцияБезВЕТИС");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Шины");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Фотоаппараты") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Фотоаппараты");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Велосипеды") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Велосипеды");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КреслаКоляски") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.КреслаКоляски");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Духи");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.АльтернативныйТабак");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.УпакованнаяВода") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.УпакованнаяВода");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Антисептики") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Антисептики");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.БАДы");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.НикотиносодержащаяПродукция");
	ИначеЕсли ОсобенностьУчетаИлиВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ОсобенностиУчетаНоменклатуры.Пиво");
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ЭтоРозничныйДокумент(Контекст) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		Партнер = Контекст.Объект.Партнер;
		Возврат РозничныеПродажиВызовСервера.ПартнерЭтоРозничныйКлиент(Партнер);
	ИначеЕсли ТипЗнч(Контекст) = Тип("Структура") Тогда
		Партнер = Контекст.Партнер;
		Возврат РозничныеПродажиВызовСервера.ПартнерЭтоРозничныйКлиент(Партнер);
	Иначе
		Возврат ИнтеграцияИСВызовСервераУТ.ПартнерЭтоРозничныйКлиентВДокументе(Контекст);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВключитьПоддержкуВидовПродукцииИС(Контекст, ПараметрыСканирования, ВидПродукции) Экспорт
	
	Если ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ЧекККМ") Тогда
		
		ЗаполнитьПараметрыСканированияЧекККМ(ПараметрыСканирования, Контекст, ВидПродукции, Ложь);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ЧекККМКоррекции") Тогда
		
		ЗаполнитьПараметрыСканированияЧекККМКоррекции(ПараметрыСканирования, Контекст, ВидПродукции, Ложь);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ЧекККМВозврат") Тогда
		
		ЗаполнитьПараметрыСканированияЧекККМВозврат(ПараметрыСканирования, Контекст, ВидПродукции, Ложь);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ПриходныйКассовыйОрдер") Тогда
		
		ЗаполнитьПараметрыСканированияПриходныйКассовыйОрдер(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.РасходныйКассовыйОрдер") Тогда
		
		ЗаполнитьПараметрыСканированияРасходныйКассовыйОрдер(ПараметрыСканирования, Контекст, ВидПродукции);

	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ОперацияПоПлатежнойКарте") Тогда
		
		ЗаполнитьПараметрыСканированияОперацияПоПлатежнойКарте(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.РеализацияТоваровУслуг") Тогда
		
		ЗаполнитьПараметрыСканированияРеализацияТоваровУслуг(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.АктОРасхожденияхПослеОтгрузки") Тогда
		
		ЗаполнитьПараметрыСканированияАктОРасхожденияхПослеОтгрузки(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.КорректировкаПриобретения") Тогда
		
		ЗаполнитьПараметрыСканированияКорректировкаПриобретения(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ВозвратТоваровОтКлиента") Тогда
		
		ЗаполнитьПараметрыСканированияВозвратТоваровОтКлиента(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ПриобретениеТоваровУслуг") Тогда
		
		ЗаполнитьПараметрыСканированияПриобретениеТоваровУслуг(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ВозвратТоваровПоставщику") Тогда
		
		ЗаполнитьПараметрыСканированияВозвратТоваровПоставщику(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ПеремещениеТоваров") Тогда
		
		ЗаполнитьПараметрыСканированияПеремещениеТоваров(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.КорректировкаРеализации") Тогда
		
		ЗаполнитьПараметрыСканированияКорректировкаРеализации(ПараметрыСканирования, Контекст, ВидПродукции);
		
	ИначеЕсли ШтрихкодированиеИСКлиентСервер.ЭтоКонтекстОбъекта(Контекст, "Документ.ПрочееОприходованиеТоваров") Тогда
		
		ЗаполнитьПараметрыСканированияПрочееОприходованиеТоваров(ПараметрыСканирования, Контекст, ВидПродукции);
		
	КонецЕсли;
	
	ЗаполнитьБазовыеПараметрыСканирования(ПараметрыСканирования, Контекст);
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьПараметрыСканированияЧекККМКоррекции(ПараметрыСканирования, Контекст, ВидПродукции, ПроверкаКоличества = Неопределено)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ПараметрыСканирования.ОперацияКонтроляАкцизныхМарок = "Продажа";
	Если Не ЗначениеЗаполнено(ВидПродукции) Тогда
		ПараметрыСканирования.ДопустимыеВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПустаяСсылка"));
	КонецЕсли;
	
	#Область ПоддержкаАлкогольнойПродукции
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ИспользуетсяРегистрацияРозничныхПродажВЕГАИС")
		И ШтрихкодированиеЕГАИСКлиентСервер.ВключитьПоддержкуАлкогольнойПродукции(ПараметрыСканирования, ВидПродукции) Тогда
		
		ШтрихкодированиеЕГАИСКлиентСервер.ЗаполнитьБазовыеПараметрыСканирования(ПараметрыСканирования, Контекст);
		ПараметрыСканирования.ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыАкцизныхМарок.ВНаличии"));
		ПараметрыСканирования.ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыАкцизныхМарок.ПустаяСсылка"));
		
		ПараметрыСканирования.КонтрольАкцизныхМарок         = Истина;
		ПараметрыСканирования.Операция                      = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЧекККМ");
		ПараметрыСканирования.СоздаватьШтрихкодУпаковки     = Истина;
		
		Если ПроверкаКоличества = Истина Тогда
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Штрихкод");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Помещение");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("НоменклатураНабора");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("ХарактеристикаНабора");
		Иначе
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Цена");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("СтавкаНДС");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Штрихкод");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Продавец");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Помещение");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("НоменклатураНабора");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("ХарактеристикаНабора");
		КонецЕсли;
		
		ПараметрыСканирования.ОрганизацияЕГАИС = ИсточникДанных.ОрганизацияЕГАИС;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничнойПродажи(ПараметрыСканирования, ВидПродукции) Тогда
		
		ПараметрыСканирования.КонтролироватьОкончаниеСрокаГодности = Ложь;
		Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
			
			Реквизиты = Новый Структура("Склад", ИсточникДанных.Склад);
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсточникДанных, "Организация") Тогда
				Реквизиты.Вставить("Организация", ИсточникДанных.Организация);
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
			ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
			ЗаполнитьККТФФД12ЧекККМ(ПараметрыСканирования, Контекст);
			
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияЧекККМ(ПараметрыСканирования, Контекст, ВидПродукции, ПроверкаКоличества = Неопределено)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ПараметрыСканирования.ОперацияКонтроляАкцизныхМарок = "Продажа";
	ПараметрыСканирования.ИспользуютсяДанныеВыбораПоМаркируемойПродукции = Ложь;
	Если Не ЗначениеЗаполнено(ВидПродукции) Тогда
		ПараметрыСканирования.ДопустимыеВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПустаяСсылка"));
	КонецЕсли;
	
	#Область ПоддержкаАлкогольнойПродукции
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст,"ИспользуетсяРегистрацияРозничныхПродажВЕГАИС")
		И ШтрихкодированиеЕГАИСКлиентСервер.ВключитьПоддержкуАлкогольнойПродукции(ПараметрыСканирования, ВидПродукции) Тогда
		
		ШтрихкодированиеЕГАИСКлиентСервер.ЗаполнитьБазовыеПараметрыСканирования(ПараметрыСканирования, ИсточникДанных);
		ПараметрыСканирования.ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыАкцизныхМарок.ВНаличии"));
		ПараметрыСканирования.ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыАкцизныхМарок.ПустаяСсылка"));
		
		ПараметрыСканирования.КонтрольАкцизныхМарок              = Истина;
		ПараметрыСканирования.Операция                           = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЧекККМ");
		ПараметрыСканирования.СоздаватьШтрихкодУпаковки          = Истина;
		ПараметрыСканирования.РазрешенаОбработкаБезУказанияМарки = Истина;
		
		Если ПроверкаКоличества = Истина Тогда
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Штрихкод");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Помещение");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("НоменклатураНабора");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("ХарактеристикаНабора");
		Иначе
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Цена");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("СтавкаНДС");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Штрихкод");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Продавец");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Помещение");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("НоменклатураНабора");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("ХарактеристикаНабора");
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничнойПродажи(ПараметрыСканирования, ВидПродукции) Тогда
		
		Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
			
			Реквизиты = Новый Структура("Склад, ДатаДокумента", ИсточникДанных.Склад, ИсточникДанных.Дата);
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ИсточникДанных, "Организация") Тогда
				Реквизиты.Вставить("Организация", ИсточникДанных.Организация);
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
			ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
			ЗаполнитьККТФФД12ЧекККМ(ПараметрыСканирования, Контекст);
			
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияЧекККМВозврат(ПараметрыСканирования, Контекст, ВидПродукции, ПроверкаКоличества = Неопределено)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	ПараметрыСканирования.ОперацияКонтроляАкцизныхМарок = "Возврат";
	Если Не ЗначениеЗаполнено(ВидПродукции) Тогда
		ПараметрыСканирования.ДопустимыеВидыПродукции.Добавить(ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПустаяСсылка"));
	КонецЕсли;
	
	Если ШтрихкодированиеЕГАИСКлиентСервер.ВключитьПоддержкуАлкогольнойПродукции(ПараметрыСканирования, ВидПродукции) Тогда
		
		ШтрихкодированиеЕГАИСКлиентСервер.ЗаполнитьБазовыеПараметрыСканирования(ПараметрыСканирования, ИсточникДанных);
		ПараметрыСканирования.ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыАкцизныхМарок.Реализована"));
		ПараметрыСканирования.ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыАкцизныхМарок.ПустаяСсылка"));
		
		ПараметрыСканирования.КонтрольАкцизныхМарок              = Истина;
		ПараметрыСканирования.Операция                           = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЧекККМ");
		ПараметрыСканирования.СоздаватьШтрихкодУпаковки          = Истина;
		ПараметрыСканирования.РазрешенаОбработкаБезУказанияМарки = Истина;
		
		Если ПроверкаКоличества = Истина Тогда
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Штрихкод");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Помещение");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("НоменклатураНабора");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("ХарактеристикаНабора");
		Иначе
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Цена");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("СтавкаНДС");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Штрихкод");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Продавец");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("Помещение");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("НоменклатураНабора");
			ПараметрыСканирования.КлючевыеРеквизиты.Добавить("ХарактеристикаНабора");
		КонецЕсли;
		
	КонецЕсли;
	
	#Область ПоддержкаПродукцииИСМП
	
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничногоВозврата(ПараметрыСканирования, ВидПродукции) Тогда
		ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
		ЗаполнитьККТФФД12ЧекККМ(ПараметрыСканирования, Контекст);
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры


Процедура ЗаполнитьПараметрыСканированияПриходныйКассовыйОрдер(ПараметрыСканирования, Контекст, ВидПродукции)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
		
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничнойПродажи(ПараметрыСканирования, ВидПродукции) Тогда
		ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
		ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст);
	КонецЕсли;

	Реквизиты = Новый Структура("ДатаДокумента", ИсточникДанных.Дата);
	ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияРасходныйКассовыйОрдер(ПараметрыСканирования, Контекст, ВидПродукции)
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничногоВозврата(ПараметрыСканирования, ВидПродукции) Тогда
		ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
		ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст);
	КонецЕсли;

	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияОперацияПоПлатежнойКарте(ПараметрыСканирования, Контекст, ВидПродукции)
	
	Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		Возврат;
	КонецЕсли;
	
	#ЕСЛИ Клиент ТОГДА
		ПоступлениеОплатыОтКлиента = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента");
		ВозвратОплатыКлиенту = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВозвратОплатыКлиенту");
	#ИНАЧЕ
		ПоступлениеОплатыОтКлиента = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента;
		ВозвратОплатыКлиенту = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту;
	#КОНЕЦЕСЛИ
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	ВключенаПоддержкаИСМП = Ложь;

	Если Контекст.Объект.ХозяйственнаяОперация = ПоступлениеОплатыОтКлиента Тогда
		ВключенаПоддержкаИСМП = ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничнойПродажи(ПараметрыСканирования, ВидПродукции);
	ИначеЕсли Контекст.Объект.ХозяйственнаяОперация = ВозвратОплатыКлиенту Тогда
		ВключенаПоддержкаИСМП = ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничногоВозврата(ПараметрыСканирования, ВидПродукции);
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ВключенаПоддержкаИСМП Тогда
		ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст);
	КонецЕсли;

	Если Контекст.Объект.ХозяйственнаяОперация = ПоступлениеОплатыОтКлиента Тогда
		Реквизиты = Новый Структура("ДатаДокумента", Контекст.Объект.Дата);
		ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры


Процедура ЗаполнитьПараметрыСканированияРеализацияТоваровУслуг(ПараметрыСканирования, Контекст, ВидПродукции)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки         = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	Если ЭтоРозничныйДокумент(Контекст) Тогда
		
		Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничнойПродажи(ПараметрыСканирования, ВидПродукции) Тогда
			ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
			ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст);
		КонецЕсли;

	Иначе
		
		ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции);
		
	КонецЕсли;
	
	Реквизиты = Новый Структура("ДатаДокумента", ИсточникДанных.Дата);
	ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияАктОРасхожденияхПослеОтгрузки(ПараметрыСканирования, Контекст, ВидПродукции)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	Реквизиты = Новый Структура("ДатаДокумента", ИсточникДанных.Дата);
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции) Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияКорректировкаПриобретения(ПараметрыСканирования, Контекст, ВидПродукции)
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	Реквизиты = Новый Структура("ДатаДокумента", ИсточникДанных.Дата);
	Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции) Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыСканирования, Реквизиты);
	КонецЕсли;
	
	#КонецОбласти
	
	ПараметрыСканирования.СопоставлятьНоменклатуру  = Ложь;
	ПараметрыСканирования.СоздаватьШтрихкодУпаковки = Ложь;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияКорректировкаРеализации(ПараметрыСканирования, Контекст, ВидПродукции)
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	Если ЭтоРозничныйДокумент(Контекст) Тогда
		
		Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничнойПродажи(ПараметрыСканирования, ВидПродукции) Тогда
			ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст);
		КонецЕсли;

	Иначе
		
		ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции);
		
	КонецЕсли;
	
	НастройкиИСМП = Новый Структура;
	НастройкиИСМП.Вставить("КонтролироватьОкончаниеСрокаГодности", Ложь);
	НастройкиИСМП.Вставить("ЗапрашиватьДанныеСервисаИСМП",         Ложь);
	НастройкиИСМП.Вставить("ДопустимыПроверкиСеройЗоныМОТП",       Ложь);
	
	ЗаполнитьЗначенияСвойств(ПараметрыСканирования, НастройкиИСМП);

	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияВозвратТоваровОтКлиента(ПараметрыСканирования, Контекст, ВидПродукции)
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	Если ЭтоРозничныйДокумент(Контекст) Тогда
		
		Если ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаРозничногоВозврата(ПараметрыСканирования, ВидПродукции) Тогда
			ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст);
			ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст);
		КонецЕсли;
		
	Иначе
		
		ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовогоВозврата(ПараметрыСканирования, ВидПродукции);
		
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияПриобретениеТоваровУслуг(ПараметрыСканирования, Контекст, ВидПродукции)

	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаЗакупки(ПараметрыСканирования, ВидПродукции);

	#КонецОбласти
	
	ПараметрыСканирования.СопоставлятьНоменклатуру  = Ложь;
	ПараметрыСканирования.СоздаватьШтрихкодУпаковки = Ложь;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияВозвратТоваровПоставщику(ПараметрыСканирования, Контекст, ВидПродукции)
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукциИСМП
	
	ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции);
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияПеремещениеТоваров(ПараметрыСканирования, Контекст, ВидПродукции)
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукцииИСМП
	
	ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаОптовойОтгрузки(ПараметрыСканирования, ВидПродукции);
	
	#КонецОбласти
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыСканированияПрочееОприходованиеТоваров(ПараметрыСканирования, Контекст, ВидПродукции)
	
	#Область Метаданные
	
	ПараметрыСканирования.ИмяТабличнойЧастиШтрихкодыУпаковок = "ШтрихкодыУпаковок";
	ПараметрыСканирования.ИмяКолонкиШтрихкодУпаковки = "ШтрихкодУпаковки";
	
	#КонецОбласти
	
	#Область ПоддержкаПродукциИСМП
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект") Тогда
		ИсточникДанных = Контекст.Объект;
	Иначе
		ИсточникДанных = Контекст;
	КонецЕсли;
	
	Настройки = ШтрихкодированиеИСМПКлиентСервер.НастройкиПараметровСканированияДокументаПроизводства();
	Настройки.Организация = ИсточникДанных.Организация;
	
	ШтрихкодированиеИСМПКлиентСервер.ЗаполнитьПараметрыСканированияДокументаПроизводства(ПараметрыСканирования, ВидПродукции, Настройки);
	
	#КонецОбласти
	
КонецПроцедуры



Процедура ЗаполнитьБазовыеПараметрыСканирования(ПараметрыСканирования, Контекст)
	
	Если ШтрихкодированиеИСКлиентСервер.ДопустимВидПродукции(ПараметрыСканирования, ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак"))
		Или ШтрихкодированиеИСКлиентСервер.ДопустимВидПродукции(ПараметрыСканирования, ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак"))
		Или ШтрихкодированиеИСКлиентСервер.ДопустимВидПродукции(ПараметрыСканирования, ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция")) Тогда
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Объект")
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст.Объект, "Организация") Тогда
			ПараметрыСканирования.Организация = Контекст.Объект.Организация;
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "Организация") Тогда
			ПараметрыСканирования.Организация = Контекст.Организация;
		КонецЕсли;
		
		Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения")
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ПараметрыУказанияСерий") Тогда
			ПараметрыСканирования.ПараметрыУказанияСерий = Контекст.ПараметрыУказанияСерий;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыРеквизитовЧастичногоВыбытия(ПараметрыСканирования, Контекст)
	
	ПараметрыСканирования.ИмяКолонкиЧастичноеВыбытиеКоличество     = "ЧастичноеВыбытиеКоличество";
	ПараметрыСканирования.ИмяКолонкиЧастичноеВыбытиеВариантУчета   = "ЧастичноеВыбытиеВариантУчета";
	ПараметрыСканирования.ИмяКолонкиЧастичноеВыбытиеНоменклатура   = "ЧастичноеВыбытиеНоменклатура";
	ПараметрыСканирования.ИмяКолонкиЧастичноеВыбытиеХарактеристика = "ЧастичноеВыбытиеХарактеристика";
	
КонецПроцедуры

Процедура ЗаполнитьККТФФД12ЧекККМ(ПараметрыСканирования, Контекст)
	
	ПараметрыКассыККМ = Неопределено;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "ПараметрыКассыККМ") Тогда
		ПараметрыКассыККМ = Контекст.ПараметрыКассыККМ;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "КассаККМ") Тогда
		ПараметрыКассыККМ = РозничныеПродажиВызовСервера.ПараметрыКассыККМ(Контекст.КассаККМ);
	КонецЕсли;
	
	Если ПараметрыКассыККМ <> Неопределено Тогда
		
		ИдентификаторУстройства = ПараметрыКассыККМ.ИдентификаторУстройства;
		
		Если МенеджерОборудованияВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства) Тогда
			ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ   = Истина;
			ПараметрыСканирования.ККТФФД12ИСМП                     = ИдентификаторУстройства;
			Если ТипЗнч(Контекст) = Тип("Структура") Тогда
				Если Контекст.Свойство("Ссылка") Тогда
					СсылкаНаОбъект = Контекст.Ссылка;
				КонецЕсли;
			ИначеЕсли ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
				СсылкаНаОбъект = Контекст.Объект.Ссылка;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ПараметрыСканирования.СсылкаНаОбъект) Тогда
				ПараметрыСканирования.СсылкаНаОбъект = СсылкаНаОбъект;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьККТФФД12(ПараметрыСканирования, Контекст)
	
	Организация = Неопределено;
	
	Если ТипЗнч(Контекст) = Тип("Структура") Тогда
		Если Контекст.Свойство("Организация") Тогда
			Организация = Контекст.Организация;
		КонецЕсли;
		Если Контекст.Свойство("Ссылка") Тогда
			СсылкаНаОбъект = Контекст.Ссылка;
		КонецЕсли;
	ИначеЕсли ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		Организация = Контекст.Объект.Организация;
		СсылкаНаОбъект = Контекст.Объект.Ссылка;
	КонецЕсли;
	
	Если Организация <> Неопределено Тогда
		
		ОборудованиеПодключенноеПоОрганизации = ПодключаемоеОборудованиеУТВызовСервера.ОборудованиеПодключенноеПоОрганизации(Организация);
		СписокПодключенныхККТ = ОборудованиеПодключенноеПоОрганизации.ККТ;
		
		Если СписокПодключенныхККТ.Количество() = 1 Тогда
			
			ИдентификаторУстройства = СписокПодключенныхККТ[0];
			
			Если МенеджерОборудованияВызовСервера.ФискальноеУстройствоПоддерживаетПроверкуКодовМаркировки(ИдентификаторУстройства) Тогда
				ПараметрыСканирования.ТребуетсяПроверкаСредствамиККТ   = Истина;
				ПараметрыСканирования.ККТФФД12ИСМП                     = ИдентификаторУстройства;
				Если Не ЗначениеЗаполнено(ПараметрыСканирования.СсылкаНаОбъект) Тогда
					ПараметрыСканирования.СсылкаНаОбъект = СсылкаНаОбъект;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
