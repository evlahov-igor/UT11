////////////////////////////////////////////////////
//// Общий модуль "КР_ФорматыМагазиновИАналогиНоменклатуры"
//// Создан: 16.02.2023, Федоров Д.Е., КРОК, JIRA№ A2105505-775
//// Разработка по ФДР С11.004, Рабочее место "Подпитка торгового зала"

#Область ПрограммныйИнтерфейс

Функция ПолучитьАссортиментМагазина(Склады, ДатаСреза = Неопределено, ТолькоРазрешеныПродажи = Истина,
	ТолькоЗапрещеныПродажи = Ложь, ТаблицаДляАнализа = Неопределено) Экспорт
	
	// << 23.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2411 
	Если ДатаСреза = Неопределено Тогда 
		ДатаСреза = ТекущаяДатаСеанса();
	КонецЕсли;	
	// >> 23.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2411
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПолучитьАссортиментМагазина();
	ГруппаВидовНоменклатурыАксессуаров = КР_ДополнительныеНастройкиПовтИсп.Значение("ГруппаВидовНоменклатурыАксессуаров");
	
	Запрос.УстановитьПараметр("ИспользоватьТаблицуДляАнализа", 		ТаблицаДляАнализа <> Неопределено);
	Если ТаблицаДляАнализа = Неопределено Тогда
		ТаблицаДляАнализа = Новый ТаблицаЗначений;
		ТаблицаДляАнализа.Колонки.Добавить("Номенклатура",		
			Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТаблицаДляАнализа.Колонки.Добавить("Характеристика",	
			Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Склады",								Склады);
	Запрос.УстановитьПараметр("ДатаСреза", 							ДатаСреза);
	Запрос.УстановитьПараметр("ГруппаВидовНоменклатурыАксессуаров", ГруппаВидовНоменклатурыАксессуаров);
	// "ТолькоРазрешеныПродажи" и "ТолькоЗапрещеныПродажи" - взаимоисключающие
	Запрос.УстановитьПараметр("ТолькоРазрешеныПродажи", 			ТолькоРазрешеныПродажи);
	Запрос.УстановитьПараметр("ТолькоЗапрещеныПродажи", 			ТолькоЗапрещеныПродажи);
	Запрос.УстановитьПараметр("ТаблицаДляАнализа", 					ТаблицаДляАнализа);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выгрузка = РезультатЗапроса.Выгрузить();
	Возврат Выгрузка;
	
КонецФункции

Функция ТекстЗапросаПолучитьАссортиментМагазина() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика КАК Характеристика
	|Поместить ТаблицаДляАнализа
	|ИЗ
	|	&ТаблицаДляАнализа КАК Т
	|ГДЕ &ИспользоватьТаблицуДляАнализа
	|
	|;
	|
	|ВЫБРАТЬ
	|	ИсторияФорматовМагазинов.Склад КАК Склад,
	|	ИсторияФорматовМагазинов.КР_ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары,
	|	ИсторияФорматовМагазинов.ФорматМагазина КАК ФорматМагазина,
	|	ИсторияФорматовМагазинов.КонтролироватьАссортимент КАК КонтролироватьАссортимент,
	|	ИсторияФорматовМагазинов.РозничныеЦеныИзФорматаМагазина КАК РозничныеЦеныИзФорматаМагазина
	|ПОМЕСТИТЬ ДанныеФорматовМагазинов
	|ИЗ
	|	РегистрСведений.ИсторияИзмененияФорматовМагазинов.СрезПоследних(&ДатаСреза, Склад В (&Склады)) КАК ИсторияФорматовМагазинов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КР_РасширенныйАссортиментСрезПоследних.Номенклатура КАК Номенклатура,
	|	КР_РасширенныйАссортиментСрезПоследних.Номенклатура.ВидНоменклатуры.Родитель КАК ГруппаВидовНоменклатуры,
	|	КР_РасширенныйАссортиментСрезПоследних.Номенклатура.ВидНоменклатуры.Родитель = &ГруппаВидовНоменклатурыАксессуаров КАК ЭтоАксессуар,
	|	КР_РасширенныйАссортиментСрезПоследних.ОбъектПланирования КАК ОбъектПланирования,
	|	КР_РасширенныйАссортиментСрезПоследних.РольАссортимента КАК РольАссортимента,
	|	КР_РасширенныйАссортиментСрезПоследних.РазрешеныПеремещения КАК РазрешеныПеремещения,
	|	КР_РасширенныйАссортиментСрезПоследних.РазрешеныПродажи КАК РазрешеныПродажи,
	|	КР_РасширенныйАссортиментСрезПоследних.ОбъектПланирования ССЫЛКА Справочник.Склады КАК ОбъектПланированияЯвляетсяСкладом,
	|	КР_РасширенныйАссортиментСрезПоследних.Номенклатура.ВидНоменклатуры.Родитель КАК ВидНоменклатурыРодитель,
	|	КР_РасширенныйАссортиментСрезПоследних.Коллекция КАК Коллекция,
	|	КР_РасширенныйАссортиментСрезПоследних.Период КАК Период,
	|	КР_РасширенныйАссортиментСрезПоследних.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(КР_РасширенныйАссортиментСрезПоследних.Характеристика.КР_Размер, ЗНАЧЕНИЕ(Справочник.КР_Размеры.ПустаяСсылка)) КАК Размер
	|ПОМЕСТИТЬ ВТ_Ассортимент
	|ИЗ
	|	РегистрСведений.КР_РасширенныйАссортимент.СрезПоследних(
	|			&ДатаСреза,
	|			НЕ(&ИспользоватьТаблицуДляАнализа
	|						И НЕ (Номенклатура, Характеристика) В
	|								(ВЫБРАТЬ
	|									Т.Номенклатура,
	|									Т.Характеристика
	|								ИЗ
	|									ТаблицаДляАнализа КАК Т))
	|			И (&ИспользоватьТаблицуДляАнализа ИЛИ Характеристика <> Значение(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И
	|			ВЫБОР
	|				КОГДА ОбъектПланирования ССЫЛКА Справочник.Склады
	|					ТОГДА ОбъектПланирования В (&Склады)
	|				ИНАЧЕ ОбъектПланирования В
	|						(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ДанныеФорматовМагазинов.ФорматМагазина КАК ФорматМагазина
	|						ИЗ
	|							ДанныеФорматовМагазинов КАК ДанныеФорматовМагазинов
	|			
	|						ОБЪЕДИНИТЬ
	|			
	|						ВЫБРАТЬ
	|							ДанныеФорматовМагазинов.ФорматМагазинаАксессуары
	|						ИЗ
	|							ДанныеФорматовМагазинов КАК ДанныеФорматовМагазинов)
	|			КОНЕЦ) КАК КР_РасширенныйАссортиментСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеФорматовМагазинов.Склад КАК Склад,
	|	ДанныеФорматовМагазинов.ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары,
	|	ДанныеФорматовМагазинов.ФорматМагазина КАК ФорматМагазина,
	|	ДанныеФорматовМагазинов.КонтролироватьАссортимент КАК КонтролироватьАссортимент,
	|	ДанныеФорматовМагазинов.РозничныеЦеныИзФорматаМагазина КАК РозничныеЦеныИзФорматаМагазина,
	|	ВТ_Ассортимент.Номенклатура КАК Номенклатура,
	|	ВТ_Ассортимент.ОбъектПланирования КАК ОбъектПланирования,
	|	ВТ_Ассортимент.РольАссортимента КАК РольАссортимента,
	|	ВТ_Ассортимент.РазрешеныПеремещения КАК РазрешеныПеремещения,
	|	ВТ_Ассортимент.РазрешеныПродажи КАК РазрешеныПродажи,
	|	ВТ_Ассортимент.ВидНоменклатурыРодитель КАК ВидНоменклатурыРодитель,
	|	ВТ_Ассортимент.Коллекция КАК Коллекция,
	|	ВТ_Ассортимент.Период КАК Период,
	|	ВТ_Ассортимент.Характеристика КАК Характеристика,
	|	ВТ_Ассортимент.Размер КАК Размер
	|ПОМЕСТИТЬ АссортиментПерваяИтерацияПоискаФорматов
	|ИЗ
	|	ДанныеФорматовМагазинов КАК ДанныеФорматовМагазинов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Ассортимент КАК ВТ_Ассортимент
	|		ПО (ВТ_Ассортимент.ОбъектПланированияЯвляетсяСкладом)
	|			И ДанныеФорматовМагазинов.Склад = ВТ_Ассортимент.ОбъектПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеФорматовМагазинов.Склад КАК Склад,
	|	ДанныеФорматовМагазинов.ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары,
	|	ДанныеФорматовМагазинов.ФорматМагазина КАК ФорматМагазина,
	|	ДанныеФорматовМагазинов.КонтролироватьАссортимент КАК КонтролироватьАссортимент,
	|	ДанныеФорматовМагазинов.РозничныеЦеныИзФорматаМагазина КАК РозничныеЦеныИзФорматаМагазина,
	|	ВТ_Ассортимент.Номенклатура КАК Номенклатура,
	|	ВТ_Ассортимент.ОбъектПланирования КАК ОбъектПланирования,
	|	ВТ_Ассортимент.РольАссортимента КАК РольАссортимента,
	|	ВТ_Ассортимент.РазрешеныПеремещения КАК РазрешеныПеремещения,
	|	ВТ_Ассортимент.РазрешеныПродажи КАК РазрешеныПродажи,
	|	ВТ_Ассортимент.ВидНоменклатурыРодитель КАК ВидНоменклатурыРодитель,
	|	ВТ_Ассортимент.Коллекция КАК Коллекция,
	|	ВТ_Ассортимент.Период КАК Период,
	|	ВТ_Ассортимент.Характеристика КАК Характеристика,
	|	ВТ_Ассортимент.Размер КАК Размер
	|ПОМЕСТИТЬ АссортиментВтораяИтерацияПоискаФорматов
	|ИЗ
	|	ДанныеФорматовМагазинов КАК ДанныеФорматовМагазинов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Ассортимент КАК ВТ_Ассортимент
	|		ПО (НЕ ВТ_Ассортимент.ОбъектПланированияЯвляетсяСкладом)
	|			И (НЕ ВТ_Ассортимент.ЭтоАксессуар)
	|			И ДанныеФорматовМагазинов.ФорматМагазина = ВТ_Ассортимент.ОбъектПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеФорматовМагазинов.Склад КАК Склад,
	|	ДанныеФорматовМагазинов.ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары,
	|	ДанныеФорматовМагазинов.ФорматМагазина КАК ФорматМагазина,
	|	ДанныеФорматовМагазинов.КонтролироватьАссортимент КАК КонтролироватьАссортимент,
	|	ДанныеФорматовМагазинов.РозничныеЦеныИзФорматаМагазина КАК РозничныеЦеныИзФорматаМагазина,
	|	ВТ_Ассортимент.Номенклатура КАК Номенклатура,
	|	ВТ_Ассортимент.ОбъектПланирования КАК ОбъектПланирования,
	|	ВТ_Ассортимент.РольАссортимента КАК РольАссортимента,
	|	ВТ_Ассортимент.РазрешеныПеремещения КАК РазрешеныПеремещения,
	|	ВТ_Ассортимент.РазрешеныПродажи КАК РазрешеныПродажи,
	|	ВТ_Ассортимент.ВидНоменклатурыРодитель КАК ВидНоменклатурыРодитель,
	|	ВТ_Ассортимент.Коллекция КАК Коллекция,
	|	ВТ_Ассортимент.Период КАК Период,
	|	ВТ_Ассортимент.Характеристика КАК Характеристика,
	|	ВТ_Ассортимент.Размер КАК Размер
	|ПОМЕСТИТЬ АссортиментТретьяИтерацияПоискаФорматов
	|ИЗ
	|	ДанныеФорматовМагазинов КАК ДанныеФорматовМагазинов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Ассортимент КАК ВТ_Ассортимент
	|		ПО (НЕ ВТ_Ассортимент.ОбъектПланированияЯвляетсяСкладом)
	|			И (ВТ_Ассортимент.ЭтоАксессуар)
	|			И ДанныеФорматовМагазинов.ФорматМагазинаАксессуары = ВТ_Ассортимент.ОбъектПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АссортиментПерваяИтерацияПоискаФорматов.Склад КАК Склад,
	|	АссортиментПерваяИтерацияПоискаФорматов.ФорматМагазина КАК ФорматМагазина,
	|	АссортиментПерваяИтерацияПоискаФорматов.ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары,
	|	АссортиментПерваяИтерацияПоискаФорматов.КонтролироватьАссортимент КАК КонтролироватьАссортимент,
	|	АссортиментПерваяИтерацияПоискаФорматов.РозничныеЦеныИзФорматаМагазина КАК РозничныеЦеныИзФорматаМагазина,
	|	АссортиментПерваяИтерацияПоискаФорматов.Номенклатура КАК Номенклатура,
	|	АссортиментПерваяИтерацияПоискаФорматов.ОбъектПланирования КАК ОбъектПланирования,
	|	АссортиментПерваяИтерацияПоискаФорматов.РольАссортимента КАК РольАссортимента,
	|	АссортиментПерваяИтерацияПоискаФорматов.РазрешеныПеремещения КАК РазрешеныПеремещения,
	|	АссортиментПерваяИтерацияПоискаФорматов.РазрешеныПродажи КАК РазрешеныПродажи,
	|	АссортиментПерваяИтерацияПоискаФорматов.Коллекция КАК Коллекция,
	|	АссортиментПерваяИтерацияПоискаФорматов.Период КАК Период,
	|	1 КАК ИтерацияПоискаФорматов,
	|	АссортиментПерваяИтерацияПоискаФорматов.Характеристика КАК Характеристика,
	|	АссортиментПерваяИтерацияПоискаФорматов.Размер КАК Размер
	|ПОМЕСТИТЬ ИтогБезМаксПериода
	|ИЗ
	|	АссортиментПерваяИтерацияПоискаФорматов КАК АссортиментПерваяИтерацияПоискаФорматов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АссортиментВтораяИтерацияПоискаФорматов.Склад,
	|	АссортиментВтораяИтерацияПоискаФорматов.ФорматМагазина,
	|	АссортиментВтораяИтерацияПоискаФорматов.ФорматМагазинаАксессуары,
	|	АссортиментВтораяИтерацияПоискаФорматов.КонтролироватьАссортимент,
	|	АссортиментВтораяИтерацияПоискаФорматов.РозничныеЦеныИзФорматаМагазина,
	|	АссортиментВтораяИтерацияПоискаФорматов.Номенклатура,
	|	АссортиментВтораяИтерацияПоискаФорматов.ОбъектПланирования,
	|	АссортиментВтораяИтерацияПоискаФорматов.РольАссортимента,
	|	АссортиментВтораяИтерацияПоискаФорматов.РазрешеныПеремещения,
	|	АссортиментВтораяИтерацияПоискаФорматов.РазрешеныПродажи,
	|	АссортиментВтораяИтерацияПоискаФорматов.Коллекция,
	|	АссортиментВтораяИтерацияПоискаФорматов.Период,
	|	2,
	|	АссортиментВтораяИтерацияПоискаФорматов.Характеристика,
	|	АссортиментВтораяИтерацияПоискаФорматов.Размер
	|ИЗ
	|	АссортиментВтораяИтерацияПоискаФорматов КАК АссортиментВтораяИтерацияПоискаФорматов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АссортиментТретьяИтерацияПоискаФорматов.Склад,
	|	АссортиментТретьяИтерацияПоискаФорматов.ФорматМагазина,
	|	АссортиментТретьяИтерацияПоискаФорматов.ФорматМагазинаАксессуары,
	|	АссортиментТретьяИтерацияПоискаФорматов.КонтролироватьАссортимент,
	|	АссортиментТретьяИтерацияПоискаФорматов.РозничныеЦеныИзФорматаМагазина,
	|	АссортиментТретьяИтерацияПоискаФорматов.Номенклатура,
	|	АссортиментТретьяИтерацияПоискаФорматов.ОбъектПланирования,
	|	АссортиментТретьяИтерацияПоискаФорматов.РольАссортимента,
	|	АссортиментТретьяИтерацияПоискаФорматов.РазрешеныПеремещения,
	|	АссортиментТретьяИтерацияПоискаФорматов.РазрешеныПродажи,
	|	АссортиментТретьяИтерацияПоискаФорматов.Коллекция,
	|	АссортиментТретьяИтерацияПоискаФорматов.Период,
	|	3,
	|	АссортиментТретьяИтерацияПоискаФорматов.Характеристика,
	|	АссортиментТретьяИтерацияПоискаФорматов.Размер
	|ИЗ
	|	АссортиментТретьяИтерацияПоискаФорматов КАК АссортиментТретьяИтерацияПоискаФорматов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Склад,
	|	Номенклатура,
	|	Характеристика,
	|	Период,
	|	ИтерацияПоискаФорматов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтогБезМаксПериода.Склад КАК Склад,
	|	ИтогБезМаксПериода.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(ИтогБезМаксПериода.Период) КАК Период,
	|	ИтогБезМаксПериода.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ МаксПериод
	|ИЗ
	|	ИтогБезМаксПериода КАК ИтогБезМаксПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ИтогБезМаксПериода.Склад,
	|	ИтогБезМаксПериода.Номенклатура,
	|	ИтогБезМаксПериода.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Склад,
	|	Номенклатура,
	|	Характеристика,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтогБезМаксПериода.Склад КАК Склад,
	|	ИтогБезМаксПериода.ФорматМагазина КАК ФорматМагазина,
	|	ИтогБезМаксПериода.ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары,
	|	ИтогБезМаксПериода.КонтролироватьАссортимент КАК КонтролироватьАссортимент,
	|	ИтогБезМаксПериода.РозничныеЦеныИзФорматаМагазина КАК РозничныеЦеныИзФорматаМагазина,
	|	ИтогБезМаксПериода.Номенклатура КАК Номенклатура,
	|	ИтогБезМаксПериода.ОбъектПланирования КАК ОбъектПланирования,
	|	ИтогБезМаксПериода.РольАссортимента КАК РольАссортимента,
	|	ИтогБезМаксПериода.РазрешеныПеремещения КАК РазрешеныПеремещения,
	|	ИтогБезМаксПериода.РазрешеныПродажи КАК РазрешеныПродажи,
	|	ИтогБезМаксПериода.Коллекция КАК Коллекция,
	|	ИтогБезМаксПериода.Период КАК Период,
	// << 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	//|	ЕСТЬNULL(ИтогБезМаксПериода.Коллекция.КР_Приоритет, 0) КАК Приоритет,   
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КР_СоставГруппАналоговТоваров.Приоритет, 0) = 0
	|			ТОГДА 99
	|		ИНАЧЕ ЕСТЬNULL(КР_СоставГруппАналоговТоваров.Приоритет, 0)
	|	КОНЕЦ КАК Приоритет,
	|	ЕСТЬNULL(КР_СоставГруппАналоговТоваров.ГруппаАналоговТоваров, ЗНАЧЕНИЕ(Справочник.КР_ГруппыАналоговТоваров.ПустаяСсылка)) КАК ГруппаАналогов,
	// >> 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|	ИтогБезМаксПериода.Характеристика КАК Характеристика,
	|	ИтогБезМаксПериода.Размер КАК Размер
	// << 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	//|ПОМЕСТИТЬ ИтогБезГруппАналогов
	|ПОМЕСТИТЬ СГруппамиАналогов
	// >> 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|ИЗ
	|	ИтогБезМаксПериода КАК ИтогБезМаксПериода
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксПериод КАК МаксПериод
	|		ПО ИтогБезМаксПериода.Склад = МаксПериод.Склад
	|			И ИтогБезМаксПериода.Номенклатура = МаксПериод.Номенклатура
	|			И ИтогБезМаксПериода.Период = МаксПериод.Период
	|			И ИтогБезМаксПериода.Характеристика = МаксПериод.Характеристика
	// << 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КР_СоставГруппАналоговТоваров КАК КР_СоставГруппАналоговТоваров
	|		ПО ИтогБезМаксПериода.Номенклатура = КР_СоставГруппАналоговТоваров.Номенклатура
	// >> 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|ГДЕ
	// Если &ТолькоРазрешеныПродажи = Истина, то отбираются только строки с РазрешеныПродажи = Истина,
	// иначе отбор на РазрешеныПродажи не накладывается
	|	НЕ(&ТолькоРазрешеныПродажи
	|				И НЕ ИтогБезМаксПериода.РазрешеныПродажи)
	// Если &ТолькоЗапрещеныПродажи = Истина, то отбираются только строки с ТолькоЗапрещеныПродажи = Ложь,
	|	И НЕ(&ТолькоЗапрещеныПродажи
	|				И ИтогБезМаксПериода.РазрешеныПродажи)
	|
	// << 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	//|ИНДЕКСИРОВАТЬ ПО
	//|	Номенклатура
	|ИНДЕКСИРОВАТЬ ПО
	|	ГруппаАналогов,
	|	Приоритет
	//|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	ИтогСАналогами.Склад КАК Склад,
	//|	ИтогСАналогами.ФорматМагазина КАК ФорматМагазина,
	//|	ИтогСАналогами.ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары,
	//|	ИтогСАналогами.КонтролироватьАссортимент КАК КонтролироватьАссортимент,
	//|	ИтогСАналогами.РозничныеЦеныИзФорматаМагазина КАК РозничныеЦеныИзФорматаМагазина,
	//|	ИтогСАналогами.Номенклатура КАК Номенклатура,
	//|	ИтогСАналогами.ОбъектПланирования КАК ОбъектПланирования,
	//|	ИтогСАналогами.РольАссортимента КАК РольАссортимента,
	//|	ИтогСАналогами.РазрешеныПеремещения КАК РазрешеныПеремещения,
	//|	ИтогСАналогами.РазрешеныПродажи КАК РазрешеныПродажи,
	//|	ИтогСАналогами.Период КАК Период,
	//|	ВЫБОР
	//|		КОГДА ИтогСАналогами.Приоритет = 0
	//|			ТОГДА 99
	//|		ИНАЧЕ ИтогСАналогами.Приоритет
	//|	КОНЕЦ КАК Приоритет,
	//|	ЕСТЬNULL(КР_СоставГруппАналоговТоваров.ГруппаАналоговТоваров, ЗНАЧЕНИЕ(Справочник.КР_ГруппыАналоговТоваров.ПустаяСсылка)) КАК ГруппаАналогов,
	//|	ИтогСАналогами.Характеристика КАК Характеристика,
	//|	ИтогСАналогами.Размер КАК Размер
	//|ПОМЕСТИТЬ СГруппамиАналогов
	//|ИЗ
	//|	ИтогБезГруппАналогов КАК ИтогСАналогами
	//|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КР_СоставГруппАналоговТоваров КАК КР_СоставГруппАналоговТоваров
	//|		ПО ИтогСАналогами.Номенклатура = КР_СоставГруппАналоговТоваров.Номенклатура
	//|
	//|ИНДЕКСИРОВАТЬ ПО
	//|	ГруппаАналогов,
	//|	Приоритет
	// >> 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СГруппамиАналогов.ГруппаАналогов КАК ГруппаАналогов,
	|	МИНИМУМ(СГруппамиАналогов.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ МаксимальныйПриоритет
	|ИЗ
	|	СГруппамиАналогов КАК СГруппамиАналогов
	|
	|СГРУППИРОВАТЬ ПО
	|	СГруппамиАналогов.ГруппаАналогов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГруппаАналогов,
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СГруппамиАналогов.Склад КАК Склад,
	|	СГруппамиАналогов.ФорматМагазина КАК ФорматМагазина,
	|	СГруппамиАналогов.ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары,
	|	СГруппамиАналогов.КонтролироватьАссортимент КАК КонтролироватьАссортимент,
	|	СГруппамиАналогов.РозничныеЦеныИзФорматаМагазина КАК РозничныеЦеныИзФорматаМагазина,
	|	СГруппамиАналогов.Номенклатура КАК Номенклатура,
	|	СГруппамиАналогов.Номенклатура.Артикул КАК Артикул,
	|	СГруппамиАналогов.ОбъектПланирования КАК ОбъектПланирования,
	|	СГруппамиАналогов.РольАссортимента КАК РольАссортимента,
	|	СГруппамиАналогов.РазрешеныПеремещения КАК РазрешеныПеремещения,
	|	СГруппамиАналогов.РазрешеныПродажи КАК РазрешеныПродажи,
	|	СГруппамиАналогов.Период КАК Период,
	|	СГруппамиАналогов.Приоритет КАК Приоритет,
	|	СГруппамиАналогов.ГруппаАналогов КАК ГруппаАналогов,
	|	СГруппамиАналогов.ГруппаАналогов <> ЗНАЧЕНИЕ(Справочник.КР_ГруппыАналоговТоваров.ПустаяСсылка)
	|		И МаксимальныйПриоритет.Приоритет ЕСТЬ NULL КАК ЭтоАналог,
	|	СГруппамиАналогов.Характеристика КАК Характеристика,
	|	СГруппамиАналогов.Размер КАК Размер,
	|	СГруппамиАналогов.Номенклатура.ВидНоменклатуры.Родитель = &ГруппаВидовНоменклатурыАксессуаров КАК ЭтоАксессуар,
	|	ИСТИНА КАК ДляОтладки
	|ИЗ
	|	СГруппамиАналогов КАК СГруппамиАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ МаксимальныйПриоритет КАК МаксимальныйПриоритет
	|		ПО СГруппамиАналогов.ГруппаАналогов = МаксимальныйПриоритет.ГруппаАналогов
	|			И СГруппамиАналогов.Приоритет = МаксимальныйПриоритет.Приоритет";
	
КонецФункции

Процедура ПолучитьОсновныеДанные(Дерево, Магазины, Дата = Неопределено, ТолькоТребующиеДействий = Ложь,
	ИспользоватьВсеОстатки = Ложь     
	// << 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	, ИсключитьСкладскиеЯчейкиПриемки = Истина    
	// >> 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	) Экспорт
	
	Если Дерево = Неопределено Тогда
		ИнциализироватьДерево(Дерево);
	КонецЕсли;
	
	Если ТипЗнч(Магазины) = Тип("СправочникСсылка.Склады") Тогда
		МагазиныОтбор = Новый Массив;
		МагазиныОтбор.Добавить(Магазины);
	Иначе
		МагазиныОтбор = Магазины;
	КонецЕсли;
	
	Если Дата = Неопределено Тогда  
		// << 23.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2411
		//Дата = '00010101';                                      
		Дата = ТекущаяДатаСеанса();                                      
		// >> 23.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2411
	КонецЕсли;
	
	АссортиментМагазина = ПолучитьАссортиментМагазина(МагазиныОтбор, Дата, Истина);
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблицДляОбхода = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблицДляОбхода;
	Запрос.Текст = ТекстЗапросаЭталоннаяПрезентация();
	
	// << 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068   
	ИсключитьТипСкладскойЯчейки = Новый Массив;
	ИсключитьТипСкладскойЯчейки.Добавить(Перечисления.ТипыСкладскихЯчеек.Отгрузка);
	// << 27.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2427
	ИсключитьТипСкладскойЯчейки.Добавить(Перечисления.ТипыСкладскихЯчеек.Архив); 
	// >> 27.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2427
	Если ИсключитьСкладскиеЯчейкиПриемки Тогда 
		ИсключитьТипСкладскойЯчейки.Добавить(Перечисления.ТипыСкладскихЯчеек.Приемка);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИсключитьТипСкладскойЯчейки", ИсключитьТипСкладскойЯчейки);
	// >> 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	Запрос.УстановитьПараметр("Магазины", МагазиныОтбор);
	Запрос.УстановитьПараметр("Дата", Дата);
	ГруппаВидовНоменклатурыАксессуаров = КР_ДополнительныеНастройкиПовтИсп.Значение("ГруппаВидовНоменклатурыАксессуаров");
	Запрос.УстановитьПараметр("ГруппаВидовНоменклатурыАксессуаров", ГруппаВидовНоменклатурыАксессуаров);
	Запрос.УстановитьПараметр("АссортиментМагазина", АссортиментМагазина);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выгрузка = РезультатЗапроса.Выгрузить();
	Иначе
		Возврат;
	КонецЕсли;
	
	ТаблицаОстатков = ЗаполнитьПотребностиПоАналогамНоменклатуры(Выгрузка, Дата, МенеджерВременныхТаблицДляОбхода
		,
		// << 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
		, ИсключитьТипСкладскойЯчейки    
		// >> 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблицДляОбхода;
	Запрос.Текст = ТекстЗапросаОбходаРезультата();
	РезультатЗапроса = Запрос.Выполнить();
	ЗаполнитьДанныеОбработки(Дерево, РезультатЗапроса, ТаблицаОстатков, ТолькоТребующиеДействий);
	
	Если ИспользоватьВсеОстатки Тогда
		// Распределять остатки для обработки "Подсортировка торгового зала" никогда не требуется,
		// но в режиме работы с отчетом "Соблюдение товарного ассортимента" - есть цель видить все остатки
		РаспределитьОстаткиПоСтрокам(Дерево, ТаблицаОстатков);
	КонецЕсли;
	
КонецПроцедуры

Процедура РаспределитьОстаткиПоСтрокам(Дерево, ТаблицаОстатков)

	Для Каждого Строка Из ТаблицаОстатков Цикл
		
		Если Строка.ОстатокПоСкладу <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПоискаОстатков = Новый Структура;
		ПараметрыПоискаОстатков.Вставить("Номенклатура", 		Строка.Номенклатура);
		ПараметрыПоискаОстатков.Вставить("Характеристика",		Строка.Характеристика);
		ПараметрыПоискаОстатков.Вставить("Магазин",				Строка.Магазин);
		МассивСтрок = Дерево.Строки.НайтиСтроки(ПараметрыПоискаОстатков, Ложь);
		Если МассивСтрок.Количество() > 0 Тогда
			ПерваяСтрока = МассивСтрок[0];
			// Смысл этого действия в том, что для отчета "Соблюдение товарного ассортимента"
			// реквизит "На складе" заполяется на основе данного реквизита. И в случае, когда остатки позиции
			// распределены на аналоги не полностью, не потеряться остаток.
			// В приоритете распредеяем оставшееся количество для Эталона (1 приоритет).
			ПерваяСтрока.ПереместитьВЗал = ПерваяСтрока.ПереместитьВЗал + Строка.ОстатокПоСкладу;
			Строка.ОстатокПоСкладу = 0;
		Иначе
			
			// В редком случае, когда строка не найдена на первом уровне дерева (эталонном), произведем полный поиск
			// для того, чтобы добавить недостаяющие количество к позиции уже в качестве аналога (2 приоритет).
			МассивСтрок = Дерево.Строки.НайтиСтроки(ПараметрыПоискаОстатков, Истина);
			Если МассивСтрок.Количество() > 0 Тогда
				ПерваяСтрока = МассивСтрок[0];
				ПерваяСтрока.ПереместитьВЗал = ПерваяСтрока.ПереместитьВЗал + Строка.ОстатокПоСкладу;
				Строка.ОстатокПоСкладу = 0;
			КонецЕсли;

		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#Область ТекстЗапросов

// Тексты запросов вынесены в экспортные функции, для удобства отладки
// Например в обработке "КР_ПодсортировкаТорговогоЗала" на странице для отладки на основе них
// строится результирующий запрос

Функция ТекстЗапросаЭталоннаяПрезентация() Экспорт

	Возврат
	"ВЫБРАТЬ
	|	АссортиментМагазина.Склад КАК Магазин,
	|	АссортиментМагазина.ФорматМагазина КАК Формат,
	|	АссортиментМагазина.ФорматМагазинаАксессуары КАК ФорматАксессуары,
	|	АссортиментМагазина.ЭтоАксессуар КАК ЭтоАксессуар,
	|	ВЫБОР КОГДА АссортиментМагазина.ЭтоАксессуар ТОГДА
	|		АссортиментМагазина.ФорматМагазинаАксессуары
	|	ИНАЧЕ
	|		АссортиментМагазина.ФорматМагазина
	|	КОНЕЦ КАК ОсновнойФорматТекущейНоменклатуры,
	|	АссортиментМагазина.Номенклатура КАК Номенклатура,
	|	АссортиментМагазина.ГруппаАналогов КАК ГруппаАналогов,
	|	АссортиментМагазина.ЭтоАналог КАК ЭтоАналог,
	|	АссортиментМагазина.Характеристика КАК Характеристика,
	|	АссортиментМагазина.Размер КАК Размер
	|ПОМЕСТИТЬ АссортиментБезРазмеровИСАналогами
	|ИЗ
	|	&АссортиментМагазина КАК АссортиментМагазина
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Размер,
	|	Формат
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АссортиментБезРазмеровИСАналогами.Магазин КАК Магазин,
	|	АссортиментБезРазмеровИСАналогами.Формат КАК Формат,
	|	АссортиментБезРазмеровИСАналогами.ФорматАксессуары КАК ФорматАксессуары,
	|	АссортиментБезРазмеровИСАналогами.ОсновнойФорматТекущейНоменклатуры,
	|	АссортиментБезРазмеровИСАналогами.Номенклатура КАК Номенклатура,
	|	АссортиментБезРазмеровИСАналогами.ГруппаАналогов КАК ГруппаАналогов,
	|	АссортиментБезРазмеровИСАналогами.ЭтоАксессуар КАК ЭтоАксессуар,
	|	АссортиментБезРазмеровИСАналогами.ЭтоАналог КАК ЭтоАналог,
	|	АссортиментБезРазмеровИСАналогами.Характеристика КАК Характеристика,
	|	АссортиментБезРазмеровИСАналогами.Размер КАК Размер
	|ПОМЕСТИТЬ Ассортимент
	|ИЗ
	|	АссортиментБезРазмеровИСАналогами КАК АссортиментБезРазмеровИСАналогами
	|ГДЕ
	|	Не АссортиментБезРазмеровИСАналогами.ЭтоАналог
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Формат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КР_МинимальнаяПрезентацияСрезПоследних.Формат КАК Формат,
	|	КР_МинимальнаяПрезентацияСрезПоследних.Характеристика КАК Характеристика,
	|	КР_МинимальнаяПрезентацияСрезПоследних.МинимальнаяПрезентация КАК МинимальнаяПрезентация,
	|	КР_МинимальнаяПрезентацияСрезПоследних.Номенклатура.ВидНоменклатуры.Родитель КАК ТоварнаяГруппа,
	|	КР_МинимальнаяПрезентацияСрезПоследних.Номенклатура КАК Номенклатура,
	|	КР_МинимальнаяПрезентацияСрезПоследних.Номенклатура.ВидНоменклатуры.Родитель = &ГруппаВидовНоменклатурыАксессуаров КАК ЭтоАксессуар
	|ПОМЕСТИТЬ ВТ_МинимальнаяПрезентация
	|ИЗ
	|	РегистрСведений.КР_МинимальнаяПрезентация.СрезПоследних(
	|			&Дата,
	|			(Номенклатура, Характеристика, Формат) В
	|				(ВЫБРАТЬ
	|					Ассортимент.Номенклатура КАК Номенклатура,
	|					Ассортимент.Характеристика КАК Характеристика,
	|					Ассортимент.ОсновнойФорматТекущейНоменклатуры КАК Формат
	|				ИЗ
	|					Ассортимент КАК Ассортимент)) КАК КР_МинимальнаяПрезентацияСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Формат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ассортимент.Номенклатура КАК Номенклатура,
	|	Ассортимент.Магазин КАК Магазин,
	|	Ассортимент.Размер КАК Размер,
	|	Ассортимент.Характеристика КАК Характеристика,
	|	Ассортимент.ЭтоАксессуар КАК ЭтоАксессуар,
	|	ISNULL(ВТ_МинимальнаяПрезентация.МинимальнаяПрезентация, 1) КАК МинимальнаяПрезентация,
	|	Ассортимент.Формат КАК Формат,
	|	Ассортимент.ФорматАксессуары КАК ФорматАксессуары,
	|	Ассортимент.ОсновнойФорматТекущейНоменклатуры КАК ОсновнойФорматТекущейНоменклатуры,
	|	Не ВТ_МинимальнаяПрезентация.МинимальнаяПрезентация ЕСТЬ NULL КАК ЕстьМинимальнаяПрезентация
	|ПОМЕСТИТЬ МинПрезентацияИАссортимент
	|ИЗ
	|	Ассортимент КАК Ассортимент
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МинимальнаяПрезентация КАК ВТ_МинимальнаяПрезентация
	|		ПО Ассортимент.Номенклатура = ВТ_МинимальнаяПрезентация.Номенклатура
	|			И Ассортимент.Характеристика = ВТ_МинимальнаяПрезентация.Характеристика
	|			И Ассортимент.ОсновнойФорматТекущейНоменклатуры = ВТ_МинимальнаяПрезентация.Формат
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МинПрезентацияИАссортимент.Номенклатура КАК Номенклатура,
	|	МинПрезентацияИАссортимент.Магазин КАК Магазин,
	|	МинПрезентацияИАссортимент.Размер КАК Размер,
	|	МинПрезентацияИАссортимент.Характеристика КАК Характеристика,
	|	МинПрезентацияИАссортимент.МинимальнаяПрезентация КАК МинимальнаяПрезентация,
	|	МинПрезентацияИАссортимент.Формат КАК Формат,
	|	МинПрезентацияИАссортимент.ФорматАксессуары КАК ФорматАксессуары,
	|	МинПрезентацияИАссортимент.ЭтоАксессуар КАК ЭтоАксессуар,
	|	МинПрезентацияИАссортимент.ОсновнойФорматТекущейНоменклатуры КАК ОсновнойФорматТекущейНоменклатуры,
	|	ЕСТЬNULL(КР_СоставГруппАналоговТоваров.ГруппаАналоговТоваров, ЗНАЧЕНИЕ(Справочник.КР_ГруппыАналоговТоваров.ПустаяСсылка)) КАК ГруппаАналоговТоваров,
	|	МинПрезентацияИАссортимент.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация
	|ПОМЕСТИТЬ МинПрезентацияИАссортиментИАналоги
	|ИЗ
	|	МинПрезентацияИАссортимент КАК МинПрезентацияИАссортимент
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КР_СоставГруппАналоговТоваров КАК КР_СоставГруппАналоговТоваров
	|		ПО МинПрезентацияИАссортимент.Номенклатура = КР_СоставГруппАналоговТоваров.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Формат,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
    // << 11.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-2254
	|	ТО.Магазин КАК Магазин,
	// >> 11.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-2254
	|	КР_УстановкаМестРазмещенияСрезПоследних.Номенклатура КАК Номенклатура,
	|	КР_УстановкаМестРазмещенияСрезПоследних.Формат КАК Формат,
	|	КР_УстановкаМестРазмещенияСрезПоследних.МестаРазмещения КАК МестаРазмещения
	|ПОМЕСТИТЬ УстановкаМестРазмещения
	|ИЗ
	|	РегистрСведений.КР_УстановкаМестРазмещения.СрезПоследних(   
	// << 23.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2411
	//|			,
	|			&Дата,
	// >> 23.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2411
	|			(Номенклатура, Формат) В
	|				(ВЫБРАТЬ
	|					МинПрезентацияИАссортимент.Номенклатура КАК Номенклатура,
	|					МинПрезентацияИАссортимент.ОсновнойФорматТекущейНоменклатуры КАК Формат
	|				ИЗ
	|					МинПрезентацияИАссортимент КАК МинПрезентацияИАссортимент)) КАК КР_УстановкаМестРазмещенияСрезПоследних
    // << 11.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-2254
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КР_ТорговоеОборудованиеМагазинов КАК ТО
	|		ПО КР_УстановкаМестРазмещенияСрезПоследних.МестаРазмещения = ТО.ТорговоеОборудование
	// >> 11.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-2254
	|ИНДЕКСИРОВАТЬ ПО
	|	Формат,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МинПрезентацияИАссортиментИАналоги.Номенклатура КАК Номенклатура,
	|	МинПрезентацияИАссортиментИАналоги.Магазин КАК Магазин,
	|	МинПрезентацияИАссортиментИАналоги.Размер КАК Размер,
	|	МинПрезентацияИАссортиментИАналоги.Характеристика КАК Характеристика,
	|	МинПрезентацияИАссортиментИАналоги.МинимальнаяПрезентация КАК МинимальнаяПрезентация,
	|	МинПрезентацияИАссортиментИАналоги.ОсновнойФорматТекущейНоменклатуры КАК ОсновнойФорматТекущейНоменклатуры,
	|	МинПрезентацияИАссортиментИАналоги.ЭтоАксессуар КАК ЭтоАксессуар,
	|	МинПрезентацияИАссортиментИАналоги.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров,
	|	ЕСТЬNULL(УстановкаМестРазмещения.МестаРазмещения, ЗНАЧЕНИЕ(Справочник.КР_МестаРазмещения.Роллы)) КАК МестаРазмещения,
	|	МинПрезентацияИАссортиментИАналоги.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация
	|ПОМЕСТИТЬ ЭталоннаяПрезентация
	|ИЗ
	|	МинПрезентацияИАссортиментИАналоги КАК МинПрезентацияИАссортиментИАналоги
	|		ЛЕВОЕ СОЕДИНЕНИЕ УстановкаМестРазмещения КАК УстановкаМестРазмещения
	|		ПО МинПрезентацияИАссортиментИАналоги.ОсновнойФорматТекущейНоменклатуры = УстановкаМестРазмещения.Формат
	|			И МинПрезентацияИАссортиментИАналоги.Номенклатура = УстановкаМестРазмещения.Номенклатура
    // << 11.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-2254
	|			И МинПрезентацияИАссортиментИАналоги.Магазин = УстановкаМестРазмещения.Магазин
	// >> 11.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-2254
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГруппаАналоговТоваров,
	|	Размер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭталоннаяПрезентация.Магазин КАК Магазин,
	|	ЭталоннаяПрезентация.Номенклатура КАК Номенклатура,
	|	ЭталоннаяПрезентация.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров,
	|	МАКСИМУМ(ЭталоннаяПрезентация.Размер) КАК Размер
	|ПОМЕСТИТЬ МаксимальныйРазмерДляДобавленияНового
	|ИЗ
	|	ЭталоннаяПрезентация КАК ЭталоннаяПрезентация
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭталоннаяПрезентация.Магазин,
	|	ЭталоннаяПрезентация.Номенклатура,
	|	ЭталоннаяПрезентация.ГруппаАналоговТоваров
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГруппаАналоговТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АссортиментБезРазмеровИСАналогами.Магазин КАК Магазин,
	|	АссортиментБезРазмеровИСАналогами.Номенклатура КАК Номенклатура,
	|	АссортиментБезРазмеровИСАналогами.ГруппаАналогов КАК ГруппаАналоговТоваров,
	|	АссортиментБезРазмеровИСАналогами.Размер КАК Размер,
	|	АссортиментБезРазмеровИСАналогами.ЭтоАналог
	|ПОМЕСТИТЬ ДопРазмеры
	|ИЗ
	|	АссортиментБезРазмеровИСАналогами КАК АссортиментБезРазмеровИСАналогами
	|ГДЕ
	|	(АссортиментБезРазмеровИСАналогами.Магазин, АссортиментБезРазмеровИСАналогами.ГруппаАналогов) В
	|			(ВЫБРАТЬ
	|				ЭталоннаяПрезентация.Магазин КАК Магазин,
	|				ЭталоннаяПрезентация.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров
	|			ИЗ
	|				ЭталоннаяПрезентация КАК ЭталоннаяПрезентация)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДопРазмеры.Магазин КАК Магазин,
	|	ДопРазмеры.Номенклатура КАК Номенклатура,
	|	ДопРазмеры.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров,
	|	ДопРазмеры.Размер КАК Размер,
	|	МаксимальныйРазмерДляДобавленияНового.Размер КАК РазмерДляДобавленияНового,
	|	МаксимальныйРазмерДляДобавленияНового.Номенклатура КАК НоменклатураДляДобавленияНового
	|ПОМЕСТИТЬ ДопРазмерыБезОтсечения
	|ИЗ
	|	ДопРазмеры КАК ДопРазмеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЭталоннаяПрезентация КАК ЭталоннаяПрезентация
	|		ПО ДопРазмеры.Магазин = ЭталоннаяПрезентация.Магазин
	|			И ДопРазмеры.ГруппаАналоговТоваров = ЭталоннаяПрезентация.ГруппаАналоговТоваров
	|			И ДопРазмеры.Размер = ЭталоннаяПрезентация.Размер
	|		ЛЕВОЕ СОЕДИНЕНИЕ МаксимальныйРазмерДляДобавленияНового КАК МаксимальныйРазмерДляДобавленияНового
	|		ПО ДопРазмеры.Магазин = МаксимальныйРазмерДляДобавленияНового.Магазин
	|			И ДопРазмеры.ГруппаАналоговТоваров = МаксимальныйРазмерДляДобавленияНового.ГруппаАналоговТоваров
	|ГДЕ
	|	ЭталоннаяПрезентация.Размер ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НоменклатураДляДобавленияНового,
	|	Размер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// В данном запросе отсекаются строки с размерами, которые есть у Эталона (их может не быть в ассортименте)
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДопРазмерыБезОтсечения.Номенклатура КАК Номенклатура,
	|	ДопРазмерыБезОтсечения.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров,
	|	ДопРазмерыБезОтсечения.Размер КАК Размер,
	|	ДопРазмерыБезОтсечения.РазмерДляДобавленияНового КАК РазмерДляДобавленияНового,
	|	ДопРазмерыБезОтсечения.НоменклатураДляДобавленияНового КАК НоменклатураДляДобавленияНового
	|ПОМЕСТИТЬ ДопРазмерыИтог
	|ИЗ
	|	ДопРазмерыБезОтсечения КАК ДопРазмерыБезОтсечения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК Характеристики
	|			ПО Характеристики.Владелец = ДопРазмерыБезОтсечения.НоменклатураДляДобавленияНового
	|			И Характеристики.КР_Размер = ДопРазмерыБезОтсечения.Размер
	|			И НЕ Характеристики.ПометкаУдаления
	|
	|ГДЕ
	|	Характеристики.КР_Размер ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГруппаАналоговТоваров,
	|	Номенклатура,
	|	Размер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭталоннаяПрезентация.Номенклатура КАК Номенклатура,
	|	ЭталоннаяПрезентация.Магазин КАК Магазин,
	|	ЭталоннаяПрезентация.Размер КАК Размер,
	|	ISNULL(ДопРазмерыИтог.Размер, ЗНАЧЕНИЕ(Справочник.КР_Размеры.ПустаяСсылка)) КАК ДобавленныйРазмер,
	// Отображение доп. размера
	|	ISNULL(ДопРазмерыИтог.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК НоменклатураДобавленногоРазмера,
	|	ЭталоннаяПрезентация.ОсновнойФорматТекущейНоменклатуры КАК ОсновнойФорматТекущейНоменклатуры,
	|	ЭталоннаяПрезентация.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров,
	|	ЭталоннаяПрезентация.МестаРазмещения КАК МестаРазмещения,
	|	ЭталоннаяПрезентация.МинимальнаяПрезентация КАК МинимальнаяПрезентация,
	|	ЭталоннаяПрезентация.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация
	|ПОМЕСТИТЬ ЭталоннаяПрезентацияИДопРазмер
	|ИЗ
	|	ЭталоннаяПрезентация КАК ЭталоннаяПрезентация
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДопРазмерыИтог КАК ДопРазмерыИтог
	|		ПО (ЭталоннаяПрезентация.ГруппаАналоговТоваров <> ЗНАЧЕНИЕ(Справочник.КР_ГруппыАналоговТоваров.ПустаяСсылка))
	|			И ЭталоннаяПрезентация.ГруппаАналоговТоваров = ДопРазмерыИтог.ГруппаАналоговТоваров
	|			И ЭталоннаяПрезентация.Номенклатура = ДопРазмерыИтог.НоменклатураДляДобавленияНового
	|			И ЭталоннаяПрезентация.Размер = ДопРазмерыИтог.РазмерДляДобавленияНового
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЭталоннаяПрезентацияИДопРазмер.Номенклатура КАК Номенклатура,
	|	ЭталоннаяПрезентацияИДопРазмер.Магазин КАК Магазин,
	|	ЭталоннаяПрезентацияИДопРазмер.Размер КАК Размер,
	|	ЭталоннаяПрезентацияИДопРазмер.ОсновнойФорматТекущейНоменклатуры КАК ОсновнойФорматТекущейНоменклатуры,
	|	ЭталоннаяПрезентацияИДопРазмер.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров,
	|	ЭталоннаяПрезентацияИДопРазмер.МестаРазмещения КАК МестаРазмещения,
	|	ЭталоннаяПрезентацияИДопРазмер.МинимальнаяПрезентация КАК МинимальнаяПрезентация,
	|	ЭталоннаяПрезентацияИДопРазмер.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	ИСТИНА КАК ЭтоИсходнаяСтрока
	|ПОМЕСТИТЬ ЭталоннаяПрезентацияИДопРазмерИтог
	|ИЗ
	|	ЭталоннаяПрезентацияИДопРазмер КАК ЭталоннаяПрезентацияИДопРазмер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	// Отображение доп. размера
	|	ЭталоннаяПрезентацияИДопРазмер.НоменклатураДобавленногоРазмера,
	|	ЭталоннаяПрезентацияИДопРазмер.Магазин,
	|	ЭталоннаяПрезентацияИДопРазмер.ДобавленныйРазмер,
	|	ЭталоннаяПрезентацияИДопРазмер.ОсновнойФорматТекущейНоменклатуры,
	|	ЭталоннаяПрезентацияИДопРазмер.ГруппаАналоговТоваров,
	|	ЭталоннаяПрезентацияИДопРазмер.МестаРазмещения,
	|	1,
	|	ЛОЖЬ,
	|	ЛОЖЬ
	|ИЗ
	|	ЭталоннаяПрезентацияИДопРазмер КАК ЭталоннаяПрезентацияИДопРазмер
	|ГДЕ
	|	ЭталоннаяПрезентацияИДопРазмер.ДобавленныйРазмер <> ЗНАЧЕНИЕ(Справочник.КР_Размеры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭталоннаяПрезентацияИДопРазмерИтог.Номенклатура КАК Номенклатура,
	|	ЭталоннаяПрезентацияИДопРазмерИтог.Магазин КАК Магазин,
	|	ЭталоннаяПрезентацияИДопРазмерИтог.Размер КАК Размер,
	|	ЭталоннаяПрезентацияИДопРазмерИтог.ОсновнойФорматТекущейНоменклатуры КАК ОсновнойФорматТекущейНоменклатуры,
	|	ЭталоннаяПрезентацияИДопРазмерИтог.МинимальнаяПрезентация КАК МинимальнаяПрезентация,
	|	ЭталоннаяПрезентацияИДопРазмерИтог.МестаРазмещения КАК МестаРазмещения,
	|	ЭталоннаяПрезентацияИДопРазмерИтог.ЭтоИсходнаяСтрока КАК ЭтоИсходнаяСтрока,
	|	ЭталоннаяПрезентацияИДопРазмерИтог.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	ЭталоннаяПрезентацияИДопРазмерИтог.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров
	|ПОМЕСТИТЬ ЭталоннаяПрезентацияБезМинПрезДопРазмера
	|ИЗ
	|	ЭталоннаяПрезентацияИДопРазмерИтог КАК ЭталоннаяПрезентацияИДопРазмерИтог
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Размер,
	|	ОсновнойФорматТекущейНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КР_МинимальнаяПрезентацияСрезПоследних.Формат КАК Формат,
	|	КР_МинимальнаяПрезентацияСрезПоследних.Характеристика.КР_Размер КАК Размер,
	|	КР_МинимальнаяПрезентацияСрезПоследних.МинимальнаяПрезентация КАК МинимальнаяПрезентация,
	|	КР_МинимальнаяПрезентацияСрезПоследних.Номенклатура КАК Номенклатура
	|ПОМЕСТИТЬ МинимальнаяПрезентацияДопРазмера
	|ИЗ
	|	РегистрСведений.КР_МинимальнаяПрезентация.СрезПоследних(
	|			&Дата,
	|			(Номенклатура, Характеристика.КР_Размер, Формат) В
	|				(ВЫБРАТЬ
	|					ЭталоннаяПрезентацияБезМинПрезДопРазмера.Номенклатура КАК Номенклатура,
	|					ЭталоннаяПрезентацияБезМинПрезДопРазмера.Размер КАК Размер,
	|					ЭталоннаяПрезентацияБезМинПрезДопРазмера.ОсновнойФорматТекущейНоменклатуры КАК Формат
	|				ИЗ
	|					ЭталоннаяПрезентацияБезМинПрезДопРазмера КАК ЭталоннаяПрезентацияБезМинПрезДопРазмера
	|				ГДЕ 
	|					НЕ ЭталоннаяПрезентацияБезМинПрезДопРазмера.ЭтоИсходнаяСтрока)) КАК КР_МинимальнаяПрезентацияСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Размер,
	|	Формат;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера.Номенклатура КАК Номенклатура,
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера.Магазин КАК Магазин,
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера.Размер КАК Размер,
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера.ОсновнойФорматТекущейНоменклатуры КАК ОсновнойФорматТекущейНоменклатуры,
	|	ВЫБОР КОГДА ЭталоннаяПрезентацияБезМинПрезДопРазмера.ЭтоИсходнаяСтрока ТОГДА
	|		ЭталоннаяПрезентацияБезМинПрезДопРазмера.МинимальнаяПрезентация
	|	ИНАЧЕ
	|		ISNULL(МинимальнаяПрезентацияДопРазмера.МинимальнаяПрезентация, 1)
	|	КОНЕЦ КАК МинимальнаяПрезентация,
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера.МестаРазмещения КАК МестаРазмещения,
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера.ЭтоИсходнаяСтрока КАК ЭтоИсходнаяСтрока,
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера.ГруппаАналоговТоваров КАК ГруппаАналоговТоваров
	|ПОМЕСТИТЬ ЭталоннаяПрезентацияИтог
	|ИЗ
	|	ЭталоннаяПрезентацияБезМинПрезДопРазмера КАК ЭталоннаяПрезентацияБезМинПрезДопРазмера
	|	ЛЕВОЕ СОЕДИНЕНИЕ МинимальнаяПрезентацияДопРазмера КАК МинимальнаяПрезентацияДопРазмера
	|			ПО ЭталоннаяПрезентацияБезМинПрезДопРазмера.Номенклатура = МинимальнаяПрезентацияДопРазмера.Номенклатура
	|			И ЭталоннаяПрезентацияБезМинПрезДопРазмера.Размер = МинимальнаяПрезентацияДопРазмера.Размер
	|			И ЭталоннаяПрезентацияБезМинПрезДопРазмера.ОсновнойФорматТекущейНоменклатуры = МинимальнаяПрезентацияДопРазмера.Формат
	|			И Не ЭталоннаяПрезентацияБезМинПрезДопРазмера.ЭтоИсходнаяСтрока
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Магазин,
	|	Размер;
	|
	// << 24.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2415
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК ВНаличии,
	//|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	//|	ТоварыНаСкладахОстатки.Характеристика.КР_Размер КАК Размер,
	//|	ТоварыНаСкладахОстатки.Склад КАК Магазин,
	//|	ВЫБОР
	//|		КОГДА ТоварыНаСкладахОстатки.Помещение.КР_ТипСкладскогоПомещения = ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.ТорговыйЗал)
	//|			ТОГДА ИСТИНА
	//|		ИНАЧЕ ЛОЖЬ
	//|	КОНЕЦ КАК ЭтоТорговыйЗал
	//|ПОМЕСТИТЬ ОстаткиТоваровДоОбработки
	//|ИЗ
	//|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	//|			&Дата,
	//|			(Номенклатура, Характеристика.КР_Размер, Склад) В
	//|					(ВЫБРАТЬ
	//|						ЭталоннаяПрезентацияИтог.Номенклатура КАК Номенклатура,
	//|						ЭталоннаяПрезентацияИтог.Размер КАК Размер,
	//|						ЭталоннаяПрезентацияИтог.Магазин КАК Магазин
	//|					ИЗ
	//|						ЭталоннаяПрезентацияИтог КАК ЭталоннаяПрезентацияИтог)
	//|				И Помещение.КР_ТипСкладскогоПомещения В (ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.ТорговыйЗал), ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.СкладМагазина))) КАК ТоварыНаСкладахОстатки
	//|
	//|ИНДЕКСИРОВАТЬ ПО
	//|	Номенклатура,
	//|	Магазин,
	//|	Размер
	//|;
	//|
	//|////////////////////////////////////////////////////////////////////////////////
	//|ВЫБРАТЬ
	//|	ОстаткиТоваровДоОбработки.ВНаличии КАК ВНаличииТорговыйЗал,
	//|	ОстаткиТоваровДоОбработки.Номенклатура КАК Номенклатура,
	//|	ОстаткиТоваровДоОбработки.Размер КАК Размер,
	//|	ОстаткиТоваровДоОбработки.Магазин КАК Магазин,
	//|	0 КАК ВНаличииСклад
	//|ПОМЕСТИТЬ ОстаткиТоварБезГруппировки
	//|ИЗ
	//|	ОстаткиТоваровДоОбработки КАК ОстаткиТоваровДоОбработки
	//|ГДЕ
	//|	ОстаткиТоваровДоОбработки.ЭтоТорговыйЗал
	//|
	//|ОБЪЕДИНИТЬ ВСЕ
	//|
	//|ВЫБРАТЬ
	//|	0,
	//|	ОстаткиТоваровДоОбработки.Номенклатура,
	//|	ОстаткиТоваровДоОбработки.Размер,
	//|	ОстаткиТоваровДоОбработки.Магазин,
	//|	ОстаткиТоваровДоОбработки.ВНаличии
	//|ИЗ
	//|	ОстаткиТоваровДоОбработки КАК ОстаткиТоваровДоОбработки
	//|ГДЕ
	//|	НЕ ОстаткиТоваровДоОбработки.ЭтоТорговыйЗал        
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика.КР_Размер КАК Размер,
	|	ТоварыНаСкладахОстатки.Склад КАК Магазин,
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток КАК ВНаличииТорговыйЗал,
	|	0 КАК ВНаличииСклад
	|ПОМЕСТИТЬ ОстаткиТоварБезГруппировки
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&Дата,
	|			Склад В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ЭталоннаяПрезентацияИтог.Магазин КАК Магазин   
	|					ИЗ
	|						ЭталоннаяПрезентацияИтог КАК ЭталоннаяПрезентацияИтог)
	|				И Помещение.КР_ТипСкладскогоПомещения = ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.ТорговыйЗал)
	|	) КАК ТоварыНаСкладахОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика.КР_Размер КАК Размер,
	|	ТоварыНаСкладахОстатки.Склад КАК Магазин,
	|	0,
	|	ТоварыНаСкладахОстатки.ВНаличииОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&Дата,
	|			(Номенклатура, Характеристика.КР_Размер, Склад) В
	|					(ВЫБРАТЬ
	|						ЭталоннаяПрезентацияИтог.Номенклатура КАК Номенклатура,
	|						ЭталоннаяПрезентацияИтог.Размер КАК Размер,    
	|						ЭталоннаяПрезентацияИтог.Магазин КАК Магазин   
	|					ИЗ
	|						ЭталоннаяПрезентацияИтог КАК ЭталоннаяПрезентацияИтог)
	|				И Помещение.КР_ТипСкладскогоПомещения = ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.СкладМагазина)
	|	) КАК ТоварыНаСкладахОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Магазин,
	|	Размер
	// >> 24.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2415
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ОстаткиТоварБезГруппировки.ВНаличииТорговыйЗал) КАК ВНаличииТорговыйЗал,
	|	ОстаткиТоварБезГруппировки.Номенклатура КАК Номенклатура,
	|	ОстаткиТоварБезГруппировки.Размер КАК Размер,
	|	ОстаткиТоварБезГруппировки.Магазин КАК Магазин,
	|	СУММА(ОстаткиТоварБезГруппировки.ВНаличииСклад) КАК ВНаличииСклад
	// << 31.07.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	//|ПОМЕСТИТЬ ОстаткиТоваров  
	|ПОМЕСТИТЬ ТоварыНаСкладах  
	// >> 31.07.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	|ИЗ
	|	ОстаткиТоварБезГруппировки КАК ОстаткиТоварБезГруппировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиТоварБезГруппировки.Номенклатура,
	|	ОстаткиТоварБезГруппировки.Размер,
	|	ОстаткиТоварБезГруппировки.Магазин
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Магазин,
	|	Номенклатура,
	|	Размер     
	// << 31.07.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика.КР_Размер КАК Размер,
	|	Т.Ячейка.Владелец КАК Магазин,
	|	СУММА(Т.ВНаличииОстаток) КАК ВНаличииСклад
	|ПОМЕСТИТЬ ТоварыВЯчейках  
	|ИЗ
	|	РегистрНакопления.ТоварыВЯчейках.Остатки(
	|			&Дата,
	|			(Номенклатура, Характеристика.КР_Размер, Ячейка.Владелец) В
	|					(ВЫБРАТЬ
	|						ЭталоннаяПрезентацияИтог.Номенклатура КАК Номенклатура,
	|						ЭталоннаяПрезентацияИтог.Размер КАК Размер,
	|						ЭталоннаяПрезентацияИтог.Магазин КАК Магазин
	|					ИЗ
	|						ЭталоннаяПрезентацияИтог КАК ЭталоннаяПрезентацияИтог)
	|				И Ячейка.Помещение.КР_ТипСкладскогоПомещения = ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.СкладМагазина)
	|				И НЕ Ячейка.ТипСкладскойЯчейки В (&ИсключитьТипСкладскойЯчейки)) КАК Т	
	|ГДЕ
	|	Т.ВНаличииОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Номенклатура,
	|	Т.Характеристика.КР_Размер,
	|	Т.Ячейка.Владелец
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладах.Размер КАК Размер,
	|	ТоварыНаСкладах.Магазин КАК Магазин,
	|	ТоварыНаСкладах.ВНаличииТорговыйЗал КАК ВНаличииТорговыйЗал,
	|	ВЫБОР 
	|		КОГДА ТоварыНаСкладах.ВНаличииСклад > ЕСТЬNULL(ТоварыВЯчейках.ВНаличииСклад, 0)
	|			ТОГДА ЕСТЬNULL(ТоварыВЯчейках.ВНаличииСклад, 0) 
	|		ИНАЧЕ ТоварыНаСкладах.ВНаличииСклад
	|	КОНЕЦ КАК ВНаличииСклад
	|ПОМЕСТИТЬ ОстаткиТоваров  
	|ИЗ
	|	ТоварыНаСкладах КАК ТоварыНаСкладах
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТоварыВЯчейках КАК ТоварыВЯчейках
	|	ПО ТоварыНаСкладах.Номенклатура = ТоварыВЯчейках.Номенклатура 
	|		И ТоварыНаСкладах.Размер = ТоварыВЯчейках.Размер 
	|		И ТоварыНаСкладах.Магазин = ТоварыВЯчейках.Магазин 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТоварыНаСкладах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТоварыВЯчейках
	// >> 31.07.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////	
	|ВЫБРАТЬ
    // 24.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2415
	// Добавлено ЕСТЬNULL из за полного соединения
	|	ЕСТЬNULL(ЭталоннаяПрезентацияИтог.Номенклатура, ОстаткиТоваров.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(ЭталоннаяПрезентацияИтог.Магазин, ОстаткиТоваров.Магазин) КАК Магазин,
	|	ЕСТЬNULL(ЭталоннаяПрезентацияИтог.Размер, ОстаткиТоваров.Размер) КАК Размер,
	|	ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МинимальнаяПрезентация, 0) КАК МинимальнаяПрезентация,
	|	ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МестаРазмещения, НЕОПРЕДЕЛЕНО) КАК МестоРазмещения,
	|	ЕСТЬNULL(ЭталоннаяПрезентацияИтог.ЕстьМинимальнаяПрезентация, ЛОЖЬ) КАК ЕстьМинимальнаяПрезентация,
	|	ЕСТЬNULL(ЭталоннаяПрезентацияИтог.ЭтоИсходнаяСтрока, ЛОЖЬ) КАК ЭтоИсходнаяСтрока,
	|	ЕСТЬNULL(ОстаткиТоваров.ВНаличииТорговыйЗал, 0) КАК ВНаличииВЗале,
	|	ЕСТЬNULL(ОстаткиТоваров.ВНаличииСклад, 0) КАК ВНаличииНаСкладе,
	|	ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МинимальнаяПрезентация, 0) - ЕСТЬNULL(ОстаткиТоваров.ВНаличииТорговыйЗал, 0) КАК ПотребностьВЗале,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МинимальнаяПрезентация, 0) - ЕСТЬNULL(ОстаткиТоваров.ВНаличииТорговыйЗал, 0) > 0
	|			ТОГДА ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МинимальнаяПрезентация, 0) - ЕСТЬNULL(ОстаткиТоваров.ВНаличииТорговыйЗал, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезакрытаяПотребностьПоЗалу,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МинимальнаяПрезентация, 0) > ЕСТЬNULL(ОстаткиТоваров.ВНаличииТорговыйЗал, 0)
	|			ТОГДА ЕСТЬNULL(ОстаткиТоваров.ВНаличииТорговыйЗал, 0)
	|		ИНАЧЕ ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МинимальнаяПрезентация, 0)
	|	КОНЕЦ КАК ЗарезервировоПоЗалу,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиТоваров.ВНаличииТорговыйЗал, 0) > ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МинимальнаяПрезентация, 0)
	|			ТОГДА ЕСТЬNULL(ОстаткиТоваров.ВНаличииТорговыйЗал, 0) - ЕСТЬNULL(ЭталоннаяПрезентацияИтог.МинимальнаяПрезентация, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СвободныйОстатокПоЗалу
	|ПОМЕСТИТЬ ЭталоннаяСОстатками
	|ИЗ
	|	ЭталоннаяПрезентацияИтог КАК ЭталоннаяПрезентацияИтог
    // << 24.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2415
	//|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиТоваров КАК ОстаткиТоваров
	|		ПОЛНОЕ СОЕДИНЕНИЕ ОстаткиТоваров КАК ОстаткиТоваров
	// >> 24.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2415
	|		ПО ЭталоннаяПрезентацияИтог.Номенклатура = ОстаткиТоваров.Номенклатура
	|			И ЭталоннаяПрезентацияИтог.Магазин = ОстаткиТоваров.Магазин
	|			И ЭталоннаяПрезентацияИтог.Размер = ОстаткиТоваров.Размер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭталоннаяСОстатками.Номенклатура КАК Номенклатура,
	|	ЭталоннаяСОстатками.Магазин КАК Магазин,
	|	ЭталоннаяСОстатками.Размер КАК Размер,
	|	ЭталоннаяСОстатками.МинимальнаяПрезентация КАК МинимальнаяПрезентация,
	|	ЭталоннаяСОстатками.МестоРазмещения КАК МестоРазмещения,
	|	ЭталоннаяСОстатками.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	ЭталоннаяСОстатками.ВНаличииВЗале КАК ВНаличииВЗале,
	|	ЭталоннаяСОстатками.ВНаличииНаСкладе КАК ВНаличииНаСкладе,
	|	ЭталоннаяСОстатками.ПотребностьВЗале КАК ПотребностьВЗале,
	|	ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу КАК НезакрытаяПотребностьПоЗалу,
	|	ЭталоннаяСОстатками.ЗарезервировоПоЗалу КАК ЗарезервировоПоЗалу,
	|	ВЫБОР
	|		КОГДА ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу - ЭталоннаяСОстатками.ВНаличииНаСкладе > 0
	|			ТОГДА ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу - ЭталоннаяСОстатками.ВНаличииНаСкладе
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СвободныйОстатокПоЗалу,
	|	ВЫБОР
	|		КОГДА ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу - ЭталоннаяСОстатками.ВНаличииНаСкладе > 0
	|			ТОГДА ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу - ЭталоннаяСОстатками.ВНаличииНаСкладе
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПотребностьСУчетомСклада,
	|	ВЫБОР
	|		КОГДА ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу - ЭталоннаяСОстатками.ВНаличииНаСкладе > 0
	|			ТОГДА ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу - ЭталоннаяСОстатками.ВНаличииНаСкладе
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НезакрытаяПотребностьСУчетомСклада,
	|	ВЫБОР
	|		КОГДА ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу > ЭталоннаяСОстатками.ВНаличииНаСкладе
	|			ТОГДА ЭталоннаяСОстатками.ВНаличииНаСкладе
	|		ИНАЧЕ ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу
	|	КОНЕЦ КАК ЗарезервировоПоСкладу,
	|	ВЫБОР
	|		КОГДА ЭталоннаяСОстатками.ВНаличииНаСкладе > ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу
	|			ТОГДА ЭталоннаяСОстатками.ВНаличииНаСкладе - ЭталоннаяСОстатками.НезакрытаяПотребностьПоЗалу
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СвободныйОстатокПоСкладу
	|ИЗ
	|	ЭталоннаяСОстатками КАК ЭталоннаяСОстатками";
	
КонецФункции

Функция ТекстЗапросаОбеспечениеПотребностиПоАналогам() Экспорт
	
	// 09.10.2023 Марченко С.Н., КРОК, JIRA№A2105505-2357
	// Изменен запрос из за изменения структуры регистра КР_ПриоритетыРазмеровТоварнойГруппы
	Возврат
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаПотребностей.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ТаблицаПотребностей.Магазин КАК Магазин,
	|	ТаблицаПотребностей.Размер КАК Размер,
	|	ТаблицаПотребностей.МестоРазмещения КАК МестоРазмещения,
	|	ТаблицаПотребностей.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	ТаблицаПотребностей.ВНаличииВЗале КАК ВНаличииВЗале,
	|	ТаблицаПотребностей.ВНаличииНаСкладе КАК ВНаличииНаСкладе,
	|	ТаблицаПотребностей.ПотребностьВЗале КАК ПотребностьВЗале,
	|	ТаблицаПотребностей.НезакрытаяПотребностьПоЗалу КАК НезакрытаяПотребностьПоЗалу,
	|	ТаблицаПотребностей.ЗарезервировоПоЗалу КАК ЗарезервировоПоЗалу,
	|	ТаблицаПотребностей.СвободныйОстатокПоЗалу КАК СвободныйОстатокПоЗалу,
	|	ТаблицаПотребностей.ПотребностьСУчетомСклада КАК ПотребностьСУчетомСклада,
	|	ТаблицаПотребностей.НезакрытаяПотребностьСУчетомСклада КАК НезакрытаяПотребность,
	|	ТаблицаПотребностей.ЗарезервировоПоСкладу КАК ЗарезервировоПоСкладу,
	|	ТаблицаПотребностей.СвободныйОстатокПоСкладу КАК СвободныйОстатокПоСкладу,
	|	ТаблицаПотребностей.НезакрытаяПотребностьСУчетомСклада > 0 КАК ЕстьПотребностьАналогов
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	&ТаблицаПотребностей КАК ТаблицаПотребностей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.Магазин КАК Магазин,
	|	Данные.Размер КАК Размер,
	|	СУММА(Данные.ЗарезервировоПоЗалу) КАК ЗарезервировоПоЗалу,
	|	СУММА(Данные.ЗарезервировоПоСкладу) КАК ЗарезервировоПоСкладу
	|ПОМЕСТИТЬ ЗарезервированныйОстаток
	|ИЗ
	|	Данные КАК Данные
	|ГДЕ
	|	(Данные.ЗарезервировоПоЗалу <> 0
	|			ИЛИ Данные.ЗарезервировоПоСкладу <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	Данные.Номенклатура,
	|	Данные.Магазин,
	|	Данные.Размер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Данные.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	Данные.Магазин КАК Магазин,
	|	Данные.Размер КАК Размер,
	|	Данные.ЗарезервировоПоЗалу КАК ЗарезервировоПоЗалу,
	|	Данные.ЗарезервировоПоСкладу КАК ЗарезервировоПоСкладу,
	|	Данные.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	Данные.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация
	|ПОМЕСТИТЬ ДанныеПотребностей
	|ИЗ
	|	Данные КАК Данные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПотребностей.Номенклатура КАК Номенклатура,
	|	ДанныеПотребностей.Магазин КАК Магазин,
	|	ДанныеПотребностей.Размер КАК Размер,
	|	ДанныеПотребностей.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	ЕСТЬNULL(КР_СоставГруппАналоговТоваров.ГруппаАналоговТоваров, ЗНАЧЕНИЕ(Справочник.КР_ГруппыАналоговТоваров.ПустаяСсылка)) КАК ГруппаАналогов,
	|	ДанныеПотребностей.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	// << 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|	ВЫБОР КОГДА НЕ ЕСТЬNULL(КР_СоставГруппАналоговТоваров.Приоритет, 0) = 0 
	|		ТОГДА КР_СоставГруппАналоговТоваров.Приоритет
	|		ИНАЧЕ 99
	|	КОНЕЦ КАК Приоритет,
	// >> 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|	ЕСТЬNULL(ДанныеПотребностей.Номенклатура.КР_Линия.Пол, ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.ПустаяСсылка)) КАК Пол
	|ПОМЕСТИТЬ ДанныеСГруппамиАналогов
	|ИЗ
	|	ДанныеПотребностей КАК ДанныеПотребностей
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КР_СоставГруппАналоговТоваров КАК КР_СоставГруппАналоговТоваров
	|		ПО ДанныеПотребностей.Номенклатура = КР_СоставГруппАналоговТоваров.Номенклатура
	|			И (ДанныеПотребностей.НезакрытаяПотребность <> 0)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеСГруппамиАналогов.Номенклатура КАК Номенклатура,
	|	ДанныеСГруппамиАналогов.Магазин КАК Магазин,
	|	ДанныеСГруппамиАналогов.Размер КАК Размер,
	|	ДанныеСГруппамиАналогов.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	ДанныеСГруппамиАналогов.ГруппаАналогов КАК ГруппаАналогов,
	// << 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|	ДанныеСГруппамиАналогов.Приоритет КАК Приоритет,
	// >> 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|	ЕСТЬNULL(ДоступныеАналоги.Номенклатура, ДанныеСГруппамиАналогов.Номенклатура) КАК НоменклатураАналог,
	|	ЕСТЬNULL(ДоступныеАналоги.Размер, ДанныеСГруппамиАналогов.Размер) КАК РазмерАльтернативный,	
	|	ДанныеСГруппамиАналогов.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	ДанныеСГруппамиАналогов.Пол КАК Пол
	|ПОМЕСТИТЬ АналогиСРазмерами
	|ИЗ
	|	ДанныеСГруппамиАналогов КАК ДанныеСГруппамиАналогов
	|		ЛЕВОЕ СОЕДИНЕНИЕ АссортиментБезРазмеровИСАналогами КАК ДоступныеАналоги
	|		ПО ДанныеСГруппамиАналогов.Магазин = ДоступныеАналоги.Магазин
	|			И ДанныеСГруппамиАналогов.ГруппаАналогов = ДоступныеАналоги.ГруппаАналогов
	// В контексте разных размеров - товары могут заменять друг друга и без участиве в группе аналогов товаров,
	// но в рамках одной номенклатуры
	|			И (ДанныеСГруппамиАналогов.НезакрытаяПотребность <> 0)
	|			И (ЕстьМинимальнаяПрезентация)
	// Для корректной работы алгоритма должна быть исходная строка (где номенклатура = номенклатура аналог и размер = размер аналог)
	// Для этого добавлено следующее условие
	|			И (ДоступныеАналоги.ЭтоАналог ИЛИ (ДанныеСГруппамиАналогов.Номенклатура = ДоступныеАналоги.Номенклатура))
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналогиСРазмерами.Номенклатура КАК Номенклатура,
	|	АналогиСРазмерами.Магазин КАК Магазин,
	|	АналогиСРазмерами.Размер КАК Размер,
	|	АналогиСРазмерами.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	АналогиСРазмерами.ГруппаАналогов КАК ГруппаАналогов,
	|	АналогиСРазмерами.НоменклатураАналог КАК НоменклатураАналог,
	|	АналогиСРазмерами.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	// << 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	//|	ВЫБОР
	//|		КОГДА ЕСТЬNULL(АналогиСРазмерами.НоменклатураАналог.КоллекцияНоменклатуры.КР_Приоритет, 0) = 0
	//|			ТОГДА 99
	//|		ИНАЧЕ АналогиСРазмерами.НоменклатураАналог.КоллекцияНоменклатуры.КР_Приоритет
	//|	КОНЕЦ КАК ПриоритетАналога,
	|	АналогиСРазмерами.Приоритет КАК ПриоритетАналога,
	// >> 14.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2334
	|	АналогиСРазмерами.Пол КАК Пол,
	|	ВЫБОР
	|		КОГДА АналогиСРазмерами.РазмерАльтернативный = АналогиСРазмерами.Размер
	|			ТОГДА -1
	|		ИНАЧЕ ЕСТЬNULL(КР_ПриоритетыРазмеров.Приоритет, 999)
	|	КОНЕЦ КАК ПриоритетРазмера,
	|	НЕ КР_ПриоритетыРазмеров.Приоритет ЕСТЬ NULL КАК ПриоритетРазмераНайден,
	|	АналогиСРазмерами.РазмерАльтернативный КАК РазмерАналога,
	|	ЕСТЬNULL(КР_ПриоритетыРазмеров.Характеристика.КР_Размер, ЗНАЧЕНИЕ(Справочник.КР_Размеры.Пустаяссылка)) = АналогиСРазмерами.Размер КАК ЭтоРазмерБезИспользованияАналога
	|ПОМЕСТИТЬ АналогиПриоритетыИсключений
	|ИЗ
	|	АналогиСРазмерами КАК АналогиСРазмерами
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КР_ПриоритетыРазмеров КАК КР_ПриоритетыРазмеров
	// Особенностью поиска приоритета, что мы ищем его основывая на приоритетах размера номенклатуры - для которой идет поиск аналогов размеров,
	// поэтому связь по АналогиСРазмерами.Номенклатура, а не по АналогиСРазмерами.НоменклатураАналог
	|		ПО АналогиСРазмерами.Номенклатура = КР_ПриоритетыРазмеров.Номенклатура
	|			И АналогиСРазмерами.РазмерАльтернативный = КР_ПриоритетыРазмеров.Характеристика.КР_Размер
	|			И (АналогиСРазмерами.ЕстьМинимальнаяПрезентация)
	|			И (АналогиСРазмерами.НезакрытаяПотребность <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналогиПриоритетыИсключений.Магазин КАК Магазин,
	|	АналогиПриоритетыИсключений.Номенклатура КАК Номенклатура,
	|	МАКСИМУМ(АналогиПриоритетыИсключений.ПриоритетРазмераНайден) КАК МинимумОдинПриоритетРазмераНайден
	|ПОМЕСТИТЬ ЕстьНастройкиИсключенийПоНоменклатуре
	|ИЗ
	|	АналогиПриоритетыИсключений КАК АналогиПриоритетыИсключений
	|
	|СГРУППИРОВАТЬ ПО
	|	АналогиПриоритетыИсключений.Магазин,
	|	АналогиПриоритетыИсключений.Номенклатура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Магазин,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналогиПриоритетыИсключений.Номенклатура КАК Номенклатура,
	|	АналогиПриоритетыИсключений.Магазин КАК Магазин,
	|	АналогиПриоритетыИсключений.Размер КАК Размер,
	|	АналогиПриоритетыИсключений.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	АналогиПриоритетыИсключений.ГруппаАналогов КАК ГруппаАналогов,
	|	АналогиПриоритетыИсключений.НоменклатураАналог КАК НоменклатураАналог,
	|	АналогиПриоритетыИсключений.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	АналогиПриоритетыИсключений.ПриоритетАналога КАК ПриоритетАналога,
	|	АналогиПриоритетыИсключений.Пол КАК Пол,
	|	АналогиПриоритетыИсключений.ПриоритетРазмера КАК ПриоритетРазмера,
	|	АналогиПриоритетыИсключений.ПриоритетРазмераНайден КАК ПриоритетРазмераНайден,
	|	АналогиПриоритетыИсключений.РазмерАналога КАК РазмерАналога,
	|	АналогиПриоритетыИсключений.ЭтоРазмерБезИспользованияАналога КАК ЭтоРазмерБезИспользованияАналога,
	|	ЕСТЬNULL(ЕстьНастройкиИсключенийПоНоменклатуре.МинимумОдинПриоритетРазмераНайден, ЛОЖЬ) КАК МинимумОдинПриоритетРазмераИсклНайден
	|ПОМЕСТИТЬ АналогиПриоритетыИсключенийИтог
	|ИЗ
	|	АналогиПриоритетыИсключений КАК АналогиПриоритетыИсключений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЕстьНастройкиИсключенийПоНоменклатуре КАК ЕстьНастройкиИсключенийПоНоменклатуре
	|		ПО АналогиПриоритетыИсключений.Магазин = ЕстьНастройкиИсключенийПоНоменклатуре.Магазин
	|			И АналогиПриоритетыИсключений.Номенклатура = ЕстьНастройкиИсключенийПоНоменклатуре.Номенклатура
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АналогиПриоритетыИсключенийИтог.Номенклатура КАК Номенклатура,
	|	АналогиПриоритетыИсключенийИтог.Магазин КАК Магазин,
	|	АналогиПриоритетыИсключенийИтог.Размер КАК Размер,
	|	АналогиПриоритетыИсключенийИтог.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	АналогиПриоритетыИсключенийИтог.ГруппаАналогов КАК ГруппаАналогов,
	|	АналогиПриоритетыИсключенийИтог.НоменклатураАналог КАК НоменклатураАналог,
	|	АналогиПриоритетыИсключенийИтог.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	АналогиПриоритетыИсключенийИтог.ПриоритетАналога КАК ПриоритетАналога,
	|	АналогиПриоритетыИсключенийИтог.Пол КАК Пол,
	|	ВЫБОР
	|		КОГДА АналогиПриоритетыИсключенийИтог.ПриоритетРазмераНайден
	|			ТОГДА АналогиПриоритетыИсключенийИтог.ПриоритетРазмера
	|		ИНАЧЕ ЕСТЬNULL(КР_ПриоритетыРазмеровТоварнойГруппы.Приоритет, 999)
	|	КОНЕЦ КАК ПриоритетРазмера,
	|	АналогиПриоритетыИсключенийИтог.ПриоритетРазмераНайден
	|		ИЛИ НЕ КР_ПриоритетыРазмеровТоварнойГруппы.Размер ЕСТЬ NULL КАК ПриоритетРазмераНайден,
	|	ВЫБОР
	|		КОГДА АналогиПриоритетыИсключенийИтог.РазмерАналога = ЗНАЧЕНИЕ(Справочник.КР_Размеры.ПустаяССылка)
	|			ТОГДА АналогиПриоритетыИсключенийИтог.Размер
	|		ИНАЧЕ АналогиПриоритетыИсключенийИтог.РазмерАналога
	|	КОНЕЦ КАК РазмерАналога
	|ПОМЕСТИТЬ Итог
	|ИЗ
	|	АналогиПриоритетыИсключенийИтог КАК АналогиПриоритетыИсключенийИтог
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КР_ПриоритетыРазмеровТоварнойГруппы КАК КР_ПриоритетыРазмеровТоварнойГруппы
	|		ПО Не АналогиПриоритетыИсключенийИтог.МинимумОдинПриоритетРазмераИсклНайден
	|			И АналогиПриоритетыИсключенийИтог.Пол = КР_ПриоритетыРазмеровТоварнойГруппы.Пол
	|			И АналогиПриоритетыИсключенийИтог.Номенклатура.ВидНоменклатуры = КР_ПриоритетыРазмеровТоварнойГруппы.ТоварнаяГруппа
	|			И АналогиПриоритетыИсключенийИтог.РазмерАналога = КР_ПриоритетыРазмеровТоварнойГруппы.Размер
	|			И (АналогиПриоритетыИсключенийИтог.ЕстьМинимальнаяПрезентация)
	|ГДЕ
	|	АналогиПриоритетыИсключенийИтог.ПриоритетРазмераНайден
	|	ИЛИ НЕ КР_ПриоритетыРазмеровТоварнойГруппы.Размер ЕСТЬ NULL
	|	ИЛИ АналогиПриоритетыИсключенийИтог.ПриоритетРазмера = -1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Итог.Номенклатура КАК Номенклатура,
	|	Итог.Магазин КАК Магазин,
	|	Итог.Размер КАК Размер,
	|	Итог.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	Итог.ГруппаАналогов КАК ГруппаАналогов,
	|	Итог.НоменклатураАналог КАК НоменклатураАналог,
	|	Итог.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	Итог.ПриоритетАналога КАК ПриоритетАналога,
	|	Итог.Пол КАК Пол,
	|	Итог.ПриоритетРазмера КАК ПриоритетРазмера,
	|	Итог.ПриоритетРазмераНайден КАК ПриоритетРазмераНайден,
	|	Итог.РазмерАналога КАК РазмерАналога,
	|	МАКСИМУМ(ХарактеристикиАналога.Ссылка) КАК ХарактеристикаАналога
	|ПОМЕСТИТЬ ИтогСХаркой
	|ИЗ
	|	Итог КАК Итог
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиАналога
	|		ПО Итог.НоменклатураАналог = ХарактеристикиАналога.Владелец
	|			И Итог.РазмерАналога = ХарактеристикиАналога.КР_Размер
	|			И (НЕ ХарактеристикиАналога.ПометкаУдаления)
	|ГДЕ
	|	НЕ ХарактеристикиАналога.Ссылка ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	Итог.Номенклатура,
	|	Итог.Магазин,
	|	Итог.Размер,
	|	Итог.НезакрытаяПотребность,
	|	Итог.ГруппаАналогов,
	|	Итог.НоменклатураАналог,
	|	Итог.ЕстьМинимальнаяПрезентация,
	|	Итог.ПриоритетАналога,
	|	Итог.Пол,
	|	Итог.ПриоритетРазмера,
	|	Итог.ПриоритетРазмераНайден,
	|	Итог.РазмерАналога
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.Магазин КАК Магазин,
	|	Данные.Размер КАК Размер,
	|	Данные.МестоРазмещения КАК МестоРазмещения,
	|	Данные.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	Данные.ВНаличииВЗале КАК ВНаличииВЗале,
	|	Данные.ВНаличииНаСкладе КАК ВНаличииНаСкладе,
	|	Данные.ПотребностьВЗале КАК ПотребностьВЗале,
	|	Данные.НезакрытаяПотребностьПоЗалу КАК НезакрытаяПотребностьПоЗалу,
	|	Данные.ЗарезервировоПоЗалу КАК ЗарезервировоПоЗалу,
	|	Данные.СвободныйОстатокПоЗалу КАК СвободныйОстатокПоЗалу,
	|	Данные.ПотребностьСУчетомСклада КАК ПотребностьСУчетомСклада,
	|	Данные.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	Данные.ЗарезервировоПоСкладу КАК ЗарезервировоПоСкладу,
	|	Данные.СвободныйОстатокПоСкладу КАК СвободныйОстатокПоСкладу,
	|	Данные.ЕстьПотребностьАналогов КАК ЕстьПотребностьАналогов,
	|	ИтогСХаркой.ГруппаАналогов КАК ГруппаАналогов,
	|	ИтогСХаркой.НоменклатураАналог КАК НоменклатураАналог,
	|	ИтогСХаркой.ПриоритетАналога КАК ПриоритетАналога,
	|	ИтогСХаркой.Пол КАК Пол,
	|	ИтогСХаркой.ПриоритетРазмера КАК ПриоритетРазмера,
	|	ИтогСХаркой.ПриоритетРазмераНайден КАК ПриоритетРазмераНайден,
	|	ИтогСХаркой.РазмерАналога КАК РазмерАналога,
	|	ИтогСХаркой.ХарактеристикаАналога КАК ХарактеристикаАналога
	|ПОМЕСТИТЬ ИтогДляОбработкиБезШтрихкодов
	|ИЗ
	|	Данные КАК Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИтогСХаркой КАК ИтогСХаркой
	|		ПО Данные.Номенклатура = ИтогСХаркой.Номенклатура
	|			И Данные.Магазин = ИтогСХаркой.Магазин
	|			И Данные.Размер = ИтогСХаркой.Размер
	|			И Данные.ЕстьМинимальнаяПрезентация = ИтогСХаркой.ЕстьМинимальнаяПрезентация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИтогДляОбработкиБезШтрихкодов.Номенклатура КАК Номенклатура,
	|	ИтогДляОбработкиБезШтрихкодов.Магазин КАК Магазин,
	|	ИтогДляОбработкиБезШтрихкодов.Размер КАК Размер,
	|	ИтогДляОбработкиБезШтрихкодов.МестоРазмещения КАК МестоРазмещения,
	|	ИтогДляОбработкиБезШтрихкодов.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	ИтогДляОбработкиБезШтрихкодов.ВНаличииВЗале КАК ВНаличииВЗале,
	|	ИтогДляОбработкиБезШтрихкодов.ВНаличииНаСкладе КАК ВНаличииНаСкладе,
	|	ИтогДляОбработкиБезШтрихкодов.ПотребностьВЗале КАК ПотребностьВЗале,
	|	ИтогДляОбработкиБезШтрихкодов.НезакрытаяПотребностьПоЗалу КАК НезакрытаяПотребностьПоЗалу,
	|	ИтогДляОбработкиБезШтрихкодов.ЗарезервировоПоЗалу КАК ЗарезервировоПоЗалу,
	|	ИтогДляОбработкиБезШтрихкодов.СвободныйОстатокПоЗалу КАК СвободныйОстатокПоЗалу,
	|	ИтогДляОбработкиБезШтрихкодов.ПотребностьСУчетомСклада КАК ПотребностьСУчетомСклада,
	|	ИтогДляОбработкиБезШтрихкодов.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	ИтогДляОбработкиБезШтрихкодов.ЗарезервировоПоСкладу КАК ЗарезервировоПоСкладу,
	|	ИтогДляОбработкиБезШтрихкодов.СвободныйОстатокПоСкладу КАК СвободныйОстатокПоСкладу,
	|	ИтогДляОбработкиБезШтрихкодов.ЕстьПотребностьАналогов КАК ЕстьПотребностьАналогов,
	|	ИтогДляОбработкиБезШтрихкодов.ГруппаАналогов КАК ГруппаАналогов,
	|	ИтогДляОбработкиБезШтрихкодов.НоменклатураАналог КАК НоменклатураАналог,
	|	ИтогДляОбработкиБезШтрихкодов.Пол КАК Пол,
	// Сложный для понимания алгоритм приоритетов (описание):
	// Итоговое упорядочивание идет по реквизиту данного запроса ПервыйПараметрУпорядочивания, затем по ВторойПараметрУпорядочивания.
	// 1. кейс с ПервыйПараметрУпорядочивания = -3 (полное совпадение номенклатуры и размера).
	// 2. кейс с ПервыйПараметрУпорядочивания = -2 (совпадение размера, без совпадения номенклатуры) + порядок по ПриоритетАналога
	// 3. кейс с ПервыйПараметрУпорядочивания = ПриоритетРазмера (без совпадений): порядок по ПриоритетРазмера, затем по ПриоритетАналога.
	|	ВЫБОР
	|		КОГДА ИтогДляОбработкиБезШтрихкодов.Размер = ИтогДляОбработкиБезШтрихкодов.РазмерАналога
	|			ТОГДА ВЫБОР
	|				КОГДА ИтогДляОбработкиБезШтрихкодов.Номенклатура = ИтогДляОбработкиБезШтрихкодов.НоменклатураАналог
	|					ТОГДА -3
	|				ИНАЧЕ -2
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ИтогДляОбработкиБезШтрихкодов.ПриоритетРазмера
	|	КОНЕЦ КАК ПервыйПараметрУпорядочивания,
	|	ВЫБОР
	|		КОГДА ИтогДляОбработкиБезШтрихкодов.Номенклатура = ИтогДляОбработкиБезШтрихкодов.НоменклатураАналог
	|			ТОГДА -1
	|		ИНАЧЕ ИтогДляОбработкиБезШтрихкодов.ПриоритетАналога
	|	КОНЕЦ КАК ВторойПараметрУпорядочивания,
	|	ИтогДляОбработкиБезШтрихкодов.ПриоритетРазмераНайден КАК ПриоритетРазмераНайден,
	|	ИтогДляОбработкиБезШтрихкодов.РазмерАналога КАК РазмерАналога,
	|	ИтогДляОбработкиБезШтрихкодов.ХарактеристикаАналога КАК ХарактеристикаАналога,
	|	МАКСИМУМ(ЕСТЬNULL(ШтрихкодыНоменклатурыАналог.Штрихкод, """")) КАК Штрихкод
	|ПОМЕСТИТЬ ИтогДляОбработки
	|ИЗ
	|	ИтогДляОбработкиБезШтрихкодов КАК ИтогДляОбработкиБезШтрихкодов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатурыАналог
	|		ПО ИтогДляОбработкиБезШтрихкодов.НоменклатураАналог = ШтрихкодыНоменклатурыАналог.Номенклатура
	|			И ИтогДляОбработкиБезШтрихкодов.ХарактеристикаАналога = ШтрихкодыНоменклатурыАналог.Характеристика
	|
	|СГРУППИРОВАТЬ ПО
	|	ИтогДляОбработкиБезШтрихкодов.НезакрытаяПотребность,
	|	ИтогДляОбработкиБезШтрихкодов.ПотребностьВЗале,
	|	ИтогДляОбработкиБезШтрихкодов.СвободныйОстатокПоЗалу,
	|	ИтогДляОбработкиБезШтрихкодов.ПотребностьСУчетомСклада,
	|	ИтогДляОбработкиБезШтрихкодов.РазмерАналога,
	|	ИтогДляОбработкиБезШтрихкодов.ХарактеристикаАналога,
	|	ИтогДляОбработкиБезШтрихкодов.МестоРазмещения,
	|	ИтогДляОбработкиБезШтрихкодов.Размер,
	|	ИтогДляОбработкиБезШтрихкодов.Пол,
	|	ИтогДляОбработкиБезШтрихкодов.ЕстьПотребностьАналогов,
	|	ИтогДляОбработкиБезШтрихкодов.ГруппаАналогов,
	|	ИтогДляОбработкиБезШтрихкодов.НезакрытаяПотребностьПоЗалу,
	|	ИтогДляОбработкиБезШтрихкодов.СвободныйОстатокПоСкладу,
	|	ИтогДляОбработкиБезШтрихкодов.ЗарезервировоПоСкладу,
	|	ИтогДляОбработкиБезШтрихкодов.ВНаличииВЗале,
	|	ИтогДляОбработкиБезШтрихкодов.ЕстьМинимальнаяПрезентация,
	|	ИтогДляОбработкиБезШтрихкодов.ПриоритетРазмераНайден,
	|	ИтогДляОбработкиБезШтрихкодов.Магазин,
	|	ИтогДляОбработкиБезШтрихкодов.Номенклатура,
	|	ИтогДляОбработкиБезШтрихкодов.ЗарезервировоПоЗалу,
	|	ИтогДляОбработкиБезШтрихкодов.ВНаличииНаСкладе,
	|	ИтогДляОбработкиБезШтрихкодов.НоменклатураАналог,
	|	ВЫБОР
	|		КОГДА ИтогДляОбработкиБезШтрихкодов.Размер = ИтогДляОбработкиБезШтрихкодов.РазмерАналога
	|			ТОГДА ВЫБОР
	|				КОГДА ИтогДляОбработкиБезШтрихкодов.Номенклатура = ИтогДляОбработкиБезШтрихкодов.НоменклатураАналог
	|					ТОГДА -3
	|				ИНАЧЕ -2
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ИтогДляОбработкиБезШтрихкодов.ПриоритетРазмера
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ИтогДляОбработкиБезШтрихкодов.Номенклатура = ИтогДляОбработкиБезШтрихкодов.НоменклатураАналог
	|			ТОГДА -1
	|		ИНАЧЕ ИтогДляОбработкиБезШтрихкодов.ПриоритетАналога
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Итог.НоменклатураАналог КАК Номенклатура,
	|	Итог.РазмерАналога КАК Размер,
	|	Итог.Магазин КАК Магазин
	|ПОМЕСТИТЬ ДляПолученияОстатков
	|ИЗ
	|	Итог КАК Итог
	|
	|СГРУППИРОВАТЬ ПО
	|	Итог.НоменклатураАналог,
	|	Итог.РазмерАналога,
	|	Итог.Магазин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДляПолученияОстатков.Номенклатура КАК Номенклатура,
	|	ДляПолученияОстатков.Размер КАК Размер,
	|	ДляПолученияОстатков.Магазин КАК Магазин,
	|	0 КАК ЗарезервировоПоЗалу,
	|	0 КАК ЗарезервировоПоСкладу
	|ПОМЕСТИТЬ ДляПолученияОстатковСРезервами
	|ИЗ
	|	ДляПолученияОстатков КАК ДляПолученияОстатков
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗарезервированныйОстаток.Номенклатура,
	|	ЗарезервированныйОстаток.Размер,
	|	ЗарезервированныйОстаток.Магазин,
	|	ЗарезервированныйОстаток.ЗарезервировоПоЗалу,
	|	ЗарезервированныйОстаток.ЗарезервировоПоСкладу
	|ИЗ
	|	ЗарезервированныйОстаток КАК ЗарезервированныйОстаток
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДляПолученияОстатковСРезервами.Номенклатура КАК Номенклатура,
	|	ДляПолученияОстатковСРезервами.Размер КАК Размер,
	|	ДляПолученияОстатковСРезервами.Магазин КАК Магазин,
	|	СУММА(ДляПолученияОстатковСРезервами.ЗарезервировоПоЗалу) КАК ЗарезервировоПоЗалу,
	|	СУММА(ДляПолученияОстатковСРезервами.ЗарезервировоПоСкладу) КАК ЗарезервировоПоСкладу
	|ПОМЕСТИТЬ ДляПолученияОстатковСРезервамиИтог
	|ИЗ
	|	ДляПолученияОстатковСРезервами КАК ДляПолученияОстатковСРезервами
	|
	|СГРУППИРОВАТЬ ПО
	|	ДляПолученияОстатковСРезервами.Магазин,
	|	ДляПолученияОстатковСРезервами.Номенклатура,
	|	ДляПолученияОстатковСРезервами.Размер";
	
КонецФункции

Функция ТекстЗапросаПолучитьОстаткиВсехВозможныхАналогов() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	ДляПолученияОстатковСРезервамиИтог.Магазин КАК Магазин,
	|	ДляПолученияОстатковСРезервамиИтог.Номенклатура КАК Номенклатура,
	|	ДляПолученияОстатковСРезервамиИтог.Размер КАК Размер,
	|	ДляПолученияОстатковСРезервамиИтог.ЗарезервировоПоЗалу КАК ЗарезервировоПоЗалу,
	|	ДляПолученияОстатковСРезервамиИтог.ЗарезервировоПоСкладу КАК ЗарезервировоПоСкладу
	|ПОМЕСТИТЬ ДанныеДляОстатка
	|ИЗ
	|	ДляПолученияОстатковСРезервамиИтог КАК ДляПолученияОстатковСРезервамиИтог
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Размер,
	|	Магазин
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика.КР_Размер КАК Размер,
	|	ТоварыНаСкладахОстатки.Склад КАК Магазин,
	|	СУММА(ВЫБОР
	|			КОГДА ТоварыНаСкладахОстатки.Помещение.КР_ТипСкладскогоПомещения = ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.ТорговыйЗал)
	|				ТОГДА ТоварыНаСкладахОстатки.ВНаличииОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстатокПоЗалу,
	|	СУММА(ВЫБОР
	|			КОГДА ТоварыНаСкладахОстатки.Помещение.КР_ТипСкладскогоПомещения = ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.СкладМагазина)
	|				ТОГДА ТоварыНаСкладахОстатки.ВНаличииОстаток
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ОстатокПоСкладу,
	|	ТоварыНаСкладахОстатки.Характеристика КАК Характеристика
	// << 31.07.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068  
	//|ПОМЕСТИТЬ ОстаткиТоваровСкладМагазина  
	|ПОМЕСТИТЬ ТоварыНаСкладах  
	//
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			&Дата,
	|			(Номенклатура, Характеристика.КР_Размер, Склад) В
	|					(ВЫБРАТЬ
	|						ДанныеДляОстатка.Номенклатура КАК Номенклатура,
	|						ДанныеДляОстатка.Размер КАК Размер,
	|						ДанныеДляОстатка.Магазин КАК Магазин
	|					ИЗ
	|						ДанныеДляОстатка КАК ДанныеДляОстатка)
	|				И Помещение.КР_ТипСкладскогоПомещения В (ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.ТорговыйЗал), ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.СкладМагазина))) КАК ТоварыНаСкладахОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыНаСкладахОстатки.Номенклатура,
	|	ТоварыНаСкладахОстатки.Характеристика.КР_Размер,
	|	ТоварыНаСкладахОстатки.Склад,
	|	ТоварыНаСкладахОстатки.Характеристика
	// << 31.07.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068  
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Номенклатура КАК Номенклатура,
	|	Т.Характеристика.КР_Размер КАК Размер,
	|	Т.Ячейка.Владелец КАК Магазин,
	|	СУММА(Т.ВНаличииОстаток) КАК ОстатокПоСкладу,
	|	Т.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ТоварыВЯчейках  
	|ИЗ
	|	РегистрНакопления.ТоварыВЯчейках.Остатки(
	|			&Дата,
	|			(Номенклатура, Характеристика.КР_Размер, Ячейка.Владелец) В
	|					(ВЫБРАТЬ
	|						ДанныеДляОстатка.Номенклатура КАК Номенклатура,
	|						ДанныеДляОстатка.Размер КАК Размер,
	|						ДанныеДляОстатка.Магазин КАК Магазин
	|					ИЗ
	|						ДанныеДляОстатка КАК ДанныеДляОстатка)
	|				И Ячейка.Помещение.КР_ТипСкладскогоПомещения = ЗНАЧЕНИЕ(Перечисление.КР_ТипыСкладскихПомещений.СкладМагазина)
	|				И НЕ Ячейка.ТипСкладскойЯчейки В (&ИсключитьТипСкладскойЯчейки)) КАК Т	
	|ГДЕ
	|	Т.ВНаличииОстаток > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Номенклатура,
	|	Т.Характеристика.КР_Размер,
	|	Т.Ячейка.Владелец,
	|	Т.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
	|	ТоварыНаСкладах.Размер КАК Размер,
	|	ТоварыНаСкладах.Магазин КАК Магазин,
	|	ТоварыНаСкладах.ОстатокПоЗалу КАК ОстатокПоЗалу,
	|	ВЫБОР 
	|		КОГДА ТоварыНаСкладах.ОстатокПоСкладу > ЕСТЬNULL(ТоварыВЯчейках.ОстатокПоСкладу, 0)
	|			ТОГДА ЕСТЬNULL(ТоварыВЯчейках.ОстатокПоСкладу, 0) 
	|		ИНАЧЕ ТоварыНаСкладах.ОстатокПоСкладу
	|	КОНЕЦ КАК ОстатокПоСкладу,
	|	ТоварыНаСкладах.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ОстаткиТоваровСкладМагазина  
	|ИЗ
	|	ТоварыНаСкладах КАК ТоварыНаСкладах
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТоварыВЯчейках КАК ТоварыВЯчейках
	|	ПО ТоварыНаСкладах.Номенклатура = ТоварыВЯчейках.Номенклатура 
	|		И ТоварыНаСкладах.Характеристика = ТоварыВЯчейках.Характеристика 
	|		И ТоварыНаСкладах.Магазин = ТоварыВЯчейках.Магазин 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТоварыНаСкладах
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТоварыВЯчейках
	// >> 31.07.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОстатка.Номенклатура КАК Номенклатура,
	|	ДанныеДляОстатка.Размер КАК Размер,
	|	ДанныеДляОстатка.Магазин КАК Магазин,
	|	ЕСТЬNULL(ОстаткиТоваровСкладМагазина.ОстатокПоЗалу, 0) - ДанныеДляОстатка.ЗарезервировоПоЗалу КАК ОстатокПоЗалу,
	|	ЕСТЬNULL(ОстаткиТоваровСкладМагазина.ОстатокПоСкладу, 0) - ДанныеДляОстатка.ЗарезервировоПоСкладу КАК ОстатокПоСкладу,
	|	ОстаткиТоваровСкладМагазина.Характеристика КАК Характеристика
	|ИЗ
	|	ДанныеДляОстатка КАК ДанныеДляОстатка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиТоваровСкладМагазина КАК ОстаткиТоваровСкладМагазина
	|		ПО ДанныеДляОстатка.Магазин = ОстаткиТоваровСкладМагазина.Магазин
	|			И ДанныеДляОстатка.Номенклатура = ОстаткиТоваровСкладМагазина.Номенклатура
	|			И ДанныеДляОстатка.Размер = ОстаткиТоваровСкладМагазина.Размер
	|ГДЕ
	|	НЕ(ЕСТЬNULL(ОстаткиТоваровСкладМагазина.ОстатокПоЗалу, 0) - ДанныеДляОстатка.ЗарезервировоПоЗалу <= 0
	|				И ЕСТЬNULL(ОстаткиТоваровСкладМагазина.ОстатокПоСкладу, 0) - ДанныеДляОстатка.ЗарезервировоПоСкладу <= 0)";
	
КонецФункции

Функция ТекстЗапросаОбходаРезультата() Экспорт
	
	Возврат
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ИтогДляОбработки.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ИтогДляОбработки.Магазин КАК Магазин,
	|	ИтогДляОбработки.Размер КАК Размер,
	|	ИтогДляОбработки.МестоРазмещения КАК МестоРазмещения,
	|	ИтогДляОбработки.ЕстьМинимальнаяПрезентация КАК ЕстьМинимальнаяПрезентация,
	|	ИтогДляОбработки.ВНаличииВЗале КАК ВНаличииВЗале,
	|	ИтогДляОбработки.ВНаличииНаСкладе КАК ВНаличииНаСкладе,
	|	ИтогДляОбработки.ПотребностьВЗале КАК ПотребностьВЗале,
	|	ИтогДляОбработки.НезакрытаяПотребностьПоЗалу КАК НезакрытаяПотребностьПоЗалу,
	|	ИтогДляОбработки.ЗарезервировоПоЗалу КАК ЗарезервировоПоЗалу,
	|	ИтогДляОбработки.СвободныйОстатокПоЗалу КАК СвободныйОстатокПоЗалу,
	|	ИтогДляОбработки.ПотребностьСУчетомСклада КАК ПотребностьСУчетомСклада,
	|	ИтогДляОбработки.НезакрытаяПотребность КАК НезакрытаяПотребность,
	|	ИтогДляОбработки.ЗарезервировоПоСкладу КАК ЗарезервировоПоСкладу,
	|	ИтогДляОбработки.СвободныйОстатокПоСкладу КАК СвободныйОстатокПоСкладу,
	|	ИтогДляОбработки.ЕстьПотребностьАналогов КАК ЕстьПотребностьАналогов,
	|	ИтогДляОбработки.ГруппаАналогов КАК ГруппаАналогов,
	|	ИтогДляОбработки.НоменклатураАналог КАК НоменклатураАналог,
	|	ИтогДляОбработки.ВторойПараметрУпорядочивания КАК ВторойПараметрУпорядочивания,
	|	ИтогДляОбработки.Пол КАК Пол,
	|	ИтогДляОбработки.ПервыйПараметрУпорядочивания КАК ПервыйПараметрУпорядочивания,
	|	ИтогДляОбработки.ПриоритетРазмераНайден КАК ПриоритетРазмераНайден,
	|	ИтогДляОбработки.РазмерАналога КАК РазмерАналога,
	|	ИтогДляОбработки.ХарактеристикаАналога КАК ХарактеристикаАналога,
	|	ВЫРАЗИТЬ(ИтогДляОбработки.НоменклатураАналог КАК Справочник.Номенклатура).Артикул КАК Артикул,
	|	ИтогДляОбработки.Штрихкод КАК Штрихкод,
	|	ВЫРАЗИТЬ(ИтогДляОбработки.Номенклатура КАК Справочник.Номенклатура).Наименование КАК НаименованиеНоменклатуры,
	|	ВЫРАЗИТЬ(ИтогДляОбработки.ХарактеристикаАналога КАК Справочник.ХарактеристикиНоменклатуры).Наименование КАК НаименованиеХарактеристика
	|ИЗ
	|	ИтогДляОбработки КАК ИтогДляОбработки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Магазин,
	|	ПервыйПараметрУпорядочивания,
	|	ВторойПараметрУпорядочивания,
	|	РазмерАналога Убыв,
	|	НаименованиеНоменклатуры,
	|	НаименованиеХарактеристика
	|ИТОГИ ПО
	|	Магазин,
	|	Номенклатура,
	|	Размер";

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыФункции

Процедура ЗаполнитьДанныеОбработки(ЗначениеТоварыДерево, РезультатЗапроса, ТаблицаОстатков, ТолькоТребующиеДействий)
	
	ЗначениеТоварыДерево.Строки.Очистить();
	
	ВыборкаМагазин = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаМагазин.Следующий() Цикл
		
		ВыборкаНоменклатура = ВыборкаМагазин.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
		Пока ВыборкаНоменклатура.Следующий() Цикл

			ВыборкаРазмер = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаРазмер.Следующий() Цикл
				
				ВыборкаДетальныеЗаписи = ВыборкаРазмер.Выбрать();
				ПараметрыОбхода = Новый Структура;
				ПараметрыОбхода.Вставить("ЭтоПерваяСтрокаГруппировки",					Истина);
				ПараметрыОбхода.Вставить("СтрокаВерхнегоУровня",						Неопределено);
				ПараметрыОбхода.Вставить("КоличествоПотребностиПоАналогам",				0);
				ПараметрыОбхода.Вставить("ГруппаСтрокТребуетДействий",					Ложь);
				ПараметрыОбхода.Вставить("ГруппаСтрокТребуетДействийИлиЕстьОстаток",	Ложь);
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					
					ПрерватьОбходДетальныхЗаписей = ОбходДетальныхЗаписей(ЗначениеТоварыДерево, ВыборкаДетальныеЗаписи,
						ТаблицаОстатков, ПараметрыОбхода);
					
					Если ПрерватьОбходДетальныхЗаписей Тогда
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
				СтрокаВерхнегоУровняУдалена = Ложь;
				Если ТолькоТребующиеДействий И Не ПараметрыОбхода.ГруппаСтрокТребуетДействий Тогда
					ЗначениеТоварыДерево.Строки.Удалить(ПараметрыОбхода.СтрокаВерхнегоУровня);
					СтрокаВерхнегоУровняУдалена = Истина;
				КонецЕсли;
				Если Не СтрокаВерхнегоУровняУдалена И Не ПараметрыОбхода.ГруппаСтрокТребуетДействийИлиЕстьОстаток Тогда
					ЗначениеТоварыДерево.Строки.Удалить(ПараметрыОбхода.СтрокаВерхнегоУровня);
					СтрокаВерхнегоУровняУдалена = Истина;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ИсключитьЛишниеПеремещенияИзЗала(ЗначениеТоварыДерево, ТаблицаОстатков, ТолькоТребующиеДействий);
	
КонецПроцедуры

Функция ОбходДетальныхЗаписей(ЗначениеТоварыДерево, ВыборкаДетальныеЗаписи, ТаблицаОстатков, Параметры)
	
	Если Параметры.ЭтоПерваяСтрокаГруппировки Тогда
		
		НоваяСтрокаВерхнегоУровня	= ЗначениеТоварыДерево.Строки.Добавить();
		
		ОбщийРезервДоАналогов			= ВыборкаДетальныеЗаписи.ЗарезервировоПоЗалу + ВыборкаДетальныеЗаписи.ЗарезервировоПоСкладу; 
		
		ЗаполнитьОсновныеПараметрыСтроки(НоваяСтрокаВерхнегоУровня, ВыборкаДетальныеЗаписи, Ложь);
		
		НоваяСтрокаВерхнегоУровня.КоличествоТорговыйЗалНаходится 	= ВыборкаДетальныеЗаписи.ВНаличииВЗале;
		НоваяСтрокаВерхнегоУровня.КоличествоТорговыйЗалТребуется 	= ОбщийРезервДоАналогов + ВыборкаДетальныеЗаписи.НезакрытаяПотребность;
		НоваяСтрокаВерхнегоУровня.ПереместитьВЗал					= ВыборкаДетальныеЗаписи.ЗарезервировоПоСкладу;
		ДельтаЗала 													= НоваяСтрокаВерхнегоУровня.КоличествоТорговыйЗалНаходится 
			- НоваяСтрокаВерхнегоУровня.КоличествоТорговыйЗалТребуется;
		НоваяСтрокаВерхнегоУровня.ПереместитьИзЗала					= ?(ДельтаЗала > 0, ДельтаЗала, 0);  
		// << 30.01.2024 Марченко С.Н., КРОК, JIRA№A2105505-2622
		НоваяСтрокаВерхнегоУровня.ВНаличииНаСкладе = ВыборкаДетальныеЗаписи.ВНаличииНаСкладе;  
		// >> 30.01.2024 Марченко С.Н., КРОК, JIRA№A2105505-2622
		
		Параметры.СтрокаВерхнегоУровня = НоваяСтрокаВерхнегоУровня;
		
		ЕстьПотребностьАналогов = ВыборкаДетальныеЗаписи.ЕстьПотребностьАналогов;
		Параметры.ГруппаСтрокТребуетДействий = 
			НоваяСтрокаВерхнегоУровня.ПереместитьВЗал <> 0 ИЛИ НоваяСтрокаВерхнегоУровня.ПереместитьИзЗала <> 0;
		Параметры.ГруппаСтрокТребуетДействийИлиЕстьОстаток = Параметры.ГруппаСтрокТребуетДействий ИЛИ
			НоваяСтрокаВерхнегоУровня.КоличествоТорговыйЗалНаходится;
		
		Если Не ЕстьПотребностьАналогов Тогда
			// Строить строки нижнего уровня не требуется, потребность устранена без учета аналогов
			Возврат Истина;
		КонецЕсли;
		
		Параметры.КоличествоПотребностиПоАналогам					= НоваяСтрокаВерхнегоУровня.КоличествоТорговыйЗалТребуется 
			- НоваяСтрокаВерхнегоУровня.КоличествоТорговыйЗалНаходится - НоваяСтрокаВерхнегоУровня.ПереместитьВЗал;
			
		Параметры.ЭтоПерваяСтрокаГруппировки = Ложь;
		
	Иначе
		
		ОбработатьСтрокуПоПотребностиАналогов(ВыборкаДетальныеЗаписи, Параметры.КоличествоПотребностиПоАналогам,
			ТаблицаОстатков, Параметры);
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ОбработатьСтрокуПоПотребностиАналогов(Выборка, КоличествоПотребностиПоАналогам, ТаблицаОстатков, Параметры)
	
	Если КоличествоПотребностиПоАналогам <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПоискаОстатков = Новый Структура;
	ПараметрыПоискаОстатков.Вставить("Номенклатура", 		Выборка.НоменклатураАналог);
	ПараметрыПоискаОстатков.Вставить("Характеристика",		Выборка.ХарактеристикаАналога);
	ПараметрыПоискаОстатков.Вставить("Магазин",				Выборка.Магазин);
	МассивСтрок = ТаблицаОстатков.НайтиСтроки(ПараметрыПоискаОстатков);
	
	СтрокаСоздана = Ложь;
	Для Каждого СтрокаОстатков Из МассивСтрок Цикл
		
		Если КоличествоПотребностиПоАналогам <= 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если СтрокаОстатков.ОстатокПоЗалу > 0 Тогда
			
			СписатьНаПотребностьСЗала = ?(СтрокаОстатков.ОстатокПоЗалу > КоличествоПотребностиПоАналогам, КоличествоПотребностиПоАналогам,
				СтрокаОстатков.ОстатокПоЗалу);
			КоличествоПотребностиПоАналогам 	= КоличествоПотребностиПоАналогам - СписатьНаПотребностьСЗала;
			СтрокаОстатков.ОстатокПоЗалу 		= СтрокаОстатков.ОстатокПоЗалу - СписатьНаПотребностьСЗала;
			СтрокаОстатков.ПереданоДляАналогов	= СтрокаОстатков.ПереданоДляАналогов + СписатьНаПотребностьСЗала;
			
			Если Не СтрокаСоздана Тогда
				СтрокаСоздана		= Истина;
				СтрокаНижнегоУровня = Параметры.СтрокаВерхнегоУровня.Строки.Добавить();
				ЗаполнитьОсновныеПараметрыСтроки(СтрокаНижнегоУровня, Выборка, Истина);
			КонецЕсли;
			СтрокаНижнегоУровня.КоличествоТорговыйЗалНаходится			= СтрокаНижнегоУровня.КоличествоТорговыйЗалНаходится +
				СписатьНаПотребностьСЗала;

		КонецЕсли;
		
		Если КоличествоПотребностиПоАналогам <= 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если СтрокаОстатков.ОстатокПоСкладу > 0 Тогда
			
			СписатьНаПотребностьСоСклада = ?(СтрокаОстатков.ОстатокПоСкладу > КоличествоПотребностиПоАналогам, КоличествоПотребностиПоАналогам,
				СтрокаОстатков.ОстатокПоСкладу);
			КоличествоПотребностиПоАналогам 	= КоличествоПотребностиПоАналогам - СписатьНаПотребностьСоСклада;
			СтрокаОстатков.ОстатокПоСкладу 		= СтрокаОстатков.ОстатокПоСкладу - СписатьНаПотребностьСоСклада;
			СтрокаОстатков.ПереданоДляАналогов	= СтрокаОстатков.ПереданоДляАналогов + СписатьНаПотребностьСоСклада;
			
			Если Не СтрокаСоздана Тогда
				СтрокаСоздана		= Истина;
				СтрокаНижнегоУровня =  Параметры.СтрокаВерхнегоУровня.Строки.Добавить();
				ЗаполнитьОсновныеПараметрыСтроки(СтрокаНижнегоУровня, Выборка, Истина);
			КонецЕсли;
			СтрокаНижнегоУровня.ПереместитьВЗал			= СтрокаНижнегоУровня.ПереместитьВЗал +
				СписатьНаПотребностьСоСклада;
				
			Параметры.ГруппаСтрокТребуетДействий				= Истина;
			Параметры.ГруппаСтрокТребуетДействийИлиЕстьОстаток	= Истина;
			
		КонецЕсли;
			
	КонецЦикла;
	
КонецПроцедуры

Функция ЗаполнитьПотребностиПоАналогамНоменклатуры(ТаблицаПотребностей, Дата, МенеджерВременныхТаблиц,
	ВариантПоискаОстатков = "ПодпиткаТорговогоЗала"
	// << 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	, ИсключитьТипСкладскойЯчейки    
	// >> 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ТаблицаПотребностей", ТаблицаПотребностей);
	// << 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068   
	Запрос.УстановитьПараметр("ИсключитьТипСкладскойЯчейки", ИсключитьТипСкладскойЯчейки);
	// >> 04.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-2068
	
	Разделитель = ОбщегоНазначения.РазделительПакетаЗапросов();
	ТекстЗапросаОбеспечениеПотребностиПоАналогам = ТекстЗапросаОбеспечениеПотребностиПоАналогам();
	ТекстПоискаОстатков = "";
	Если ВариантПоискаОстатков = "ПодпиткаТорговогоЗала" Тогда
		ТекстПоискаОстатков = ТекстЗапросаПолучитьОстаткиВсехВозможныхАналогов();
	ИначеЕсли ВариантПоискаОстатков = "ОбеспечениеФормированияЗаказовПоПотребностям" Тогда
		
	КонецЕсли;
	
	ШаблонТекста = "%1%2%3%2%4";
	Запрос.Текст = СтрШаблон(ШаблонТекста, ТекстЗапросаОбеспечениеПотребностиПоАналогам, Разделитель,
		ТекстПоискаОстатков);
	
	РезультатЗапроса = Запрос.Выполнить();
	ОстаткиДляРаспределения = РезультатЗапроса.Выгрузить();
	ОстаткиДляРаспределения.Колонки.Добавить("ПереданоДляАналогов",	ОбщегоНазначения.ОписаниеТипаЧисло(15, 2));
	
	Возврат ОстаткиДляРаспределения;
	
КонецФункции

Процедура ЗаполнитьОсновныеПараметрыСтроки(Строка, Данные, ЭтоАналог)
	
	Если ЭтоАналог Тогда
		Строка.Номенклатура						= Данные.НоменклатураАналог;
	Иначе
		Строка.Номенклатура						= Данные.Номенклатура;
	КонецЕсли;
	Строка.Характеристика 					= Данные.ХарактеристикаАналога;
	Строка.Артикул							= Данные.Артикул;
	Строка.Штрихкод							= Данные.Штрихкод;
	Строка.МестоРазмещения					= Данные.МестоРазмещения;
	Строка.Магазин							= Данные.Магазин;
	
КонецПроцедуры

Процедура ИсключитьЛишниеПеремещенияИзЗала(ЗначениеТоварыДерево, ТаблицаОстатков, ТолькоТребующиеДействий)
	
	// Обойдем строки эталонов (верхний уровень дерева) с возвратом на склад <> 0 
	// и проверим на наличие кейса, когда есть потребность возврата излишка на склад,
	// но этот излишек обеспечивает потребности посредством выступления аналогом для других эталонов
	// (следовательно излишкем не является)
	
	Для Каждого СтрокаЭталона Из ЗначениеТоварыДерево.Строки Цикл
		
		Если СтрокаЭталона.ПереместитьИзЗала <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыПоискаОстатков = Новый Структура;
		ПараметрыПоискаОстатков.Вставить("Номенклатура", 	СтрокаЭталона.Номенклатура);
		ПараметрыПоискаОстатков.Вставить("Характеристика", 	СтрокаЭталона.Характеристика);
		ПараметрыПоискаОстатков.Вставить("Магазин		", 	СтрокаЭталона.Магазин);
		
		МассивСтрок = ТаблицаОстатков.НайтиСтроки(ПараметрыПоискаОстатков);
		Для Каждого СтрокаМассива Из МассивСтрок Цикл
			Если СтрокаМассива.ПереданоДляАналогов <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаЭталона.ПереместитьИзЗала > СтрокаМассива.ПереданоДляАналогов Тогда
				Дельта = СтрокаЭталона.ПереместитьИзЗала - СтрокаМассива.ПереданоДляАналогов;
			Иначе
				Дельта = СтрокаЭталона.ПереместитьИзЗала;
			КонецЕсли;
			СтрокаЭталона.ПереместитьИзЗала 	= СтрокаЭталона.ПереместитьИзЗала - Дельта;
			СтрокаЭталона.КоличествоТорговыйЗалНаходится				 	= СтрокаЭталона.КоличествоТорговыйЗалНаходится - Дельта;
			СтрокаМассива.ПереданоДляАналогов	= СтрокаМассива.ПереданоДляАналогов - Дельта;
			
			// Возможен кейс, когда после пересчета строка станет попадать под условие "не требует действия"
			Если ТолькоТребующиеДействий И СтрокаЭталона.ПереместитьИзЗала = 0 И СтрокаЭталона.ПереместитьВЗал = 0 Тогда
				ЗначениеТоварыДерево.Строки.Удалить(СтрокаЭталона);
				СтрокаВерхнегоУровняУдалена = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ИнциализироватьДерево(Дерево)
	
	ОписаниеКоличества = ОбщегоНазначения.ОписаниеТипаЧисло(15, 2);
	
	Дерево = Новый ДеревоЗначений;
	Дерево.Колонки.Добавить("Номенклатура", 					Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Дерево.Колонки.Добавить("Артикул", 							ОбщегоНазначения.ОписаниеТипаСтрока(50));
	Дерево.Колонки.Добавить("Штрихкод", 						ОбщегоНазначения.ОписаниеТипаСтрока(200));
	Дерево.Колонки.Добавить("Характеристика", 					Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Дерево.Колонки.Добавить("МестоРазмещения", 					Новый ОписаниеТипов("СправочникСсылка.КР_МестаРазмещения"));
	Дерево.Колонки.Добавить("КоличествоТорговыйЗалТребуется", 	ОписаниеКоличества);
	Дерево.Колонки.Добавить("КоличествоТорговыйЗалНаходится", 	ОписаниеКоличества);
	Дерево.Колонки.Добавить("ПереместитьВЗал", 					ОписаниеКоличества);
	Дерево.Колонки.Добавить("ПереместитьИзЗала", 				ОписаниеКоличества);
	Дерево.Колонки.Добавить("Магазин", 							Новый ОписаниеТипов("СправочникСсылка.Склады"));
	// << 30.01.2024 Марченко С.Н., КРОК, JIRA№A2105505-2622
	Дерево.Колонки.Добавить("ВНаличииНаСкладе", ОписаниеКоличества);  
	// >> 30.01.2024 Марченко С.Н., КРОК, JIRA№A2105505-2622
	
КонецПроцедуры

#КонецОбласти