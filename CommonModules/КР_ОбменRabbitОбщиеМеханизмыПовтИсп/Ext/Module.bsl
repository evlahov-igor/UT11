
#Область ПрограммныйИнтерфейс

// Получение строки из настроек выгрузки объекта по его имени метаданных
//
// Параметры:
//  ИмяОбъекта  - Строка - Имя метаданного для поиска алгоритмов выгрузки
//
// Возвращаемое значение:
//   СтрокаТЗ, Неопределено   - Строка ТЗ с параметрами выгрузки объекта
//
Функция ПолучитьНастройкиВыгрузкиОбъекта(ИмяОбъекта) Экспорт

	ПараметрыВыгрузки = КР_ОбменRabbitОбщиеМеханизмыПовтИсп.ПолучитьНастройкиВыгрузкиОбъектов();
	
	СтруктураПоиска = Новый Структура("ИмяМД", ИмяОбъекта);
	ПараметрыВыгрузкиОбъекта = ПараметрыВыгрузки.НайтиСтроки(СтруктураПоиска);
	
	Возврат ПараметрыВыгрузкиОбъекта; 
	
КонецФункции

// Получение таблицы настроек выгрузки всех объектов
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица значений со всеми параметрами выгрузки всех объектов
//
Функция ПолучитьНастройкиВыгрузкиОбъектов() Экспорт
	
	ПараметрыВыгрузки = Новый ТаблицаЗначений;
	ПараметрыВыгрузки.Колонки.Добавить("ИмяМД");
	ПараметрыВыгрузки.Колонки.Добавить("ТочкаОбмена");
	ПараметрыВыгрузки.Колонки.Добавить("ПравилоРегистрации");
	ПараметрыВыгрузки.Колонки.Добавить("ПравилоВыгрузки");
	ПараметрыВыгрузки.Колонки.Добавить("МассивСообщений");
	ПараметрыВыгрузки.Колонки.Добавить("СсылкиНаОбъекты");
	ПараметрыВыгрузки.Колонки.Добавить("ДополнительныеПараметры");
	
	// A2105505-1241    
	КР_УТ11_ЦБ_Магазин_ВыгрузкаДанныхСервер.ДобавитьНастройкиВыгрузкиОбъектов(ПараметрыВыгрузки);   
	//
	
	// << 15.08.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-296
	КР_БП_УТ11_ВыгрузкаДанныхСервер.ДобавитьНастройкиВыгрузкиОбъектов(ПараметрыВыгрузки);
	// >> 15.08.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-296
	
	// << 06.09.2022 Марченко С.Н., КРОК, JIRA№A2105505-416
	КР_УТ10_УТ11_ВыгрузкаДанныхСервер.ДобавитьНастройкиВыгрузкиОбъектов(ПараметрыВыгрузки);
	// >> 06.09.2022 Марченко С.Н., КРОК, JIRA№A2105505-416
	
	Возврат ПараметрыВыгрузки; 
	
КонецФункции

// Получение таблицы настроек загрузки всех объектов
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица значений со всеми параметрами загрузки всех объектов
//
Функция ПолучитьНастройкиЗагрузкиОбъектов() Экспорт

	ПараметрыЗагрузки = Новый ТаблицаЗначений;
	ПараметрыЗагрузки.Колонки.Добавить("ТипДанных");   
	ПараметрыЗагрузки.Колонки.Добавить("Отправитель");
	ПараметрыЗагрузки.Колонки.Добавить("ОчередьОбмена");
	ПараметрыЗагрузки.Колонки.Добавить("ПравилоЗагрузки");
	ПараметрыЗагрузки.Колонки.Добавить("СсылкиНаОбъекты");
	ПараметрыЗагрузки.Колонки.Добавить("ДополнительныеПараметры");    

	// АлгоритмСериализации                                     
	// По умолчанию "JSONВДанныеXDTO"
	// Варианты: JSONВДанныеXDTO, XMLВДанныеXDTO, JSONВДанныеСтруктура, JSONВДанныеСоответствие, "НеПрименять"
	// В результате преобразования в сообщении обмена появляется свойство "Данные", свойство "ТелоСообщения" удаляется
	// Если указан вариант "НеПрименять" преобразования не выполняется и свойство "ТелоСообщения" не удаляется  
	ПараметрыЗагрузки.Колонки.Добавить("АлгоритмСериализации"); 
	
	// A2105505-1241    
	КР_УТ11_ЦБ_Магазин_ЗагрузкаДанныхСервер.ДобавитьНастройкиЗагрузкиОбъектов(ПараметрыЗагрузки);
	//
	
	// << 22.08.2022 Марченко С.Н., КРОК, JIRA№A2105505-401
	КР_УТ10_УТ11_ЗагрузкаДанныхСервер.ДобавитьНастройкиЗагрузкиОбъектов(ПараметрыЗагрузки);
	// >> 22.08.2022 Марченко С.Н., КРОК, JIRA№A2105505-401
	
	// << 28.09.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-489
	КР_PLM_УТ11_ЗагрузкаДанныхСервер.ДобавитьНастройкиЗагрузкиОбъектов(ПараметрыЗагрузки);
	// >> 28.09.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-489
	
	// << 29.09.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-570
	КР_ЗУП_УТ11_ЗагрузкаДанныхСервер.ДобавитьНастройкиЗагрузкиОбъектов(ПараметрыЗагрузки);
	// >> 29.09.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-570
	
	// << 01.11.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-626
	КР_Демо_УТ11_ЗагрузкаДанныхСервер.ДобавитьНастройкиЗагрузкиОбъектов(ПараметрыЗагрузки);
	// >> 01.11.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-626
	
	// << 17.11.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-820
	КР_БП_УТ11_ЗагрузкаДанныхСервер.ДобавитьНастройкиЗагрузкиОбъектов(ПараметрыЗагрузки);
	// >> 17.11.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-820
	
	// << 24.11.2022, Федоров Д.Е., КРОК, JIRA№ A2105505-802	
	КР_OMS_УТ11_ЗагрузкаДанныхСервер.ДобавитьНастройкиЗагрузкиОбъектов(ПараметрыЗагрузки);
	// >> 24.11.2022, Федоров Д.Е., КРОК, JIRA№ A2105505-802
	
	// << 06.12.2022, Федотов А.М., КРОК, JIRA№ A2105505-749	
	КР_ImportDepartment_УТ11_ЗагрузкаДанныхСервер.ДобавитьНастройкиЗагрузкиОбъектов(ПараметрыЗагрузки);
	// >> 06.12.2022, Федотов А.М., КРОК, JIRA№ A2105505-749
	
	Возврат ПараметрыЗагрузки; 
	
КонецФункции

// Получение параметров текущей ИБ
//
// Возвращаемое значение:
//   Структура   - Структура с параметрами текущей ИБ
//
Функция ПолучитьТекущуюИнформационнуюБазу() Экспорт
	
	Возврат КР_ОбменRabbitПодготовкаДанных.ПолучитьТекущуюИнформационнуюБазу();
	
КонецФункции

// Получает ссылку на справочник ИБ по наименованию (дополнительно создает при отсутствии)
//
// Параметры:
//  НаименованиеИБ  - Строка - наименование искомой ИБ
//
// Возвращаемое значение:
//   СправочникСссылка.КР_ИнформационныеБазы   - Ссылка на найденную/созданную ИБ
//
Функция ПолучитьИнформационнуюБазу(НаименованиеИБ) Экспорт
	
	Если ТипЗнч(НаименованиеИБ) = Тип("Строка") Тогда
		Возврат КР_ОбменRabbitПодготовкаДанных.ПолучитьИнформационнуюБазу(НаименованиеИБ);
	Иначе
		Возврат НаименованиеИБ;	
	КонецЕсли;

КонецФункции // << 24.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-626

// Проверка того, что данная ИБ является центральной
//
// Возвращаемое значение:
//   Булево   - Истина, если текущая ИБ являеятся центральной
//
Функция ТекущаяИБЯвляетсяЦентральной() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	ЭтоЦБ = Константы.КР_БазаЯвляетсяЦентральной.Получить();	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЭтоЦБ;
	
КонецФункции

// Получение склада магазина текущей ИБ
//
// Возвращаемое значение:
//   Булево   - Истина, если текущая ИБ являеятся центральной
//
Функция ПолучитьЗначениеСкладаМагазинаТекущейИБ() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	Склад = Константы.КР_СкладМагазина.Получить();	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Склад;	
	
КонецФункции

Функция ПолучитьНастройкиПодключенияКRabbit(ИнформационнаяБаза) Экспорт 
	
	Возврат КР_ОбменRabbitПодготовкаДанных.ПолучитьНастройкиПодключенияКRabbit(ИнформационнаяБаза);	
	
КонецФункции

// --> Евлахов Игорь Николаевич (Начало) 22.12.2023
// Задача #3531

// Определяет доступность регистрации по типу данных
//
// Параметры:
//  ТипДанных  - Строка - имя объекта входящего в состав XDTO-пакета, который
//						  проверяется на доступность регистрации.
//  Пакет  - Строка - имя XDTO-пакета
//
// Возвращаемое значение:
//   Булево   - Истина - доступна регистрация; Ложь - регистрация не доступна.
//
Функция РегистрироватьДанныеТипа(ТипДанных, Пакет) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	#Область ТекстЗапроса
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	КР_РегистрируемыеТипыДанных.ТипДанных КАК ТипДанных
	               |ИЗ
	               |	РегистрСведений.КР_РегистрируемыеТипыДанных КАК КР_РегистрируемыеТипыДанных
	               |ГДЕ
	               |	КР_РегистрируемыеТипыДанных.Пакет = &Пакет";
	
	#КонецОбласти
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Пакет", Пакет);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		// Регистрируются все объекты
		Результат = Истина;
	Иначе
		РегистрируемыеТипы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("ТипДанных");
			
		Результат = (РегистрируемыеТипы.Найти(ТипДанных) <> Неопределено);
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции
// <-- Евлахов Игорь Николаевич (Конец) 22.12.2023

#КонецОбласти
