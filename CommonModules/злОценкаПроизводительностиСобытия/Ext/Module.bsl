
#Область ПрограммныйИнтерфейс

// --> Евлахов Игорь Николаевич (Начало) 23.08.2023
// Задача #3069

Процедура ЗамерПроизводительностиОткрытияФормы(Источник, ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка) Экспорт
				
	Если Не ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительностиОткрытияФорм() Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьЗамерВремени = Ложь;
		
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗнч(Источник));
	
	Если ОбъектМетаданных <> Неопределено Тогда		
		Если Метаданные.Справочники.Содержит(ОбъектМетаданных) Тогда
		    ТипМетаданных = "Справочник";
		ИначеЕсли Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
		    ТипМетаданных = "Документ";
		ИначеЕсли Метаданные.ЖурналыДокументов.Содержит(ОбъектМетаданных) Тогда
		    ТипМетаданных = "ЖурналДокументов";
		ИначеЕсли Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) Тогда
		    ТипМетаданных = "РегистрСведений";
		ИначеЕсли Метаданные.Обработки.Содержит(ОбъектМетаданных) Тогда
		    ТипМетаданных = "Обработка";
		Иначе
			ТипМетаданных = "";
		КонецЕсли;
		ИмяМетаданного = ОбъектМетаданных.Имя;		 
	Иначе
		ТипМетаданных = "";
		ИмяМетаданного = "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТипМетаданных) И ЗначениеЗаполнено(ИмяМетаданного) Тогда						
		Если ВидФормы = "ФормаОбъекта" 
				И ОбъектМетаданных.ОсновнаяФормаОбъекта <> Неопределено Тогда
			ВыполнитьЗамерВремени = Истина;
			ВыбраннаяФорма = ОбъектМетаданных.ОсновнаяФормаОбъекта.Имя;
		ИначеЕсли ВидФормы = "ФормаСписка" 
				И ТипМетаданных = "ЖурналДокументов" 
				И ОбъектМетаданных.ОсновнаяФорма <> Неопределено Тогда
			ВыполнитьЗамерВремени = Истина;
			ВыбраннаяФорма = ОбъектМетаданных.ОсновнаяФорма.Имя;			
		ИначеЕсли ВидФормы = "ФормаСписка" 
				И (ТипМетаданных = "Документ" ИЛИ ТипМетаданных = "Справочник" ИЛИ ТипМетаданных = "РегистрСведений") 
				И ОбъектМетаданных.ОсновнаяФормаСписка <> Неопределено Тогда
			ВыполнитьЗамерВремени = Истина;
			ВыбраннаяФорма = ОбъектМетаданных.ОсновнаяФормаСписка.Имя;
		ИначеЕсли ВидФормы = "ФормаВыбора" 
				И ОбъектМетаданных.ОсновнаяФормаДляВыбора <> Неопределено Тогда
			ВыполнитьЗамерВремени = Истина;
			ВыбраннаяФорма = ОбъектМетаданных.ОсновнаяФормаДляВыбора.Имя;
		ИначеЕсли ВидФормы = "Форма" 
				И ОбъектМетаданных.ОсновнаяФорма <> Неопределено Тогда
			ВыполнитьЗамерВремени = Истина;
			ВыбраннаяФорма = ОбъектМетаданных.ОсновнаяФорма.Имя; 
		ИначеЕсли ВидФормы = "ФормаЗаписи" 
				И ОбъектМетаданных.ОсновнаяФормаЗаписи <> Неопределено Тогда
			ВыполнитьЗамерВремени = Истина;
			ВыбраннаяФорма = ОбъектМетаданных.ОсновнаяФормаЗаписи.Имя;
		Иначе	
			ВыбраннаяФорма = ВидФормы;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыполнитьЗамерВремени Тогда
		СтандартнаяОбработка = Ложь;
		// &ЗамерПроизводительности
		ТекстШаблона = НСтр("ru = 'ОткрытиеФормы%1%2%3'");
		КлючеваяОперация = СтрШаблон(ТекстШаблона, ТипМетаданных, ИмяМетаданного, ВыбраннаяФорма);
		
		ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();		
		
		ПараметрыОценкиПроизводительности = Новый Структура();
		ПараметрыОценкиПроизводительности.Вставить("КлючеваяОперация", КлючеваяОперация);
		ПараметрыОценкиПроизводительности.Вставить("ВремяНачала", ВремяНачала);
		
		Параметры.Вставить("ПараметрыОценкиПроизводительности", ПараметрыОценкиПроизводительности);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти