
////////////////////////////////////////////////////
//// Общий модуль "КР_ДополнительныеДействияВМагазине"
//// Создан: 16.05.2023, Федотов А.М., КРОК, JIRA№ A2105505-1657
//// Общие доработки документов. 
//// Блокирование документов межскладского перемещения во избежание конфликтов при интеграции Магазин-Магазин


#Область ПрограммныйИнтерфейс

Процедура ПроверитьЗаблокироватьФормуДокументаВМагазине(Форма, ИмяРеквизитаСклад = "Склад") Экспорт

	ЗначениеСклада = Форма.Объект[ИмяРеквизитаСклад];	
	//A2105505-2188
	//ИзменениеДоступно = ИзменениеПоСкладуДоступно(ЗначениеСклада);
	ИзменениеДоступно = ?(ЗначениеСклада=Справочники.Склады.ПустаяСсылка(), Истина, ИзменениеПоСкладуДоступно(ЗначениеСклада));
	
	Если Не ИзменениеДоступно Тогда
		ТекстСообщения = НСтр("ru='Изменение объекта не может быть выполнено в базе магазина'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Форма.ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьЗаблокироватьЗаписьДокументаВМагазине(Источник, Отказ, ИмяРеквизитаСклад = "Склад") Экспорт

	Если Источник.ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("КР_РазрешитьЗаписьДокументаВМагазине")
		Или Источник.ДополнительныеСвойства.Свойство("КР_ПропуститьВыгрузкуОбъектаВRabbitMQ") Тогда
		Возврат;		
	КонецЕсли;
	
	ЗначениеСклада = Источник[ИмяРеквизитаСклад];	
	ИзменениеДоступно = ИзменениеПоСкладуДоступно(ЗначениеСклада);
	
	Если Не ИзменениеДоступно Тогда
		ТекстСообщения = НСтр("ru='Запись объекта не может быть выполнена в базе магазина'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИзменениеПоСкладуДоступно(Склад)

	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат Истина;	
	КонецЕсли;
	
	Если КР_ДополнительныеНастройкиПовтИсп.БазаЯвляетсяЦентральной() Тогда
		Возврат Истина;		
	КонецЕсли;
	
	МассивСкладовТекущейБазы = КР_ДополнительныеНастройкиПовтИсп.МассивЗначений("СкладыТекущейБазыДанных", Ложь);
	
	ИзменениеДоступно = МассивСкладовТекущейБазы.Найти(Склад) <> Неопределено;
	
	Возврат ИзменениеДоступно;
	
КонецФункции

#КонецОбласти