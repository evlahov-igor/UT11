///////////////////////////////////////////////////
//// Общий модуль "КР_РаботаСКоробамиСервер"
//// Создан: 22.08.2022,  Мельников А.А.,  КРОК,  Jira№ A2105505-384

#Область ПрограммныйИнтерфейс

#Область ДополнительныеОбработчикиСобытийЭлементовФормы  

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384
// Добавляется в событие ТЧ изменения реквизита
// Параметры:
// Элемент - Из события
// ИмяРеквизита - Строка - Имя колонки ТЧ Объекта для которого вызывется событие
// ЭтаФорма - Форма из которой вызывается событие
// ОписаниеОповещенияПослеПодтверждения - (Необязательный) вспомогательное, для реализации частных случаев
// ИмяСобытияИзменения - Строка -(Необязательный) Имя метода которым реализуется типовое событие.
// 
Процедура ПриОкончанииРедактированияРеквизитаСтроки(Элемент, 
													ИмяРеквизита, 
													ЭтаФорма, 
													ОписаниеОповещенияПослеПодтверждения = Неопределено, 
													ИмяСобытияИзменения = Неопределено) Экспорт

	Если ЗначениеПараметраРаботыСКоробами(ЭтаФорма, "ФлагПрограммнойОбработкиИзмененияРеквизита") = Истина Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ЭтаФорма.Элементы.Товары.ТекущиеДанные; 
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Не ЭтоСтрокаКороба(ТекущиеДанные) Тогда
		Возврат;
	КонецЕсли;   
	
	ТекущийКороб = ТекущиеДанные.КР_Короб;
	
	Если ОписаниеОповещенияПослеПодтверждения = Неопределено Тогда    
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Короб", ТекущийКороб);
		ПараметрыОповещения.Вставить("ЭтаФорма", ЭтаФорма);
		ПараметрыОповещения.Вставить("Элемент", Элемент);
		ПараметрыОповещения.Вставить("ТекущиеДанные", ТекущиеДанные);
		ПараметрыОповещения.Вставить("ИмяРеквизита", ИмяРеквизита); 
		ПараметрыОповещения.Вставить("ИмяСобытияИзменения", ИмяСобытияИзменения);
		ОписаниеОповещенияПослеПодтверждения = Новый ОписаниеОповещения(
			"ИзменитьРеквизитВСтрокеТЧПоВсемуКоробуПослеПодтверждения", ЭтотОбъект, ПараметрыОповещения);
	КонецЕсли;
	               		
	ПоказатьВопрос(ОписаниеОповещенияПослеПодтверждения, 
					НСтр("ru = 'Будут изменены все строки товаров с текущим коробом.	
						|Принять изменение?'"), 
							РежимДиалогаВопрос.ДаНет);   	
	
КонецПроцедуры  // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384  
// Добавляется в событие ТЧ изменения реквизита, устанавливает доп ограничения   
// Параметры:
// Элемент - Из события
// ЭтаФорма - Форма из которой вызывается событие
//
Процедура ТоварыПриАктивизацииСтроки(Элемент, ЭтаФорма)  Экспорт
	
	Элементы = ЭтаФорма.Элементы;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные; 
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоСтрокаКороба = ЭтоСтрокаКороба(ТекущиеДанные); 
	
	Если ЭтоСтрокаКороба = ЗначениеПараметраРаботыСКоробами(ЭтаФорма, "ПредыдущаяСтрокаЭтоКороб") Тогда
		Возврат;		
	КонецЕсли;                                                                                        
	УстановитьЗначениеПараметраРаботыСКоробами(ЭтаФорма, "ПредыдущаяСтрокаЭтоКороб", ЭтоСтрокаКороба);
	
	// Тут можно указывать все вне зависимости от их наличия на конкретной форме.
	// Помимо этого, ТолькоПросмотр для колонок ТЧ устанавливается 
	//		в методе КР_РаботаСКоробамиСервер.УстановитьУсловноеОформлениеДополнительно
	ЭлементыНедоступныеУКороба = Новый Массив;
	ЭлементыНеДоступныеУКороба.Добавить("ТоварыКонтекстноеМенюРазбитьСтроку");
	ЭлементыНеДоступныеУКороба.Добавить("ТоварыРазбитьСтроку");
	ЭлементыНеДоступныеУКороба.Добавить("ТоварыЗаполнитьПодразделение");
	ЭлементыНеДоступныеУКороба.Добавить("ТоварыКонтекстноеМенюСоставНабора");
	ЭлементыНеДоступныеУКороба.Добавить("ТоварыСоставНабора");
	ЭлементыНеДоступныеУКороба.Добавить("ТоварыПоискПоШтрихкоду");
		
	РеквизитыИзКэша = ЗначениеПараметраРаботыСКоробами(ЭтаФорма, "НедоступныеРеквизитыИзКэша");
	Если Не ЗначениеЗаполнено(РеквизитыИзКэша) Тогда
		РеквизитыИзКэша = Новый Массив; 		
		Для Каждого ИмяЭлемента Из ЭлементыНедоступныеУКороба Цикл
			Если ЭтаФорма.Элементы.Найти(ИмяЭлемента) <> Неопределено Тогда
				РеквизитыИзКэша.Добавить(ИмяЭлемента);	
			КонецЕсли;		
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого ИмяЭлемента Из РеквизитыИзКэша Цикл	
		Элементы[ИмяЭлемента].Доступность = Не ЭтоСтрокаКороба;		
	КонецЦикла;
	
КонецПроцедуры  // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384  

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384
// Требуется ограничить возможности редактирования ТЧ от заполненности в строке Короба   
// Параметры:
// ЭтаФорма - Форма из которой вызывается событие
// Элемент - Из события
// Отказ - Булево - из события
// Копирование - Булево - из события
// Родитель - из события
// Группа - из события
//
Процедура ТоварыПередНачаломДобавленияДополнительно(ЭтаФорма, Элемент, Отказ, Копирование, Родитель, Группа) Экспорт
	
	ТекущиеДанные = ЭтаФорма.Элементы.Товары.ТекущиеДанные; 
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если ЭтоСтрокаКороба(ТекущиеДанные) И Копирование Тогда
		Отказ = Истина;
	КонецЕсли;
	           
КонецПроцедуры // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384
// Требуется ограничить возможности редактирования ТЧ от заполненности в строке Короба
// Параметры:
// ЭтаФорма - Форма из которой вызывается событие
// Элемент - Из события
// Отказ - Булево - из события
//
Процедура ТоварыПередУдалениемДополнительно(ЭтаФорма, Элемент, Отказ) Экспорт
	
	ВыделенныеСтроки = ЭтаФорма.Элементы.Товары.ВыделенныеСтроки;

	ЕстьСтрокиКороба = Ложь; 
	МассивЗатронутыхКоробов = Новый Массив; 
	КоличествоВыделенныхСтрокСКоробами = 0;
	Для Каждого Идентификатор Из ВыделенныеСтроки Цикл
		СтрокаТЧ = ЭтаФорма.Объект.Товары.НайтиПоИдентификатору(Идентификатор);
		Если ЭтоСтрокаКороба(СтрокаТЧ) Тогда			
			МассивЗатронутыхКоробов.Добавить(СтрокаТЧ.КР_Короб); 
			КоличествоВыделенныхСтрокСКоробами = КоличествоВыделенныхСтрокСКоробами + 1;
		КонецЕсли;
	КонецЦикла; 
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивЗатронутыхКоробов); 
	
	ЕстьСтрокиКороба = МассивЗатронутыхКоробов.Количество() > 0;
	ВсеСтрокиКоробов = МассивСтрокКороба(МассивЗатронутыхКоробов, ЭтаФорма);
	
	Если ЕстьСтрокиКороба И ВсеСтрокиКоробов.Количество() > КоличествоВыделенныхСтрокСКоробами Тогда
		Отказ = Истина;       
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Короба", МассивЗатронутыхКоробов);
		ПараметрыОповещения.Вставить("ЭтаФорма", ЭтаФорма);
		ПараметрыОповещения.Вставить("Элемент", Элемент);
		ПараметрыОповещения.Вставить("ВсеСтрокиКоробов", ВсеСтрокиКоробов);
        ОписаниеОповещенияПослеПодтверждения = Новый ОписаниеОповещения("ПослеПодтвержденияУдаленияКороба", 
																				ЭтотОбъект, ПараметрыОповещения);	
		ПоказатьВопрос(ОписаниеОповещенияПослеПодтверждения, 
					НСтр("ru = 'Будут удалены все строки товаров с текущим коробом.	
						|Удалить текущий короб из списка товаров?'"), 
							РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 

// << 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
// Процедура анализирует нужно ли задать вопрос и выполнить не типовую массовую обработку строку в разрезе Короба
// Параметры:
//  ЭтаФорма - ФормаКлиентскогоПриложения - Форма из которой вызывается событие.
//  Короб - ДокументСсылка.УпаковочныйЛист - Короб текущей строки.
//  Отменено - Булево - признак отмены строки.
//
Процедура ТоварыОтмененоПриИзменении(ЭтаФорма, Короб, Отменено) Экспорт

	Если ЭтаФорма.КР_ЭтоМассоваяОтменаСтрок Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьОтмененоПриИзмененииДляВсеКоробов = Ложь;
	
	Если ЗначениеЗаполнено(Короб) Тогда
		СтруктураДанных			= Новый Структура("КР_Короб, Отменено", Короб, НЕ Отменено);
		СтрокиСТекущемКоробом	= ЭтаФорма.Объект.Товары.НайтиСтроки(СтруктураДанных);
		Если СтрокиСТекущемКоробом.Количество() > 0 Тогда
			ВыполнитьОтмененоПриИзмененииДляВсеКоробов = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыполнитьОтмененоПриИзмененииДляВсеКоробов Тогда
		Оповещение = Новый ОписаниеОповещения("КР_ОтменитьВсеТекущиеКоробаОтвет", ЭтаФорма, СтруктураДанных);
		ТекстВопроса = НСтр("ru='Значение поля будет изменено у всех строк короба. Продолжить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры // << 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс 

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384
// Обработчик оповещения, вызывается из КР_ТоварыПередУдалениемДополнительно.
// Удаляет сразу несколько строк с одним и тем же коробом. 
// Параметры:
// Результат - КодВозвратаДиалога 
// ДополнительныеПараметры - Структура
//
Процедура ПослеПодтвержденияУдаленияКороба(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	ОбъектТовары = ДополнительныеПараметры.ЭтаФорма.Объект.Товары;

	СтрокиКороба = ДополнительныеПараметры.ВсеСтрокиКоробов; 
	
	Для Каждого СтрокаКороба Из  СтрокиКороба Цикл       
		Если ОбъектТовары.Индекс(СтрокаКороба) <> -1 Тогда
			ОбъектТовары.Удалить(СтрокаКороба);	            
		КонецЕсли;
	КонецЦикла;   
	
КонецПроцедуры // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 
// Параметры:
// Результат - КодВозвратаДиалога 
// ДополнительныеПараметры - Структура
//
Процедура ИзменитьРеквизитВСтрокеТЧПоВсемуКоробуПослеПодтверждения(Результат, ДополнительныеПараметры) Экспорт

	ЭтаФорма = ДополнительныеПараметры.ЭтаФорма;
	Элемент = ДополнительныеПараметры.Элемент;	
	ИмяТЧ = ДополнительныеПараметры.Элемент.Родитель.Заголовок;
	ИмяРеквизита = ДополнительныеПараметры.ИмяРеквизита; 
	ТекущиеДанные  = ДополнительныеПараметры.ТекущиеДанные;
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		КэшированныеДанные = ЭтаФорма.КэшированныеСтроки[ИмяТЧ];
		ЗаполнитьЗначенияСвойств(ТекущиеДанные, КэшированныеДанные);
		Возврат;	
	КонецЕсли;   
	
	УстановитьЗначениеПараметраРаботыСКоробами(ЭтаФорма, "ФлагПрограммнойОбработкиИзмененияРеквизита", Истина);
	СтрокиКороба = МассивСтрокКороба(ДополнительныеПараметры.Короб, ЭтаФорма);
	Для Каждого СтрокаКороба Из СтрокиКороба Цикл
		СтрокаКороба[ИмяРеквизита] = ТекущиеДанные[ИмяРеквизита];
		Если ЗначениеЗаполнено(ДополнительныеПараметры.ИмяСобытияИзменения) Тогда 
			Элемент.Родитель.ТекущаяСтрока = СтрокаКороба.ПолучитьИдентификатор(); 
			ЭтаФорма.КР_ВызывательСобытий(ДополнительныеПараметры.ИмяСобытияИзменения, Элемент.Родитель.ТекущийЭлемент); 
		КонецЕсли;   
	КонецЦикла;
	УстановитьЗначениеПараметраРаботыСКоробами(ЭтаФорма, "ФлагПрограммнойОбработкиИзмененияРеквизита", Ложь);
	 
КонецПроцедуры // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 
// Возвращает массив строк ТЧ Товары Объекта с заданным коробом/коробами  
// Параметры:
// Короба - ДокументСсылка.УпаковочныйЛист или Массив из  ДокументСсылка.УпаковочныйЛист 
// ЭтаФорма - форма из которой идет вызов
// Возвращаемое значение:
// Массив из строк ТЧ Товары
//
Функция МассивСтрокКороба(Короба, ЭтаФорма) 
		
	Результат = Новый Массив;    
	ОбъектТовары = ЭтаФорма.Объект.Товары;
	
	Если ТипЗнч(Короба) = Тип("Массив") Тогда  
		Для Каждого Короб Из Короба Цикл                                  
			Отбор = Новый Структура("КР_Короб", Короб);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, ОбъектТовары.НайтиСтроки(Отбор), Истина);
		КонецЦикла;
	Иначе           
		Отбор = Новый Структура("КР_Короб", Короба);
		Результат = ОбъектТовары.НайтиСтроки(Отбор);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции  // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 
// Проверяет Является ли переданная строка строкой в которой заполнен реквизит КР_Короб
// СтрокаТЧ - СтрокаТабличнойЧасти
// Возвращаемое значение:
// Булево
//
Функция ЭтоСтрокаКороба(СтрокаТЧ)   

	ДанныеСтрокиВрем = Новый Структура("КР_Короб", Неопределено);
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиВрем, СтрокаТЧ);	
	
	Возврат  ЗначениеЗаполнено(ДанныеСтрокиВрем.КР_Короб);
	
КонецФункции // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384
 
// << 23.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384
// Дабы не добавлять кучу реквизитов на форму в которых буду кэшировать нужные мне значения, закину структуру.
// ЭтаФорма - форма из которой вызывается
// ИмяПараметра - Строка
// Возвращаемое значение:
// Любой тип, Неопределено если параметр не был проинициализирован ранее.
Функция ЗначениеПараметраРаботыСКоробами(ЭтаФорма, ИмяПараметра)
	
	ИмяРеквизитаПараметров = "КР_ПараметрыРаботыСКоробами";
	Если Не ЗначениеЗаполнено(ЭтаФорма[ИмяРеквизитаПараметров]) Тогда
		ЭтаФорма[ИмяРеквизитаПараметров] = Новый Структура;
	КонецЕсли;
	
	КР_ПараметрыРаботыСКоробами = ЭтаФорма[ИмяРеквизитаПараметров]; 
	                                          
	Если КР_ПараметрыРаботыСКоробами.Свойство(ИмяПараметра) = Ложь Тогда
		КР_ПараметрыРаботыСКоробами.Вставить(ИмяПараметра, Неопределено);
	КонецЕсли;
	
	Возврат КР_ПараметрыРаботыСКоробами[ИмяПараметра];
	
КонецФункции  // >> 23.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384    

// << 23.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384
// Дабы не добавлять кучу реквизитов на форму в которых буду кэшировать нужные мне значения, закину структуру. 
// ЭтаФорма - форма из которой вызывается
// ИмяПараметра - Строка
// Значение - Любой тип
//
Процедура УстановитьЗначениеПараметраРаботыСКоробами(ЭтаФорма, ИмяПараметра, Значение)
	
	Если Не ЗначениеЗаполнено(ЭтаФорма["КР_ПараметрыРаботыСКоробами"]) Тогда
		ЭтаФорма["КР_ПараметрыРаботыСКоробами"] = Новый Структура;
	КонецЕсли;
	
	КР_ПараметрыРаботыСКоробами = ЭтаФорма["КР_ПараметрыРаботыСКоробами"]; 
	                                          
	Если КР_ПараметрыРаботыСКоробами.Свойство(ИмяПараметра) = Ложь Тогда
		КР_ПараметрыРаботыСКоробами.Вставить(ИмяПараметра, Неопределено);
	КонецЕсли;
	
	КР_ПараметрыРаботыСКоробами[ИмяПараметра] = Значение;
	
КонецПроцедуры // >> 23.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384

#КонецОбласти    
