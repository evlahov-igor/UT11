
#Область ПрограммныйИнтерфейс

// Заполняет значения реквизитов учета импорта и прослеживаемости по данным номенклатуры.
//
// Параметры:
//	Номенклатура - СправочникОбъект.Номенклатура, ДанныеФормыСтруктура - обрабатываемый элемент номенклатуры.
//	РеквизитыПрослеживаемости - см. НоменклатураКлиентСервер.РеквизитыПрослеживаемостиПоУмолчанию.
//
Процедура ЗаполнитьРеквизитыИмпортаИПрослеживаемости(Номенклатура, РеквизитыПрослеживаемости) Экспорт
	
	//++ Локализация
	СтранаПроисхожденияРФ = Номенклатура.СтранаПроисхождения = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	
	Номенклатура.ПрослеживаемыйТовар = Не СтранаПроисхожденияРФ
										И РеквизитыПрослеживаемости.ПрослеживаемыйТовар;
	
	ЗаполнитьВестиУчетПоГТД(Номенклатура);
	//-- Локализация
	
	Возврат;
	
КонецПроцедуры

// Заполняет значение реквизита 'ВестиУчетПоГТД' по данным номенклатуры.
//
// Параметры:
//	Номенклатура - СправочникОбъект.Номенклатура, ДанныеФормыСтруктура - обрабатываемый элемент номенклатуры.
//
Процедура ЗаполнитьВестиУчетПоГТД(Номенклатура) Экспорт
	
	//++ Локализация
	СтранаПроисхожденияРФ = Номенклатура.СтранаПроисхождения = ПредопределенноеЗначение("Справочник.СтраныМира.Россия");
	
	Номенклатура.ВестиУчетПоГТД = Номенклатура.ПрослеживаемыйТовар
									Или Не СтранаПроисхожденияРФ
										И (ЗначениеЗаполнено(Номенклатура.СтранаПроисхождения)
											Или Номенклатура.ВестиУчетПоГТД);
	//-- Локализация
	
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИспользоватьСтатусСерийМожноУказать(Использовать, ПараметрыУказанияСерий) Экспорт
	
	//++ Локализация
	ИнтеграцияИСКлиентСерверПереопределяемый.ИспользоватьСтатусСерийМожноУказать(
		Использовать,
		ПараметрыУказанияСерий);
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти
