
////////////////////////////////////////////////////
//// Общий модуль "КР_УправлениеДоступом"
//// Создан: 06.10.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-325
//// Разработка по ФДР 21.010 Разработка НСИ для внутренней логистики

#Область ПрограммныйИнтерфейс

Процедура ПриЗаполненииПоставляемыхПрофилейГруппДоступа(ОписанияПрофилей, ПараметрыОбновления) Экспорт
	
	ИндексПрофилей = ИндексироватьПрофили(ОписанияПрофилей, "Имя");
	
	#Область Дополнить_профиль_МенеджерПоДоставке
	ОписаниеПрофиля = НайтиПрофиль("МенеджерПоДоставке", ОписанияПрофилей, ИндексПрофилей);	
	Если ОписаниеПрофиля <> Неопределено Тогда
		ДополнитьПрофиль(ОписаниеПрофиля, "МенеджерПоДоставке");
	КонецЕсли;
	#КонецОбласти
	
КонецПроцедуры

// Позволяет указать объекты метаданных, для которых задана логика ограничения доступа к данным.
// В модулях менеджеров указанных списков размещается процедура обработчика, например:
//
//// Параметры:
////   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
////
//Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
//	
//  Ограничение.Текст =
//  "РазрешитьЧтениеИзменение
//  |ГДЕ
//  |	ЗначениеРазрешено(Организация)
//  |	И ЗначениеРазрешено(Контрагент)";
//	
//КонецПроцедуры
//
// Логика ограничения доступа к данным также может быть переопределена в процедуре
// УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.
//
// Параметры:
//  Списки - Соответствие из КлючИЗначение - списки с ограничением доступа:
//             * Ключ     - ОбъектМетаданных - список с ограничением доступа.
//             * Значение - Булево - Истина - текст ограничения в модуле менеджера,
//                                   Ложь   - текст ограничения в этом переопределяемом
//                          модуле в процедуре ПриЗаполненииОграниченияДоступа.
//
Процедура ПриЗаполненииСписковСОграничениемДоступа(Списки) Экспорт
	
	Списки.Вставить(Метаданные.Справочники.КР_ЗоныПересчетаТоваров, Истина);
	Списки.Вставить(Метаданные.Документы.КР_ЗональныйПересчетТоваров, Истина);
	
КонецПроцедуры

// Заполняет состав видов доступа, используемых при ограничении прав объектов метаданных.
// Если состав видов доступа не заполнен, отчет "Права доступа" покажет некорректные сведения.
//
// Обязательно требуется заполнить только виды доступа, используемые в шаблонах ограничения доступа
// явно, а виды доступа, используемые в наборах значений доступа, могут быть получены из текущего
// состояния регистра сведений НаборыЗначенийДоступа.
//
//  Для автоматической подготовки содержимого процедуры следует воспользоваться инструментами
// разработчика для подсистемы Управление доступом.
//
// Параметры:
//  Описание     - Строка - многострочная строка формата <Таблица>.<Право>.<ВидДоступа>[.Таблица объекта],
//                 например, "Документ.ПриходнаяНакладная.Чтение.Организации",
//                           "Документ.ПриходнаяНакладная.Чтение.Контрагенты",
//                           "Документ.ПриходнаяНакладная.Изменение.Организации",
//                           "Документ.ПриходнаяНакладная.Изменение.Контрагенты",
//                           "Документ.ЭлектронныеПисьма.Чтение.Объект.Документ.ЭлектронныеПисьма",
//                           "Документ.ЭлектронныеПисьма.Изменение.Объект.Документ.ЭлектронныеПисьма",
//                           "Документ.Файлы.Чтение.Объект.Справочник.ПапкиФайлов",
//                           "Документ.Файлы.Чтение.Объект.Документ.ЭлектронноеПисьмо",
//                           "Документ.Файлы.Изменение.Объект.Справочник.ПапкиФайлов",
//                           "Документ.Файлы.Изменение.Объект.Документ.ЭлектронноеПисьмо".
//                 Вид доступа Объект предопределен как литерал. Этот вид доступа используется в
//                 шаблонах ограничений доступа как "ссылка" на другой объект, по которому
//                 ограничивается текущий объект таблицы.
//                 Когда вид доступа "Объект" задан, также требуется задать типы таблиц,
//                 которые используются для этого вида доступа. Т.е. перечислить типы,
//                 которые соответствуют полю, использованному в шаблоне ограничения доступа
//                 в паре с видом доступа "Объект". При перечислении типов по виду доступа "Объект"
//                 нужно перечислить только те типы поля, которые есть
//                 у поля РегистрыСведений.НаборыЗначенийДоступа.Объект, остальные типы лишние.
// 
Процедура ПриЗаполненииВидовОграниченийПравОбъектовМетаданных(Описание) Экспорт
	
	Описание = Описание + "
	|Справочник.КР_ЗоныПересчетаТоваров.Чтение.Склады
	|Справочник.КР_ЗоныПересчетаТоваров.Изменение.Склады
	|Документ.КР_ЗональныйПересчетТоваров.Чтение.Объект.Справочник.КР_ЗоныПересчетаТоваров
	|Документ.КР_ЗональныйПересчетТоваров.Изменение.Объект.Справочник.КР_ЗоныПересчетаТоваров
	|";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДополнитьПрофиль(ОписаниеПрофиля, ИмяПрофиля) Экспорт
	
	Если ИмяПрофиля = "МенеджерПоДоставке" Тогда
		ДополнитьПрофильМенеджерПоДоставке(ОписаниеПрофиля);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИндексироватьПрофили(ОписанияПрофилей, ПолеПрофиля = "Имя")
	
	Результат = Новый Соответствие;
	Для Индекс = 0 По ОписанияПрофилей.ВГраница() Цикл
		
		Профиль = ОписанияПрофилей[Индекс];
		Результат.Вставить(Профиль[ПолеПрофиля], Индекс);
		
	КонецЦикла;
	Возврат Результат;
	
КонецФункции

Функция НайтиПрофиль(ИмяПрофиля, ОписанияПрофилей, ИндексПрофилей)

	ИндексПрофиля = ИндексПрофилей[ИмяПрофиля];
	Если ИндексПрофиля = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат ОписанияПрофилей[ИндексПрофиля];
	
КонецФункции

#Область ЗаполнениеПрофилей

Процедура ДополнитьПрофильМенеджерПоДоставке(ОписаниеПрофиля)
	
	ОписаниеПрофиля.Роли.Добавить("КР_ДобавлениеИзменениеТарифовПеревозчика");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
