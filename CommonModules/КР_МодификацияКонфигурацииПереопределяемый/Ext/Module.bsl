////////////////////////////////////////////////////
//// Объект ОбщийМодуль.КР_МодификацияКонфигурацииПереопределяемый
//// Переопределяемые процедуры, вызываемые из обработчиков форм, таких как:
//// "ПриСозданииНаСервере", "ПриЧтенииНаСервере", "ПередЗаписьюНаСервере", 
//// "ПослеЗаписи", а также при изменении некоторых реквизитов табличной части,
//// таких как "Номенклатура", "Характеристика".
//// Создан: 30.09.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-570  
//// Комплексный рефакторинг: 09.08.2023 Марченко С.Н., КРОК, JIRA№A2105505-1953


#Область ПрограммныйИнтерфейс

#Область ЗаполнениеОбработчиковФормы

// Переопределяемая процедура, вызываемая из одноименного обработчика события формы.
//
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма, из обработчика события которой происходит вызов процедуры.
// 	Отказ - Булево -
// 	СтандартнаяОбработка - Булево - 
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ИмяФормыМассив = СтрРазделить(Форма.ИмяФормы, ".");
	
	Если ВРег(ИмяФормыМассив[ИмяФормыМассив.ВГраница()]) = "ФОРМАДОКУМЕНТА" Тогда
		
		ИмяДокумента = ИмяФормыМассив[1];
		
		ИзменитьСоставКолонокТабличныхЧастейДокумента(Форма, ИмяДокумента);
		
		// << 02.03.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-619
		ПриСозданииНаСервереДокументыЗапретИзмененияНомера(Форма);
		// >> 02.03.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-619
	КонецЕсли;
	
КонецПроцедуры

// Переопределяемая процедура, вызываемая из одноименного обработчика события формы.
//
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения - форма, из обработчика события которой происходит вызов процедуры.
// 	ТекущийОбъект - ДокументОбъект, СправочникОбъект - 
//
Процедура ПриЧтенииНаСервере(Форма, ТекущийОбъект) Экспорт
	
	Если ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.ОтчетОРозничныхПродажах")	Тогда	// #4045.. Фомин Д.Ю. 17.06.2024.
		
		Если РольДоступна("ПолныеПрава") Тогда
			
			Форма.Элементы.ТоварыЦена.ТолькоПросмотр = Ложь;
			Форма.Элементы.ТоварыСумма.ТолькоПросмотр = Ложь;
			Форма.Элементы.ТоварыСуммаНДС.ТолькоПросмотр = Ложь;
			Форма.Элементы.ОплатаПлатежнымиКартамиСумма.ТолькоПросмотр = Ложь;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// << 02.03.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-619
Процедура ПриСозданииНаСервереДокументыЗапретИзмененияНомера(Форма)
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	МассивТиповДокументов = ДокументыЗапретИзмененияНомера();
	
	ТипДокумента = ТипЗнч(Форма.Объект.Ссылка);
	
	Если Не МассивТиповДокументов.Найти(ТипДокумента) = Неопределено Тогда
		ЭлементНомер = Форма.Элементы.Найти("Номер");
		Если Не ЭлементНомер = Неопределено Тогда
			ЭлементНомер.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		ЭлементКод = Форма.Элементы.Найти("Код");
		Если Не ЭлементКод = Неопределено Тогда
			ЭлементКод.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // >> 02.03.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-619

Процедура ИзменитьСоставКолонокТабличныхЧастейДокумента(Форма, ИмяДокумента)
		
	Если ИмяДокумента = "ОтборРазмещениеТоваров" Тогда
		
		//
		ИмяТабличнойЧасти = "ТоварыОтбор"; 
		
		ДобавитьКолонкуШтрихкод(Форма, ИмяТабличнойЧасти);  
		ЗаменитьКолонкуАртикул(Форма, ИмяТабличнойЧасти);
		
		//
		ИмяТабличнойЧасти = "ТоварыРазмещение";

		ДобавитьКолонкуШтрихкод(Форма, ИмяТабличнойЧасти);
		ЗаменитьКолонкуАртикул(Форма, ИмяТабличнойЧасти);
		
	ИначеЕсли ИмяДокумента = "ПересортицаТоваров" Тогда
		
 		//
		ДобавитьКолонкуШтрихкод(Форма, , "НоменклатураСписание", "ХарактеристикаСписание");
		ЗаменитьКолонкуАртикул(Форма);
		
		ДобавитьКолонкуШтрихкод(Форма, , "НоменклатураОприходование", "ХарактеристикаОприходование");
		ЗаменитьКолонкуАртикул(Форма, , "НоменклатураОприходованиеАртикул");

	Иначе
		ДобавитьКолонкуШтрихкод(Форма);
		ЗаменитьКолонкуАртикул(Форма);
	КонецЕсли;
		
	//КонецЕсли;
	
КонецПроцедуры

#Область РаботаСКолонкойШтрихкод

Процедура ДобавитьКолонкуШтрихкод(Форма, 
	ИмяТабличнойЧасти = "Товары",
	ИмяЭлементаНоменклатура = "Номенклатура", ИмяЭлементаХарактеристика = "Характеристика")
	
	ФормаЭлементы = Форма.Элементы;
	 
	ЭлементНоменклатура = ФормаЭлементы.Найти(ИмяТабличнойЧасти + ИмяЭлементаНоменклатура);
	ЭлементХарактеристика = ФормаЭлементы.Найти(ИмяТабличнойЧасти + ИмяЭлементаХарактеристика); 
	Если ЭлементНоменклатура = Неопределено
		Или ЭлементХарактеристика = Неопределено Тогда 
		Возврат;
	КонецЕсли;	
	 
	ПутьКДанным = "%1.КР_Штрихкод";
	ПутьКДанным = СтрШаблон(ПутьКДанным, ЭлементХарактеристика.ПутьКДанным);  
	
	ШтрихкодЭлемент = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(Форма, 
		ПутьКДанным, , ЭлементНоменклатура);  

	ШтрихкодЭлемент.Ширина = 16;
	ШтрихкодЭлемент.ТолькоПросмотр = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКолонкойАртикул

Процедура ЗаменитьКолонкуАртикул(Форма, 
	ИмяТабличнойЧасти = "Товары", ИмяЭлементаАртикул = "НоменклатураАртикул")
		
	// У типового элемента Артикул табличной части установлена пользовательская видимость = Ложь
	// Принудительно устанавливаем Видимость = Ложь, чтобы элемент не отображался в настройках формы
	// Создаем новый элемент, путь к данным устанавливаем = пути к данным типового элемента
	
	ФормаЭлементы = Форма.Элементы;
	
	СтарыйЭлементАртикул = ФормаЭлементы.Найти(ИмяТабличнойЧасти + ИмяЭлементаАртикул);
	Если СтарыйЭлементАртикул = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	СтарыйЭлементАртикул.Видимость = Ложь;     
	
	НовыйЭлементАртикул = ФормаЭлементы.Вставить(
		"КР_" + СтарыйЭлементАртикул.Имя, 
		Тип(СтарыйЭлементАртикул), 
		СтарыйЭлементАртикул.Родитель, СтарыйЭлементАртикул);
		
	НовыйЭлементАртикул.ПутьКДанным = СтарыйЭлементАртикул.ПутьКДанным;	
	НовыйЭлементАртикул.Вид = СтарыйЭлементАртикул.Вид;	
	
	НовыйЭлементАртикул.АвтоМаксимальнаяШирина = СтарыйЭлементАртикул.АвтоМаксимальнаяШирина;
	НовыйЭлементАртикул.МаксимальнаяШирина = СтарыйЭлементАртикул.МаксимальнаяШирина;
	НовыйЭлементАртикул.Ширина = СтарыйЭлементАртикул.Ширина;
	
КонецПроцедуры

// << 02.03.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-619
Функция ДокументыЗапретИзмененияНомера()
	
	МассивДокументов = Новый Массив;
	МассивДокументов.Добавить(Тип("ДокументСсылка.АктОРасхожденияхПослеПеремещения"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ВнутреннееПотреблениеТоваров"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ВозвратТоваровОтКлиента"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ЗаказНаВнутреннееПотребление"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ЗаказНаПеремещение"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ИзменениеАссортимента"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ИнвентаризационнаяОпись"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ИнвентаризацияНаличныхДенежныхСредств"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.КорректировкаРеализации"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ЛистКассовойКниги"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ОприходованиеИзлишковТоваров"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ОтборРазмещениеТоваров"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ПеремещениеТоваров"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ПересортицаТоваров"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ПересчетТоваров"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ПоступлениеТоваровОтХранителя"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ПриобретениеТоваровУслуг"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.СписаниеНедостачТоваров"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.УпаковочныйЛист"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.КР_ДвижениеКоробов"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.КР_УстановкаМестРазмещения"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.КР_УстановкаМинимальнойПрезентации"));
	МассивДокументов.Добавить(Тип("ДокументСсылка.КР_УстановкаПриоритетовРазмеров"));
	
	Возврат МассивДокументов;
	
КонецФункции // >> 02.03.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-619

#КонецОбласти

#КонецОбласти
