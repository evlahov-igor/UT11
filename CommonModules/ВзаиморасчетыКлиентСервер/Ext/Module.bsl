#Область ПрограммныйИнтерфейс

// Возвращает пустую структуру параметров формирования надписи Валюты и курсы документа.
//
// Возвращаемое значение:
//  Структура - параметры для заполнения в форме, где:
//   * НеПоказыватьРасчеты - Булево.
//   * ВалютаДокумента - СправочникСсылка.Валюты.
//   * ВалютаВзаиморасчетов - СправочникСсылка.Валюты.
//   * ВалютаРеглУчета - СправочникСсылка.Валюты.
//   * СуммаДокумента - Число.
//   * СуммаВзаиморасчетов - Число.
//   * КурсЧислитель - Число.
//   * КурсЗнаменатель - Число.
//
Функция ПараметрыНадписиВалюты() Экспорт
	
	Возврат Новый Структура("НеПоказыватьРасчеты,
							|ВалютаДокумента,
							|ВалютаВзаиморасчетов,
							|ВалютаРеглУчета,
							|СуммаДокумента,
							|СуммаВзаиморасчетов,
							|КурсЧислитель,
							|КурсЗнаменатель", ЛОЖЬ);
	
КонецФункции

// Рассчитывает конечное сальдо в табличной части Группировка финансовых инструментов документа СверкаВзаиморасчетов.
//
// Параметры:
//  Группировка - ДанныеФормыКоллекция - Текущая строка табличной части группировки.
//  ДетальныеЗаписи - ДанныеФормыКоллекция - Табличная часть детальных записей финансовых инструментов.
//
Процедура РассчитатьКонечноеСальдоПоФинансовымИнструментам(Группировка, ДетальныеЗаписи) Экспорт
	
	Отбор = Новый Структура("Договор");
	ЗаполнитьЗначенияСвойств(Отбор, Группировка);
	
	ДеталиГруппировки = ДетальныеЗаписи.НайтиСтроки(Отбор);
	
	ОборотАктив = Новый Структура("Дт, Кт", 0, 0);
	ОборотПассив = Новый Структура("Дт, Кт", 0, 0);
	Для Каждого Запись Из ДеталиГруппировки Цикл
		Если Группировка.ТипРасчетов = ПредопределенноеЗначение("Перечисление.ТипыРасчетовСПартнерами.РасчетыСАрендодателем") Тогда
			ДобавитьОборот(ОборотПассив, Запись, "АрендныеОбязательства");
			ДобавитьОборот(ОборотАктив, Запись, "ОбеспечительныйПлатеж");
			ДобавитьОборот(ОборотПассив, Запись, "УслугаПоАренде");
			ДобавитьОборот(ОборотПассив, Запись, "ВыкупнаяСтоимость");
			
		ИначеЕсли Группировка.ТипРасчетов = ПредопределенноеЗначение("Перечисление.ТипыРасчетовСПартнерами.РасчетыСКредитором") Тогда
			ДобавитьОборот(ОборотПассив, Запись, "ОсновнойДолг");
			ДобавитьОборот(ОборотПассив, Запись, "Проценты");
			ДобавитьОборот(ОборотПассив, Запись, "Комиссия");
			
		Иначе
			ДобавитьОборот(ОборотАктив, Запись, "ОсновнойДолг");
			ДобавитьОборот(ОборотАктив, Запись, "Проценты");
			ДобавитьОборот(ОборотАктив, Запись, "Комиссия");
			
		КонецЕсли;
	КонецЦикла;
	
	Группировка.КонечноеСальдоДт = Группировка.НачальноеСальдоДт + ОборотАктив.Дт - ОборотАктив.Кт;
	Группировка.КонечноеСальдоКт = Группировка.НачальноеСальдоКт - ОборотПассив.Дт + ОборотПассив.Кт;
	Группировка.ОборотПриход = ОборотАктив.Дт + ОборотПассив.Дт;
	Группировка.ОборотРасход = ОборотАктив.Кт + ОборотПассив.Кт;
	
	// установим конечное сальдо, если не было начального
	Если Группировка.НачальноеСальдоДт = 0 Тогда
		Группировка.КонечноеСальдоДт = ОборотАктив.Дт - ОборотАктив.Кт;
	КонецЕсли;
		
	Если Группировка.НачальноеСальдоКт = 0 Тогда
		Группировка.КонечноеСальдоКт = -ОборотПассив.Дт + ОборотПассив.Кт;
	КонецЕсли;
	
	Если Группировка.ТипРасчетов <> ПредопределенноеЗначение("Перечисление.ТипыРасчетовСПартнерами.РасчетыСАрендодателем") Тогда
		КонечноеСальдо = Группировка.КонечноеСальдоДт - Группировка.КонечноеСальдоКт;
		Если КонечноеСальдо > 0 Тогда
			Группировка.КонечноеСальдоДт = КонечноеСальдо;
			Группировка.КонечноеСальдоКт = 0;
		Иначе
			Группировка.КонечноеСальдоДт = 0;
			Группировка.КонечноеСальдоКт = -КонечноеСальдо;
		КонецЕсли;
	КонецЕсли;
	
	Если Группировка.КонечноеСальдоДт < 0 Тогда
		Группировка.КонечноеСальдоКт = Группировка.КонечноеСальдоКт - Группировка.КонечноеСальдоДт;
		Группировка.КонечноеСальдоДт = 0;
	КонецЕсли;
	Если Группировка.КонечноеСальдоКт < 0 Тогда
		Группировка.КонечноеСальдоДт = Группировка.КонечноеСальдоДт - Группировка.КонечноеСальдоКт;
		Группировка.КонечноеСальдоКт = 0;
	КонецЕсли;
	
КонецПроцедуры

// Рассчитывает конечное сальдо в табличной части Группировка взаиморасчетов документа СверкаВзаиморасчетов.
//
// Параметры:
//  Группировка - ДанныеФормыКоллекция - Текущая строка табличной части группировки.
//  ДетальныеЗаписи - ДанныеФормыКоллекция - Табличная часть детальных записей взаиморасчетов.
//  РеглСуммы - Булево - Использовать для расчета ресурсы с регл. суммами.
//
Процедура РассчитатьКонечноеСальдоПоВзаиморасчетам(Группировка, ДетальныеЗаписи, РеглСуммы = Ложь) Экспорт
	
	Отбор = Новый Структура("ТипРасчетов,ОбъектРасчетов,Партнер,Договор");
	ЗаполнитьЗначенияСвойств(Отбор, Группировка);
	
	СуммаДолг = ?(РеглСуммы, "СуммаДолгРегл", "СуммаДолг");
	СуммаАванс = ?(РеглСуммы, "СуммаАвансРегл", "СуммаАванс");
	ОборотПриход = ?(РеглСуммы, "ОборотПриходРегл", "ОборотПриход");
	ОборотРасход = ?(РеглСуммы, "ОборотРасходРегл", "ОборотРасход");
	НачальноеСальдо = ?(РеглСуммы, "НачальноеСальдоРегл", "НачальноеСальдо");
	КонечноеСальдо = ?(РеглСуммы, "КонечноеСальдоРегл", "КонечноеСальдо");
	
	ДеталиГруппировки = ДетальныеЗаписи.НайтиСтроки(Отбор);
	Обороты = Новый Структура("Приход, Расход",0,0);
	Для Каждого Запись Из ДеталиГруппировки Цикл
			
		Если Запись[СуммаДолг] > 0 Тогда
			Обороты.Приход = Обороты.Приход + Запись[СуммаДолг];
		Иначе
			Обороты.Расход = Обороты.Расход + (-Запись[СуммаДолг]);
		КонецЕсли;
		
		Если Запись[СуммаАванс] > 0 Тогда
			Обороты.Расход = Обороты.Расход + Запись[СуммаАванс];
		Иначе
			Обороты.Приход = Обороты.Приход + (-Запись[СуммаАванс]);
		КонецЕсли;
		
	КонецЦикла;
	
	Группировка[ОборотПриход] = Обороты.Приход;
	Группировка[ОборотРасход] = Обороты.Расход;
	Группировка[КонечноеСальдо] = Группировка[НачальноеСальдо] + Группировка[ОборотПриход] - Группировка[ОборотРасход];
	
КонецПроцедуры

// Возвращет список типов договоро договоров с клиентами.
// 
// Возвращаемое значение:
// 	СписокЗначений - Список типов договоров с контрагентом, как с клиентом.
Функция ТипыДоговоровСКлиентом() Экспорт
	
	Типы = Новый СписокЗначений;
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПокупателем"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СКомиссионером"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СХранителем"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СДавальцем"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СКомитентомНаЗакупку"));
	
	Возврат Типы;
КонецФункции

// Возвращет список типов договоро договоров с поставщиками.
// 
// Возвращаемое значение:
// 	СписокЗначений - Список типов договоров с контрагентом, как с поставщиком.
Функция ТипыДоговоровСПоставщиком() Экспорт
	
	Типы = Новый СписокЗначений;
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПоставщиком"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.Импорт"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПереработчиком"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПереработчиком2_5"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПоклажедателем"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СКомитентом"));
	Типы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыДоговоров.ВвозИзЕАЭС"));
	
	Возврат Типы;
КонецФункции

#Область Модульность

// Служебная процедура, заполняет текст гиперссылки правил оплаты 
// 
// Параметры:
//	Форма       - ФормаКлиентскогоПриложения - Договор, указанный в документе:
//	 * Элементы - ЭлементыФормы - элементы вызывающей формы
//	СтруктураПараметров - см. ВзаиморасчетыСервер.ПараметрыМеханизма
//	СистемныеНастройки  - Структура - Системные настройки из дополненных параметров, если уже получены.
//
// Возвращаемое значение:
//  Строка - Форматируемая строка этапов оплаты
Функция ФорматируемаяСтрокаЭтаповОплаты(Форма, СтруктураПараметров, СистемныеНастройки) Экспорт
	
	ФормаОплаты               = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ФормаОплаты);
	ГрафикОплаты              = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ГрафикОплаты);
	ПорядокРасчетов           = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ПорядокРасчетов);
	ЭтапыОплаты               = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ПутьКДаннымТЧЭтапыОплаты);
	ДатаПлатежа               = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ДатаПлатежа);
	Если ЗначениеЗаполнено(СтруктураПараметров.СуммаДокументаФорма) Тогда
		СуммаКОплате              = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.СуммаДокументаФорма);
	ИначеЕсли ЗначениеЗаполнено(СтруктураПараметров.ПутьКДаннымТЧ) Тогда
		ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ПутьКДаннымТЧ);
		СуммаКОплате              = ТЧ.Итог(СтруктураПараметров.ИмяРеквизитаТЧСуммаСНДС);
	Иначе
		СуммаКОплате = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.СуммаДокумента);
	КонецЕсли;
	ЗаданГрафикИсполнения     = СтруктураПараметров.ЗаданГрафикИсполнения;
	ЭтоЗаказ                  = СтруктураПараметров.ЭтоЗаказ;
	ИспользоватьГрафикиОплаты = СистемныеНастройки.ИспользоватьГрафикиОплаты;
	
	ЕстьЭтапыОплаты = ЭтапыОплаты <> Неопределено;
	
	Если ЭтапыОплаты <> Неопределено Тогда
		КоличествоЭтаповОплаты    = ЭтапыОплаты.Количество();
	Иначе
		КоличествоЭтаповОплаты    = ?(ЗначениеЗаполнено(ДатаПлатежа), 1, 0);
	КонецЕсли;
	
	
	МассивСтрок = Новый Массив;
	
	Если ЭтоЗаказ И ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоНакладным") Тогда
		
		МассивСтрок.Добавить(НСтр("ru='По платежам и накладным'"));
		
	ИначеЕсли ЗаданГрафикИсполнения И (ЭтоЗаказ ИЛИ ЭтапыОплаты = Неопределено)
		И (ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов") 
			Или ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамНакладным"))Тогда
		
		МассивСтрок.Добавить(НСтр("ru='По графику договора'"));
		
	Иначе
		
		Если СуммаКОплате <= 0 И КоличествоЭтаповОплаты = 0 
			ИЛИ НЕ СтруктураПараметров.ИзменяетПланОплаты И НЕ СтруктураПараметров.ЗаказКакСчет 
			ИЛИ СуммаКОплате <= 0 И СтруктураПараметров.ЭтоПродажаЗакупка И НЕ СтруктураПараметров.ЭтоЗаказ Тогда
			
			МассивСтрок.Добавить(НСтр("ru='Оплата не требуется'"));
			
		Иначе
			
			МассивСтрок.Добавить(ПредставлениеФормыОплаты(ФормаОплаты));
			Если КоличествоЭтаповОплаты = 0 Тогда
				
				ТекстОшибки = ?(ЕстьЭтапыОплаты, НСтр("ru='этапы не указаны'"), НСтр("ru='не указана дата платежа'"));
				
				МассивСтрок.Добавить(", ");
				МассивСтрок.Добавить("<span style=""color: ПросроченныйДокумент"">" + ТекстОшибки +"</span>");
				
			ИначеЕсли Не ЕстьЭтапыОплаты Тогда
				
				МассивСтрок.Добавить(" ");
				МассивСтрок.Добавить(Формат(ДатаПлатежа, "ДЛФ=D"));
				
			ИначеЕсли КоличествоЭтаповОплаты <= 2 Тогда
				
				МассивСтрок.Добавить(" ");
				Для Сч=1 По КоличествоЭтаповОплаты Цикл
					СтрокаОплаты = ЭтапыОплаты[Сч-1];
					Если ЗначениеЗаполнено(СтрокаОплаты.ДатаПлатежа) И СтрокаОплаты.ПроцентПлатежа > 0 Тогда
						МассивСтрок.Добавить("<span style=""color: ГиперссылкаЦвет"">" + Формат(СтрокаОплаты.ДатаПлатежа, "ДЛФ=D") +"</span>");
						МассивСтрок.Добавить(" (" + Формат(СтрокаОплаты.ПроцентПлатежа, "ЧЦ=3; ЧДЦ=; ЧН=0") + "%)");
						Если сч < КоличествоЭтаповОплаты Тогда
							МассивСтрок.Добавить(", ");
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
				
			ИначеЕсли ИспользоватьГрафикиОплаты И ЗначениеЗаполнено(ГрафикОплаты)  И НЕ ЗаданГрафикИсполнения Тогда
				
				МассивСтрок.Добавить(" ");
				МассивСтрок.Добавить(НСтр("ru='по графику'") + " """ + Строка(ГрафикОплаты) + """");
				
			Иначе
				
				ТекстЭтапа = ОбщегоНазначенияУТКлиентСервер.СклонениеСлова(
					КоличествоЭтаповОплаты,
					НСтр("ru='этапы'"), НСтр("ru='этапа'"), НСтр("ru='этапов'"), НСтр("ru='м'"));
					
				МассивСтрок.Добавить(" ");
				МассивСтрок.Добавить(НСтр("ru='в'") +" " + Формат(КоличествоЭтаповОплаты, "ЧН=0") +" " + ТекстЭтапа);
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтрСоединить(МассивСтрок);
	
КонецФункции

// Формирует заголовок элемента НадписьВалюты
// 
// Параметры:
//	Форма       - ФормаКлиентскогоПриложения - Договор, указанный в документе:
//	 * Элементы - ЭлементыФормы - элементы вызывающей формы
//	СтруктураПараметров - см. ВзаиморасчетыСервер.ПараметрыМеханизма
//
Процедура ОбновитьТекстГиперссылкиВалюты(Форма, СтруктураПараметров = Неопределено) Экспорт
	
	Если СтруктураПараметров <> Неопределено Тогда
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(СтруктураПараметров);
	Иначе
		ДополненныеПараметрыМеханизма = ОбщегоНазначенияУТКлиентСервер.ПолучитьДанныеМеханизмаИзКэшаФормы(Форма, "Взаиморасчеты");
		МассивПараметров = ДополненныеПараметрыМеханизма.МассивПараметров;
	КонецЕсли;
	
	Для Каждого СтруктураПараметров Из МассивПараметров Цикл
	
		Если НЕ ЗначениеЗаполнено(СтруктураПараметров.КурсЧислитель) Тогда
			Продолжить;
		КонецЕсли;
		
		ВалютаДокумента      = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ВалютаДокумента);
		ВалютаВзаиморасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ВалютаВзаиморасчетов);
		КурсЧислитель        = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.КурсЧислитель);
		КурсЗнаменатель      = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.КурсЗнаменатель);
		Организация          = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.Организация);
		
		НеПоказыватьРасчеты  = СтруктураПараметров.НеПоказыватьРасчеты;
		
		ВалютаРегламентированногоУчета = ЗначениеНастроекКлиентСерверПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
		
		НадписьВалютыЭлемент = Форма.Элементы[СтруктураПараметров.ЭлементыФормы.НадписьВалюты]; // ПолеФормы
		
		Если ЗначениеЗаполнено(СтруктураПараметров.СуммаДокументаФорма) И ВалютаДокумента = ВалютаВзаиморасчетов Тогда
			СуммаДокумента = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(форма, СтруктураПараметров.СуммаДокументаФорма, , 0);
			СуммаВзаиморасчетов = СуммаДокумента;
		ИначеЕсли ЗначениеЗаполнено(СтруктураПараметров.ПутьКДаннымТЧ) И ЗначениеЗаполнено(СтруктураПараметров.ИмяРеквизитаТЧСуммаСНДС) Тогда
			ТЧ = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ПутьКДаннымТЧ);
			СуммаДокумента = ТЧ.Итог(СтруктураПараметров.ИмяРеквизитаТЧСуммаСНДС);
			Если ТЧ.Количество() > 0 И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ТЧ[0],"СуммаВзаиморасчетов") Тогда
				СуммаВзаиморасчетов = ТЧ.Итог("СуммаВзаиморасчетов");
			Иначе
				СуммаВзаиморасчетов = 0;
			КонецЕсли;
		Иначе
			СуммаВзаиморасчетов  = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.СуммаВзаиморасчетов);
			СуммаДокумента       = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.СуммаДокумента);
		КонецЕсли;
		
		Если СуммаДокумента = 0 Тогда
			ТекстСуммаДокумента = "";
		Иначе
			ТекстСуммаДокумента = " " + Формат(СуммаДокумента, "ЧДЦ=2");
		КонецЕсли;
		
		Если ВалютаДокумента = ВалютаВзаиморасчетов ИЛИ НеПоказыватьРасчеты Тогда
			
			НадписьВалюты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Документ и расчеты:%1 %2'"),
					ТекстСуммаДокумента,
					ВалютаДокумента);
			
		Иначе
			
			ТекстДокумент = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Документ%1 %2'"),
					ТекстСуммаДокумента,
					ВалютаДокумента);
			
			Если СуммаВзаиморасчетов = 0 Тогда
				ТекстСуммаВзаиморасчетов = "";
			Иначе
				ТекстСуммаВзаиморасчетов = " " + Формат(СуммаВзаиморасчетов, "ЧДЦ=2");
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВалютаВзаиморасчетов) Тогда
				ТекстВалютаВзаиморасчетов = ВалютаВзаиморасчетов;
			Иначе
				ТекстВалютаВзаиморасчетов = НСтр("ru='<Не выбрана>'")
			КонецЕсли;
			
			ТекстРасчеты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru=', Расчеты%1 %2'"),
					ТекстСуммаВзаиморасчетов,
					ТекстВалютаВзаиморасчетов);
			
			Если НЕ ЗначениеЗаполнено(ВалютаВзаиморасчетов) Тогда
				
				ТекстРасшифровка = "";
				
			ИначеЕсли ВалютаДокумента = ВалютаРегламентированногоУчета
				ИЛИ ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
				
				ВалютаНадписи = ?(ВалютаРегламентированногоУчета = ВалютаДокумента,
								ВалютаВзаиморасчетов,
								ВалютаДокумента);
				
				Если КурсЗнаменатель = 1 Тогда
					
						ТекстРасшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"(%1=%2 %3)",
							ВалютаНадписи,
							КурсЧислитель,
							ВалютаРегламентированногоУчета);
					
					Иначе
						
						ТекстРасшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru='(%1 %2 за %3 %4)'"),
							КурсЧислитель,
							ВалютаРегламентированногоУчета,
							КурсЗнаменатель,
							ВалютаНадписи);
						
				КонецЕсли;
				
			Иначе
				
				Если КурсЗнаменатель = 1 Тогда
					
					ТекстРасшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"(%1=%2 %3)",
						ВалютаДокумента,
						КурсЧислитель,
						ВалютаВзаиморасчетов);
				Иначе
					
					ТекстКурс = Строка(КурсЧислитель) + " " + ВалютаВзаиморасчетов;
					
					ТекстКратность= Строка(КурсЗнаменатель);
					
					ТекстРасшифровка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"(%1=%2 %3)",
						ТекстКурс,
						ТекстКратность,
						ВалютаДокумента);
						
				КонецЕсли;
				
			КонецЕсли;
			
			НадписьВалюты = ТекстДокумент + " " + ТекстРасчеты + " " + ТекстРасшифровка;
			
		КонецЕсли;
		
		НадписьВалютыЭлемент.Заголовок = НадписьВалюты;
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает видимость кнопки зачета оплаты по порядку расчетов документа, если такая есть на форме.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма, в которой находится команда зачета оплаты.
//	СтруктураПараметров - см. ВзаиморасчетыСервер.ПараметрыМеханизма
//
Процедура УстановитьВидимостьЗачетаОплаты(Форма, СтруктураПараметров) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СтруктураПараметров.ЭлементыФормы.ЗачетОплаты) Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьОбязательныеПараметры(СтруктураПараметров, "ПорядокРасчетов");
	
	ДоступенЗачетОплаты = СтруктураПараметров.ИзменяетРасчеты;
	ПорядокРасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.ПорядокРасчетов);
	
	Если СтруктураПараметров.ЭтоЗаказ Тогда
		ВидимостьЭлемента = ПорядокРасчетов <> ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоНакладным")
							И ПорядокРасчетов <> ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамНакладным")
							И ДоступенЗачетОплаты;
	ИначеЕсли ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(Форма, СтруктураПараметров.НакладнаяПоЗаказам,,Ложь) Тогда
		ВидимостьЭлемента = ПорядокРасчетов <> ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоЗаказам") И ДоступенЗачетОплаты;
	ИначеЕсли СтруктураПараметров.ЭтоСправочник Тогда
		ВидимостьЭлемента = (ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов")
							Или ПорядокРасчетов = ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамНакладным")) И ДоступенЗачетОплаты;
	Иначе
		ВидимостьЭлемента = ДоступенЗачетОплаты;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
		Форма.Элементы, 
		СтруктураПараметров.ЭлементыФормы.ЗачетОплаты,
		"Видимость",
		ВидимостьЭлемента);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьОборот(Обороты, Запись, ИмяРесурса)
	
	ЗначениеРесурса = Запись[ИмяРесурса];
	Если ЗначениеРесурса > 0 Тогда
		Обороты.Дт = Обороты.Дт + ЗначениеРесурса;
	Иначе
		Обороты.Кт = Обороты.Кт + (-ЗначениеРесурса);
	КонецЕсли;
	
КонецПроцедуры

//Проверяется заполненность переданных параметров в структуре, при незаполненности выдает исключение.
Процедура ПроверитьОбязательныеПараметры(СтруктураПараметров, ОбязательныеПараметры) Экспорт
	
	Реквизиты = СтрРазделить(ОбязательныеПараметры, ",");
	Для Каждого Реквизит Из Реквизиты Цикл
		Если НЕ СтруктураПараметров.Свойство(СокрЛП(Реквизит)) 
			ИЛИ НЕ ЗначениеЗаполнено(СтруктураПараметров[СокрЛП(Реквизит)]) Тогда
				ВызватьИсключение(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru='Не заполнен обязательный параметр механизма ""Взаиморасчеты"":%1'"),
										Реквизит));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//   МассивПараметров - см. ВзаиморасчетыСервер.ДополненныеПараметрыМеханизма
//   ИменаЭлементов - Строка, Массив из Строка - Имя поля формы.
//
// Возвращаемое значение:
//   Массив из Структура:
//    * СтруктураПараметров - см. ВзаиморасчетыСервер.ПараметрыМеханизма 
//    * ИспользуемыеЭлементыФормы - Массив из см. ВзаиморасчетыСервер.ПараметрыМеханизма 
//
Функция МассивСтруктурПараметровПоЭлементам(МассивПараметров, ИменаЭлементов) Экспорт
	Если ТипЗнч(ИменаЭлементов) = Тип("Массив") Тогда
		Элементы = ИменаЭлементов;
	Иначе
		Элементы = Новый Массив;
		Элементы.Добавить(ИменаЭлементов);
	КонецЕсли;
	Результат = Новый Массив;
	Для Каждого СтруктураПараметров Из МассивПараметров Цикл
		МассивНайденныхЭлементов = Новый Массив;
		Для Каждого Элемент Из Элементы Цикл
			Если СтруктураПараметров.ИспользуемыеЭлементыФормы.Найти(Элемент) <> Неопределено Тогда
				МассивНайденныхЭлементов.Добавить(Элемент);
			КонецЕсли;
		КонецЦикла;
		Если МассивНайденныхЭлементов.Количество() > 0 Тогда
			СтруктураРезультата = Новый Структура;
			СтруктураРезультата.Вставить("СтруктураПараметров",       СтруктураПараметров);
			СтруктураРезультата.Вставить("ИспользуемыеЭлементыФормы", МассивНайденныхЭлементов);
			Результат.Добавить(СтруктураРезультата);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

// Параметры:
//   МассивПараметров - массив из см. ВзаиморасчетыСервер.ПараметрыМеханизма.
//   ИмяЭлемента - Строка.
//
// Возвращаемое значение:
//   см. ВзаиморасчетыСервер.ПараметрыМеханизма.
//
Функция СтруктураПараметровПоИмениЭлемента(МассивПараметров, ИмяЭлемента) Экспорт
	
	МассивСтруктурПараметров = МассивСтруктурПараметровПоЭлементам(МассивПараметров, ИмяЭлемента);
	
	Если МассивСтруктурПараметров.Количество() > 1 Тогда
		ВызватьИсключение(НСтр("ru='Элемент привязан к нескольким параметрам механизма взаиморасчетов.'"));
	ИначеЕсли МассивСтруктурПараметров.Количество() = 0 Тогда
		ВызватьИсключение(НСтр("ru='Элемент не привязан к параметрам механизма взаиморасчетов.'"));
	Иначе
		Возврат МассивСтруктурПараметров[0].СтруктураПараметров;
	КонецЕсли;
	
КонецФункции

// Возвращает информативное представление формы оплаты для документов
//
// Параметры:
// 		ФормаОплаты - ПеречислениеСсылка.ФормыОплаты - форма оплаты. для которой нужно получить представление.
//
// Возвращаемое значение:
// 		Строка - представление формы оплаты.
//
Функция ПредставлениеФормыОплаты(ФормаОплаты)
	
	Представление = "";
	
	Если Не ЗначениеЗаполнено(ФормаОплаты) Тогда
		Представление = НСтр("ru='К оплате'");
	ИначеЕсли ФормаОплаты = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Безналичная") Тогда
		Представление = НСтр("ru='К оплате безнал'");
	ИначеЕсли ФормаОплаты = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Наличная") Тогда
		Представление = НСтр("ru='К оплате нал'");
	ИначеЕсли ФормаОплаты = ПредопределенноеЗначение("Перечисление.ФормыОплаты.ПлатежнаяКарта") Тогда
		Представление = НСтр("ru='К оплате платежной картой'");
	ИначеЕсли ФормаОплаты = ПредопределенноеЗначение("Перечисление.ФормыОплаты.Взаимозачет") Тогда
		Представление = НСтр("ru='Взаимозачет'");
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция ЭтоТабличнаяЧасть(Таблица) Экспорт
	Возврат ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Таблица, "Колонки");
КонецФункции

#КонецОбласти
