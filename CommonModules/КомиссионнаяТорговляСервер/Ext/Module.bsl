
#Область ПрограммныйИнтерфейс

// Процедура проверяет правильность заполнения начала и конца периода.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект - Текущий документ
//	Отказ - Булево - Признак отказа от продолжения работы.
//
Процедура ПроверитьКорректностьПериода(ДокументОбъект, Отказ) Экспорт
	
	Если ЗначениеЗаполнено(ДокументОбъект.НачалоПериода)
	 И ДокументОбъект.НачалоПериода > ДокументОбъект.КонецПериода Тогда
	 
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Дата начала периода не должна быть больше окончания периода %1'"),
			Формат(ДокументОбъект.КонецПериода, "ДЛФ=DD"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ДокументОбъект,
			"КонецПериода",
			, // ПутьКДанным
			Отказ);
	 
	КонецЕсли;
	
КонецПроцедуры

// Процедура проверяет корректность указания услуги по комиссионному вознаграждению.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект - Текущий документ
//	Отказ - Булево - Признак отказа от продолжения работы.
//
Процедура ПроверитьУслугуПоКомиссионномуВознаграждению(ДокументОбъект, Отказ) Экспорт
	
	Если ЗначениеЗаполнено(ДокументОбъект.Услуга) Тогда
		
		ХарактеристикиИспользуются = Справочники.Номенклатура.ХарактеристикиИспользуются(ДокументОбъект.Услуга);
		Если ХарактеристикиИспользуются Тогда
			ТекстОшибки = НСтр("ru='Необходимо указать услугу для которой не используются характеристики'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ДокументОбъект,
				"Услуга",
				, // ПутьКДанным
				Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа по остаткам товаров к оформлению отчетов комитенту о списании.
//
// Параметры:
//	Объект - ДанныеФормыКоллекция - объект, в котором осуществляется заполнение.
//	ЕстьНомерГТД - Булево - признак наличия в таблице объекта колонки 'НомерГТД'. Значение по умолчанию Ложь.
//
Процедура ЗаполнитьТоварыПоОстаткамКОформлениюОтчетовКомитентуОСписании(Объект, ЕстьНомерГТД = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов
	|
	|ПОМЕСТИТЬ ВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И ВидыЗапасов.Организация = &Организация
	|	И ВидыЗапасов.ВладелецТовара = &Комитент
	|	И (ВидыЗапасов.Соглашение = &Соглашение
	|		ИЛИ &Соглашение В (
	|			НЕОПРЕДЕЛЕНО,
	|			ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)))
	|	И (ВидыЗапасов.Контрагент = &Контрагент
	|		ИЛИ &Контрагент В (
	|			НЕОПРЕДЕЛЕНО,
	|			ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|			ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)))	
	|	И (ВидыЗапасов.Договор = &Договор
	|		ИЛИ (ВидыЗапасов.Договор В (
	|			НЕОПРЕДЕЛЕНО,
	|			ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|			ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)) И
	|		&Договор В (
	|			НЕОПРЕДЕЛЕНО,
	|			ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|			ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка))
	|		))
	|	И ВидыЗапасов.НалогообложениеНДС = &НалогообложениеНДС
	|	И ВидыЗапасов.Валюта = &Валюта
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обороты.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ЕстьНомерГТД
	|				И ЕСТЬNULL(Обороты.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|			ТОГДА Обороты.НомерГТД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НомерГТД,
	|	Обороты.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	Обороты.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	Обороты.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	СУММА(Обороты.КоличествоСписаноКОформлениюКонечныйОстаток) КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, Период,,
	|			ВидЗапасов В
	|				(ВЫБРАТЬ
	|					ВидЗапасов
	|				ИЗ
	|					ВидыЗапасов КАК ВидыЗапасов)
	|			И АналитикаУчетаНоменклатуры.МестоХранения = &Комитент
	|	
	|	) КАК Обороты
	|
	|ГДЕ
	|	Обороты.КоличествоСписаноКОформлениюКонечныйОстаток <> 0
	|	И (Обороты.КоличествоСписаноКОформлениюПриход <> 0
	|		ИЛИ Обороты.КоличествоСписаноКОформлениюРасход <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	Обороты.АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ЕстьНомерГТД
	|				И ЕСТЬNULL(Обороты.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|			ТОГДА Обороты.НомерГТД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Обороты.АналитикаУчетаНоменклатуры.Номенклатура,
	|	Обороты.АналитикаУчетаНоменклатуры.Характеристика,
	|	Обороты.АналитикаУчетаНоменклатуры.Серия";
	
	ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Валюта", Объект.Валюта);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Комитент", Объект.Партнер);
	Запрос.УстановитьПараметр("Соглашение", Объект.Соглашение);
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("Договор", Объект.Договор);
	Запрос.УстановитьПараметр("НалогообложениеНДС", Объект.НалогообложениеНДС);
	Запрос.УстановитьПараметр("НачалоПериода", ?(ЗначениеЗаполнено(Объект.НачалоПериода), Объект.НачалоПериода, '00010101'));
	Запрос.УстановитьПараметр("КонецПериода", ?(ЗначениеЗаполнено(Объект.КонецПериода), КонецДня(Объект.КонецПериода), КонецМесяца(ДатаДокумента)));
	Запрос.УстановитьПараметр("ЕстьНомерГТД", ЕстьНомерГТД);
	
	Объект.Товары.Очистить();
	
	КэшированныеЗначения = Неопределено;
	СтруктураДействий = Новый Структура;
	ПараметрыДействия = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(Объект);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", ПараметрыДействия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество;
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;
	
	Если ТипЗнч(Объект.Партнер) = Тип("СправочникСсылка.Партнеры") Тогда
		НоменклатураПартнеровСервер.ЗаполнитьНоменклатуруПартнераПоНоменклатуреВТаблице(Объект.Товары, Объект.Партнер);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		
		СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
		ПараметрыЗаполнения = ЦеныПартнеровЗаполнениеСервер.НовыйПараметрыЗаполненияЗаполнитьЦены();
		ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена, СтавкаНДС");
		ПараметрыЗаполнения.Вставить("Дата", Объект.Дата);
		ПараметрыЗаполнения.Вставить("Валюта", Объект.Валюта);
		ПараметрыЗаполнения.Вставить("Соглашение", Объект.Соглашение);
		ПараметрыЗаполнения.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьСумму", "КоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
		
		ЦеныПартнеровЗаполнениеСервер.ЗаполнитьЦены(
			Объект.Товары,
			Неопределено, // Массив строк
			ПараметрыЗаполнения,
			СтруктураДействий);
			
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения документа по результатам продаж за период товаров к оформлению отчетов комитенту.
//
// Параметры:
//	Объект - ДанныеФормыКоллекция - объект, в котором осуществляется заполнение.
//	НачалоПериода - Дата - дата начала заполнения.
//	КонецПериода - Дата - дата окончания заполнения.
//	ЕстьНомерГТД - Булево - признак наличия в таблице объекта колонки 'НомерГТД'. Значение по умолчанию Ложь.
//	ИспользоватьНоменклатуруПартнера - Булево - признак наличия в таблице объекта колонки 'НоменклатураПартнера'.
//										Значение по умолчанию Ложь.
//
Процедура ЗаполнитьПоТоварамКОформлениюОтчетовКомитентуЗаПериод(Объект,
																НачалоПериода,
																КонецПериода,
																ЕстьНомерГТД = Ложь,
																ИспользоватьНоменклатуруПартнера = Ложь) Экспорт
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Дата",				Объект.Дата);
	СтруктураПараметров.Вставить("Валюта",				Объект.Валюта);
	СтруктураПараметров.Вставить("Организация",			Объект.Организация);
	СтруктураПараметров.Вставить("Партнер",				Объект.Партнер);
	СтруктураПараметров.Вставить("Соглашение",			Объект.Соглашение);
	СтруктураПараметров.Вставить("Контрагент",			?(ЗначениеЗаполнено(Объект.Контрагент),
															Объект.Контрагент,
															Неопределено));
	СтруктураПараметров.Вставить("Договор",				?(ЗначениеЗаполнено(Объект.Договор),
															Объект.Договор,
															Неопределено));
	СтруктураПараметров.Вставить("НалогообложениеНДС",	Объект.НалогообложениеНДС);
	СтруктураПараметров.Вставить("НачалоПериода",		НачалоПериода);
	СтруктураПараметров.Вставить("КонецПериода",		КонецДня(?(ЗначениеЗаполнено(КонецПериода),
																	КонецПериода,
																	ТекущаяДатаСеанса())));
	СтруктураПараметров.Вставить("ЕстьНомерГТД",		ЕстьНомерГТД);
	СтруктураПараметров.Вставить("ЕстьСуммаПродажиНДС",	Истина);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗапросПоТоварамКОформлениюОтчетовКомитентуЗаПериод(СтруктураПараметров, МенеджерВременныхТаблиц);
	ЗапросПоТоварамКОформлениюОтчетовПринципалуЗаПериод(СтруктураПараметров, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыКОформлению.Номенклатура			КАК Номенклатура,
	|	ТоварыКОформлению.Характеристика		КАК Характеристика,
	|	СУММА(ТоварыКОформлению.Количество)		КАК Количество,
	|	СУММА(ТоварыКОформлению.Количество)		КАК КоличествоОстаток,
	|	СУММА(ТоварыКОформлению.СуммаВыручки)	КАК СуммаПродажи,
	|	ТоварыКОформлению.ДатаСчетаФактуры		КАК ДатаСчетаФактуры,
	|	ТоварыКОформлению.ДатаСчетаФактуры		КАК ДатаСчетаФактурыКомиссионера,
	|	ТоварыКОформлению.НомерСчетаФактуры		КАК НомерСчетаФактуры,
	|	ТоварыКОформлению.Покупатель			КАК Покупатель,
	|	ТоварыКОформлению.СчетФактура			КАК СчетФактураВыставленный,
	|	ТоварыКОформлению.СчетФактура			КАК СчетФактураВыставленныйКомиссионера,
	|	ТоварыКОформлению.НомерГТД				КАК НомерГТД,
	|	ТоварыКОформлению.ЕстьСуммаПродажиНДС	КАК ЕстьСуммаПродажиНДС
	|ИЗ
	|	ТоварыКОформлению КАК ТоварыКОформлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.Номенклатура,
	|	ТоварыКОформлению.Характеристика,
	|	ТоварыКОформлению.ДатаСчетаФактуры,
	|	ТоварыКОформлению.НомерСчетаФактуры,
	|	ТоварыКОформлению.Покупатель,
	|	ТоварыКОформлению.СчетФактура,
	|	ТоварыКОформлению.НомерГТД,
	|	ТоварыКОформлению.ЕстьСуммаПродажиНДС
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.Количество) <> 0
	|	ИЛИ СУММА(ТоварыКОформлению.СуммаВыручки) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УслугиКОформлению.Номенклатура			КАК Номенклатура,
	|	УслугиКОформлению.Характеристика		КАК Характеристика,
	|	УслугиКОформлению.Количество			КАК Количество,
	|	УслугиКОформлению.КоличествоОстаток		КАК КоличествоОстаток,
	|	УслугиКОформлению.СуммаВыручки			КАК СуммаПродажи,
	|	УслугиКОформлению.ДатаСчетаФактуры		КАК ДатаСчетаФактуры,
	|	УслугиКОформлению.ДатаСчетаФактуры		КАК ДатаСчетаФактурыКомиссионера,
	|	УслугиКОформлению.НомерСчетаФактуры		КАК НомерСчетаФактуры,
	|	УслугиКОформлению.Покупатель			КАК Покупатель,
	|	УслугиКОформлению.СчетФактура			КАК СчетФактураВыставленный,
	|	УслугиКОформлению.СчетФактура			КАК СчетФактураВыставленныйКомиссионера,
	|	НЕОПРЕДЕЛЕНО							КАК НомерГТД,
	|	УслугиКОформлению.ЕстьСуммаПродажиНДС	КАК ЕстьСуммаПродажиНДС
	|ИЗ
	|	УслугиКОформлению КАК УслугиКОформлению
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаСчетаФактуры УБЫВ
	|
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	Номенклатура,
	|	Характеристика";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЗаполнитьТЧПоТоварамКОформлениюОтчетовКомитентуЗаПериод(Объект.Товары, РезультатЗапроса, СтруктураПараметров);
	
	Если ИспользоватьНоменклатуруПартнера Тогда
		НоменклатураПартнеровСервер.ЗаполнитьНоменклатуруПартнераПоНоменклатуреВТаблице(Объект.Товары, Объект.Партнер);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Соглашение) Тогда
		СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
		
		ПараметрыЗаполнения = ЦеныПартнеровЗаполнениеСервер.НовыйПараметрыЗаполненияЗаполнитьЦены();
		ПараметрыЗаполнения.Вставить("ПоляЗаполнения",		"Цена");
		ПараметрыЗаполнения.Вставить("Дата",				Объект.Дата);
		ПараметрыЗаполнения.Вставить("Валюта",				Объект.Валюта);
		ПараметрыЗаполнения.Вставить("Соглашение",			Объект.Соглашение);
		ПараметрыЗаполнения.Вставить("НалогообложениеНДС",	Объект.НалогообложениеНДС);
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьСумму",		"КоличествоУпаковок");
		СтруктураДействий.Вставить("ПересчитатьСуммуСНДС",	СтруктураПересчетаСуммы);
		СтруктураДействий.Вставить("ПересчитатьСуммуНДС",	СтруктураПересчетаСуммы);
		
		ЦеныПартнеровЗаполнениеСервер.ЗаполнитьЦены(Объект.Товары, Неопределено, ПараметрыЗаполнения, СтруктураДействий);
	Иначе
		ЗаполнитьСуммуСНДС(Объект.Товары, Объект.ЦенаВключаетНДС);
	КонецЕсли;
	
КонецПроцедуры

// Формирует временную таблицу "ТоварыКОформлению" по результатам продаж комиссионных товаров за период.
//
// Параметры:
//	СтруктураПараметров - Структура - параметры запроса.
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц, хранящий результат запроса.
//
Процедура ЗапросПоТоварамКОформлениюОтчетовКомитентуЗаПериод(СтруктураПараметров, МенеджерВременныхТаблиц) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов
	|ПОМЕСТИТЬ ВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|
	|ГДЕ
	|	ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И ВидыЗапасов.Организация = &Организация
	|	И ВидыЗапасов.ВладелецТовара = &Комитент
	|	И (ВидыЗапасов.Соглашение = &Соглашение
	|		ИЛИ &Соглашение В (НЕОПРЕДЕЛЕНО,
	|							ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)))
	|	И (ВидыЗапасов.Контрагент = &Контрагент
	|		ИЛИ &Контрагент В (НЕОПРЕДЕЛЕНО,
	|							ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка),
	|							ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)))
	|	И (ВидыЗапасов.Договор = &Договор
	|		ИЛИ ВидыЗапасов.Договор В (НЕОПРЕДЕЛЕНО,
	|									ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|									ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка))
	|			И &Договор В (НЕОПРЕДЕЛЕНО,
	|							ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка),
	|							ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка)))
	|	И ВидыЗапасов.НалогообложениеНДС = &НалогообложениеНДС
	|	И ВидыЗапасов.Валюта = &Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлениюОтчетовКомитентуОстатки.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТоварыКОформлениюОтчетовКомитентуОстатки.ВидЗапасов					КАК ВидЗапасов,
	|	ТоварыКОформлениюОтчетовКомитентуОстатки.НомерГТД					КАК НомерГТД,
	|	ТоварыКОформлениюОтчетовКомитентуОстатки.Валюта						КАК Валюта
	|ПОМЕСТИТЬ ТоварыКОформлениюОтбор
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту.Остатки(&ГраницаКонецПериода,
	|			ВидЗапасов В
	|				(ВЫБРАТЬ
	|					ВидыЗапасов.ВидЗапасов КАК ВидЗапасов
	|				ИЗ
	|					ВидыЗапасов КАК ВидыЗапасов)
	|	
	|	) КАК ТоварыКОформлениюОтчетовКомитентуОстатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация									КАК Организация,
	|	ТоварыКОформлению.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура	КАК Номенклатура,
	|	КлючиАналитикиУчетаНоменклатуры.Характеристика	КАК Характеристика,
	|	СУММА(ВЫБОР
	|			КОГДА ТоварыКОформлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТоварыКОформлению.КоличествоКОформлению
	|			ИНАЧЕ -ТоварыКОформлению.КоличествоКОформлению
	|		КОНЕЦ)										КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ТоварыКОформлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА ТоварыКОформлению.СуммаВыручкиКОформлению
	|			ИНАЧЕ -ТоварыКОформлению.СуммаВыручкиКОформлению
	|		КОНЕЦ)										КАК СуммаВыручки,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлению.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|				И ВЫРАЗИТЬ(ТоварыКОформлению.Регистратор КАК Документ.КорректировкаРеализации).ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок)
	|			ТОГДА ТоварыКОформлению.Регистратор
	|		КОГДА НЕ ВозвратТоваровОтКлиента.Ссылка ЕСТЬ NULL
	|				И НЕ ВозвратТоваровОтКлиента.ПокупательНеПлательщикНДС
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыКОформлению.ДокументРеализации
	|	КОНЕЦ											КАК ДокументРеализации,
	|	ТоварыКОформлению.ДатаСчетаФактуры				КАК ДатаСчетаФактуры,
	|	ТоварыКОформлению.НомерСчетаФактуры				КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.РеализацияТоваровУслуг)
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
	|			ТОГДА ПередачаТоваров.ОрганизацияПолучатель
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.Регистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	|			ТОГДА ВозвратТоваровОтКлиента.Контрагент
	|		КОГДА ТоварыКОформлению.Покупатель = НЕОПРЕДЕЛЕНО
	|				ИЛИ ТоварыКОформлению.Покупатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|		ИНАЧЕ ТоварыКОформлению.Покупатель
	|	КОНЕЦ											КАК Покупатель,
	|	ВЫБОР
	|		КОГДА &ЕстьНомерГТД
	|				И ЕСТЬNULL(КлючиАналитикиУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|			ТОГДА ТоварыКОформлению.НомерГТД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ											КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлению.КоличествоКОформлению >= 0
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ											КАК Знак,
	|	&ЕстьСуммаПродажиНДС							КАК ЕстьСуммаПродажиНДС
	|ПОМЕСТИТЬ ТоварыКОформлению
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ТоварыКОформлению
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента КАК ВозвратТоваровОтКлиента
	|		ПО ТоварыКОформлению.Регистратор = ВозвратТоваровОтКлиента.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ТоварыКОформлению.ДокументРеализации = РеализацияТоваровУслуг.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК ПередачаТоваров
	|		ПО ТоварыКОформлению.ДокументРеализации = ПередачаТоваров.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТоварыКОформлениюОтбор КАК ТоварыКОформлениюОтбор
	|		ПО ТоварыКОформлению.АналитикаУчетаНоменклатуры = ТоварыКОформлениюОтбор.АналитикаУчетаНоменклатуры
	|			И ТоварыКОформлению.ВидЗапасов = ТоварыКОформлениюОтбор.ВидЗапасов
	|			И ТоварыКОформлению.НомерГТД = ТоварыКОформлениюОтбор.НомерГТД
	|			И ТоварыКОформлению.Валюта = ТоварыКОформлениюОтбор.Валюта
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|		ПО ТоварыКОформлению.АналитикаУчетаНоменклатуры = КлючиАналитикиУчетаНоменклатуры.Ссылка
	|
	|ГДЕ
	|	ТоварыКОформлению.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТоварыКОформлению.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОформлению.АналитикаУчетаНоменклатуры,
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура,
	|	КлючиАналитикиУчетаНоменклатуры.Характеристика,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлению.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|				И ВЫРАЗИТЬ(ТоварыКОформлению.Регистратор КАК Документ.КорректировкаРеализации).ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок)
	|			ТОГДА ТоварыКОформлению.Регистратор
	|		КОГДА НЕ ВозвратТоваровОтКлиента.Ссылка ЕСТЬ NULL
	|				И НЕ ВозвратТоваровОтКлиента.ПокупательНеПлательщикНДС
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыКОформлению.ДокументРеализации
	|	КОНЕЦ,
	|	ТоварыКОформлению.ДатаСчетаФактуры,
	|	ТоварыКОформлению.НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.РеализацияТоваровУслуг)
	|			ТОГДА РеализацияТоваровУслуг.Контрагент
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
	|			ТОГДА ПередачаТоваров.ОрганизацияПолучатель
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.Регистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	|			ТОГДА ВозвратТоваровОтКлиента.Контрагент
	|		КОГДА ТоварыКОформлению.Покупатель = НЕОПРЕДЕЛЕНО
	|				ИЛИ ТоварыКОформлению.Покупатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|		ИНАЧЕ ТоварыКОформлению.Покупатель
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА &ЕстьНомерГТД
	|				И ЕСТЬNULL(КлючиАналитикиУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|			ТОГДА ТоварыКОформлению.НомерГТД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыКОформлению.КоличествоКОформлению >= 0
	|			ТОГДА 1
	|		ИНАЧЕ -1
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОформлению.КоличествоКОформлению) <> 0
	|	И СУММА(ТоварыКОформлению.СуммаВыручкиКОформлению) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика";
	
	Запрос.УстановитьПараметр("НачалоПериода",			СтруктураПараметров.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",			СтруктураПараметров.КонецПериода);
	Запрос.УстановитьПараметр("ГраницаКонецПериода",	Новый Граница(СтруктураПараметров.КонецПериода,
																		ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",			СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("Комитент",				СтруктураПараметров.Партнер);
	Запрос.УстановитьПараметр("Контрагент",				СтруктураПараметров.Контрагент);
	Запрос.УстановитьПараметр("Соглашение",				СтруктураПараметров.Соглашение);
	Запрос.УстановитьПараметр("Договор",				СтруктураПараметров.Договор);
	Запрос.УстановитьПараметр("Валюта",					СтруктураПараметров.Валюта);
	Запрос.УстановитьПараметр("НалогообложениеНДС",		СтруктураПараметров.НалогообложениеНДС);
	Запрос.УстановитьПараметр("ЕстьНомерГТД",			СтруктураПараметров.ЕстьНомерГТД);
	Запрос.УстановитьПараметр("ЕстьСуммаПродажиНДС",	СтруктураПараметров.ЕстьСуммаПродажиНДС);
	
	Запрос.Выполнить();
	
	УчетНДСУП.ДополнитьТаблицуДаннымиСчетовФактурВыданныхПокупателям(МенеджерВременныхТаблиц, "ТоварыКОформлению");
	
КонецПроцедуры

// Заполняет табличную часть объекта по данным временной таблицы "ТоварыКОформлению".
//
// Параметры:
//	ТабличнаяЧасть - ТабличнаяЧасть - табличная часть объекта для заполнения.
//	РезультатЗапроса - РезультатЗапроса - результат запроса, по данным которого необходимо заполнить таблицу объекта.
//	ПараметрыЗаполнения - Структура - параметры заполнения табличной части, которые содержат следующие значения:
//					* Дата - Дата - дата документа.
//					* Организация - СправочникСсылка.Организации - организация документа.
//					* НалогообложениеНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - налогообложение НДС документа.
//
Процедура ЗаполнитьТЧПоТоварамКОформлениюОтчетовКомитентуЗаПериод(ТабличнаяЧасть,
																	РезультатЗапроса,
																	ПараметрыЗаполнения) Экспорт
	
	ТабличнаяЧасть.Очистить();
	
	КэшированныеЗначения = Неопределено;
	
	СтруктураДействий = Новый Структура;
	ПараметрыДействия = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияСтавкиНДС(ПараметрыЗаполнения);
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", ПараметрыДействия);
	
	ВыборкаПоНоменклатуре = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоНоменклатуре.Следующий() Цикл
		
		ВыборкаПоХарактеристике = ВыборкаПоНоменклатуре.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаПоХарактеристике.Следующий() Цикл
			
			Если ВыборкаПоХарактеристике.КоличествоОстаток >= ВыборкаПоХарактеристике.Количество Тогда
				
				Выборка = ВыборкаПоХарактеристике.Выбрать();
				
				Пока Выборка.Следующий() Цикл
					ДобавитьСтрокуПоТоварамКОформлениюОтчетовКомитентуЗаПериод(ТабличнаяЧасть,
																				ПараметрыЗаполнения,
																				Выборка,
																				Выборка.Количество,
																				СтруктураДействий,
																				КэшированныеЗначения);
				КонецЦикла;
				
			Иначе
				
				КоличествоКРаспределению = ВыборкаПоХарактеристике.Количество;
				
				Выборка = ВыборкаПоХарактеристике.Выбрать();
				
				Пока Выборка.Следующий() Цикл
				
					Если Выборка.Количество <= КоличествоКРаспределению Тогда
						ДобавитьСтрокуПоТоварамКОформлениюОтчетовКомитентуЗаПериод(ТабличнаяЧасть,
																					ПараметрыЗаполнения,
																					Выборка,
																					Выборка.Количество,
																					СтруктураДействий,
																					КэшированныеЗначения);
						
						КоличествоКРаспределению = КоличествоКРаспределению - Выборка.Количество;
					Иначе
						ДобавитьСтрокуПоТоварамКОформлениюОтчетовКомитентуЗаПериод(ТабличнаяЧасть,
																					ПараметрыЗаполнения,
																					Выборка,
																					КоличествоКРаспределению,
																					СтруктураДействий,
																					КэшированныеЗначения);
						
						КоличествоКРаспределению = 0;
					КонецЕсли;
					
					Если КоличествоКРаспределению = 0 Тогда
						Прервать;
					КонецЕсли;
				
				КонецЦикла;
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура формирования временной таблицы "ТоварыКОформлениюПринципалу" по результатам продаж за период.
//
// Параметры:
//  СтруктураПараметров - Структура - параметры запроса.
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц  - менеджер временных таблиц для запроса.
//
Процедура ЗапросПоТоварамКОформлениюОтчетовПринципалуЗаПериод(СтруктураПараметров, МенеджерВременныхТаблиц) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НачалоПериода", СтруктураПараметров.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", СтруктураПараметров.КонецПериода);
	Запрос.УстановитьПараметр("Валюта", СтруктураПараметров.Валюта);
	Запрос.УстановитьПараметр("Организация", СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("Комитент", СтруктураПараметров.Партнер);
	Запрос.УстановитьПараметр("Соглашение", ?(ЗначениеЗаполнено(СтруктураПараметров.Соглашение), 
								СтруктураПараметров.Соглашение, Справочники.СоглашенияСПоставщиками.ПустаяСсылка()));
	Запрос.УстановитьПараметр("ЕстьСуммаПродажиНДС", СтруктураПараметров.ЕстьСуммаПродажиНДС);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	&Комитент КАК Принципал,
	|	&Валюта КАК Валюта,
	|	&Соглашение КАК Соглашение
	|ПОМЕСТИТЬ ВтОтбор
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	УслугиКОформлению.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	УслугиКОформлению.ДокументРеализации КАК ДокументРеализации,
	|	УслугиКОформлению.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	УслугиКОформлению.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(УслугиКОформлению.ДокументРеализации) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|		КОГДА УслугиКОформлению.Покупатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|		ИНАЧЕ УслугиКОформлению.Покупатель
	|	КОНЕЦ КАК Покупатель,
	|	ВЫБОР КОГДА УслугиКОформлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		УслугиКОформлению.Количество
	|	ИНАЧЕ
	|		-УслугиКОформлению.Количество
	|	КОНЕЦ КАК Количество,
	|	
	|	ВЫБОР КОГДА УслугиКОформлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		УслугиКОформлению.СуммаВыручки
	|	ИНАЧЕ
	|		-УслугиКОформлению.СуммаВыручки
	|	КОНЕЦ КАК СуммаВыручки
	|
	|ПОМЕСТИТЬ УслугиКОформлениюЗаПериод
	|ИЗ
	|	РегистрНакопления.УслугиКОформлениюОтчетовПринципалу КАК УслугиКОформлению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтбор КАК Отбор
	|	ПО УслугиКОформлению.Организация = Отбор.Организация
	|		И УслугиКОформлению.АналитикаУчетаНоменклатуры.МестоХранения = Отбор.Принципал
	|		И УслугиКОформлению.Валюта = Отбор.Валюта
	|		И УслугиКОформлению.Соглашение = Отбор.Соглашение
	|
	|ГДЕ
	|	УслугиКОформлению.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И УслугиКОформлению.Активность
	|";
	Запрос.Выполнить();
	
	УчетНДСУП.ДополнитьТаблицуДаннымиСчетовФактурВыданныхПокупателям(МенеджерВременныхТаблиц, "УслугиКОформлениюЗаПериод");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УслугиКОформлениюЗаПериод.Организация КАК Организация,
	|	УслугиКОформлениюЗаПериод.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	УслугиКОформлениюЗаПериод.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	УслугиКОформлениюЗаПериод.СчетФактура КАК СчетФактура,
	|	УслугиКОформлениюЗаПериод.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	УслугиКОформлениюЗаПериод.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	УслугиКОформлениюЗаПериод.Покупатель КАК Покупатель,
	|	СУММА(УслугиКОформлениюЗаПериод.Количество) КАК Количество,
	|	СУММА(УслугиКОформлениюЗаПериод.СуммаВыручки) КАК СуммаВыручки,
	|	СУММА(УслугиКОформлениюЗаПериод.Количество) КАК КоличествоОстаток,
	|	&ЕстьСуммаПродажиНДС КАК ЕстьСуммаПродажиНДС,
	|	УслугиКОформлениюЗаПериод.АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ УслугиКОформлению
	|ИЗ
	|	УслугиКОформлениюЗаПериод КАК УслугиКОформлениюЗаПериод
	|
	|СГРУППИРОВАТЬ ПО
	|	УслугиКОформлениюЗаПериод.Организация,
	|	УслугиКОформлениюЗаПериод.АналитикаУчетаНоменклатуры.Номенклатура,
	|	УслугиКОформлениюЗаПериод.АналитикаУчетаНоменклатуры.Характеристика,
	|	УслугиКОформлениюЗаПериод.СчетФактура,
	|	УслугиКОформлениюЗаПериод.ДатаСчетаФактуры,
	|	УслугиКОформлениюЗаПериод.НомерСчетаФактуры,
	|	УслугиКОформлениюЗаПериод.Покупатель,
	|	УслугиКОформлениюЗаПериод.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(УслугиКОформлениюЗаПериод.Количество) <> 0
	|	И СУММА(УслугиКОформлениюЗаПериод.СуммаВыручки) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	УслугиКОформлениюЗаПериод.АналитикаУчетаНоменклатуры.Номенклатура,
	|	УслугиКОформлениюЗаПериод.АналитикаУчетаНоменклатуры.Характеристика
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ УслугиКОформлениюЗаПериод
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Процедура заполнения документа по остаткам товаров переданных на комиссию
//
// Параметры:
//	Объект - ДокументОбъект - Текущий документ
//	КонецПериода - Дата - Дата заполнения
//	ЕстьСуммаПродажи - Булево - В документе есть данные о сумме продажи.
//
Процедура ЗаполнитьТоварыПоОстаткамПереданныхНаКомиссию(Объект, КонецПериода, ЕстьСуммаПродажи) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &КомиссионерНеВедетУчетПоРНПТ
	|				ИЛИ НЕ ЕСТЬNULL(ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыПереданные.НомерГТД
	|	КОНЕЦ										КАК НомерГТД,
	|	СУММА(ТоварыПереданные.КоличествоОстаток)	КАК КоличествоОстаток,
	|	СУММА(ВЫБОР
	|			КОГДА &КомиссионерНеВедетУчетПоРНПТ
	|				ТОГДА 0
	|			ИНАЧЕ ТоварыПереданные.КоличествоПоРНПТОстаток
	|		КОНЕЦ)									КАК КоличествоПоРНПТОстаток
	|ПОМЕСТИТЬ ТоварыПереданные
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&Граница,
	|			Организация = &Организация
	|			И АналитикаУчетаНоменклатуры.Партнер = &Партнер
	|			И Соглашение = &Соглашение
	|	
	|	) КАК ТоварыПереданные
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &КомиссионерНеВедетУчетПоРНПТ
	|				ИЛИ НЕ ЕСТЬNULL(ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыПереданные.НомерГТД
	|	КОНЕЦ
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &КомиссионерНеВедетУчетПоРНПТ
	|				ИЛИ НЕ ЕСТЬNULL(ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыПереданные.НомерГТД
	|	КОНЕЦ										КАК НомерГТД,
	|	СУММА(ТоварыПереданные.КоличествоОстаток)	КАК КоличествоОстаток,
	|	СУММА(ВЫБОР
	|			КОГДА &КомиссионерНеВедетУчетПоРНПТ
	|				ТОГДА 0
	|			ИНАЧЕ ТоварыПереданные.КоличествоПоРНПТОстаток
	|		КОНЕЦ)									КАК КоличествоПоРНПТОстаток,
	|	ВЫБОР
	|		КОГДА ТоварыПереданные.КоличествоОстаток < 0
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Знак
	|ПОМЕСТИТЬ ТоварыПереданныеОстатки
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(,
	|			Организация = &Организация
	|			И АналитикаУчетаНоменклатуры.Партнер = &Партнер
	|			И Соглашение = &Соглашение
	|	
	|	) КАК ТоварыПереданные
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &КомиссионерНеВедетУчетПоРНПТ
	|				ИЛИ НЕ ЕСТЬNULL(ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура.ПрослеживаемыйТовар, ЛОЖЬ)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыПереданные.НомерГТД
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТоварыПереданные.КоличествоОстаток < 0
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	НомерГТД
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Номенклатура	КАК Номенклатура,
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Характеристика	КАК Характеристика,
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры.Серия			КАК Серия,
	|	ТоварыПереданные.НомерГТД									КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПереданныеОстатки.КоличествоОстаток * ТоварыПереданныеОстатки.Знак, 0)
	|				< (ТоварыПереданные.КоличествоОстаток * ЕСТЬNULL(ТоварыПереданныеОстатки.Знак, 1))
	|			ТОГДА ЕСТЬNULL(ТоварыПереданныеОстатки.КоличествоОстаток, 0)
	|		ИНАЧЕ ТоварыПереданные.КоличествоОстаток
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыПереданныеОстатки.КоличествоПоРНПТОстаток * ТоварыПереданныеОстатки.Знак, 0)
	|				< (ТоварыПереданные.КоличествоПоРНПТОстаток * ЕСТЬNULL(ТоварыПереданныеОстатки.Знак, 1))
	|			ТОГДА ЕСТЬNULL(ТоварыПереданныеОстатки.КоличествоПоРНПТОстаток, 0)
	|		ИНАЧЕ ТоварыПереданные.КоличествоПоРНПТОстаток
	|	КОНЕЦ КАК КоличествоПоРНПТ
	|ИЗ
	|	ТоварыПереданные КАК ТоварыПереданные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыПереданныеОстатки КАК ТоварыПереданныеОстатки
	|		ПО ТоварыПереданные.АналитикаУчетаНоменклатуры = ТоварыПереданныеОстатки.АналитикаУчетаНоменклатуры
	|			И ТоварыПереданные.НомерГТД = ТоварыПереданныеОстатки.НомерГТД
	|
	|ГДЕ
	|	ТоварыПереданные.КоличествоОстаток <> 0
	|	И ЕСТЬNULL(ТоварыПереданныеОстатки.КоличествоОстаток, 0) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТоварыПереданные.АналитикаУчетаНоменклатуры";
	
	ДатаЗаполнения	= ?(ЗначениеЗаполнено(КонецПериода), КонецПериода, ТекущаяДатаСеанса());
	Граница			= Новый Граница(КонецДня(ДатаЗаполнения), ВидГраницы.Включая);
	
	КомиссионерНеВедетУчетПоРНПТ = Не Справочники.СоглашенияСКлиентами.КомиссионерВедетУчетПоРНПТ(Объект.Соглашение)
									Или Не УчетПрослеживаемыхТоваровЛокализация.ИспользоватьУчетПрослеживаемыхИмпортныхТоваров(Объект.Дата);
	
	Запрос.УстановитьПараметр("Граница",		Граница);
	Запрос.УстановитьПараметр("Организация",	Объект.Организация);
	Запрос.УстановитьПараметр("Партнер",		Объект.Партнер);
	Запрос.УстановитьПараметр("Соглашение",		Объект.Соглашение);
	Запрос.УстановитьПараметр("КомиссионерНеВедетУчетПоРНПТ", КомиссионерНеВедетУчетПоРНПТ);
	
	Объект.Товары.Очистить();
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", Новый Структура("НалогообложениеНДС, Дата, Организация", Объект.НалогообложениеНДС, ДатаЗаполнения, Объект.Организация));
	КэшированныеЗначения = Неопределено;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.КоличествоУпаковокУчет = НоваяСтрока.Количество;
		НоваяСтрока.КоличествоУпаковок = НоваяСтрока.Количество;
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
	КонецЦикла;
	
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС", Новый Структура("НалогообложениеНДС, Дата, Организация", Объект.НалогообложениеНДС, ДатаЗаполнения, Объект.Организация));
	СтруктураДействий.Вставить("ПересчитатьСумму", "КоличествоУпаковок");
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Дата", ДатаЗаполнения);
	ПараметрыЗаполнения.Вставить("Валюта", Объект.Валюта);
	ПараметрыЗаполнения.Вставить("Организация", Объект.Организация);
	ПараметрыЗаполнения.Вставить("Соглашение", Объект.Соглашение);
	ПараметрыЗаполнения.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
	ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "Цена, СтавкаНДС, ВидЦены");
	
	ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(
		Объект.Товары,
		, // Массив строк или структура отбора
		ПараметрыЗаполнения,
		СтруктураДействий);
	
	Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
		СтрокаТаблицы.СуммаСНДС = СтрокаТаблицы.Сумма + ?(Объект.ЦенаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
		Если ЕстьСуммаПродажи Тогда
			СтрокаТаблицы.СуммаПродажи = СтрокаТаблицы.СуммаСНДС;
			СтрокаТаблицы.СуммаПродажиНДС = СуммаПродажиНДС(СтрокаТаблицы.СуммаПродажи, СтрокаТаблицы.СтавкаНДС);
			СтрокаТаблицы.ЦенаПродажи = ?(СтрокаТаблицы.КоличествоУпаковок <> 0, СтрокаТаблицы.СуммаПродажи / СтрокаТаблицы.КоличествоУпаковок, 0);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполнения документа по остаткам товаров переданных на комиссию
//
// Параметры:
//	Объект - ДокументОбъект - Текущий документ
//	ОтчетКомиссионера - Булево
//
Процедура ЗаполнитьТоварыПоОстаткамПереданныхНаКомиссиюПоНовойСхеме(Объект, ОтчетКомиссионера = Истина) Экспорт
	
	Если ОтчетКомиссионера Тогда
		ИмяКолонкиЦенаПродажи = "ЦенаПродажи";
		ИмяКолонкиВидЦенПродажи = "ВидЦеныПродажи";
	Иначе
		ИмяКолонкиЦенаПродажи = "Цена";
		ИмяКолонкиВидЦенПродажи = "ВидЦены";
	КонецЕсли;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	КлючиАналитики.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ КлючиАналитики
		|ИЗ
		|	Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
		|ГДЕ
		|	КлючиАналитики.МестоХранения = &Договор
		|;
		|
		|//////////////////////////////////////////////////////////////////////////////// 1
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыПереданные.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТоварыПереданные.НомерГТД                   КАК НомерГТД,
		|	ТоварыПереданные.КоличествоОстаток          КАК КоличествоОстаток,
		|	ТоварыПереданные.КоличествоПоРНПТОстаток    КАК КоличествоПоРНПТОстаток
		|ПОМЕСТИТЬ ТоварыПереданные
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(,
		|			АналитикаУчетаНоменклатуры В
		|					(ВЫБРАТЬ
		|						КлючиАналитики.Ссылка
		|					ИЗ
		|						КлючиАналитики КАК КлючиАналитики)
		|			И Организация = &Организация) КАК ТоварыПереданные
		|
		|ГДЕ
		|	ТоварыПереданные.КоличествоОстаток > 0
		|;
		|
		|//////////////////////////////////////////////////////////////////////////////// 2
		|ВЫБРАТЬ
		|	ЕСТЬNULL(КлючиАналитики.Номенклатура,
		|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
		|	ЕСТЬNULL(КлючиАналитики.Характеристика,
		|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
		|	ВЫБОР
		|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		ИНАЧЕ КлючиАналитики.Назначение
		|	КОНЕЦ                                         КАК Назначение,
		|	ЕСТЬNULL(КлючиАналитики.Серия,
		|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК Серия,
		|	ТоварыПереданные.НомерГТД                     КАК НомерГТД,
		|	ТоварыПереданные.НомерГТД.СтранаПроисхождения КАК СтранаПроисхождения,
		|	Товары.ЕдиницаИзмерения                       КАК Упаковка,
		|	СУММА(ТоварыПереданные.КоличествоОстаток)     КАК Количество,
		|	СУММА(ТоварыПереданные.КоличествоОстаток)     КАК КоличествоУпаковок,
		|	СУММА(ТоварыПереданные.КоличествоПоРНПТОстаток) КАК КоличествоПоРНПТ
		|ИЗ
		|	ТоварыПереданные КАК ТоварыПереданные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
		|		ПО ТоварыПереданные.АналитикаУчетаНоменклатуры = КлючиАналитики.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
		|		ПО КлючиАналитики.Номенклатура = Товары.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(КлючиАналитики.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
		|	ЕСТЬNULL(КлючиАналитики.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
		|	ВЫБОР
		|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		ИНАЧЕ КлючиАналитики.Назначение
		|	КОНЕЦ,
		|	ЕСТЬNULL(КлючиАналитики.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)),
		|	ТоварыПереданные.НомерГТД,
		|	Товары.ЕдиницаИзмерения
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Серия,
		|	ВЫБОР
		|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		ИНАЧЕ КлючиАналитики.Назначение
		|	КОНЕЦ,
		|	НомерГТД";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Договор",     Объект.Договор);
	
	МассивСтрок = Новый Массив;
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	ДатаДокумента = ?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДатаСеанса());
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьСтавкуНДС",  Новый Структура("Организация, НалогообложениеНДС, Дата",
		Объект.Организация, Объект.НалогообложениеНДС, ДатаДокумента));
	
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		ТекущаяСтрока = Объект.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара);
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, Неопределено);
		
	КонецЦикла;
	
	ЦенаВключаетНДС = Неопределено;
	
	ВидЦены = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор,?(ОтчетКомиссионера, "ВидЦенУчетный", "ВидЦенПродажи"));
	Если Не ВидЦены = Неопределено Тогда
		ЦенаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦены,"ЦенаВключаетНДС");
	КонецЕсли;
	
	Если ЦенаВключаетНДС = Объект.ЦенаВключаетНДС Тогда
		
		ПараметрыЗаполненияЦен = Новый Структура();
		ПараметрыЗаполненияЦен.Вставить("ПоляЗаполнения",     "ВидЦены, Цена, СтавкаНДС");
		ПараметрыЗаполненияЦен.Вставить("Дата",               ДатаДокумента);
		ПараметрыЗаполненияЦен.Вставить("Валюта",             Объект.Валюта);
		ПараметрыЗаполненияЦен.Вставить("Организация",        Объект.Организация);
		ПараметрыЗаполненияЦен.Вставить("НалогообложениеНДС", Объект.НалогообложениеНДС);
		ПараметрыЗаполненияЦен.Вставить("ВидЦены", ВидЦены);
		
		СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
		
		Для Каждого СтрокаТовара Из Объект.Товары Цикл
			
			СтрокаТовара[ИмяКолонкиВидЦенПродажи] = ВидЦены;
			МассивСтрок.Добавить(СтрокаТовара);
			
		КонецЦикла;
		
		СтруктураДействийРасчетаЦен = Новый Структура;
		СтруктураДействийРасчетаЦен.Вставить("ПересчитатьСумму");
		СтруктураДействийРасчетаЦен.Вставить("ПересчитатьСуммуНДС",  СтруктураПересчетаСуммы);
		СтруктураДействийРасчетаЦен.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
		СтруктураДействийРасчетаЦен.Вставить("ПересчитатьКоличествоУпаковок");
		
		ПараметрыЗаполненияЦеныПродажи = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыЗаполненияЦеныВСтрокеТЧ(Объект);
		ПараметрыЗаполненияЦеныПродажи.Вставить("ИмяПоляЦена", ИмяКолонкиЦенаПродажи);
		ПараметрыЗаполненияЦеныПродажи.Вставить("ИмяКолонкиВидЦены", ИмяКолонкиВидЦенПродажи);
		ПараметрыЗаполненияЦеныПродажи.Вставить("Дата", ДатаДокумента);
		
		СтруктураДействийРасчетаЦен.Вставить("ЗаполнитьЦенуПродажи", ПараметрыЗаполненияЦеныПродажи);
		
		Если ОтчетКомиссионера Тогда
			СтруктураДействийРасчетаЦен.Вставить("ПересчитатьСуммуПродажи");
			СтруктураДействийРасчетаЦен.Вставить("ПересчитатьСуммуПродажиНДС");
		КонецЕсли;
		
		СтруктураДействийРасчетаЦен.Вставить("ЗаполнитьСтавкуНДС",  Новый Структура("Организация, НалогообложениеНДС, Дата",
			Объект.Организация, Объект.НалогообложениеНДС, ДатаДокумента));
		
		ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(Объект.Товары,
													МассивСтрок,
													ПараметрыЗаполненияЦен,
													СтруктураДействийРасчетаЦен);
		
	ИначеЕсли ЗначениеЗаполнено(Объект.Соглашение) 
		И ЗначениеЗаполнено(Объект.Договор) Тогда
		
		ТекстОшибки = НСтр("ru='В соглашении и у вида цен в договоре различаются значения признака Цена включает НДС. Виды цен и цены не будут заполнены.'");
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			Объект);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполнения колонки документа по данным учета.
//
// Параметры:
//	Объект - ДокументОбъект - Текущий документ
//	КонецПериода - Дата - Дата заполнения
//	ЕстьСуммаПродажи - Булево - В документе есть данные о сумме продажи
//	РассчитыватьВознаграждение - Булево - Признак необходимости расчета комиссионного вознаграждения.
//	НомерСтроки - Неопределено, Число - 
//
Процедура ПересчитатьОстаткиПоДаннымУчета(Объект, КонецПериода, ЕстьСуммаПродажи, РассчитыватьВознаграждение = Ложь, НомерСтроки  = Неопределено) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.НомерГТД КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	Товары.Упаковка КАК Упаковка
	|
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|ГДЕ
	|	&НомерСтроки = Неопределено
	|	ИЛИ Товары.НомерСтроки = &НомерСтроки
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аналитика.КлючАналитики КАК КлючАналитики
	|
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Товары КАК Товары
	|	ПО
	|		Аналитика.Номенклатура = Товары.Номенклатура
	|		И Аналитика.Характеристика = Товары.Характеристика
	|		И Аналитика.Серия = Товары.Серия
	|		И Аналитика.Назначение = Товары.Назначение
	|ГДЕ
	|	Аналитика.МестоХранения = &Партнер
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	Остатки.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	Остатки.НомерГТД КАК НомерГТД,
	|	Остатки.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	Остатки.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|	Остатки.КоличествоОстаток КАК КоличествоУпаковокУчет
	|
	|ПОМЕСТИТЬ ТоварыПереданные
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&Граница,
	|		Организация = &Организация
	|		И Соглашение = &Соглашение
	|		И АналитикаУчетаНоменклатуры В (
	|			ВЫБРАТЬ
	|				Аналитика.КлючАналитики
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|			)
	|	) КАК Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	НомерГТД,
	|	Серия
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Серия КАК Серия,
	|	Товары.Назначение КАК Назначение,
	|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК Коэффициент,
	|	ЕСТЬNULL(ТоварыПереданные.КоличествоУпаковокУчет, 0) КАК КоличествоУпаковокУчет
	|ИЗ
	|	Товары КАК Товары
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТоварыПереданные КАК ТоварыПереданные
	|	ПО
	|		Товары.Номенклатура = ТоварыПереданные.Номенклатура
	|		И Товары.Характеристика = ТоварыПереданные.Характеристика
	|		И Товары.НомерГТД = ТоварыПереданные.НомерГТД
	|		И Товары.Серия = ТоварыПереданные.Серия
	|		И Товары.Назначение = ТоварыПереданные.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.УпаковкиЕдиницыИзмерения КАК Упаковки
	|	ПО
	|		Товары.Упаковка = Упаковки.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|");
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Упаковки",
		"Товары.Номенклатура"));

	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Партнер", Объект.Партнер);
	Запрос.УстановитьПараметр("Соглашение", Объект.Соглашение);
	Запрос.УстановитьПараметр("НомерСтроки", НомерСтроки);
	
	ДатаЗаполнения = ?(ЗначениеЗаполнено(КонецПериода), КонецПериода, ТекущаяДатаСеанса());
	Граница = Новый Граница(КонецДня(ДатаЗаполнения), ВидГраницы.Включая);
	Запрос.УстановитьПараметр("Граница", Граница);
	
	Товары = Объект.Товары.Выгрузить();
	Запрос.УстановитьПараметр("Товары", Товары);
	
	КэшированныеЗначения = Неопределено;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", Новый Структура("ЦенаВключаетНДС", Объект.ЦенаВключаетНДС));
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	Если ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура") Тогда
		УчетПрослеживаемыхТоваровКлиентСерверЛокализация.ДополнитьОписаниеНастроекПересчетаРеквизитовТабличнойЧасти(
			Объект,
			СтруктураДействий,
			"Партнер");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаТаблицы = Объект.Товары[Выборка.НомерСтроки - 1];
		КоличествоУпаковокУчет = ?(Выборка.Коэффициент <> 0, Окр(Выборка.КоличествоУпаковокУчет / Выборка.Коэффициент, 2, 1), 0);
		Если СтрокаТаблицы.КоличествоУпаковокУчет <> КоличествоУпаковокУчет Тогда
			
			СтрокаТаблицы.КоличествоУпаковокУчет = КоличествоУпаковокУчет;
			СтрокаТаблицы.КоличествоУпаковок = Выборка.КоличествоУпаковокУчет - СтрокаТаблицы.КоличествоУпаковокФакт;
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТаблицы, СтруктураДействий, КэшированныеЗначения);
			
			СтрокаТаблицы.СуммаСНДС = СтрокаТаблицы.Сумма + ?(Объект.ЦенаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
			Если ЕстьСуммаПродажи Тогда
				СтрокаТаблицы.СуммаПродажи = СтрокаТаблицы.ЦенаПродажи * СтрокаТаблицы.КоличествоУпаковок;
				СтрокаТаблицы.СуммаПродажиНДС = СуммаПродажиНДС(СтрокаТаблицы.СуммаПродажи, СтрокаТаблицы.СтавкаНДС);
			КонецЕсли;
			
			РассчитыватьВознаграждение = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполняет сумму с НДС в строках табличной части "Товары".
//
// Параметры:
//  Товары - ДанныеФормыКоллекция - таблица, в которой необходимо заполнить поле СуммаСНДС.
//  ЦенаВключаетНДС - Булево - признак включения НДС в цену.
//
Процедура ЗаполнитьСуммуСНДС(Товары, ЦенаВключаетНДС) Экспорт
	
	Для Каждого СтрокаТаблицы Из Товары Цикл
		СтрокаТаблицы.СуммаСНДС = СтрокаТаблицы.Сумма
			+ ?(ЦенаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	КонецЦикла;
	
КонецПроцедуры

// Процедура заполняет сумму НДС продажи в строках табличной части "Товары".
//
// Параметры:
//  Товары - ДанныеФормыКоллекция - таблица, в которой необходимо заполнить поле СуммаПродажиНДС.
//
Процедура ЗаполнитьСуммуПродажиНДС(Товары) Экспорт
	
	Для Каждого СтрокаТаблицы Из Товары Цикл
		СтрокаТаблицы.СуммаПродажиНДС = СуммаПродажиНДС(СтрокаТаблицы.СуммаПродажи, СтрокаТаблицы.СтавкаНДС);
	КонецЦикла;
	
КонецПроцедуры

// Процедура рассчитывает комиссионное вознаграждение.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура, ДанныеФормыЭлементКоллекции - Текущий документ или текущая строка.
//	ПроцентНДС - Число - Процент НДС.
//
Процедура РассчитатьСуммуНДСВознаграждения(Объект, ПроцентНДС) Экспорт
	
	Объект.СуммаНДСВознаграждения = Окр(Объект.СуммаВознаграждения * ПроцентНДС / (100 + ПроцентНДС), 2, РежимОкругления.Окр15как20);
	
КонецПроцедуры

// Процедура рассчитывает комиссионное вознаграждение построчно.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - Текущий документ.
//	ПроцентНДС - Число - Процент НДС.
//
Процедура РассчитатьСуммуНДСВознагражденияВСтроках(Объект, ПроцентНДС) Экспорт
	
	Для каждого СтрокаТоваров из Объект.Товары Цикл
		РассчитатьСуммуНДСВознаграждения(СтрокаТоваров, ПроцентНДС);
	КонецЦикла;
	
	Объект.СуммаНДСВознаграждения = Объект.Товары.Итог("СуммаНДСВознаграждения");
	
КонецПроцедуры

// Процедура рассчитывает комиссионное вознаграждение.
//
// Параметры:
//  Объект - ДанныеФормыКоллекция - объект, в котором осуществляется расчет.
//  КолонкаСуммы - Строка - имя колонки с суммой номенклатуры, для которой происходит расчет вознаграждения.
//  ЕстьСуммаНДСВСтроке - Булево - признак, что расчет суммы НДС вознаграждения происходит построчно.
//
Процедура РассчитатьСуммуВознаграждения(Объект, КолонкаСуммы = "СуммаПродажи", ЕстьСуммаНДСВСтроке = Ложь) Экспорт
	
	ПроцентНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(Объект.СтавкаНДСВознаграждения);
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		
		Если Объект.СпособРасчетаВознаграждения = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроцентОтРазностиСуммыПродажиИСуммыКомитента Тогда
			СтрокаТоваров.СуммаВознаграждения = (СтрокаТоваров[КолонкаСуммы] - СтрокаТоваров.СуммаСНДС) * Объект.ПроцентВознаграждения / 100;
			
		ИначеЕсли Объект.СпособРасчетаВознаграждения = Перечисления.СпособыРасчетаКомиссионногоВознаграждения.ПроцентОтСуммыПродажи Тогда
			СтрокаТоваров.СуммаВознаграждения = СтрокаТоваров[КолонкаСуммы] * Объект.ПроцентВознаграждения / 100;
			
		Иначе
			СтрокаТоваров.СуммаВознаграждения = 0;
			
		КонецЕсли;
		
		Если ЕстьСуммаНДСВСтроке Тогда
			РассчитатьСуммуНДСВознаграждения(СтрокаТоваров, ПроцентНДС);
		КонецЕсли;
		
	КонецЦикла;
	
	Объект.СуммаВознаграждения = Объект.Товары.Итог("СуммаВознаграждения");
	
	Если ЕстьСуммаНДСВСтроке Тогда
		Объект.СуммаНДСВознаграждения = Объект.Товары.Итог("СуммаНДСВознаграждения");
	Иначе
		РассчитатьСуммуНДСВознаграждения(Объект, ПроцентНДС);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет ставку НДС для комиссионного вознаграждения.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - Текущий документ
//	ПроцентНДС - Число - Процент НДС.
//  ЕстьСуммаНДСВСтроке - Булево - признак, что расчет суммы НДС вознаграждения происходит построчно.
//
Процедура ЗаполнитьСтавкуНДСкомиссионногоВознаграждения(Объект, ПроцентНДС = 0, ЕстьСуммаНДСВСтроке = Ложь) Экспорт
	
	ПараметрыУчетаПоОрганизации = УчетНДСУП.ПараметрыУчетаПоОрганизации(Объект.Организация, Объект.Дата);
	
	НалогообложениеНДС = ПараметрыУчетаПоОрганизации.ОсновноеНалогообложениеНДСПродажи;
		
	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС Тогда
		Объект.СтавкаНДСВознаграждения = УчетНДСУП.СтавкаНДСПоНоменклатуреИНалогообложению(
				Объект.Услуга, Объект.НалогообложениеНДС, Объект.Организация, Объект.Дата);
	Иначе
		Объект.СтавкаНДСВознаграждения = Справочники.СтавкиНДС.БезНДС;
	КонецЕсли;
	ПроцентНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(Объект.СтавкаНДСВознаграждения);
	
	Если ЕстьСуммаНДСВСтроке Тогда
		РассчитатьСуммуНДСВознагражденияВСтроках(Объект, ПроцентНДС);
	Иначе
		РассчитатьСуммуНДСВознаграждения(Объект, ПроцентНДС);
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет сумму НДС комиссионного вознаграждения в табличной части документа.
//
// Параметры:
//  Товары - ТабличнаяЧасть - таблица, в которой осуществляется заполнение.
//  СуммаНДСВознаграждения - Число - сумма НДС вознаграждения.
//
Процедура ЗаполнитьСуммуНДСВознагражденияВТабличнойЧасти(Товары, Знач СуммаНДСВознаграждения) Экспорт
	
	ВсегоСуммаВознаграждения = Товары.Итог("СуммаВознаграждения");
	
	Для Каждого СтрокаТаблицы Из Товары Цикл
		
		СтрокаТаблицы.СуммаНДСВознаграждения = ?(ВсегоСуммаВознаграждения <> 0, Окр(СуммаНДСВознаграждения * СтрокаТаблицы.СуммаВознаграждения / ВсегоСуммаВознаграждения, 2, 1), 0);
		
		ВсегоСуммаВознаграждения = ВсегоСуммаВознаграждения - СтрокаТаблицы.СуммаВознаграждения;
		СуммаНДСВознаграждения = СуммаНДСВознаграждения - СтрокаТаблицы.СуммаНДСВознаграждения;
			
	КонецЦикла;
	
КонецПроцедуры

// Функция возвращает вид цены из договора комиссионных продаж
//
// Параметры:
//  Договор     - СправочникСсылка.ДоговорыКонтрагентов - договор с комиссионером
//  ТипВидаЦены - Булево - "Истина" для получения учетного вида цены, "Ложь" для получения вида цены продажи
//
// Возвращаемое значение:
//   СправочникСсылка.ВидыЦен - СправочникСсылка.ВидыЦен - вид цены, указанный в договоре
//
Функция ВидЦеныДоговора(Договор, ТипВидаЦены) Экспорт
	
	ВидЦены = Справочники.ВидыЦен.ПустаяСсылка();
	ПолеВидаЦен = ?(ТипВидаЦены, "ВидЦенУчетный", "ВидЦеныПродажи");
	
	Если ЗначениеЗаполнено(Договор) И Договор.КомиссионныеПродажи25 Тогда
		ВидЦены = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, ПолеВидаЦен);
	КонецЕсли;
	
	Возврат ВидЦены;
	
КонецФункции

#Область РеализацияЧерезКомиссионера

// Выполняет поиск действующего договора с конечным клиентом по заданным реквизитам.
// Если найден один действующий договор, возвращает ссылку на него, в противном случае - пустую ссылку.
//
// Параметры:
//   Объект                  - ДокументОбъект - Объект, из которого будут взяты основные параметры для поиска.
//   ХозяйственныеОперации   - ПеречислениеСсылка.ХозяйственныеОперации, Массив - Одна или несколько хозяйственных
//                             операций (тип договора).
//   ВалютаВзаиморасчетов    - СправочникСсылка.Валюты - Валюта взаиморасчетов с партнером по договору.
// 
// Возвращаемое значение:
//   СправочникСсылка.ДоговорыКонтрагентов  - договор контрагента по умолчанию.
//
Функция ПолучитьДоговорПоУмолчанию(
			Объект,
			ХозяйственныеОперации,
			ВалютаВзаиморасчетов = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") Тогда
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	СписокПартнеров = Новый СписокЗначений;
	ПартнерыИКонтрагенты.ЗаполнитьСписокПартнераСРодителями(Объект.КлиентПартнер, СписокПартнеров);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка
	|
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|
	|ГДЕ
	|	(НЕ ДоговорыКонтрагентов.ПометкаУдаления)
	|	И ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|	И ДоговорыКонтрагентов.Партнер В (&СписокПартнеров)
	|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.ДоговорСКомиссионером = &ДоговорСКомиссионером
	|	И ((НЕ &ОтборХозяйственнаяОперация)
	|			ИЛИ ДоговорыКонтрагентов.ХозяйственнаяОперация В (&ХозяйственнаяОперация))
	|	И ((НЕ &ОтборВалютаВзаиморасчетов)
	|			ИЛИ ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаВзаиморасчетов)
	|	И ДоговорыКонтрагентов.Ссылка = &ТекущийДоговор;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ДоговорыКонтрагентов.Ссылка
	|
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|
	|ГДЕ
	|	(НЕ ДоговорыКонтрагентов.ПометкаУдаления)
	|	И ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|	И ДоговорыКонтрагентов.Партнер В (&СписокПартнеров)
	|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.ДоговорСКомиссионером = &ДоговорСКомиссионером
	|	И ((НЕ &ОтборХозяйственнаяОперация)
	|			ИЛИ ДоговорыКонтрагентов.ХозяйственнаяОперация В (&ХозяйственнаяОперация))
	|	И ((НЕ &ОтборВалютаВзаиморасчетов)
	|			ИЛИ ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаВзаиморасчетов)
	|");
	Запрос.УстановитьПараметр("ТекущийДоговор", Объект.КлиентДоговор);
	Запрос.УстановитьПараметр("СписокПартнеров", СписокПартнеров);
	Запрос.УстановитьПараметр("Контрагент", Объект.КлиентКонтрагент);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ДоговорСКомиссионером", Объект.Договор);
	Запрос.УстановитьПараметр("ОтборХозяйственнаяОперация", ЗначениеЗаполнено(ХозяйственныеОперации));
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственныеОперации);
	Запрос.УстановитьПараметр("ОтборВалютаВзаиморасчетов", ЗначениеЗаполнено(ВалютаВзаиморасчетов));
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", ВалютаВзаиморасчетов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если Не МассивРезультатов[0].Пустой() Тогда
		
		Выборка = МассивРезультатов[0].Выбрать();
		Выборка.Следующий();
		
		ДоговорПоУмолчанию = Выборка.Ссылка;
		
	Иначе
		Выборка = МассивРезультатов[1].Выбрать();
	
		Если Не Выборка.Следующий() Тогда
			ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ИначеЕсли Выборка.Количество() = 1 Тогда
			ДоговорПоУмолчанию = Выборка.Ссылка;
		Иначе
			ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

// Выполняет поиск действующего договора с комиссионером по заданным реквизитам.
// Если найден один действующий договор, возвращает ссылку на него, в противном случае - пустую ссылку.
//
// Параметры:
//   Объект - СправочникОбъект.ДоговорыКонтрагентов - Объект, из которого будут взяты основные параметры для поиска.
//
// Возвращаемое значение:
//   СправочникСсылка.ДоговорыКонтрагентов  - договор контрагента по умолчанию.
//
Функция ПолучитьДоговорСКомиссионеромПоУмолчанию(Объект) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") Тогда
		Возврат Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
	СписокПартнеров = Новый СписокЗначений;
	ПартнерыИКонтрагенты.ЗаполнитьСписокПартнераСРодителями(Объект.КомиссионерПартнер, СписокПартнеров);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка
	|
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|
	|ГДЕ
	|	(НЕ ДоговорыКонтрагентов.ПометкаУдаления)
	|	И ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|	И ДоговорыКонтрагентов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|	И ДоговорыКонтрагентов.Партнер В (&СписокПартнеров)
	|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаВзаиморасчетов
	|	И ДоговорыКонтрагентов.Ссылка = &ТекущийДоговор;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ДоговорыКонтрагентов.Ссылка
	|
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|
	|ГДЕ
	|	(НЕ ДоговорыКонтрагентов.ПометкаУдаления)
	|	И ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|	И ДоговорыКонтрагентов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссию)
	|	И ДоговорыКонтрагентов.Партнер В (&СписокПартнеров)
	|	И ДоговорыКонтрагентов.Контрагент = &Контрагент
	|	И ДоговорыКонтрагентов.Организация = &Организация
	|	И ДоговорыКонтрагентов.ВалютаВзаиморасчетов = &ВалютаВзаиморасчетов
	|");
	Запрос.УстановитьПараметр("ТекущийДоговор", Объект.ДоговорСКомиссионером);
	Запрос.УстановитьПараметр("СписокПартнеров", СписокПартнеров);
	Запрос.УстановитьПараметр("Контрагент", Объект.КомиссионерКонтрагент);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ВалютаВзаиморасчетов", Объект.ВалютаВзаиморасчетов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если Не МассивРезультатов[0].Пустой() Тогда
		
		Выборка = МассивРезультатов[0].Выбрать();
		Выборка.Следующий();
		
		ДоговорПоУмолчанию = Выборка.Ссылка;
		
	Иначе
		Выборка = МассивРезультатов[1].Выбрать();
	
		Если Не Выборка.Следующий() Тогда
			ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		ИначеЕсли Выборка.Количество() = 1 Тогда
			ДоговорПоУмолчанию = Выборка.Ссылка;
		Иначе
			ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

//Функция возвращает версию комиссии, который может использовать документ
//
// Параметры:
//  Идентификатор - СправочникСсылка.ИдентификаторыОбъектовМетаданных - идентификатор документа
//
// Возвращаемое значение:
//  Строка - версии комиссии
//
Функция ПостфиксСхемыКомиссии(Идентификатор = "") Экспорт

	Версия = "11.0";
	
	МетаданныеОбъекта = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(Идентификатор, Ложь);
	
	Если МетаданныеОбъекта = Метаданные.Документы.ПоступлениеТоваровОтХранителя 
		ИЛИ МетаданныеОбъекта = Метаданные.Документы.ПередачаТоваровХранителю 
		ИЛИ МетаданныеОбъекта = Метаданные.Документы.АктОРасхожденияхПослеПриемки 
		ИЛИ МетаданныеОбъекта = Метаданные.Документы.АктОРасхожденияхПослеОтгрузки Тогда
		Версия = "11.5";
	КонецЕсли;

	Возврат Версия;

КонецФункции

//Функция возвращает представление постфикса для схемы комиссии версии 2.5/11.5
//
// Возвращаемое значение:
//  Строка - версии комиссии
//
Функция ПостфиксСхемыКомиссии25() Экспорт
	
	Версия = "11.5";
	
	Возврат Версия;

КонецФункции

//Функция возвращает представление постфикса для схемы комиссии версии 2.0/11.0
//
// Возвращаемое значение:
//  Строка - версии комиссии
//
Функция ПостфиксСхемыКомиссии20() Экспорт
	
	Версия = "11.0";
	
	Возврат Версия;

КонецФункции

// Определяет, относится ли хозяйственная операция к операциям реализации по схеме комиссионной торговли версии 2.5.
// 
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации
// 
// Возвращаемое значение:
//  Булево
//
Функция РеализацияЧерезКомиссионера(ХозяйственнаяОперация) Экспорт
	
	Возврат ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияЧерезКомиссионера
		ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности
	
КонецФункции

// Определяет, относится ли хозяйственная операция к операциям возврата по схеме комиссионной торговли версии 2.5.
// 
// Параметры:
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации
// 
// Возвращаемое значение:
//  Булево
//
Функция ВозвратТоваровЧерезКомиссионера(ХозяйственнаяОперация) Экспорт
	
	Возврат ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровЧерезКомиссионера;
	
КонецФункции

// Возвращает текст для подстановки в запрос условий отбора по хозяйственным операциям реализации 
// по схеме комиссионной торговли версии 2.5.
// 
// Параметры:
//  ТекстЗапросаПереопределенияХозяйственнойОперации - Строка
// 
// Возвращаемое значение:
//  Строка
//
Функция ТекстЗапросаРеализацияЧерезКомиссионера(ТекстЗапросаПереопределенияХозяйственнойОперации) Экспорт
	
	Возврат СтрШаблон("(%1 = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионера)
		|	ИЛИ %1 = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))", 
		ТекстЗапросаПереопределенияХозяйственнойОперации);
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "КомиссионнаяТорговляСервер.ТолькоКомиссионныеПродажи25_ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.8.230";
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.Комментарий = НСтр("ru = 'Заполняет ФО использования старой и новой схемы комиссии'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ОтчетКомиссионера.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Константы.ИспользоватьСоглашенияСКлиентами.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Константы.ИспользоватьДоговорыСКлиентами.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Константы.ВыбиратьВерсиюКомиссионныхПродаж.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Константы.ИспользоватьКомиссиюПриПродажах.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
		
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Константы.ТолькоКомиссионныеПродажи25.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Константы.ИспользуетсяКомиссионнаяПродажа20.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Константы.ИспользоватьДоговорыСКлиентами.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Константы.ВыбиратьВерсиюКомиссионныхПродаж.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Константы.ИспользоватьКомиссиюПриПродажах.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Константы.ИспользоватьДоговорыСКлиентами.ПолноеИмя());	
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.РеализацияТоваровУслуг.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Документы.ОтчетКомиссионера.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "Любой";
	
КонецПроцедуры

Процедура ТолькоКомиссионныеПродажи25_ОбработатьДанныеДляПереходаНаНовуюВерсию() Экспорт
	
	МенеджерЗначенияТолькоКомиссионныеПродажи25  = Константы.ТолькоКомиссионныеПродажи25.СоздатьМенеджерЗначения();
	МенеджерИспользуетсяКомиссионнаяПродажа20    = Константы.ИспользуетсяКомиссионнаяПродажа20.СоздатьМенеджерЗначения();
	МенеджерВыбиратьВерсиюКомиссионныхПродаж     = Константы.ВыбиратьВерсиюКомиссионныхПродаж.СоздатьМенеджерЗначения();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию);
	Запрос.УстановитьПараметр("ПустаяХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	РеализацияТоваровУслуг.Ссылка
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.ХозяйственнаяОперация = &ХозяйственнаяОперация
	|	И РеализацияТоваровУслуг.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетКомиссионера.Ссылка
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ОтчетКомиссионера
	|ГДЕ
	|	ОтчетКомиссионера.ХозяйственнаяОперация = &ПустаяХозяйственнаяОперация
	|	И ОтчетКомиссионера.Проведен";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЕстьДокументыПоСтаройКомиссии = Выборка.Следующий();
	МенеджерЗначенияТолькоКомиссионныеПродажи25.Значение = НЕ ЕстьДокументыПоСтаройКомиссии;
	МенеджерИспользуетсяКомиссионнаяПродажа20.Значение = ЕстьДокументыПоСтаройКомиссии;
	
	ИспользоватьСоглашенияСКлиентами = Константы.ИспользоватьСоглашенияСКлиентами.Получить();
	ИспользоватьДоговорыСКлиентами = Константы.ИспользоватьДоговорыСКлиентами.Получить();
	ИспользоватьКомиссиюПриПродажах = Константы.ИспользоватьКомиссиюПриПродажах.Получить();
	
	Если ИспользоватьКомиссиюПриПродажах И НЕ ИспользоватьДоговорыСКлиентами
		И ЕстьДокументыПоСтаройКомиссии Тогда
		МенеджерИспользоватьДоговорыСКлиентами = Константы.ИспользоватьДоговорыСКлиентами.СоздатьМенеджерЗначения();
		МенеджерИспользоватьДоговорыСКлиентами.Значение = Истина;
		МенеджерИспользоватьДоговорыСКлиентами.Записать();
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерИспользоватьДоговорыСКлиентами);
	КонецЕсли;
	
	МенеджерВыбиратьВерсиюКомиссионныхПродаж.Значение = ЕстьДокументыПоСтаройКомиссии И ИспользоватьКомиссиюПриПродажах;
	
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерЗначенияТолькоКомиссионныеПродажи25);
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерИспользуетсяКомиссионнаяПродажа20);
	ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерВыбиратьВерсиюКомиссионныхПродаж);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьСтрокуПоТоварамКОформлениюОтчетовКомитентуЗаПериод(ТабличнаяЧасть,
																	ПараметрыЗаполнения,
																	ДанныеСтроки,
																	Количество,
																	СтруктураДействий,
																	КэшированныеЗначения)
	
	НоваяСтрока = ТабличнаяЧасть.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
	
	НоваяСтрока.Количество			= Количество;
	НоваяСтрока.КоличествоУпаковок	= Количество;
	
	Если ДанныеСтроки.Количество < Количество Тогда
		НоваяСтрока.СуммаПродажи = ДанныеСтроки.СуммаПродажи / ДанныеСтроки.Количество * Количество;
	КонецЕсли;
	
	НоваяСтрока.ЦенаПродажи = ?(НоваяСтрока.Количество <> 0, НоваяСтрока.СуммаПродажи / НоваяСтрока.Количество, 0);
	
	ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	Если ДанныеСтроки.ЕстьСуммаПродажиНДС Тогда
		НоваяСтрока.СуммаПродажиНДС = СуммаПродажиНДС(НоваяСтрока.СуммаПродажи, НоваяСтрока.СтавкаНДС);
	КонецЕсли;
	
КонецПроцедуры

Функция СуммаПродажиНДС(СуммаПродажи, СтавкаНДС) Экспорт
	
	СуммаПродажиНДС = ЦенообразованиеКлиентСервер.РассчитатьСуммуНДС(СуммаПродажи, СтавкаНДС);
	Возврат Окр(СуммаПродажиНДС, 2, РежимОкругления.Окр15как20);
	
КонецФункции

#КонецОбласти
