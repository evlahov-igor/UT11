////////////////////////////////////////////////////////////////////////////////
// Модуль "НаправленияДеятельностиКлиентСервер" содержит процедуры,
// связанные с интерактивной работой пользователя в формах документов.
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Предназначена для использования в формах документах в обработчиках событий, которые приводят к изменению назначения в
// строке табличной части документа. Добавляет в структуру действий действие, которое отрабатывается механизмами
// обработки табличных частей (см. процедуру ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ) Выполнение такого
// действия актуализирует назначение в строке табличной части документа, в соответствии с изменившимся направлением деятельности.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма в которой необходимо выполнить действие по обработки строки табличной части.
//  СтруктураДействий - Структура - Структура действий, используемая механизмами обработки табличных частей (см.
//                                  процедуру ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ).
//
Процедура СтруктураДействийВставитьПриДобавленииСтроки(Форма, СтруктураДействий) Экспорт
	
	ПорядокОбработкиДокумента = Форма.МетаданныеФормы;
	
	ИмяРеквизитаПоЗаказу = ПорядокОбработкиДокумента.ИмяРеквизитаПоЗаказу;
	ИмяРеквизитаОбособленно = ПорядокОбработкиДокумента.Служебные.ИмяРеквизитаОбособленно;

	НазначениеПоУмолчанию = Неопределено;
	Если ИмяРеквизитаПоЗаказу = Неопределено
		Или Не ЗначениеЗаполнено(Форма.Объект[ИмяРеквизитаПоЗаказу])
		Или Не Форма.Объект[ИмяРеквизитаПоЗаказу] Тогда
			НазначениеПоУмолчанию = Форма.НаправленияДеятельностиКэшированныеЗначения.НазначениеПоУмолчанию;
	КонецЕсли;
		
	СтруктураДействий.Вставить("ПроверитьЗаполнитьНазначение", НазначениеПоУмолчанию);
	
	Если ИмяРеквизитаОбособленно <> Неопределено И ЗначениеЗаполнено(НазначениеПоУмолчанию) Тогда
		СтруктураДействий.Вставить("ЗаполнитьФлагОбособленноПоНазначению", ИмяРеквизитаОбособленно);
	КонецЕсли;
	
КонецПроцедуры

// Заполняет невидимое поле Назначение в документах исходя из заполненного поля Обособленно.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма в которой необходимо выполнить действие по обработки строки табличной части.
//  ТекущаяСтрока - ДанныеФормыЭлементКоллекции - строка в которой нужно заполнить назначение исходя из флага Обособленно.
//
Процедура ЗаполнитьНазначениеПоФлагуОбособленно(Форма, ТекущаяСтрока) Экспорт
	
	Если ТекущаяСтрока.Обособленно Тогда
	
		ТекущаяСтрока.Назначение = Форма.НаправленияДеятельностиКэшированныеЗначения.НазначениеПоУмолчанию;
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

