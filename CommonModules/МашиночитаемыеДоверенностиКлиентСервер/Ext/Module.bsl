#Область ПрограммныйИнтерфейс

// Коллекция видов документов
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение- где:
//   * Ключ - Строка - код вида документа, 
//   * Значение - Строка - наименование вида документа.
//
Функция ВидыДокументовФизическихЛиц() Экспорт
	
	ВидыДокументов = Новый Соответствие();;
	
	ВидыДокументов.Вставить("07", "Военный билет");
	ВидыДокументов.Вставить("10", "Паспорт иностранного гражданина");
	ВидыДокументов.Вставить("11", "Свидетельство о рассмотрении ходатайства о признании лица беженцем на территории Российской Федерации по существу");
	ВидыДокументов.Вставить("12", "Вид на жительство в Российской Федерации");
	ВидыДокументов.Вставить("13", "Удостоверение беженца");
	ВидыДокументов.Вставить("15", "Разрешение на временное проживание в Российской Федерации");
	ВидыДокументов.Вставить("19", "Свидетельство о предоставлении временного убежища на территории Российской Федерации");
	ВидыДокументов.Вставить("21", "Паспорт гражданина Российской Федерации");
	ВидыДокументов.Вставить("24", "Удостоверение личности военнослужащего Российской Федерации");
	
	Возврат ВидыДокументов;
	
КонецФункции

// Возвращает статус МЧД, полученной из реестра
//
// Параметры:
//  СтатусДоверенности - Строка - "CREATED" - дата начала действия не наступила, "ACTIVE" - действует,
//                                "EXPIRED" - истекла, "REVOKED/DECLINED" - отменена (отозвана), "" - не запрашивался
//
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыМашиночитаемойДоверенности
//
Функция СтатусМЧД(СтатусДоверенности) Экспорт
	Если СтатусДоверенности = "ACTIVE" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенности.Зарегистрировано");
	ИначеЕсли СтатусДоверенности = "CREATED" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенности.ВРаботе");
	ИначеЕсли СтатусДоверенности = "EXPIRED" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенности.ИстекСрокДействия");
	ИначеЕсли СтатусДоверенности = "REVOKED" ИЛИ СтатусДоверенности = "DECLINED" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенности.Отозвано");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.СтатусыМашиночитаемойДоверенности.ПустаяСсылка");
	КонецЕсли;
КонецФункции

// Возвращает результат проверки на необходимость наличия доверенности на подпись.
//
// Параметры:
//  ИННДоверителя  - Строка
//  ИННПодписанта  - Строка
//
// Возвращаемое значение:
//  Булево - Истина, если для подписи требуется МЧД
//
Функция ТребуетсяМашиночитаемаяДоверенность(ИННДоверителя, ИННПодписанта) Экспорт
	
	ДоверительИНН = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ИННДоверителя, 12);
	ПодписантИНН = СтроковыеФункцииКлиентСервер.ДополнитьСтроку(ИННПодписанта, 12);
	
	Возврат ДоверительИНН <> ПодписантИНН;
	
КонецФункции

#КонецОбласти