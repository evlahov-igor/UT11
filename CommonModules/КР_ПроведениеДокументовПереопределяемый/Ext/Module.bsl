
////////////////////////////////////////////////////
//// Общий модуль "КР_ПроведениеДокументовПереопределяемый"
//// Создан: 08.09.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-376
//// Разработка по ФДР 21.001 Доработка документа "Задание на перевозку"


#Область ПрограммныйИнтерфейс   

#Область ОбменСМагазинами

// << 11.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
// Метод выполняет удаление (остечение) движений по складам, которые не являются складами магазина
//	(не указаны в доп. настройках СкладыТекущейБазыДанных) 
// Удаление записей производится только для базы магазина (Константы.КР_БазаЯвляетсяЦентральной = Ложь)
//
// Параметры:
//  НаборЗаписей - НаборЗаписей - Набор записей регистра
//
Процедура МагазинВыполнитьОтсечениеДвиженийПоСкладам(НаборЗаписей) Экспорт 
	
	// перенести в общий модуль
	РегистраторПолноеИмя = НаборЗаписей.Отбор.Регистратор.Значение.Метаданные().ПолноеИмя(); 
	РегистрПолноеИмя = НаборЗаписей.Метаданные().ПолноеИмя();
	НастройкиОтсечениеДвиженийПоСкладам = КР_ДополнительныеНастройкиПовтИсп.ПолучитьНастройкиОтсечениеДвиженийПоСкладам(
		РегистраторПолноеИмя, 
		РегистрПолноеИмя
	);
	
	// Если нет настроек, то это центральная база
	// Отсечение в этом случае не требуестя
	Если НастройкиОтсечениеДвиженийПоСкладам = Неопределено Тогда   
		Возврат;
	КонецЕсли;
	
		
	ПутьКОтборуПоСкладу = НастройкиОтсечениеДвиженийПоСкладам.ПутьКОтборуПоСкладу;	
	МассивСкладов = НастройкиОтсечениеДвиженийПоСкладам.МассивСкладов;

	ТекстЗапроса = 
	"ВЫБРАТЬ 
	|	НаборЗаписей.* 
	|ПОМЕСТИТЬ НаборЗаписей 
	|ИЗ
	|	&НаборЗаписей КАК НаборЗаписей
	|;
	|
	|//////////////////////////
	|ВЫБРАТЬ 
	|	Т.НомерСтроки - 1 КАК ИндексЗаписи
	|ИЗ
	|	НаборЗаписей КАК Т
	|ГДЕ
	|	НЕ %1 В (&МассивСкладов)
	|УПОРЯДОЧИТЬ ПО
	|	ИндексЗаписи Убыв";     
	
	ТекстЗапроса = СтрШаблон(ТекстЗапроса, ПутьКОтборуПоСкладу); 
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("НаборЗаписей", НаборЗаписей);
	Запрос.УстановитьПараметр("МассивСкладов", МассивСкладов);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		НаборЗаписей.Удалить(ВыборкаДетальныеЗаписи.ИндексЗаписи);			
	КонецЦикла;	
		
КонецПроцедуры // >> 11.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Описывает учетные механизмы используемые в доработка КРОК.
//
// Параметры:
//  МеханизмыКонфигурации - Структура - список учетных механизмов,
//              где Ключ - имя механизма, а Значение - модуль реализующий интерфейс проведения.
//
Процедура ДополнитьУчетныеМеханизмыКонфигурации(МеханизмыКонфигурации) Экспорт
	
	МеханизмыКонфигурации.Вставить("КР_РасходыНаПеревозку", "РегистрыНакопления.КР_РасходыНаПеревозку");
	
	// << 19.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-376
	МеханизмыКонфигурации.Вставить("КР_ВладениеТранспортнымСредством", "РегистрыСведений.КР_ВладениеТранспортнымСредством");
	МеханизмыКонфигурации.Вставить("КР_ЗаданияНаПеревозку"           , "РегистрыНакопления.КР_ЗаданияНаПеревозку");
	// >> 19.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-376
	
	// << 20.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-689
	МеханизмыКонфигурации.Вставить("КР_МинимальнаяПрезентация", "РегистрыСведений.КР_МинимальнаяПрезентация");
	// >> 20.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-689
	
	// << 06.10.2022 Марченко С.Н., КРОК, JIRA№A2105505-671
	МеханизмыКонфигурации.Вставить("КР_ПриоритетыРазмеров", "РегистрыСведений.КР_ПриоритетыРазмеров");
	// >> 06.10.2022 Марченко С.Н., КРОК, JIRA№A2105505-671

	// << 20.10.2022 Марченко С.Н., КРОК, JIRA№A2105505-702
	МеханизмыКонфигурации.Вставить("КР_КоробаКПоступлению", "РегистрыНакопления.КР_КоробаКПоступлению");
	МеханизмыКонфигурации.Вставить("КР_РасхожденияПоКоробам", "РегистрыНакопления.КР_РасхожденияПоКоробам");
	// >> 20.10.2022 Марченко С.Н., КРОК, JIRA№A2105505-702
	
	// << 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
	МеханизмыКонфигурации.Вставить("КР_КоробаНаСкладах", "РегистрыНакопления.КР_КоробаНаСкладах");
	МеханизмыКонфигурации.Вставить("КР_КоробаКОтгрузке", "РегистрыНакопления.КР_КоробаКОтгрузке");
	// >> 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751  
	
	// << 14.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-742
	МеханизмыКонфигурации.Вставить("КР_ВесНеттоТовара", "РегистрыСведений.КР_ВесНеттоТовара");
	// >> 14.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-742
	
	// << 23.12.2022 Федоров Д.Е., КРОК, JIRA№A2105505-838
	МеханизмыКонфигурации.Вставить("КР_УстановкаМестРазмещения", "РегистрыСведений.КР_УстановкаМестРазмещения");
	// >> 23.12.2022 Федоров Д.Е., КРОК, JIRA№A2105505-838
	
	// << 26.12.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-948
	МеханизмыКонфигурации.Вставить("КР_КонтейнерАссортимента", "РегистрыСведений.КР_КонтейнерАссортимента");
	МеханизмыКонфигурации.Вставить("КР_РасширенныйАссортимент", "РегистрыСведений.КР_РасширенныйАссортимент");
	// >> 26.12.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-948
	
	// << 13.02.2023 Маскаев П.Ю., КРОК, JIRA№ A2105505-1166
	МеханизмыКонфигурации.Вставить("КР_РозничныеПродажи", "РегистрыНакопления.КР_РозничныеПродажи");
	МеханизмыКонфигурации.Вставить("КР_РозничныеОплатыБезналичные", "РегистрыНакопления.КР_РозничныеОплатыБезналичные");
	// >> 13.02.2023 Маскаев П.Ю., КРОК, JIRA№ A2105505-1166
	
	// << 22.02.2023 Марченко С.Н., КРОК, JIRA№A2105505-725
	МеханизмыКонфигурации.Вставить("КР_ТоварыКОформлениюОптовыхПродаж", "РегистрыНакопления.КР_ТоварыКОформлениюОптовыхПродаж");
	// >> 22.02.2023 Марченко С.Н., КРОК, JIRA№A2105505-725
	
	// << 22.03.2023 Марченко С.Н., КРОК, JIRA№A2105505-1148
	МеханизмыКонфигурации.Вставить("КР_ИсторияИзмененияТоварныхОграничений", "РегистрыСведений.КР_ИсторияИзмененияТоварныхОграничений");
	// >> 22.03.2023 Марченко С.Н., КРОК, JIRA№A2105505-1148

КонецПроцедуры

// << 24.10.2022 Марченко С.Н., КРОК, JIRA№2105505-702
Функция ДоступноИзменениеШаблоновПроведения(ИмяРегистра, ИмяОперации, ИмяШаблона, Регистры) Экспорт
	
	// При пометке удаления или "частичном" проведении может не быть свойства 
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда 
		Возврат Ложь;    
	КонецЕсли;
	
	ПредыдущееЗначениеСвойстваРегистра = Регистры[ИмяРегистра];	

	// В качестве дополнительных параметров 
	//	передаем структуру с ключем - именем переменной шаблона 
	//	и значением - заполненной предопределенной структурой,
	//	заранее описанной в СкладыСервер.КР_ПараметрыШаблонаЗапроса
	ШаблоныЗапросов = Новый Структура;  
	Для Каждого ИмяШаблона Из СтрРазделить(ИмяШаблона, ",") Цикл   
		Если ШаблоныЗапросов.Свойство(ИмяШаблона) Тогда 
			Продолжить;
		КонецЕсли;	
		ПараметрыШаблонаЗапроса = СкладыСервер.КР_ПараметрыШаблонаЗапроса(ИмяОперации, ИмяШаблона);
		ШаблоныЗапросов.Вставить(ИмяШаблона, ПараметрыШаблонаЗапроса);
	КонецЦикла;	   
	ШаблоныЗапросов.Вставить("ПредыдущееЗначение", ПредыдущееЗначениеСвойстваРегистра);
	Регистры[ИмяРегистра] = ШаблоныЗапросов;
	
	Возврат Истина;
	
КонецФункции // >> 24.10.2022 Марченко С.Н., КРОК, JIRA№2105505-702

#КонецОбласти

