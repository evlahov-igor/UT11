///////////////////////////////////////////////////
//// Общий модуль "КР_РаботаСКоробамиСервер"
//// Создан: 22.08.2022,  Мельников А.А.,  КРОК,  Jira№ A2105505-384

#Область ПрограммныйИнтерфейс

#Область ДополнительныеОбработчикиСобытийЭлементовФормы

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384  
// Добавляет реквизиты и элементы на форму в ТЧ "Товары" для отображения реквизита "КР_Короб"
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Элементы = ЭтаФорма.Элементы;
	
	ДобавляемыеРеквизиты = Новый Массив;  
	ИмяРеквизитаПараметров = "КР_ПараметрыРаботыСКоробами";
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтаФорма, ИмяРеквизитаПараметров) Тогда
		// В этот реквизит в структуре, буду кэшировать всякое что мне нужно для работы. Например:
		// "ФлагПрограммнойОбработкиИзмененияРеквизита", с помощью которого, я буду вызывать события изменения
		// реквизитов из события этого же события не уходя в рекурсию. 
		Новый_Реквизит = Новый РеквизитФормы(ИмяРеквизитаПараметров, Новый ОписаниеТипов(), "", ИмяРеквизитаПараметров);
		ДобавляемыеРеквизиты.Добавить(Новый_Реквизит);		
	КонецЕсли;
	
	Если ДобавляемыеРеквизиты.Количество() > 0 Тогда
		ЭтаФорма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);	
	КонецЕсли;	
	
	// << 21.09.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-377
	КР_ТоварыКоробЭлемент = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(
		ЭтаФорма, "Объект.Товары.КР_Короб", Элементы.Товары, Элементы.ТоварыНоменклатураТипНоменклатуры);
	КР_ТоварыКоробЭлемент.ТолькоПросмотр = Истина;
	// >> 21.09.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-377
	
	УстановитьУсловноеОформлениеДополнительно(ЭтаФорма);  
	        	
КонецПроцедуры  // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384         

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// << 18.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 
// Дополняет Массив СтрокТЧ строками с коробами 
// Возможно частный случай. Изначально вызывался с формы ЗаказаКлиента при измении вида цен командой
// Параметры;
// ТоварыТЧ - Табличная часть объекта
// МассивСтрок - МассивСтрок из ТЧ Товары объекта
// ЭтаФорма - Форма с которой идет вызов
//
Процедура ДополнитьМассивСтрокСтрокамиКоробов(ТоварыТЧ, МассивСтрок, ЭтаФорма) Экспорт

	МассивКоробов = Новый Массив;
	Для Каждого СтрокаТЧ Из МассивСтрок Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.КР_Короб) Тогда
			МассивКоробов.Добавить(СтрокаТЧ.КР_Короб);	
		КонецЕсли;
	КонецЦикла;      
	
	СтрокиКоробов = МассивСтрокКороба(МассивКоробов, ЭтаФорма);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрок, СтрокиКоробов, Истина); 
			
КонецПроцедуры // >> 18.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384 
// Если в ТЧ есть выделенная строка с Коробом, после вызова этой процедуры выделятся все строки с этим коробом 
// ЭтаФорма - Форма с которой идет вызов
//
Процедура ДополнитьВыделенныеСтрокиКоробами(ЭтаФорма) Экспорт

	ВыделенныеСтроки = ЭтаФорма.Элементы.Товары.ВыделенныеСтроки;
	
	ЕстьСтрокиКороба = Ложь; 
	МассивЗатронутыхКоробов = Новый Массив;
	Для Каждого Идентификатор Из ВыделенныеСтроки Цикл
		СтрокаТЧ = ЭтаФорма.Объект.Товары.НайтиПоИдентификатору(Идентификатор);
		Если ЭтоСтрокаКороба(СтрокаТЧ) Тогда			
			МассивЗатронутыхКоробов.Добавить(СтрокаТЧ.КР_Короб);
		КонецЕсли;
	КонецЦикла; 
	
	ЕстьСтрокиКороба = МассивЗатронутыхКоробов.Количество() > 0;
	Если ЕстьСтрокиКороба Тогда  	
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивЗатронутыхКоробов);  
		СтрокиКороба = МассивСтрокКороба(МассивЗатронутыхКоробов, ЭтаФорма);
		МассивИдентификаторов = Новый Массив;
		Для Каждого СтрокаКороба Из СтрокиКороба Цикл
			МассивИдентификаторов.Добавить(СтрокаКороба.ПолучитьИдентификатор());		
		КонецЦикла;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВыделенныеСтроки, МассивИдентификаторов, Истина);
	КонецЕсли;
	
КонецПроцедуры // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384   

// << 23.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384   
// ЭтаФорма - Форма с которой идет вызов
//
Процедура УстановитьУсловноеОформлениеДополнительно(ЭтаФорма) Экспорт
	
	УсловноеОформление = ЭтаФорма.УсловноеОформление;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	// Имена Элементов формы/колонки ТЧ которые ТолькоПросмотр, если короб Заполнен
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТоварыНоменклатура");
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТоварыХарактеристика");
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТоварыУпаковка");
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТоварыНоменклатураЕдиницаИзмерения");
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТоварыВариантОбеспечения");
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ТоварыКоличествоУпаковок");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КР_Короб");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	   	
КонецПроцедуры // >> 23.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384

// << 29.08.2022, Мельников А.А., КРОК, Jira№ A2105505-ХХХ 
// Очищает из Товаров объекта строки с коробами, кроме тех что пришли из таблицы подбора
// Параметры:
// ТоварыОбъекта - ТЧ объекта
// ТаблицаПодбора - ТаблицаЗначений - таблица вернувшаяся из формы подбора
//
Процедура УдалитьИсчезнувшиеСтрокиПослеПодбора(ТоварыОбъекта, ТаблицаПодбора) Экспорт
	
	НеизменившиесяКороба = Новый Массив;
	Для каждого СтрокаТовара Из ТаблицаПодбора Цикл		
		Если ЗначениеЗаполнено(СтрокаТовара.НомерСтроки) Тогда
			НеизменившиесяКороба.Добавить(СтрокаТовара.КР_Короб);
		КонецЕсли;
	КонецЦикла;
	
	КоробаНаУдаление = Новый Массив;
	Для Каждого СтрокаТовара Из ТоварыОбъекта Цикл
		Если Не ЗначениеЗаполнено(СтрокаТовара.КР_Короб) Тогда
			Продолжить;				
		КонецЕсли;     
		Если НеизменившиесяКороба.Найти(СтрокаТовара.КР_Короб) <> Неопределено 	Тогда
			Продолжить;
		КонецЕсли;
		КоробаНаУдаление.Добавить(СтрокаТовара.КР_Короб);
	КонецЦикла;
	
	Для Каждого КоробНаУдаление Из КоробаНаУдаление Цикл
		Отбор = Новый Структура("КР_Короб", КоробНаУдаление);
		НайденныеСтроки = ТоварыОбъекта.НайтиСтроки(Отбор);
		Для Каждого СтрокаТовара Из НайденныеСтроки Цикл
			ТоварыОбъекта.Удалить(СтрокаТовара);			
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры // >> 29.08.2022, Мельников А.А., КРОК, Jira№ A2105505-ХХХ 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384  
// возвращает строки ТЧ где установлен указанный Короб/Короба
// Параметры:
// Короба - Массив, ДокументСсылка.УпаковочныйЛист - Собственно список коробов по которым нам надо найти строки
// ЭтаФорма - Форма Документа для из которого ищем строки
// Возвращаемое значение;
// Массив - Массив строк ТЧ объекта.
Функция МассивСтрокКороба(Короба, ЭтаФорма) 
		
	Результат = Новый Массив;    
	
	Если ТипЗнч(Короба) = Тип("Массив") Тогда  
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(Короба);
		Для Каждого Короб Из Короба Цикл                                  
			Отбор = Новый Структура("КР_Короб", Короб);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, ЭтаФорма.Объект.Товары.НайтиСтроки(Отбор), Истина);
		КонецЦикла;
	Иначе   
		Результат = ЭтаФорма.Объект.Товары.НайтиСтроки("КР_Короб", Короба);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции  // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384

// << 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384
// Проверяет Является ли переданная строка строкой в которой заполнен реквизит КР_Короб
// СтрокаТЧ - СтрокаТабличнойЧасти
// Возвращаемое значение:
// Булево
//  
Функция ЭтоСтрокаКороба(СтрокаТЧ) Экспорт  

	ДанныеСтрокиВрем = Новый Структура("КР_Короб", Неопределено);
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиВрем, СтрокаТЧ);	
	
	Возврат  ЗначениеЗаполнено(ДанныеСтрокиВрем.КР_Короб);
	
КонецФункции  // >> 17.08.2022, Мельников А.А., КРОК, Jira№ A2105505-384

#КонецОбласти