////////////////////////////////////////////////////
//// Объект КР_ФормаНастройкиArtix - предназначен для настройки интеграции Artix
//// Создан: 27.01.2023, Гашников А.П., КРОК, JIRA№ A2105505-1150

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Идентификатор", Идентификатор);
	Параметры.ПараметрыОборудования.Свойство("КаталогОбменаТовары", 		КаталогОбменаТовары);
	Параметры.ПараметрыОборудования.Свойство("КаталогОбменаКассиры", 		КаталогОбменаКассиры);
	Параметры.ПараметрыОборудования.Свойство("КаталогОбменаОперПродажи", 	КаталогОбменаОперПродажи);
	Параметры.ПараметрыОборудования.Свойство("КаталогОбменаПродажиЗаСмену", КаталогОбменаПродажиЗаСмену);
	
	Заголовок = НСтр("ru='Оборудование:'") + Символы.НПП  + Строка(Идентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КаталогВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеПродолжения = Новый ОписаниеОповещения(
		"ВыборКаталогаЗавершение",
		ЭтотОбъект,
		"КаталогОбменаТовары");
		
	ФайловаяСистемаКлиент.ВыбратьКаталог(ОповещениеПродолжения);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбменаКассирыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеПродолжения = Новый ОписаниеОповещения(
		"ВыборКаталогаЗавершение",
		ЭтотОбъект,
		"КаталогОбменаКассиры");
	
	ФайловаяСистемаКлиент.ВыбратьКаталог(ОповещениеПродолжения);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбменаОперПродажиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеПродолжения = Новый ОписаниеОповещения(
		"ВыборКаталогаЗавершение",
		ЭтотОбъект,
		"КаталогОбменаОперПродажи");
		
	ФайловаяСистемаКлиент.ВыбратьКаталог(ОповещениеПродолжения);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбменаПродажиЗаСменуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеПродолжения = Новый ОписаниеОповещения(
		"ВыборКаталогаЗавершение",
		ЭтотОбъект,
		"КаталогОбменаПродажиЗаСмену");
		
	ФайловаяСистемаКлиент.ВыбратьКаталог(ОповещениеПродолжения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ТекстОшибкиОбщий = "";

	Если ПустаяСтрока(КаталогОбменаТовары) Тогда
		Результат = Ложь;
		ТекстОшибкиОбщий = НСтр("ru='Каталог обмена (товары) не указан.'");
	КонецЕсли;
	
	Если ПустаяСтрока(КаталогОбменаКассиры) Тогда
		Результат = Ложь;
		ТекстОшибкиОбщий = ТекстОшибкиОбщий + ?(ПустаяСтрока(ТекстОшибкиОбщий), "", Символы.ПС);
		ТекстОшибкиОбщий = ТекстОшибкиОбщий + НСтр("ru='Каталог обмена (кассиры) не указан.'");
	КонецЕсли;
	
	Если ПустаяСтрока(КаталогОбменаОперПродажи) Тогда
		Результат = Ложь;
		ТекстОшибкиОбщий = ТекстОшибкиОбщий + ?(ПустаяСтрока(ТекстОшибкиОбщий), "", Символы.ПС); 
		ТекстОшибкиОбщий = ТекстОшибкиОбщий + НСтр("ru='Каталог обмена (опер. продажи) не указан.'");
	КонецЕсли;
	
	Если ПустаяСтрока(КаталогОбменаПродажиЗаСмену) Тогда
		Результат = Ложь;
		ТекстОшибкиОбщий = ТекстОшибкиОбщий + ?(ПустаяСтрока(ТекстОшибкиОбщий), "", Символы.ПС); 
		ТекстОшибкиОбщий = ТекстОшибкиОбщий + НСтр("ru='Каталог обмена (продажи за смену) не указан.'");
	КонецЕсли;
	
	Если ПустаяСтрока(ТекстОшибкиОбщий) Тогда
		
		НовыеЗначениеПараметров = Новый Структура;
		НовыеЗначениеПараметров.Вставить("КаталогОбменаТовары", 		КаталогОбменаТовары);
		НовыеЗначениеПараметров.Вставить("КаталогОбменаКассиры", 		КаталогОбменаКассиры);
		НовыеЗначениеПараметров.Вставить("КаталогОбменаОперПродажи", 	КаталогОбменаОперПродажи);
		НовыеЗначениеПараметров.Вставить("КаталогОбменаПродажиЗаСмену", КаталогОбменаПродажиЗаСмену);
		
		Результат = Новый Структура;
		Результат.Вставить("Идентификатор", Идентификатор);
		Результат.Вставить("ПараметрыОборудования", НовыеЗначениеПараметров);
		
		МенеджерОфлайнОборудованияКлиентПереопределяемый.ФормаНастройкиОфлайнОборудованияПриСохраненииПараметров(
			ЭтотОбъект,
			Идентификатор,
			НовыеЗначениеПараметров);
		
		Закрыть(Результат);
		
	Иначе
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru= 'При проверке были обнаружены следующие ошибки:'")
			+ Символы.ПС + ТекстОшибкиОбщий);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборКаталогаЗавершение(Результат, ИмяРеквизита) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ЭтотОбъект[ИмяРеквизита] = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти