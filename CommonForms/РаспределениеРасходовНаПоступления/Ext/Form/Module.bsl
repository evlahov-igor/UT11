
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// << 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918  
	КР_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
	// >> 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	НаименованиеЕдиницыИзмеренияВеса = Строка(Константы.ЕдиницаИзмеренияВеса.Получить());
	НаименованиеЕдиницыИзмеренияОбъема = Строка(Константы.ЕдиницаИзмеренияОбъема.Получить());
	
	Если Параметры.Свойство("Валюта") Тогда
		Валюта = Параметры.Валюта;
	Иначе
		Валюта = Константы.ВалютаУправленческогоУчета.Получить();
	КонецЕсли;
	
	УстановитьЗаголовкиПолей();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ТаблицаСтрок") Тогда
		ЗаполнитьРеквизитыДокумента();
	КонецЕсли;
	
	УстановитьПравилоРаспределения();
	
	Если ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		ЭтаФорма.Заголовок = НСтр("ru = 'Распределение расходов на себестоимость товаров'");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПересчитатьОстатокКРаспределению();
	РассчитатьИтоговыеСуммы();
	ПроверитьЗаполнениеКоэффициентов();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПриИзменении(Элемент)
	ПересчитатьОстатокКРаспределению();
	РассчитатьИтоговыеСуммы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантРаспределенияПриИзменении(Элемент)
	ПроверитьЗаполнениеКоэффициентов();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументов

&НаКлиенте
Процедура СписокДокументовДокументПриИзменении(Элемент)
	Результат = ЗаполнитьИнформационныеПоля(Элементы.СписокДокументов.ТекущиеДанные.АналитикаРасходов);
	Если ЗначениеЗаполнено(Результат) Тогда
		ЗаполнитьЗначенияСвойств(Элементы.СписокДокументов.ТекущиеДанные, Результат);
		
		СтрокаТЧ = Элементы.СписокДокументов.ТекущиеДанные;
		СтрокаТЧ.Количество = Количество;
		СтрокаТЧ.Содержание = Содержание;
		СтрокаТЧ.СтавкаНДС = СтавкаНДС;
		СтрокаТЧ.Подразделение = Подразделение;
		СтрокаТЧ.СтатьяРасходов = СтатьяРасходов;
		
		ПересчитатьОстатокКРаспределению();
		РассчитатьИтоговыеСуммы();
		ПроверитьЗаполнениеКоэффициентов();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	Если ОсталосьРаспределить <> 0 Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Необходимо дораспределить на документы поступления'") + " " + ОсталосьРаспределить + " " + Валюта;
		Сообщение.Сообщить();
	Иначе
		Закрыть(ПоместитьРезультатВоВременноеХранилище());
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьБазуРаспределения(Команда)
	МассивДокументов = Новый СписокЗначений();
	Для Каждого ТекСтрока Из СписокДокументов Цикл
		МассивДокументов.Добавить(ТекСтрока.АналитикаРасходов);
	КонецЦикла;
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Отбор", Новый Структура("МассивДокументов", МассивДокументов));
	ПараметрыОткрытия.Вставить("КлючВарианта", "ПоступленияСоСтрокамиКонтекстный");
	ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОткрытия.Вставить("ВидимостьКомандВариантовОтчетов", Ложь);
	
	ОткрытьФорму("Отчет.РасшифровкаПоступленийПоНоменклатуре.Форма", ПараметрыОткрытия);
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНезаполненныеСтроки(Команда)
	МассивДокументов = Новый СписокЗначений();
	Для Каждого ТекСтрока Из СписокДокументов Цикл
		МассивДокументов.Добавить(ТекСтрока.АналитикаРасходов);
	КонецЦикла;
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("Отбор", Новый Структура("МассивДокументов", МассивДокументов));
	ПараметрыОткрытия.Вставить("КлючВарианта", "НезаполненныеВесОбъемВПоступленияхКонтекстный");
	ПараметрыОткрытия.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыОткрытия.Вставить("ВидимостьКомандВариантовОтчетов", Ложь);
	
	ОткрытьФорму("Отчет.РасшифровкаПоступленийПоНоменклатуре.Форма", ПараметрыОткрытия);
КонецПроцедуры

&НаКлиенте
Процедура Распределить(Команда)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"ОбщаяФорма.РаспределениеРасходовНаПоступления.Команда.Распределить");
	
	Если ПравилоРаспределения = "ПоКоличеству" Тогда
		Итог = ИтогоКоличество;
		ИмяКоэффициента = "КоэффициентКоличество";
		КомментарийРаспределения = НСтр("ru='Распределена сумма %РаспределенаСумма% (%Валюта%) на %КоличествоСтрок% пропорционально количеству накладной'");
	ИначеЕсли ПравилоРаспределения = "ПоВесу" Тогда
		Итог = ИтогоВес;
		ИмяКоэффициента = "КоэффициентВес";
		КомментарийРаспределения = НСтр("ru='Распределена сумма %РаспределенаСумма% (%Валюта%) на %КоличествоСтрок% пропорционально весу накладной'");
	ИначеЕсли ПравилоРаспределения = "ПоОбъему" Тогда
		Итог = ИтогоОбъем;
		ИмяКоэффициента = "КоэффициентОбъем";
		КомментарийРаспределения = НСтр("ru='Распределена сумма %РаспределенаСумма% (%Валюта%) на %КоличествоСтрок% пропорционально объему накладной'");
	Иначе
		Итог = ИтогоСумма;
		ИмяКоэффициента = "КоэффициентСумма";
		КомментарийРаспределения = НСтр("ru='Распределена сумма %РаспределенаСумма% (%Валюта%) на %КоличествоСтрок% пропорционально сумме накладной'");
	КонецЕсли;
	
	КомментарийРаспределения = СтрЗаменить(КомментарийРаспределения, "%РаспределенаСумма%", СуммаКРаспределению);
	КомментарийРаспределения = СтрЗаменить(КомментарийРаспределения, "%Валюта%", Валюта);
	КоличествоСтрок = СписокДокументов.Количество();
	Если КоличествоСтрок = 1 Тогда
		 ТекстКоличествоСтрок = "" + КоличествоСтрок + " строку ";
	ИначеЕсли КоличествоСтрок > 1 И КоличествоСтрок < 5 Тогда
		 ТекстКоличествоСтрок = "" + КоличествоСтрок + " строки ";
	Иначе
		 ТекстКоличествоСтрок = "" + КоличествоСтрок + " строк ";
	КонецЕсли;
	КомментарийРаспределения = СтрЗаменить(КомментарийРаспределения, "%КоличествоСтрок%", ТекстКоличествоСтрок);
	
	КРаспределению = СуммаКРаспределению;
	Для Каждого Строка Из СписокДокументов Цикл
		Если Итог * Строка[ИмяКоэффициента] = 0 Тогда
			Списать = 0;
		Иначе
			Списать = Окр(СуммаКРаспределению / Итог * Строка[ИмяКоэффициента], 2, РежимОкругления.Окр15как20);
		КонецЕсли;
		Строка.КомментарийРаспределения = КомментарийРаспределения;
		Если Списать > КРаспределению Тогда
			Строка.Сумма = КРаспределению;
			КРаспределению = 0;
			Прервать;
		Иначе
			Строка.Сумма = Списать;
			КРаспределению = КРаспределению - Списать;
		КонецЕсли;
	КонецЦикла;
	Если КРаспределению <> 0
		И КРаспределению <> СуммаКРаспределению 
	Тогда
		Строка.Сумма = Строка.Сумма + КРаспределению;
	КонецЕсли;
	
	ПересчитатьОстатокКРаспределению();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовкиПолей()

	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Валюта",
				"Заголовок", Валюта);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ВалютаНеРаспределено",
				"Заголовок", Валюта);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокДокументовСумма",
				"Заголовок", НСтр("ru='Сумма'") +" (" + ВалютаУпр + ")");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокДокументовВес",
				"Заголовок", НСтр("ru='Вес'") +" (" + НаименованиеЕдиницыИзмеренияВеса + ")");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокДокументовОбъем",
				"Заголовок", НСтр("ru='Объем'") +" (" + НаименованиеЕдиницыИзмеренияОбъема + ")");

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыДокумента()
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Строки.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	Строки.АналитикаРасходов КАК Ссылка,
	|	Строки.Содержание        КАК Содержание,
	|	Строки.Количество        КАК Количество,
	|	Строки.Сумма             КАК Сумма,
	|	Строки.СтавкаНДС         КАК СтавкаНДС,
	|	Строки.Подразделение     КАК Подразделение,
	|	Строки.СтатьяРасходов    КАК СтатьяРасходов
	|ПОМЕСТИТЬ ВтМассивСтрок
	|ИЗ
	|	&МассивСтрок КАК Строки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Строки.АналитикаРасходов) 
	|		= ТИП(Документ.ПриобретениеТоваровУслуг)
	|;
	|////////////////////////////
	|ВЫБРАТЬ
	|	Строки.НомерСтрокиДокумента КАК НомерСтрокиДокумента,
	|	Строки.Ссылка            КАК Ссылка,
	|	Строки.Содержание        КАК Содержание,
	|	Строки.Количество        КАК Количество,
	|	Строки.Сумма             КАК Сумма,
	|	Строки.СтавкаНДС         КАК СтавкаНДС,
	|	Строки.Подразделение     КАК Подразделение,
	|	Строки.СтатьяРасходов    КАК СтатьяРасходов
	|ПОМЕСТИТЬ МассивСтрок
	|ИЗ
	|	ВтМассивСтрок КАК Строки
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Строки.Ссылка) 
	|		= ТИП(Документ.ПриобретениеТоваровУслуг)
	|;
	|////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(Строки.Сумма) КАК Сумма
	|ИЗ
	|	МассивСтрок КАК Строки
	|;
	|////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Строки.Содержание     КАК Содержание,
	|	Строки.Количество     КАК Количество,
	|	Строки.СтавкаНДС      КАК СтавкаНДС,
	|	Строки.Подразделение  КАК Подразделение,
	|	Строки.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ СодержаниеРасходов
	|ИЗ
	|	МассивСтрок КАК Строки
	|ГДЕ
	|	Строки.Содержание <> НЕОПРЕДЕЛЕНО
	|;
	|////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Строки.Содержание     КАК Содержание,
	|	Строки.Количество     КАК Количество,
	|	Строки.СтавкаНДС      КАК СтавкаНДС,
	|	Строки.Подразделение  КАК Подразделение,
	|	Строки.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	СодержаниеРасходов КАК Строки
	|;
	|////////////////////////////
	|ВЫБРАТЬ
	|	Строки.НомерСтрокиДокумента КАК НомерСтрокиДокумента
	|ИЗ ВтМассивСтрок КАК Строки
	|ГДЕ
	|	Строки.Ссылка = ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)
	|;
	|////////////////////////////
	|ВЫБРАТЬ
	|	Строки.НомерСтрокиДокумента                     КАК НомерСтрокиДокумента,
	|	Себестоимость.Регистратор                       КАК АналитикаРасходов,
	|	Себестоимость.Организация                       КАК Организация,
	|	Себестоимость.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	Текст.Содержание                                КАК Содержание,
	|	Текст.Количество                                КАК Количество,
	|	Текст.СтавкаНДС                                 КАК СтавкаНДС,
	|	Текст.Подразделение                             КАК Подразделение,
	|	Текст.СтатьяРасходов                            КАК СтатьяРасходов,
	|	СУММА(Себестоимость.Количество)                 КАК КоэффициентКоличество,
	|	СУММА(Себестоимость.Стоимость)                  КАК КоэффициентСумма,
	|	СУММА(Себестоимость.Количество * &ТекстЗапросаВесУпаковки)   	  КАК КоэффициентВес,
	|	СУММА(Себестоимость.Количество * &ТекстЗапросаОбъемУпаковки) 	  КАК КоэффициентОбъем,
	|	МАКСИМУМ(ВЫБОР КОГДА &ТекстЗапросаВесУпаковки = 0
	|		ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                        КАК ОшибкаКоэффициентВес,
	|	МАКСИМУМ(ВЫБОР КОГДА &ТекстЗапросаОбъемУпаковки = 0
	|		ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                        КАК ОшибкаКоэффициентОбъем
	|ИЗ
	|	МассивСтрок КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|		ПО Строки.Ссылка = Себестоимость.Регистратор
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ СодержаниеРасходов КАК Текст
	|		ПО ИСТИНА
	|
	|СГРУППИРОВАТЬ ПО
	|	Строки.НомерСтрокиДокумента,
	|	Себестоимость.Регистратор,
	|	Себестоимость.Период,
	|	Себестоимость.Организация,
	|	Себестоимость.АналитикаУчетаПоПартнерам.Партнер,
	|	Текст.Содержание,
	|	Текст.Количество,
	|	Текст.СтавкаНДС,
	|	Текст.Подразделение,
	|	Текст.СтатьяРасходов
	|
	|ИМЕЮЩИЕ
	|	СУММА(Себестоимость.Количество) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтрокиДокумента
	|";
	
	// << 15.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-742
	СтрокаЗамены = "ВЫРАЗИТЬ(Себестоимость.Регистратор КАК Документ.ПриобретениеТоваровУслуг).КР_ВесБруттоДокумента";
	ТекстОшибки = НСтр("ru = 'Исходный текст запроса был изменен. Не найдена строка замены.'");
	
	ИскомаяСтрока = "СУММА(Себестоимость.Количество * &ТекстЗапросаВесУпаковки)";
	Если СтрНайти(ТекстЗапроса, ИскомаяСтрока) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИскомаяСтрока, СтрокаЗамены);
	Иначе
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ИскомаяСтрока = "&ТекстЗапросаВесУпаковки";
	Если СтрНайти(ТекстЗапроса, ИскомаяСтрока) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИскомаяСтрока, СтрокаЗамены);
	Иначе
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	// >> 15.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-742
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаВесУпаковки",
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаОбъемУпаковки",
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки(
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("МассивСтрок", ПолучитьИзВременногоХранилища(Параметры.ТаблицаСтрок));
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[2].Выбрать();
	Выборка.Следующий();
	СуммаКРаспределению = Выборка.Сумма;
	
	Выборка = Результат[4].Выбрать();
	Выборка.Следующий();
	
	Содержание = Выборка.Содержание;
	Количество = Выборка.Количество;
	СтавкаНДС = Выборка.СтавкаНДС;
	Подразделение = Выборка.Подразделение;
	СтатьяРасходов = Выборка.СтатьяРасходов;
	
	СтрокиКУдалению.Загрузить(Результат[5].Выгрузить());
	СписокДокументов.Загрузить(Результат[6].Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПравилоРаспределения() 
	ПравилоРаспределенияВСтатье = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяРасходов, "ПравилоРаспределенияНаСебестоимость");
	Если ПравилоРаспределенияВСтатье = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноКоличеству
		ИЛИ ПравилоРаспределенияВСтатье = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПустаяСсылка()
	Тогда
		ПравилоРаспределения = "ПоКоличеству";
	ИначеЕсли ПравилоРаспределенияВСтатье = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноСебестоимости Тогда
		ПравилоРаспределения = "ПоСумме";
	ИначеЕсли ПравилоРаспределенияВСтатье = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноВесу Тогда
		ПравилоРаспределения = "ПоВесу";
	ИначеЕсли ПравилоРаспределенияВСтатье = Перечисления.ПравилаРаспределенияНаСебестоимостьТоваров.ПропорциональноОбъему Тогда
		ПравилоРаспределения = "ПоОбъему";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьИтоговыеСуммы()
	ИтогоСумма = СписокДокументов.Итог("КоэффициентСумма");
	ИтогоКоличество = СписокДокументов.Итог("КоэффициентКоличество");
	ИтогоВес = СписокДокументов.Итог("КоэффициентВес");
	ИтогоОбъем = СписокДокументов.Итог("КоэффициентОбъем");
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьОстатокКРаспределению()
	ОсталосьРаспределить = СуммаКРаспределению - СписокДокументов.Итог("Сумма")
КонецПроцедуры

&НаСервере
Функция ПоместитьРезультатВоВременноеХранилище()

	ВыгружаемыеПоля = "
	|НомерСтрокиДокумента, 
	|Сумма,
	|Содержание,
	|КомментарийРаспределения,
	|Количество,
	|СтавкаНДС,
	|Подразделение,
	|АналитикаРасходов,
	// << 19.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
	|КР_СтатьяРасходовБазовая,
	|КР_Коллекция,
	// >> 19.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
	|СтатьяРасходов
	|";
	ПараметрыЗакрытия = Новый Структура("АдресВоВременномХранилище, СтрокиКУдалению",
						ПоместитьВоВременноеХранилище(СписокДокументов.Выгрузить(,ВыгружаемыеПоля), АдресВоВременномХранилище),
						ПоместитьВоВременноеХранилище(СтрокиКУдалению.Выгрузить(,"НомерСтрокиДокумента"), АдресСтрокКУдалению));
	Возврат ПараметрыЗакрытия;

КонецФункции

&НаСервере
Функция ЗаполнитьИнформационныеПоля(Данные)

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Реестр.Партнер, НЕОПРЕДЕЛЕНО) КАК Партнер,   
	|	СУММА(Себестоимость.Количество) КАК КоэффициентКоличество,
	|	СУММА(Себестоимость.Стоимость)  КАК КоэффициентСумма,
	|	СУММА(Себестоимость.Количество
	|		* ЕСТЬNULL(&ТекстЗапросаВесУпаковки, 0)
	|	)                               КАК КоэффициентВес,
	|	СУММА(Себестоимость.Количество 
	|		* ЕСТЬNULL(&ТекстЗапросаОбъемУпаковки, 0)
	|	)                               КАК КоэффициентОбъем,
	|	МАКСИМУМ(ВЫБОР КОГДА &ТекстЗапросаВесУпаковки = 0
	|		ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ)                          КАК ОшибкаКоэффициентВес,
	|	МАКСИМУМ(ВЫБОР КОГДА &ТекстЗапросаОбъемУпаковки = 0
	|		ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ)                          КАК ОшибкаКоэффициентОбъем
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|		ПО Реестр.Ссылка = Себестоимость.Регистратор
	|		И Реестр.Организация = Себестоимость.Организация
	|		И НЕ Реестр.ДополнительнаяЗапись
	|ГДЕ
	|	Себестоимость.Регистратор = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	Себестоимость.Регистратор,
	|	ЕСТЬNULL(Реестр.Партнер, НЕОПРЕДЕЛЕНО)
	|";
	
	// << 15.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-742
	СтрокаЗамены = "ВЫРАЗИТЬ(Себестоимость.Регистратор КАК Документ.ПриобретениеТоваровУслуг).КР_ВесБруттоДокумента";
	ТекстОшибки = НСтр("ru = 'Исходный текст запроса был изменен. Не найдена строка замены.'");
	
	ИскомаяСтрока =
		"СУММА(Себестоимость.Количество
		|		* ЕСТЬNULL(&ТекстЗапросаВесУпаковки, 0)
		|	)";
	Если СтрНайти(ТекстЗапроса, ИскомаяСтрока) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИскомаяСтрока, СтрокаЗамены);
	Иначе
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ИскомаяСтрока = "&ТекстЗапросаВесУпаковки";
	Если СтрНайти(ТекстЗапроса, ИскомаяСтрока) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИскомаяСтрока, СтрокаЗамены);
	Иначе
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	// >> 15.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-742
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаВесУпаковки", 
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура"));
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаОбъемУпаковки", 
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки(
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Ссылка", Данные);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Если Результат.Количество() <> 0 Тогда
		СтрокаРезультата = Новый Структура("Партнер,
		|КоэффициентКоличество,
		|КоэффициентСумма,
		|КоэффициентВес,
		|КоэффициентОбъем,
		|ОшибкаКоэффициентВес,
		|ОшибкаКоэффициентОбъем
		|");
		ЗаполнитьЗначенияСвойств(СтрокаРезультата, Результат[0]);
	Иначе
		СтрокаРезультата = Неопределено;
	КонецЕсли;
	
	Возврат СтрокаРезультата;

КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеКоэффициентов()
	Если СписокДокументов.Количество() = 0 Тогда
		Элементы.ГруппаПредупреждение.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	Если ПравилоРаспределения = "ПоКоличеству" Тогда
		ЕстьОшибки = ?(СписокДокументов.Итог("КоэффициентКоличество") = 0, Истина, Ложь);
		Элементы.СписокДокументовОшибкаКоэффициентВес.Видимость = Ложь;
		Элементы.СписокДокументовОшибкаКоэффициентОбъем.Видимость = Ложь;
	ИначеЕсли ПравилоРаспределения = "ПоВесу" Тогда
		ЕстьОшибки = ?(СписокДокументов.Итог("ОшибкаКоэффициентВес") <> 0, Истина, Ложь);
		Элементы.СписокДокументовОшибкаКоэффициентВес.Видимость = Истина;
		Элементы.СписокДокументовОшибкаКоэффициентОбъем.Видимость = Ложь;
		Элементы.ПредупреждениеДекорацияТекст.Заголовок = НСтр("ru='В документах поступления есть строки с незаполненным весом. Распределение не будет произведено на данные строки.'");
	ИначеЕсли ПравилоРаспределения = "ПоОбъему" Тогда
		ЕстьОшибки = ?(СписокДокументов.Итог("ОшибкаКоэффициентОбъем") <> 0, Истина, Ложь);
		Элементы.СписокДокументовОшибкаКоэффициентВес.Видимость = Ложь;
		Элементы.СписокДокументовОшибкаКоэффициентОбъем.Видимость = Истина;
		Элементы.ПредупреждениеДекорацияТекст.Заголовок = НСтр("ru='В документах поступления есть строки с незаполненным объемом. Распределение не будет произведено на данные строки.'");
	ИначеЕсли ПравилоРаспределения = "ПоСумме" Тогда
		ЕстьОшибки = ?(СписокДокументов.Итог("КоэффициентКоличество") = 0, Истина, Ложь);
		Элементы.СписокДокументовОшибкаКоэффициентВес.Видимость = Ложь;
		Элементы.СписокДокументовОшибкаКоэффициентОбъем.Видимость = Ложь;
	КонецЕсли;
	Элементы.ГруппаПредупреждение.Видимость = ЕстьОшибки;
КонецПроцедуры

#КонецОбласти        

#Область КР_ДобавленныеПроцедурыИФункции

// << 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
&НаСервере
Процедура КР_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	// КР_ДетализироватьДоКоллекций
	РеквизитФормы = КР_МетодыМодификацииФорм.ДобавитьРеквизитФормы(ЭтотОбъект,
		"КР_ДетализироватьДоКоллекций", Новый ОписаниеТипов("Булево"));     
	Параметры.Свойство("КР_ДетализироватьДоКоллекций", ЭтотОбъект.КР_ДетализироватьДоКоллекций);
	ДетализироватьДоКоллекций = ЭтотОбъект.КР_ДетализироватьДоКоллекций;
	
	// СписокДокументов.КР_Коллекция
	РеквизитФормы = КР_МетодыМодификацииФорм.ДобавитьРеквизитФормы(ЭтотОбъект,
		"КР_Коллекция", Новый ОписаниеТипов("СправочникСсылка.КоллекцииНоменклатуры"), "СписокДокументов",
		НСтр("ru = 'Коллекция'"));     
	ЭлементФормы = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(ЭтотОбъект,
		РеквизитФормы, , Элементы.СписокДокументовПартнер);
	ЭлементФормы.Видимость = ДетализироватьДоКоллекций;                  
	
	// СписокДокументов.КР_СтатьяРасходовБазовая
	РеквизитФормы = КР_МетодыМодификацииФорм.ДобавитьРеквизитФормы(ЭтотОбъект,
		"КР_СтатьяРасходовБазовая", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов"), "СписокДокументов");     
	
	Если ДетализироватьДоКоллекций Тогда  
		УстановитьДействие("ОбработкаВыбора", "КР_ОбработкаВыбора");
		Элементы.СписокДокументов.УстановитьДействие("ПередНачаломДобавления", "КР_СписокДокументовПередНачаломДобавления");
		Элементы.СписокДокументовПартнер.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры // >> 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918

// << 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
&НаКлиенте
Процедура КР_СписокДокументовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)	
	// Выполняется только если КР_ДетализироватьДоКоллекций = Истина                                        
	
	Отказ = Истина;
	ОткрытьФорму("Документ.ПриобретениеТоваровУслуг.ФормаВыбора", , ЭтотОбъект, , , , , 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // >> 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918

// << 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
&НаКлиенте
Процедура КР_ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	КР_ЗаполнитьДетализациейПоКоллекциям(ВыбранноеЗначение);

	ПересчитатьОстатокКРаспределению();
	РассчитатьИтоговыеСуммы();
	ПроверитьЗаполнениеКоэффициентов();
	
КонецПроцедуры // >> 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918

// << 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
&НаСервере
Процедура КР_ЗаполнитьДетализациейПоКоллекциям(ПриобретениеТоваровУслуг)  
	
	// Вес брутто учитывается не верно. не прописанно в ФДР
	ТекстЗапроса = КР_ТекстЗапросаДетализацияПриобретенияПоКоллекциям();

	// << 15.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-742
	//ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
	//							"&ТекстЗапросаВесУпаковки", 
	//							Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
	//								"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
	//								"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура"));
	СтрокаЗамены = "ВЫРАЗИТЬ(Себестоимость.Регистратор КАК Документ.ПриобретениеТоваровУслуг).КР_ВесБруттоДокумента";
	ТекстОшибки = НСтр("ru = 'Исходный текст запроса был изменен. Не найдена строка замены.'");
		
	ИскомаяСтрока = "&ТекстЗапросаВесУпаковки";
	Если СтрНайти(ТекстЗапроса, ИскомаяСтрока) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ИскомаяСтрока, СтрокаЗамены);
	Иначе
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	// >> 15.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-742
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"&ТекстЗапросаОбъемУпаковки", 
								Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки(
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения",
									"Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка", ПриобретениеТоваровУслуг);
	РезультатЗапроса = Запрос.Выполнить();
		
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
		НоваяСтрока = СписокДокументов.Добавить();    
		НоваяСтрока.АналитикаРасходов = ПриобретениеТоваровУслуг;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);  
		
		НоваяСтрока.Количество = Количество;
		НоваяСтрока.Содержание = Содержание;
		НоваяСтрока.СтавкаНДС = СтавкаНДС;
		НоваяСтрока.Подразделение = Подразделение;
		НоваяСтрока.КР_СтатьяРасходовБазовая = СтатьяРасходов;
		НоваяСтрока.СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.КР_ПолучитьСтатьюРасходовКоллекции(
			СтатьяРасходов, НоваяСтрока.КР_Коллекция);
		
	КонецЦикла;	
	
КонецПроцедуры // >> 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
		
#Область ТекстыЗапросов

// << 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
&НаСервере
Функция КР_ТекстЗапросаДетализацияПриобретенияПоКоллекциям()
	
	Возврат
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Реестр.Партнер, НЕОПРЕДЕЛЕНО) КАК Партнер,   
	// << 16.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
	|	Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура.КоллекцияНоменклатуры КАК КР_Коллекция,
	// >> 16.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
	|	СУММА(Себестоимость.Количество) КАК КоэффициентКоличество,
	|	СУММА(Себестоимость.Стоимость)  КАК КоэффициентСумма,   
	// << 15.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-742
	//|	СУММА(Себестоимость.Количество
	//|		* ЕСТЬNULL(&ТекстЗапросаВесУпаковки, 0)
	//|	)                               КАК КоэффициентВес, 
	|	ВЫРАЗИТЬ(Себестоимость.Регистратор 
	|		КАК Документ.ПриобретениеТоваровУслуг).КР_ВесБруттоДокумента КАК КоэффициентВес,
	// >> 15.11.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-742
	|	СУММА(Себестоимость.Количество 
	|		* ЕСТЬNULL(&ТекстЗапросаОбъемУпаковки, 0)
	|	)                               КАК КоэффициентОбъем,
	|	МАКСИМУМ(ВЫБОР КОГДА &ТекстЗапросаВесУпаковки = 0
	|		ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ)                          КАК ОшибкаКоэффициентВес,
	|	МАКСИМУМ(ВЫБОР КОГДА &ТекстЗапросаОбъемУпаковки = 0
	|		ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ)                          КАК ОшибкаКоэффициентОбъем
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|		ПО Реестр.Ссылка = Себестоимость.Регистратор
	|		И Реестр.Организация = Себестоимость.Организация
	|		И НЕ Реестр.ДополнительнаяЗапись
	|ГДЕ
	|	Себестоимость.Регистратор = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	Себестоимость.Регистратор,
	// << 16.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
	|	Себестоимость.АналитикаУчетаНоменклатуры.Номенклатура.КоллекцияНоменклатуры,
	// >> 16.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918
	|	ЕСТЬNULL(Реестр.Партнер, НЕОПРЕДЕЛЕНО)
	|";

КонецФункции // >> 15.12.2022 Марченко С.Н., КРОК, JIRA№A2105505-918 

#КонецОбласти

#КонецОбласти
