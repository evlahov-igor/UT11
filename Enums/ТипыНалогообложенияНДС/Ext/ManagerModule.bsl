#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Приводит переданное значение перечисления к двум: ПродажаОблагаетсяНДС или ПродажаНеОблагаетсяНДС
//
// Параметры:
//  НалогообложениеНДС	 - ПеречислениеСсылка.ТипыНалогообложенияНДС 
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ТипыНалогообложенияНДС 
//
Функция СНдсБезНдс(НалогообложениеНДС) Экспорт
	
	Если ТипыСНДС().Найти(НалогообложениеНДС) <> Неопределено Тогда
		
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
		
	ИначеЕсли ТипыБезНДС().Найти(НалогообложениеНДС) <> Неопределено Тогда
		
		Возврат Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
		
	Иначе
		
		//ОпределяетсяРаспределением
		//ВводОСВЭксплуатацию
		//НалоговыйАгентПоНДС
		ТекстИсключения = НСтр("ru = 'Неподдерживаемый тип налогообложения НДС %НалогообложенияНДС%'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%НалогообложенияНДС%", НалогообложениеНДС);
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
КонецФункции

// Возвращает массив типов налогообложения НДС, предусматривающих учет НДС
// 
// Возвращаемое значение:
//  Массив 
//
Функция ТипыСНДС() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(ПоФактическомуИспользованию);
	Массив.Добавить(ПродажаНаЭкспорт);
	Массив.Добавить(ПродажаОблагаетсяНДС);
	Массив.Добавить(ЭкспортСырьевыхТоваровУслуг);
	Массив.Добавить(ЭкспортНесырьевыхТоваров);
	Массив.Добавить(ОблагаетсяНДСУПокупателя);
	Массив.Добавить(ПроизводствоСДЦ);
	Массив.Добавить(РеализацияРаботУслугНаЭкспорт);
	
	Возврат Массив;
	
КонецФункции

// Возвращает массив типов налогообложения НДС, не предусматривающих учет НДС
// 
// Возвращаемое значение:
//  Массив 
//
Функция ТипыБезНДС() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить(ПродажаНеОблагаетсяНДС);
	Массив.Добавить(ПродажаОблагаетсяЕНВД);
	//++ Локализация
	Массив.Добавить(ПродажаПоПатенту);
	//-- Локализация
	Возврат Массив;
	
КонецФункции
#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений();
	ДоступныеТипыНалогообложения = УчетНДСУПКлиентСервер.ПрименяемыеТипыНалогообложенияНДС(Истина, Истина, Истина);
	Для каждого Элемент Из ДоступныеТипыНалогообложения Цикл
		ДанныеВыбора.Добавить(Элемент.Значение, Элемент.Представление);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
