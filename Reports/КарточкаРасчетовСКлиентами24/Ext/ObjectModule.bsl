#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета:
//   	* Параметры - Структура:
//   		** ОписаниеКоманды - Структура:
//   			*** ДополнительныеПараметры - Структура.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда 
		
		СформироватьПараметрыОтчета(Параметры.ПараметрКоманды, ЭтаФорма.ФормаПараметры, Параметры);
		
	КонецЕсли;
	
	ФормаПараметры = ЭтаФорма.ФормаПараметры;
	
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Подробнее - см. ОтчетыПереопределяемый.ПередЗагрузкойВариантаНаСервере
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт
	Отчет = ЭтаФорма.Отчет;
	КомпоновщикНастроекФормы = Отчет.КомпоновщикНастроек;
	
	// Изменение настроек по функциональным опциям
	НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы);
	
	// Установка значений по умолчанию
	УстановитьОбязательныеНастройки(КомпоновщикНастроекФормы, Истина);
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	УстановитьОбязательныеНастройки(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы);
	
	// Сформируем отчет
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос = ТекстЗапроса();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	//Предупреждение о невыполенном распределении взаиморасчетов.
	ВзаиморасчетыСервер.ВывестиПредупреждениеОбОбновлении(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОбязательныеНастройки(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы)
	
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикНастроек);
	
КонецПроцедуры

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов") Тогда
		КомпоновкаДанныхСервер.УдалитьЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, "Контрагент");
	КонецЕсли;
КонецПроцедуры

Функция ТекстЗапроса()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сегменты.Партнер КАК Партнер,
	|	ИСТИНА КАК ИспользуетсяОтборПоСегментуПартнеров
	|ПОМЕСТИТЬ ОтборПоСегментуПартнеров
	|ИЗ
	|	РегистрСведений.ПартнерыСегмента КАК Сегменты
	|{ГДЕ
	|	Сегменты.Сегмент.* КАК СегментПартнеров,
	|	Сегменты.Партнер.* КАК Партнер}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	ИспользуетсяОтборПоСегментуПартнеров
	|;
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация,
	|	КурсВалюты.Валюта КАК Валюта,
	|	ВЫБОР &ДанныеОтчета
	|		КОГДА 4
	|			ТОГДА 1
	|		КОГДА 2
	|			ТОГДА ЕСТЬNULL(КурсВалюты.КурсЧислитель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсВалюты.КурсЗнаменатель, 1) * ЕСТЬNULL(КурсВалютыУпр.КурсЧислитель, 1))
	|		ИНАЧЕ ЕСТЬNULL(КурсВалюты.КурсЧислитель, 1) * ЕСТЬNULL(КурсВалютыРегл.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсВалюты.КурсЗнаменатель, 1) * ЕСТЬNULL(КурсВалютыРегл.КурсЧислитель, 1))
	|	КОНЕЦ КАК Коэффициент
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних({(&КонецПериода)}, БазоваяВалюта = &БазоваяВалюта) КАК КурсВалюты
	|			ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних({(&КонецПериода)}, Валюта = &ВалютаУправленческогоУчета И БазоваяВалюта = &БазоваяВалюта) КАК КурсВалютыУпр
	|			ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних({(&КонецПериода)}, БазоваяВалюта = &БазоваяВалюта) КАК КурсВалютыРегл
	|			ПО Организации.ВалютаРегламентированногоУчета = КурсВалютыРегл.Валюта
	|;
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Расчеты.Организация                 КАК Организация,
	|	Расчеты.Партнер                     КАК Партнер,
	|	Расчеты.Контрагент                  КАК Контрагент,
	|	Расчеты.Договор                     КАК Договор,
	|	Расчеты.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов              КАК ОбъектРасчетов,
	|	Расчеты.Валюта                      КАК Валюта,
	|	Расчеты.РасчетныйДокумент           КАК РасчетныйДокумент,
	|	Расчеты.ДокументРегистратор         КАК ДокументРегистратор,
	|	Расчеты.ДатаВозникновения           КАК ДатаВозникновения,
	|	Расчеты.ДатаПлановогоПогашения      КАК ДатаПлановогоПогашения,
	|	Расчеты.Сторно                      КАК Сторно,
	|	НАЧАЛОПЕРИОДА(Расчеты.Период, ДЕНЬ) КАК Период,
	|	МАКСИМУМ(Расчеты.ПорядокОперации)   КАК ПорядокОперации,
	|	
	|	СУММА(Расчеты.ДолгКлиента)          КАК ДолгКлиента,
	|	СУММА(Расчеты.НашДолг)              КАК НашДолг,
	|	СУММА(Расчеты.ПлановаяОплата)       КАК ПлановаяОплата,
	|	СУММА(Расчеты.ПлановаяОтгрузка)     КАК ПлановаяОтгрузка,
	|	СУММА(Расчеты.СписаниеПереоценка)   КАК СписаниеПереоценка,
	|	СУММА(Расчеты.Оплачено)             КАК Оплачено,
	|	СУММА(Расчеты.Отгружено)            КАК Отгружено,
	|	СУММА(Расчеты.Зачтено)              КАК Зачтено
	|ПОМЕСТИТЬ ВтРасчеты
	|ИЗ (
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                                     КАК Организация,
	|		АналитикаУчета.Партнер                                         КАК Партнер,
	|		АналитикаУчета.Контрагент                                      КАК Контрагент,
	|		АналитикаУчета.Договор                                         КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности                         КАК НаправлениеДеятельности,
	|		РасчетыПоСрокамОстатки.ОбъектРасчетов                          КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокамОстатки.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                                          КАК Валюта,
	|		РасчетыПоСрокамОстатки.РасчетныйДокумент                       КАК РасчетныйДокумент,
	|		ВЫРАЗИТЬ(&ТекстНачальныйОстаток КАК СТРОКА(17))                КАК ДокументРегистратор,
	|		РасчетыПоСрокамОстатки.ДатаПлановогоПогашения                  КАК ДатаПлановогоПогашения,
	|		РасчетыПоСрокамОстатки.ДатаВозникновения                       КАК ДатаВозникновения,
	|		&НачалоПериода                                                 КАК Период,
	|		""""                                                           КАК ПорядокОперации,
	|		ЛОЖЬ                                                           КАК Сторно,
	|		
	|		0                                                              КАК Заказано,
	|		0                                                              КАК Отгружено,
	|		0                                                              КАК Зачтено,
	|		0                                                              КАК Оплачено,
	|		0                                                              КАК СписаниеПереоценка,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокамОстатки.ДолгНачальныйОстаток
	|			КОГДА 2
	|				ТОГДА РасчетыПоСрокамОстатки.ДолгУпрНачальныйОстаток
	|			ИНАЧЕ РасчетыПоСрокамОстатки.ДолгРеглНачальныйОстаток
	|		КОНЕЦ                                                          КАК ДолгКлиента,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПоСрокамОстатки.ПредоплатаНачальныйОстаток
	|			КОГДА 2
	|				ТОГДА РасчетыПоСрокамОстатки.ПредоплатаУпрНачальныйОстаток
	|			ИНАЧЕ РасчетыПоСрокамОстатки.ПредоплатаРеглНачальныйОстаток
	|		КОНЕЦ                                                          КАК НашДолг,
	|		0                                                              КАК ПлановаяОплата,
	|		0                                                              КАК ПлановаяОтгрузка
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам.ОстаткиИОбороты({&НачалоПериода},{&КонецПериода}) КАК РасчетыПоСрокамОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПоСрокамОстатки.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|	ГДЕ
	|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	{ГДЕ
	|		АналитикаУчета.Организация.* КАК Организация,
	|		АналитикаУчета.Партнер.* КАК Партнер,
	|		АналитикаУчета.Контрагент.* КАК Контрагент,
	|		АналитикаУчета.Договор.* КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|		(АналитикаУчета.Партнер В
	|				(ВЫБРАТЬ
	|					ОтборПоСегментуПартнеров.Партнер
	|				ИЗ
	|					ОтборПоСегментуПартнеров
	|				ГДЕ
	|					ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров)),
	|		&НачалоПериода <> ДАТАВРЕМЯ(1,1,1)}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                        КАК Организация,
	|		АналитикаУчета.Партнер                            КАК Партнер,
	|		АналитикаУчета.Контрагент                         КАК Контрагент,
	|		АналитикаУчета.Договор                            КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|		РасчетыПланОплатОстатки.ОбъектРасчетов            КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланОплатОстатки.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                             КАК Валюта,
	|		РасчетыПланОплатОстатки.ДокументПлан              КАК ДокументПлан,
	|		ВЫРАЗИТЬ(&ТекстНачальныйОстаток КАК СТРОКА(17))   КАК ДокументРегистратор,
	|		РасчетыПланОплатОстатки.ДатаПлановогоПогашения    КАК ДатаПлановогоПогашения,
	|		РасчетыПланОплатОстатки.ДатаВозникновения         КАК ДатаВозникновения,
	|		&НачалоПериода                                    КАК Период,
	|		""""                                              КАК ПорядокОперации,
	|		ЛОЖЬ                                              КАК Сторно,
	|		
	|		0                                                 КАК Заказано,
	|		0                                                 КАК Отгружено,
	|		0                                                 КАК Зачтено,
	|		0                                                 КАК Оплачено,
	|		0                                                 КАК СписаниеПереоценка,
	|		
	|		0                                                 КАК ДолгКлиента,
	|		0                                                 КАК НашДолг,
	|		(РасчетыПланОплатОстатки.КОплатеНачальныйОстаток - РасчетыПланОплатОстатки.ОплаченоНачальныйОстаток)
	|			* КурсВалюты.Коэффициент                      КАК ПлановаяОплата,
	|		0                                                 КАК ПлановаяОтгрузка
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПланОплат.ОстаткиИОбороты({&НачалоПериода},{&КонецПериода}) КАК РасчетыПланОплатОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПланОплатОстатки.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|				ПО (КурсВалюты.Валюта = РасчетыПланОплатОстатки.Валюта)
	|					И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|	ГДЕ
	|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	{ГДЕ
	|		АналитикаУчета.Организация.* КАК Организация,
	|		АналитикаУчета.Партнер.* КАК Партнер,
	|		АналитикаУчета.Контрагент.* КАК Контрагент,
	|		АналитикаУчета.Договор.* КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|		(АналитикаУчета.Партнер В
	|				(ВЫБРАТЬ
	|					ОтборПоСегментуПартнеров.Партнер
	|				ИЗ
	|					ОтборПоСегментуПартнеров
	|				ГДЕ
	|					ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                        КАК Организация,
	|		АналитикаУчета.Партнер                            КАК Партнер,
	|		АналитикаУчета.Контрагент                         КАК Контрагент,
	|		АналитикаУчета.Договор                            КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности            КАК НаправлениеДеятельности,
	|		РасчетыПланОтгрузокОстатки.ОбъектРасчетов         КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланОтгрузокОстатки.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                             КАК Валюта,
	|		РасчетыПланОтгрузокОстатки.ДокументПлан           КАК ДокументПлан,
	|		ВЫРАЗИТЬ(&ТекстНачальныйОстаток КАК СТРОКА(17))   КАК ДокументРегистратор,
	|		РасчетыПланОтгрузокОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
	|		РасчетыПланОтгрузокОстатки.ДатаВозникновения      КАК ДатаВозникновения,
	|		&НачалоПериода                                    КАК Период,
	|		""""                                              КАК ПорядокОперации,
	|		ЛОЖЬ                                              КАК Сторно,
	|		
	|		0                                                 КАК Заказано,
	|		0                                                 КАК Отгружено,
	|		0                                                 КАК Зачтено,
	|		0                                                 КАК Оплачено,
	|		0                                                 КАК СписаниеПереоценка,
	|		0                                                 КАК ДолгКлиента,
	|		0                                                 КАК НашДолг,
	|		0                                                 КАК ПлановаяОплата,
	|		РасчетыПланОтгрузокОстатки.СуммаНачальныйОстаток
	|			* КурсВалюты.Коэффициент                      КАК ПлановаяОтгрузка
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПланОтгрузок.ОстаткиИОбороты({&НачалоПериода},{&КонецПериода}) КАК РасчетыПланОтгрузокОстатки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПланОтгрузокОстатки.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|				ПО (КурсВалюты.Валюта = РасчетыПланОтгрузокОстатки.Валюта)
	|					И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|	ГДЕ
	|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	{ГДЕ
	|		АналитикаУчета.Организация.* КАК Организация,
	|		АналитикаУчета.Партнер.* КАК Партнер,
	|		АналитикаУчета.Контрагент.* КАК Контрагент,
	|		АналитикаУчета.Договор.* КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|		(АналитикаУчета.Партнер В
	|				(ВЫБРАТЬ
	|					ОтборПоСегментуПартнеров.Партнер
	|				ИЗ
	|					ОтборПоСегментуПартнеров
	|				ГДЕ
	|					ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                                     КАК Организация,
	|		АналитикаУчета.Партнер                                         КАК Партнер,
	|		АналитикаУчета.Контрагент                                      КАК Контрагент,
	|		АналитикаУчета.Договор                                         КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности                         КАК НаправлениеДеятельности,
	|		РасчетыПоСрокам.ОбъектРасчетов                                 КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                                          КАК Валюта,
	|		РасчетыПоСрокам.РасчетныйДокумент                              КАК РасчетныйДокумент,
	|		РасчетыПоСрокам.ДокументРегистратор                            КАК ДокументРегистратор,
	|		РасчетыПоСрокам.ДатаПлановогоПогашения                         КАК ДатаПлановогоПогашения,
	|		РасчетыПоСрокам.ДатаВозникновения                              КАК ДатаВозникновения,
	|		РасчетыПоСрокам.Период                                         КАК Период,
	|		РасчетыПоСрокам.ПорядокОперации                                КАК ПорядокОперации,
	|		РасчетыПоСрокам.Сторно                                         КАК Сторно,
	|		
	|		0                                                              КАК Заказано,
	|		&ОтгруженоКлиенту                                              КАК Отгружено,
	|		&ЗачтеноКлиенту                                                КАК Зачтено,
	|		&ОплаченоКлиентом                                              КАК Оплачено,
	|		&СписаниеПереоценка                                            КАК СписаниеПереоценка,
	|		ВЫБОР КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА
	|				ВЫБОР &ДанныеОтчета
	|					КОГДА 4
	|						ТОГДА РасчетыПоСрокам.Долг
	|					КОГДА 2
	|						ТОГДА РасчетыПоСрокам.ДолгУпр
	|					ИНАЧЕ РасчетыПоСрокам.ДолгРегл
	|				КОНЕЦ
	|			ИНАЧЕ
	|				- ВЫБОР &ДанныеОтчета
	|					КОГДА 4
	|						ТОГДА РасчетыПоСрокам.Долг
	|					КОГДА 2
	|						ТОГДА РасчетыПоСрокам.ДолгУпр
	|					ИНАЧЕ РасчетыПоСрокам.ДолгРегл
	|				КОНЕЦ
	|		КОНЕЦ                                                          КАК ДолгКлиента,
	|		ВЫБОР КОГДА РасчетыПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА
	|				ВЫБОР &ДанныеОтчета
	|					КОГДА 4
	|						ТОГДА РасчетыПоСрокам.Предоплата
	|					КОГДА 2
	|						ТОГДА РасчетыПоСрокам.ПредоплатаУпр
	|					ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
	|				КОНЕЦ
	|			ИНАЧЕ
	|				- ВЫБОР &ДанныеОтчета
	|					КОГДА 4
	|						ТОГДА РасчетыПоСрокам.Предоплата
	|					КОГДА 2
	|						ТОГДА РасчетыПоСрокам.ПредоплатаУпр
	|					ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
	|				КОНЕЦ
	|		КОНЕЦ                                                          КАК НашДолг,
	|		0                                                              КАК ПлановаяОплата,
	|		0                                                              КАК ПлановаяОтгрузка
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПоСрокам.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|	ГДЕ
	|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|		И (ВЫБОР &ДанныеОтчета
	|				КОГДА 4
	|					ТОГДА РасчетыПоСрокам.Долг
	|				КОГДА 2
	|					ТОГДА РасчетыПоСрокам.ДолгУпр
	|				ИНАЧЕ РасчетыПоСрокам.ДолгРегл
	|			КОНЕЦ <> 0
	|			ИЛИ ВЫБОР &ДанныеОтчета
	|				КОГДА 4
	|					ТОГДА РасчетыПоСрокам.Предоплата
	|				КОГДА 2
	|					ТОГДА РасчетыПоСрокам.ПредоплатаУпр
	|				ИНАЧЕ РасчетыПоСрокам.ПредоплатаРегл
	|			КОНЕЦ <> 0)
	|	{ГДЕ
	|		РасчетыПоСрокам.Период МЕЖДУ &НачалоПериода И &КонецПериода,
	|		АналитикаУчета.Организация.* КАК Организация,
	|		АналитикаУчета.Партнер.* КАК Партнер,
	|		АналитикаУчета.Контрагент.* КАК Контрагент,
	|		АналитикаУчета.Договор.* КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|		(АналитикаУчета.Партнер В
	|				(ВЫБРАТЬ
	|					ОтборПоСегментуПартнеров.Партнер
	|				ИЗ
	|					ОтборПоСегментуПартнеров
	|				ГДЕ
	|					ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация               КАК Организация,
	|		АналитикаУчета.Партнер                   КАК Партнер,
	|		АналитикаУчета.Контрагент                КАК Контрагент,
	|		АналитикаУчета.Договор                   КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности   КАК НаправлениеДеятельности,
	|		РасчетыПланОплат.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланОплат.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                    КАК Валюта,
	|		РасчетыПланОплат.ДокументПлан            КАК ДокументПлан,
	|		РасчетыПланОплат.ДокументРегистратор     КАК ДокументРегистратор,
	|		РасчетыПланОплат.ДатаПлановогоПогашения  КАК ДатаПлановогоПогашения,
	|		РасчетыПланОплат.ДатаВозникновения       КАК ДатаВозникновения,
	|		РасчетыПланОплат.Период                  КАК Период,
	|		РасчетыПланОплат.ПорядокОперации         КАК ПорядокОперации,
	|		ЛОЖЬ                                     КАК Сторно,
	|		
	|		0                                        КАК Заказано,
	|		0                                        КАК Отгружено,
	|		0                                        КАК Зачтено,
	|		0                                        КАК Оплачено,
	|		0                                        КАК СписаниеПереоценка,
	|		
	|		0                                        КАК ДолгКлиента,
	|		0                                        КАК НашДолг,
	|		ВЫБОР КОГДА РасчетыПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыПланОплат.КОплате - РасчетыПланОплат.Оплачено
	|			ИНАЧЕ -РасчетыПланОплат.КОплате + РасчетыПланОплат.Оплачено
	|		КОНЕЦ * КурсВалюты.Коэффициент           КАК ПлановаяОплата,
	|		0                                        КАК ПлановаяОтгрузка
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПланОплат КАК РасчетыПланОплат
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПланОплат.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|				ПО (КурсВалюты.Валюта = РасчетыПланОплат.Валюта)
	|					И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|	ГДЕ
	|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	{ГДЕ
	|		РасчетыПланОплат.Период МЕЖДУ &НачалоПериода И &КонецПериода,
	|		АналитикаУчета.Организация.* КАК Организация,
	|		АналитикаУчета.Партнер.* КАК Партнер,
	|		АналитикаУчета.Контрагент.* КАК Контрагент,
	|		АналитикаУчета.Договор.* КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|		(АналитикаУчета.Партнер В
	|				(ВЫБРАТЬ
	|					ОтборПоСегментуПартнеров.Партнер
	|				ИЗ
	|					ОтборПоСегментуПартнеров
	|				ГДЕ
	|					ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаУчета.Организация                  КАК Организация,
	|		АналитикаУчета.Партнер                      КАК Партнер,
	|		АналитикаУчета.Контрагент                   КАК Контрагент,
	|		АналитикаУчета.Договор                      КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|		РасчетыПланОтгрузок.ОбъектРасчетов          КАК ОбъектРасчетов,
	|		ВЫБОР &ДанныеОтчета
	|			КОГДА 4
	|				ТОГДА РасчетыПланОтгрузок.Валюта
	|			КОГДА 2
	|				ТОГДА &ВалютаУправленческогоУчета
	|			ИНАЧЕ АналитикаУчета.Организация.ВалютаРегламентированногоУчета
	|		КОНЕЦ                                       КАК Валюта,
	|		РасчетыПланОтгрузок.ДокументПлан            КАК ДокументПлан,
	|		РасчетыПланОтгрузок.ДокументРегистратор     КАК ДокументРегистратор,
	|		РасчетыПланОтгрузок.ДатаПлановогоПогашения  КАК ДатаПлановогоПогашения,
	|		РасчетыПланОтгрузок.ДатаВозникновения       КАК ДатаВозникновения,
	|		РасчетыПланОтгрузок.Период                  КАК Период,
	|		РасчетыПланОтгрузок.ПорядокОперации         КАК ПорядокОперации,
	|		ЛОЖЬ                                        КАК Сторно,
	|		
	|		ВЫБОР КОГДА РасчетыПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыПланОтгрузок.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ * КурсВалюты.Коэффициент              КАК Заказано,
	|		0                                           КАК Отгружено,
	|		0                                           КАК Зачтено,
	|		0                                           КАК Оплачено,
	|		0                                           КАК СписаниеПереоценка,
	|		0                                           КАК ДолгКлиента,
	|		0                                           КАК НашДолг,
	|		0                                           КАК ПлановаяОплата,
	|		ВЫБОР КОГДА РасчетыПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыПланОтгрузок.Сумма
	|			ИНАЧЕ -РасчетыПланОтгрузок.Сумма
	|		КОНЕЦ * КурсВалюты.Коэффициент              КАК ПлановаяОтгрузка
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПланОтгрузок КАК РасчетыПланОтгрузок
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчета
	|				ПО РасчетыПланОтгрузок.АналитикаУчетаПоПартнерам = АналитикаУчета.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалюты
	|				ПО (КурсВалюты.Валюта = РасчетыПланОтгрузок.Валюта)
	|					И (КурсВалюты.Организация = АналитикаУчета.Организация)
	|	ГДЕ
	|		АналитикаУчета.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|	{ГДЕ
	|		РасчетыПланОтгрузок.Период МЕЖДУ &НачалоПериода И &КонецПериода,
	|		АналитикаУчета.Организация.* КАК Организация,
	|		АналитикаУчета.Партнер.* КАК Партнер,
	|		АналитикаУчета.Контрагент.* КАК Контрагент,
	|		АналитикаУчета.Договор.* КАК Договор,
	|		АналитикаУчета.НаправлениеДеятельности.* КАК НаправлениеДеятельности,
	|		(АналитикаУчета.Партнер В
	|				(ВЫБРАТЬ
	|					ОтборПоСегментуПартнеров.Партнер
	|				ИЗ
	|					ОтборПоСегментуПартнеров
	|				ГДЕ
	|					ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}) КАК Расчеты
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.Организация,
	|	Расчеты.Партнер,
	|	Расчеты.Контрагент,
	|	Расчеты.Договор,
	|	Расчеты.НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.Валюта,
	|	Расчеты.РасчетныйДокумент,
	|	Расчеты.ДокументРегистратор,
	|	Расчеты.ДатаВозникновения,
	|	Расчеты.ДатаПлановогоПогашения,
	|	Расчеты.Сторно,
	|	НАЧАЛОПЕРИОДА(Расчеты.Период, ДЕНЬ)
	|;
	|
	|ВЫБРАТЬ 
	|	Расчеты.ОбъектРасчетов              КАК ОбъектРасчетов,
	|	Расчеты.ДокументРегистратор         КАК ДокументРегистратор,
	|	Расчеты.Организация                 КАК Организация,
	|	Расчеты.Период                      КАК Период,
	|	
	|	ВЫБОР КОГДА СУММА(Расчеты.Оплачено) = 0 
	|				И СУММА(Расчеты.Отгружено) = 0
	|				И СУММА(Расчеты.СписаниеПереоценка) = 0 
	|		ТОГДА ВЫБОР КОГДА СУММА(Расчеты.Зачтено)+СУММА(Расчеты.ПлановаяОтгрузка) = 0 
	|				ТОГДА СУММА(Расчеты.ПлановаяОплата)
	|				ИНАЧЕ ВЫБОР КОГДА СУММА(Расчеты.ПлановаяОтгрузка) > СУММА(Расчеты.Зачтено) И СУММА(Расчеты.ПлановаяОтгрузка) > 0
	|								ИЛИ СУММА(Расчеты.ПлановаяОтгрузка) < СУММА(Расчеты.Зачтено) И СУММА(Расчеты.ПлановаяОтгрузка) < 0
	|						ТОГДА СУММА(Расчеты.ПлановаяОтгрузка)
	|						ИНАЧЕ СУММА(Расчеты.Зачтено)
	|					КОНЕЦ
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР  КОГДА СУММА(Расчеты.Оплачено) > СУММА(Расчеты.Отгружено) И СУММА(Расчеты.Оплачено) > 0
	|					ТОГДА СУММА(Расчеты.Оплачено)
	|				КОГДА СУММА(Расчеты.Отгружено) > СУММА(Расчеты.Оплачено) И СУММА(Расчеты.Отгружено) > 0
	|					ТОГДА СУММА(Расчеты.Отгружено)
	|				КОГДА СУММА(Расчеты.Оплачено) < СУММА(Расчеты.Отгружено) И СУММА(Расчеты.Оплачено) < 0 
	|					ТОГДА СУММА(Расчеты.Оплачено)
	|				КОГДА СУММА(Расчеты.Отгружено) < СУММА(Расчеты.Оплачено) И СУММА(Расчеты.Отгружено) < 0  
	|					ТОГДА СУММА(Расчеты.Отгружено)
	|				ИНАЧЕ СУММА(Расчеты.СписаниеПереоценка)
	|			КОНЕЦ
	|	КОНЕЦ                               КАК СуммаПоДокументу
	|
	|ПОМЕСТИТЬ ВтСуммыПоДокументам
	|ИЗ 
	|	ВтРасчеты КАК Расчеты
	|ГДЕ
	|	НЕ Расчеты.Сторно
	|СГРУППИРОВАТЬ ПО
	|	Расчеты.ОбъектРасчетов,
	|	Расчеты.ДокументРегистратор,
	|	Расчеты.Организация,
	|	Расчеты.Период
	|;
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Расчеты.Организация                 КАК Организация,
	|	Расчеты.Партнер                     КАК Партнер,
	|	Расчеты.Контрагент                  КАК Контрагент,
	|	Расчеты.Договор                     КАК Договор,
	|	Расчеты.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	Расчеты.ОбъектРасчетов              КАК ОбъектРасчетов,
	|	Расчеты.Валюта                      КАК Валюта,
	|	Расчеты.РасчетныйДокумент           КАК РасчетныйДокумент,
	|	Расчеты.ДокументРегистратор         КАК ДокументРегистратор,
	|	Расчеты.ДатаВозникновения           КАК ДатаВозникновения,
	|	Расчеты.ДатаПлановогоПогашения      КАК ДатаПлановогоПогашения,
	|	Расчеты.Период                      КАК Период,
	|	Расчеты.ПорядокОперации             КАК ПорядокОперации,
	|	
	|	Расчеты.ДолгКлиента                 КАК ДолгКлиента,
	|	Расчеты.НашДолг                     КАК НашДолг,
	|	Расчеты.ПлановаяОплата              КАК ПлановаяОплата,
	|	Расчеты.ПлановаяОтгрузка            КАК ПлановаяОтгрузка,
	|	ВЫБОР КОГДА Суммы.СуммаПоДокументу = 0 И Расчеты.ДолгКлиента <> 0
	|			ТОГДА Расчеты.ДолгКлиента
	|		КОГДА Суммы.СуммаПоДокументу = 0 И Расчеты.НашДолг <> 0
	|			ТОГДА Расчеты.НашДолг
	|		ИНАЧЕ Суммы.СуммаПоДокументу
	|	КОНЕЦ                               КАК СуммаПоДокументу
	|ИЗ ВтРасчеты КАК Расчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыПоДокументам КАК Суммы
	|			ПО Суммы.ОбъектРасчетов = Расчеты.ОбъектРасчетов
	|				И Суммы.ДокументРегистратор = Расчеты.ДокументРегистратор
	|				И Суммы.Организация = Расчеты.Организация
	|				И Суммы.Период = Расчеты.Период
	|ГДЕ
	|	Расчеты.ДолгКлиента <> 0
	|	ИЛИ Расчеты.НашДолг <> 0
	|	ИЛИ Расчеты.ПлановаяОплата <> 0
	|	ИЛИ Расчеты.ПлановаяОтгрузка <> 0
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОтгруженоКлиенту", ВзаиморасчетыСервер.ШаблонПоляОтгруженоКлиенту());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ЗачтеноКлиенту", ВзаиморасчетыСервер.ШаблонПоляЗачтеноКлиенту());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ОплаченоКлиентом", ВзаиморасчетыСервер.ШаблонПоляОплаченоКлиентом());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&СписаниеПереоценка", ВзаиморасчетыСервер.ШаблонПоляСписаниеПереоценкаЗадолженности());
	
	Возврат ТекстЗапроса;
КонецФункции

Процедура СформироватьПараметрыОтчета(ПараметрКоманды, ПараметрыФормы, Параметры)
	
	Если ТипЗнч(ПараметрКоманды) = Тип("Массив") Тогда
		ЭтоМассив = Истина;
		Если ПараметрКоманды.Количество() > 0 Тогда
			ПервыйЭлемент = ПараметрКоманды[0];
		Иначе
			ПервыйЭлемент = Неопределено;
		КонецЕсли;
	Иначе
		ЭтоМассив = Ложь;
		ПервыйЭлемент = ПараметрКоманды;
	КонецЕсли;
	
	Если ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.Партнеры") Тогда
		Если ЭтоМассив Тогда
			ЕстьПодчиненныеПартнеры = Ложь;
			Для Каждого ЭлементПараметраКоманды Из ПараметрКоманды Цикл
				Если ПартнерыИКонтрагенты.ЕстьПодчиненныеПартнеры(ЭлементПараметраКоманды) Тогда
					ЕстьПодчиненныеПартнеры = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ЕстьПодчиненныеПартнеры = ПартнерыИКонтрагенты.ЕстьПодчиненныеПартнеры(ПараметрКоманды);
		КонецЕсли;
		
		Если ЕстьПодчиненныеПартнеры Тогда
			ФиксированныеНастройки = Новый НастройкиКомпоновкиДанных();
			ЭлементОтбора = ФиксированныеНастройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Партнер");
			ЗначениеОтбора = ПараметрКоманды;
			Если ЭтоМассив Тогда
				ЗначениеОтбора = Новый СписокЗначений;
				ЗначениеОтбора.ЗагрузитьЗначения(ПараметрКоманды);
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
			Иначе
				ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
			КонецЕсли;
			ЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;
			ПараметрыФормы.ФиксированныеНастройки = ФиксированныеНастройки;
			Параметры.ФиксированныеНастройки = ФиксированныеНастройки;
			ПараметрыФормы.КлючНазначенияИспользования = "ГруппаПартнеров";
			Параметры.КлючНазначенияИспользования = "ГруппаПартнеров";
		Иначе
			ПараметрыФормы.Отбор = Новый Структура("Партнер", ПараметрКоманды);
			ПараметрыФормы.КлючНазначенияИспользования = ПараметрКоманды;
			Параметры.КлючНазначенияИспользования = ПараметрКоманды;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.ДоговорыКонтрагентов") 
		ИЛИ ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
		ПараметрыФормы.Отбор = Новый Структура("Договор", ПараметрКоманды);
		ПараметрыФормы.КлючНазначенияИспользования = ПараметрКоманды;
		Параметры.КлючНазначенияИспользования = ПараметрКоманды;
	ИначеЕсли ТипЗнч(ПервыйЭлемент) = Тип("СправочникСсылка.ОбъектыРасчетов") Тогда
		Если ЭтоМассив Тогда
			ПараметрыФормы.Отбор = Новый Структура("ОбъектРасчетов", ПараметрКоманды);
			ПараметрыФормы.КлючНазначенияИспользования = ПараметрКоманды;
			Параметры.КлючНазначенияИспользования = ПараметрКоманды;
		Иначе
			ПараметрыФормы.Отбор = Новый Структура("ОбъектРасчетов", ПервыйЭлемент);
			ПараметрыФормы.КлючНазначенияИспользования = ПервыйЭлемент;
			Параметры.КлючНазначенияИспользования = ПервыйЭлемент;
		КонецЕсли;
	Иначе
		ОбъектРасчетов = ОбъектыРасчетовСервер.ОбъектРасчетовИзСсылки(ПервыйЭлемент);
		ПараметрыФормы.Отбор = Новый Структура("ОбъектРасчетов", ОбъектРасчетов);
		ПараметрыФормы.КлючНазначенияИспользования = ОбъектРасчетов;
		Параметры.КлючНазначенияИспользования = ОбъектРасчетов;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли