
////////////////////////////////////////////////////
//// Модуль объекта документа "КР_УстановкаМинимальнойПрезентации"
//// Создан: 20.10.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-689
//// Разработка по ФДР С31.008, Документ "Установка минимальной презентации"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроверитьДублиСтрокМинПрезентации(Отказ);
	КонецЕсли;
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// A2105505-1926
	МассивНепроверяемыхРеквизитов = Новый Массив;

	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	//
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ПроверитьДублиСтрокМинПрезентации(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаТовары", Товары.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.ФорматМагазина КАК ФорматМагазина
	|ПОМЕСТИТЬ врт_ТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ФорматМагазина КАК ФорматМагазина,
	|	ВложенныйЗапрос.НомерСтроки КАК КоличествоСтрок,
	|	ВложенныйЗапрос.Штрихкод КАК КоличествоШтрихкодов,
	|	ВложенныйЗапрос.Штрихкод КАК Штрихкод,
	|	ВложенныйЗапрос.Артикул КАК Артикул,
	|	ВложенныйЗапрос.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	(ВЫБРАТЬ
	|		врт_ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|		МАКСИМУМ(ЕСТЬNULL(рс_ШтрихкодыНоменклатуры.Штрихкод, """")) КАК Штрихкод,
	|		ЕСТЬNULL(спр_Номенклатура.Артикул, """") КАК Артикул,
	|		врт_ТаблицаТовары.ФорматМагазина КАК ФорматМагазина
	|	ИЗ
	|		врт_ТаблицаТовары КАК врт_ТаблицаТовары
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК рс_ШтрихкодыНоменклатуры
	|			ПО врт_ТаблицаТовары.Номенклатура = рс_ШтрихкодыНоменклатуры.Номенклатура
	|				И врт_ТаблицаТовары.Характеристика = рс_ШтрихкодыНоменклатуры.Характеристика
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спр_Номенклатура
	|			ПО врт_ТаблицаТовары.Номенклатура = спр_Номенклатура.Ссылка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		врт_ТаблицаТовары.ФорматМагазина,
	|		врт_ТаблицаТовары.НомерСтроки,
	|		ЕСТЬNULL(спр_Номенклатура.Артикул, """")) КАК ВложенныйЗапрос
	|ИТОГИ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КоличествоСтрок),
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КоличествоШтрихкодов),
	|	МАКСИМУМ(Артикул)
	|ПО
	|	ФорматМагазина,
	|	Штрихкод";
	
	ВыборкаОбщая = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОбщая.Следующий() Цикл
		Если ВыборкаОбщая.КоличествоСтрок = ВыборкаОбщая.КоличествоШтрихкодов Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = ВыборкаОбщая.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка.Следующий() Цикл
			Если Выборка.КоличествоСтрок = Выборка.КоличествоШтрихкодов Тогда
				Продолжить;
			КонецЕсли;
			
			МассивНомеровСтрок = Новый Массив;
			ВыборкаДетальныеЗаписи = Выборка.Выбрать();
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				МассивНомеровСтрок.Добавить(ВыборкаДетальныеЗаписи.НомерСтроки);
			КонецЦикла;
			
			ТекстСообщения = НСтр("ru = 'Обнаружены дубли по ЦВМ ""%1"" для формата магазина ""%2"" в строках %3'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Артикул, Выборка.ФорматМагазина, СтрСоединить(МассивНомеровСтрок, ", "));
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
