#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Взаиморасчеты");
	МеханизмыДокумента.Добавить("Обеспечение");
	МеханизмыДокумента.Добавить("ОборотныеРегистрыУправленческогоУчета");
	МеханизмыДокумента.Добавить("ОперативныйУчетТоваровОрганизаций");
	МеханизмыДокумента.Добавить("ПриемНаОтветхранение");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	МеханизмыДокумента.Добавить("СебестоимостьИПартионныйУчет");
	МеханизмыДокумента.Добавить("СерийныйУчет");
	МеханизмыДокумента.Добавить("СуммыДокументовВВалютахУчета");
	МеханизмыДокумента.Добавить("АдресныйСклад");
	МеханизмыДокумента.Добавить("УчетДоходовРасходов");
	МеханизмыДокумента.Добавить("УчетНДС");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("УчетРабот");
	МеханизмыДокумента.Добавить("ОформлениеОтчетовКомитентуОЗакупках");
	КорректировкаПриобретенияЛокализация.ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов - таблиц значений - данных для отражения в регистр.
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	Если ДопПараметры = Неопределено Тогда
		ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	КонецЕсли;
	
	Запрос			= Новый Запрос;
	ТекстыЗапроса	= Новый СписокЗначений;
	
	Если Не ДопПараметры.ПолучитьТекстыЗапроса Тогда
		////////////////////////////////////////////////////////////////////////////
		// Создадим запрос инициализации движений
		
		ЗаполнитьПараметрыИнициализации(Запрос, Документ);
		
		////////////////////////////////////////////////////////////////////////////
		// Сформируем текст запроса
		СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры);
		
		ТекстЗапросаТаблицаПринятаяВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииРасходовНаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаПартииПроизводственныхЗатрат(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаЗакупки(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияКонтрагентДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияНоменклатураДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
		ТекстЗапросаТаблицаТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках(Запрос, ТекстыЗапроса, Регистры);
		
		РасчетСебестоимостиПроведениеДокументов.ОтразитьВМеханизмеУчетаЗатратИСебестоимости(Документ, Запрос, ТекстыЗапроса, Регистры);
		
		ОтразитьВУчетеНДС(Запрос, ТекстыЗапроса, Регистры);
		
		КорректировкаПриобретенияЛокализация.ДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	КонецЕсли;
	
	ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, Регистры);
	ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры);
	ОформитьИзлишкиНедостачиНаСкладе(Запрос, ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

#КонецОбласти

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Обработки.СправочноеРазмещениеНоменклатуры.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ИсправлениеДокументов.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	
	Справочники.Претензии.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	КорректировкаПриобретенияЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

// Добавляет команду создания документа "Корректировка поступления".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//	ТаблицаЗначений, Неопределено - сформированные команды для вывода в подменю.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	Если ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаПриобретения) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.КорректировкаПриобретения.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(Метаданные.Документы.КорректировкаПриобретения);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьКорректировкиПриобретений";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	КомандаОтчет = Отчеты.СправкаРасчетНачисленияПроцентныхРасходовДисконтирования.ДобавитьКомандуОтчета(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.ВидимостьВФормах = "ФормаДокумента,ФормаСписка,СписокДокументов";
	КонецЕсли;
	
	ВзаиморасчетыСервер.КарточкаРасчетовСПоставщиком_ДобавитьКомандуОтчетаПоДокументам(КомандыОтчетов);
	ВзаиморасчетыСервер.ЗадолженностьПоставщикам_ДобавитьКомандуОтчетаПоДокументам(КомандыОтчетов);
	
	КорректировкаПриобретенияЛокализация.ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);

КонецПроцедуры

// Возвращает массив допустимых наименований входящих документов.
// 
// Возвращаемое значение:
// 	Массив - массив наименований.
Функция НаименованияВходящихДокументов() Экспорт
	МассивНаименований = Новый Массив();
	КорректировкаПриобретенияЛокализация.ДополнитьНаименованияВходящихДокументов(МассивНаименований);
	Возврат МассивНаименований
КонецФункции

#Область Заполнение

// Заполняет таблицу реквизитов, зависимых от типа документа-основания.
//
// Параметры:
//	ДокументОснование - ДокументСсылка - Документ, на основании которого введена корректировка;
//	МассивВсехРеквизитов - Массив - реквизиты, которые не зависят от документа-основания;
//	МассивРеквизитовОперации - Массив - реквизиты, которые зависят от документа-основания.
//
Процедура ЗаполнитьИменаРеквизитовПоОснованию(ДокументОснование, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Склад");
	МассивВсехРеквизитов.Добавить("Соглашение");
	МассивВсехРеквизитов.Добавить("Договор");
	МассивВсехРеквизитов.Добавить("Товары.НоменклатураПартнера");
	МассивВсехРеквизитов.Добавить("Товары.Номенклатура");
	МассивВсехРеквизитов.Добавить("Товары.Характеристика");
	МассивВсехРеквизитов.Добавить("Товары.Назначение");
	МассивВсехРеквизитов.Добавить("Товары.Содержание");
	МассивВсехРеквизитов.Добавить("Товары.Упаковка");
	МассивВсехРеквизитов.Добавить("Товары.КоличествоУпаковок");
	МассивВсехРеквизитов.Добавить("Товары.Количество");
	МассивВсехРеквизитов.Добавить("Товары.Цена");
	МассивВсехРеквизитов.Добавить("Товары.Склад");
	МассивВсехРеквизитов.Добавить("Товары.ЗаказПоставщику");
	МассивВсехРеквизитов.Добавить("Товары.КодСтроки");
	МассивВсехРеквизитов.Добавить("Товары.НомерГТД");
	МассивВсехРеквизитов.Добавить("Товары.Сделка");
	МассивВсехРеквизитов.Добавить("Товары.Серия");
	МассивВсехРеквизитов.Добавить("Товары.СтатусУказанияСерий");
	МассивВсехРеквизитов.Добавить("Товары.СписатьНаРасходы");
	МассивВсехРеквизитов.Добавить("Товары.НаправлениеДеятельности");
	МассивВсехРеквизитов.Добавить("Товары.НомерГруппыЗатрат");
	МассивВсехРеквизитов.Добавить("Товары.СтатьяКалькуляции");
	
	МассивВсехРеквизитов.Добавить("Расхождения.Серия");
	МассивВсехРеквизитов.Добавить("Расхождения.Номенклатура");
	МассивВсехРеквизитов.Добавить("Расхождения.Характеристика");
	МассивВсехРеквизитов.Добавить("Расхождения.Назначение");
	МассивВсехРеквизитов.Добавить("Расхождения.Содержание");
	МассивВсехРеквизитов.Добавить("Расхождения.Упаковка");
	МассивВсехРеквизитов.Добавить("Расхождения.КоличествоУпаковок");
	МассивВсехРеквизитов.Добавить("Расхождения.Склад");
	МассивВсехРеквизитов.Добавить("Расхождения.ЗаказПоставщику");
	МассивВсехРеквизитов.Добавить("Расхождения.КодСтроки");
	МассивВсехРеквизитов.Добавить("Расхождения.НомерГТД");
	МассивВсехРеквизитов.Добавить("Расхождения.Сделка");
	МассивВсехРеквизитов.Добавить("Расхождения.СписатьНаРасходы");
	МассивВсехРеквизитов.Добавить("Расхождения.НаправлениеДеятельности");
	МассивВсехРеквизитов.Добавить("Расхождения.НомерГруппыЗатрат");
	МассивВсехРеквизитов.Добавить("Расхождения.СтатьяКалькуляции");
	
	МассивРеквизитовОперации = Новый Массив;
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		МассивРеквизитовОперации.Добавить("Склад");
		МассивРеквизитовОперации.Добавить("Соглашение");
		МассивРеквизитовОперации.Добавить("Договор");
		МассивРеквизитовОперации.Добавить("Товары.НоменклатураПартнера");
		МассивРеквизитовОперации.Добавить("Товары.Номенклатура");
		МассивРеквизитовОперации.Добавить("Товары.Характеристика");
		МассивРеквизитовОперации.Добавить("Товары.Назначение");
		МассивРеквизитовОперации.Добавить("Товары.Упаковка");
		МассивРеквизитовОперации.Добавить("Товары.КоличествоУпаковок");
		МассивРеквизитовОперации.Добавить("Товары.Количество");
		МассивРеквизитовОперации.Добавить("Товары.Склад");
		МассивРеквизитовОперации.Добавить("Товары.Цена");
		МассивРеквизитовОперации.Добавить("Товары.ЗаказПоставщику");
		МассивРеквизитовОперации.Добавить("Товары.КодСтроки");
		МассивРеквизитовОперации.Добавить("Товары.НомерГТД");
		МассивРеквизитовОперации.Добавить("Товары.Сделка");
		МассивРеквизитовОперации.Добавить("Товары.Серия");
		МассивРеквизитовОперации.Добавить("Товары.СтатусУказанияСерий");
		МассивРеквизитовОперации.Добавить("Товары.СписатьНаРасходы");
		МассивРеквизитовОперации.Добавить("Расхождения.Серия");
		МассивРеквизитовОперации.Добавить("Расхождения.Номенклатура");
		МассивРеквизитовОперации.Добавить("Расхождения.Характеристика");
		МассивРеквизитовОперации.Добавить("Расхождения.Назначение");
		МассивРеквизитовОперации.Добавить("Расхождения.Упаковка");
		МассивРеквизитовОперации.Добавить("Расхождения.КоличествоУпаковок");
		МассивРеквизитовОперации.Добавить("Расхождения.Склад");
		МассивРеквизитовОперации.Добавить("Расхождения.ЗаказПоставщику");
		МассивРеквизитовОперации.Добавить("Расхождения.КодСтроки");
		МассивРеквизитовОперации.Добавить("Расхождения.НомерГТД");
		МассивРеквизитовОперации.Добавить("Расхождения.Сделка");
		МассивРеквизитовОперации.Добавить("Расхождения.СписатьНаРасходы");
		
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов") Тогда
		
		МассивРеквизитовОперации.Добавить("Соглашение");
		МассивРеквизитовОперации.Добавить("Договор");
		МассивРеквизитовОперации.Добавить("Товары.Содержание");
		МассивРеквизитовОперации.Добавить("Товары.НаправлениеДеятельности");
		МассивРеквизитовОперации.Добавить("Товары.СписатьНаРасходы");
		МассивРеквизитовОперации.Добавить("Товары.Количество");
		МассивРеквизитовОперации.Добавить("Товары.Цена");
		МассивРеквизитовОперации.Добавить("Расхождения.Содержание");
		МассивРеквизитовОперации.Добавить("Расхождения.НаправлениеДеятельности");
		МассивРеквизитовОперации.Добавить("Расхождения.СписатьНаРасходы");
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет табличную часть Товары по акту о расхождениях
//
// Параметры:
// 	Объект - ДокументОбъект.КорректировкаПриобретения - Документ, который необходимо заполнить.
//
Процедура ЗаполнитьТоварыПоАктуОРасхождениях(Объект) Экспорт
	
	Объект.Товары.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	АктОРасхожденияхПослеПриемкиТовары.НомерСтроки            КАК НомерСтроки,
	|	АктОРасхожденияхПослеПриемкиТовары.НоменклатураПартнера КАК НоменклатураПартнера,
	|	АктОРасхожденияхПослеПриемкиТовары.Номенклатура           КАК Номенклатура,
	|	АктОРасхожденияхПослеПриемкиТовары.Характеристика         КАК Характеристика,
	|	АктОРасхожденияхПослеПриемкиТовары.Назначение             КАК Назначение, 
	|	АктОРасхожденияхПослеПриемкиТовары.Упаковка               КАК Упаковка,
	|	АктОРасхожденияхПослеПриемкиТовары.Цена                   КАК Цена,
	|	АктОРасхожденияхПослеПриемкиТовары.СтавкаНДС              КАК СтавкаНДС,
	|	АктОРасхожденияхПослеПриемкиТовары.ЗаказПоставщику        КАК ЗаказПоставщику,
	|	АктОРасхожденияхПослеПриемкиТовары.КодСтроки              КАК КодСтроки,
	|	АктОРасхожденияхПослеПриемкиТовары.Склад                  КАК Склад,
	|	АктОРасхожденияхПослеПриемкиТовары.Серия                  КАК Серия,
	|	АктОРасхожденияхПослеПриемкиТовары.СтатусУказанияСерий    КАК СтатусУказанияСерий,
	|	АктОРасхожденияхПослеПриемкиТовары.СписатьНаРасходы       КАК СписатьНаРасходы,
	|	АктОРасхожденияхПослеПриемкиТовары.СтатьяРасходов         КАК СтатьяРасходов,
	|	АктОРасхожденияхПослеПриемкиТовары.АналитикаРасходов      КАК АналитикаРасходов,
	|	АктОРасхожденияхПослеПриемкиТовары.Сделка                 КАК Сделка,
	|	АктОРасхожденияхПослеПриемкиТовары.Подразделение          КАК Подразделение,
	|	АктОРасхожденияхПослеПриемкиТовары.НомерГТД               КАК НомерГТД,
	|
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхПослеПриемкиТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленное), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачуИОжидатьДопоставку), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачу))
	|			ТОГДА АктОРасхожденияхПослеПриемкиТовары.Сумма
	|		ИНАЧЕ АктОРасхожденияхПослеПриемкиТовары.СуммаПоДокументу
	|	КОНЕЦ КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхПослеПриемкиТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленное), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачуИОжидатьДопоставку), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачу))
	|			ТОГДА АктОРасхожденияхПослеПриемкиТовары.СуммаНДС
	|		ИНАЧЕ АктОРасхожденияхПослеПриемкиТовары.СуммаНДСПоДокументу
	|	КОНЕЦ КАК СуммаНДС,
	|
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхПослеПриемкиТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленное), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачуИОжидатьДопоставку), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачу))
	|			ТОГДА АктОРасхожденияхПослеПриемкиТовары.СуммаСНДС
	|		ИНАЧЕ АктОРасхожденияхПослеПриемкиТовары.СуммаСНДСПоДокументу
	|	КОНЕЦ КАК СуммаСНДС,
	|
	|	ВЫБОР 
	|		КОГДА АктОРасхожденияхПослеПриемкиТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиПерепоставленноеНаПрочиеДоходы), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы),ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ВернутьПерепоставленноеБезОформления))
	|			ТОГДА  АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковокПоДокументу
	|		ИНАЧЕ  АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковок
	|	КОНЕЦ КАК КоличествоУпаковок,
	|
	|	ВЫБОР 
	|		КОГДА АктОРасхожденияхПослеПриемкиТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиПерепоставленноеНаПрочиеДоходы), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОтнестиНедостачуНаПрочиеРасходы),ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ВернутьПерепоставленноеБезОформления))
	|			ТОГДА  АктОРасхожденияхПослеПриемкиТовары.КоличествоПоДокументу
	|		ИНАЧЕ  АктОРасхожденияхПослеПриемкиТовары.Количество
	|	КОНЕЦ КАК Количество,
	|
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхПослеПриемкиТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленное), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачуИОжидатьДопоставку), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачу))
	|				И (АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковок > АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковокПоДокументу
	|					ИЛИ АктОРасхожденияхПослеПриемкиТовары.КодСтроки <> 0
	|						И АктОРасхожденияхПослеПриемкиТовары.ЗаказПоставщику В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)))
	|			ТОГДА АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковок - АктОРасхожденияхПослеПриемкиТовары.КоличествоУпаковокПоДокументу
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоУпаковокСверхЗаказа,
	|
	|	ВЫБОР
	|		КОГДА АктОРасхожденияхПослеПриемкиТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленноеИВернуть), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьПерепоставленное), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачуИОжидатьДопоставку), ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачу))
	|				И (АктОРасхожденияхПослеПриемкиТовары.Количество > АктОРасхожденияхПослеПриемкиТовары.КоличествоПоДокументу
	|					ИЛИ АктОРасхожденияхПослеПриемкиТовары.КодСтроки <> 0
	|						И АктОРасхожденияхПослеПриемкиТовары.ЗаказПоставщику В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)))
	|			ТОГДА АктОРасхожденияхПослеПриемкиТовары.Количество - АктОРасхожденияхПослеПриемкиТовары.КоличествоПоДокументу
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоСверхЗаказа
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПриемки.Товары КАК АктОРасхожденияхПослеПриемкиТовары
	|ГДЕ
	|	АктОРасхожденияхПослеПриемкиТовары.Ссылка = &АктОРасхожденияхПослеПриемкиОснование
	|	И АктОРасхожденияхПослеПриемкиТовары.ДокументОснование = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("АктОРасхожденияхПослеПриемкиОснование", Объект.АктОРасхожденияхПослеПриемкиОснование);
	Запрос.УстановитьПараметр("ДокументОснование", Объект.ДокументОснование);
	
	СтруктураДействий = Новый Структура;
	СтруктураПересчетаСуммы = ОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(Объект);
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
	СтруктураДействий.Вставить("ПересчитатьСумму");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если (Выборка.Количество - Выборка.КоличествоСверхЗаказа) > 0 Тогда
			
			НоваяСтрокаТовары = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, Выборка);
			Если Выборка.КоличествоСверхЗаказа > 0 Тогда
				НоваяСтрокаТовары.КоличествоУпаковок = Выборка.КоличествоУпаковок - Выборка.КоличествоУпаковокСверхЗаказа;
				НоваяСтрокаТовары.Количество         = Выборка.Количество - Выборка.КоличествоСверхЗаказа;
				ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(НоваяСтрокаТовары, СтруктураДействий, Неопределено);
			КонецЕсли;
			
		КонецЕсли;
			
		Если Выборка.КоличествоСверхЗаказа > 0 Тогда
			
			ДополнительнаяСтрокаТовары = Объект.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(ДополнительнаяСтрокаТовары, Выборка);
			ДополнительнаяСтрокаТовары.КоличествоУпаковок = Выборка.КоличествоУпаковокСверхЗаказа;
			ДополнительнаяСтрокаТовары.Количество         = Выборка.КоличествоСверхЗаказа;
			ДополнительнаяСтрокаТовары.КодСтроки          = 0;
			
			ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ДополнительнаяСтрокаТовары, СтруктураДействий, Неопределено);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОперацииРаздельнойЗакупки	= Новый Массив;
	ОперацииРаздельнойЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
	ОперацииРаздельнойЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
	ОперацииРаздельнойЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	ОперацииРаздельнойЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки);
	ОперацииРаздельнойЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	
	ОперацияОснования = ?(ЗначениеЗаполнено(Объект.ДокументОснование)
								И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг"),
							ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ХозяйственнаяОперация"),
							Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	
	ИспользуетсяДокументПоступлениеТоваров = ОперацииРаздельнойЗакупки.Найти(ОперацияОснования) <> Неопределено;
	
	Объект.ЗаполнитьРасхождения(ИспользуетсяДокументПоступлениеТоваров);
	
КонецПроцедуры

#КонецОбласти

#Область Серии

// Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	ИменаРеквизитов = "Склад,Дата,ДокументОснование";
	
	Возврат ИменаРеквизитов;
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе
//
// Параметры:
//  Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//
// Возвращаемое значение:
//  Структура - Состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.КорректировкаПриобретения";
	
	ПараметрыСерийСклада = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.Склад, Истина);
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры  = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.ИмяТЧСерии = "Товары";
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПустаяСсылка());
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Склад");
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Назначение");
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	ЭтоТоварыВПути = Ложь;
	ХозОперацияТоварыВПутиИНеотфактуровка = Ложь;
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ХозОперацияДокументаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ХозяйственнаяОперация");
		
		ОперацииВПути = Новый Массив();
		ОперацииВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути);
		ОперацииВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
		ОперацииВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
		
		ОперацииНеотфактуровки = Новый Массив();
		ОперацииНеотфактуровки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
		ОперацииНеотфактуровки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки);
		
		ХозОперацияТоварыВПутиИНеотфактуровка = ОперацииВПути.Найти(ХозОперацияДокументаОснования) <> Неопределено
			Или ОперацииНеотфактуровки.Найти(ХозОперацияДокументаОснования) <> Неопределено;
		ЭтоТоварыВПути = ОперацииВПути.Найти(ХозОперацияДокументаОснования) <> Неопределено;
	КонецЕсли;
	
	ПараметрыУказанияСерий.ПараметрыЗапроса.Вставить("ЭтоТоварыВПути", ЭтоТоварыВПути);
	ПараметрыУказанияСерий.ПараметрыЗапроса.Вставить("ИспользуетсяДокументПоступлениеТоваров", ХозОперацияТоварыВПутиИНеотфактуровка);
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Склад,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Серия,
	|	Товары.Количество,
	|	Товары.СтатусУказанияСерий,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Склад,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	СУММА(Товары.Количество) КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Склад,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL
	|			ИЛИ &ИспользуетсяДокументПоступлениеТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|					ТОГДА ВЫБОР
	|							КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 14
	|							ИНАЧЕ 13
	|						КОНЕЦ
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ТоварыДляЗапроса.ВидНоменклатуры.ПолитикаУчетаСерий ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТоварыДляЗапроса.ВидНоменклатуры.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|					И &ИспользуетсяДокументПоступлениеТоваров
	|					ТОГДА
	|						ВЫБОР
	|							КОГДА ((ТоварыДляЗапроса.ВидНоменклатуры.ПолитикаУчетаСерий.УчетСерийВНеотфактурованныхПоставкахТоваров
	|										И НЕ &ЭтоТоварыВПути)
	|									ИЛИ (ТоварыДляЗапроса.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям)
	|										И &ЭтоТоварыВПути)
	|								ТОГДА ВЫБОР
	|										КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|											ТОГДА 18
	|										ИНАЧЕ 17
	|									КОНЕЦ
	|								ИНАЧЕ 0
	|						КОНЕЦ
	|					ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатусУказанияСерийТоварыУПартнеров
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|				ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
	|			ПО (ПолитикиУчетаСерий.Склад = ТоварыДляЗапроса.Склад)
	|				И ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Назначение = ТоварыДляЗапроса.Назначение
	|			И Товары.Склад = ТоварыДляЗапроса.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА Статусы.СтатусУказанияСерий = 0
	|			ТОГДА Статусы.СтатусУказанияСерийТоварыУПартнеров
	|		ИНАЧЕ Статусы.СтатусУказанияСерий
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	ВЫБОР
	|		КОГДА &ИспользуетсяДокументПоступлениеТоваров
	|			ТОГДА Статусы.СтатусУказанияСерийТоварыУПартнеров
	|		ИНАЧЕ Статусы.СтатусУказанияСерий
	|	КОНЕЦ <> Статусы.СтарыйСтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПараметрыРегистрацииСчетовФактурПолученных(Объект) Экспорт
	
	ПараметрыРегистрации = УчетНДСУПКлиентСервер.ПараметрыРегистрацииСчетовФактурПолученных();
	ПараметрыРегистрации.Ссылка = Объект.Ссылка;
	ПараметрыРегистрации.Дата = Объект.Дата;
	ПараметрыРегистрации.Организация = Объект.Организация;
	ПараметрыРегистрации.Контрагент = Объект.Контрагент;
	ПараметрыРегистрации.НалогообложениеНДС = Объект.НалогообложениеНДС;
	Если Объект.ВидКорректировки = Перечисления.ХозяйственныеОперации.ИсправлениеОшибок Тогда
		ПараметрыРегистрации.ИсправлениеОшибок = Истина;
	ИначеЕсли Объект.ВидКорректировки = Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон Тогда
		ПараметрыРегистрации.КорректировкаПоСогласованиюСторон = Истина;
	КонецЕсли;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#КонецОбласти


// Возвращает структуру параметров порядка обработки документа
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения
// 	
// Возвращаемое значение:
// Структура - см. НаправленияДеятельностиСервер.ПорядокОбработкиДокументаПриИзмененииНаправленияДеятельности
//
Функция ПорядокОбработкиДокументаПриИзмененииНаправленияДеятельности(Форма) Экспорт
	
	ПорядокОбработкиДокумента = НаправленияДеятельностиСервер.ПорядокОбработкиДокументаПриИзмененииНаправленияДеятельности();
	ПорядокОбработкиДокумента.ИменаТабличныхЧастейДляОчисткиНекорректныхНазначений = "Товары";
	ТаблицаУсловий = НаправленияДеятельностиСервер.УсловияОбработкиНазначенийВСтроках("ТипНоменклатуры,КодСтроки");
	ПорядокОбработкиДокумента.УсловияОбработкиСтрок.Вставить("Товары", ТаблицаУсловий);
	
	Возврат ПорядокОбработкиДокумента;
	
КонецФункции

// Инициализирует параметры, обслуживающие выбор назначений в формах документа.
//
//  Возвращаемое значение:
//  Структура - структура параметров, см. Справочники.Назначения.МакетФормыВыбораНазначений().
//
Функция МакетФормыВыбораНазначений() Экспорт
	
	МакетФормы = Справочники.Назначения.МакетФормыВыбораНазначений();
	
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы);
	ШаблонНазначения.НаправлениеДеятельности  = "Объект.НаправлениеДеятельности";
	
	// Потребности в товарах на складе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказов", Истина, "Объект.Товары.Назначение");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Потребность").Пометка = Истина;
	ОписаниеКолонок.УсловиеИспользования = "Объект.Товары.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.Товары.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.Товары.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад            = "Объект.Товары.Склад";
	
	// Потребности в работах в подразделении-получателе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказовРаботами", Истина, "Объект.Товары.Назначение");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Потребность").Пометка = Истина;
	ОписаниеКолонок.УсловиеИспользования = "Объект.Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.Товары.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.Товары.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Подразделение    = "Объект.Товары.Подразделение";
	
	Возврат МакетФормы;
	
КонецФункции


// Возвращает параметры выбора статей в документе.
// 
// Параметры:
// 	ПараметрыОпределенияДоступности - Структура:
// 	* ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Хозяйственная операция документа (ВидКорректировки)
//	* ДокументОснование - ДокументСсылка- Документ основание
//	* ДатаДокумента - Дата - Дата документа
//	* ОтразитьНаПрочихДоходах - Булево
//	* СписатьНаРасходы - Булево.
// 
// Возвращаемое значение:
// 	Массив - Массив параметров настройки счетов учета (См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики)
//
Функция ПараметрыВыбораСтатейИАналитик(ПараметрыОпределенияДоступности) Экспорт
	
	КорректировкаПриобретенияУслугПрочихАктивов = (ТипЗнч(ПараметрыОпределенияДоступности.ДокументОснование) = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов"));	
	МассивПаметровВыбора = Новый Массив;
	
	#Область СтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным =    "Объект";
	ПараметрыВыбора.Статья = "СтатьяДоходов";
	
	ПараметрыВыбора.СкрыватьСтатьюНедоступнуюПоОперации = Ложь;	
	ПараметрыВыбора.ДоступностьПоОперации = Истина;
	Если (ПараметрыОпределенияДоступности.Свойство("ОтразитьНаПрочихДоходах")
			И НЕ ПараметрыОпределенияДоступности.ОтразитьНаПрочихДоходах) Тогда
		ПараметрыВыбора.ДоступностьПоОперации = Ложь;
	КонецЕсли;
	ПараметрыВыбора.ТипСтатьи = "СтатьяДоходовТипСтатьи";

	ПараметрыВыбора.ВыборСтатьиДоходов = Истина;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходов");
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяДоходовКорректировкаЗадолженности");
		
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("АналитикаДоходовКорректировкаЗадолженности");
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаПассивов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаПассивов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаПассивовКорректировкаЗадолженности");
		
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область СтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным =    "Объект";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.СкрыватьСтатьюНедоступнуюПоОперации = Ложь;
	ПараметрыВыбора.ДоступностьПоОперации = Истина;
	
	Если (ПараметрыОпределенияДоступности.Свойство("СписатьНаРасходы")
			И НЕ ПараметрыОпределенияДоступности.СписатьНаРасходы) Тогда
		ПараметрыВыбора.ДоступностьПоОперации = Ложь;
	КонецЕсли;
	ПараметрыВыбора.ТипСтатьи = "СтатьяРасходовТипСтатьи";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовКорректировкаЗадолженности");
	
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовКорректировкаЗадолженности");
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовКорректировкаЗадолженности");
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область ТоварыСтатьяДоходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным =    "Объект.Товары";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("ТоварыСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("ТоварыАналитикаРасходов");
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = КорректировкаПриобретенияУслугПрочихАктивов;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("ТоварыАналитикаАктивовПассивов");
	
	ПараметрыВыбора.ВыборСтатьиДоходов = КорректировкаПриобретенияУслугПрочихАктивов;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("ТоварыАналитикаДоходов");
	
	ПараметрыВыбора.УсловияДоступностиСтатьиВСтроках.Вставить("СписатьНаРасходы", Истина);
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область РасходенияСтатьяРасходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным =    "Объект.Расхождения";
	ПараметрыВыбора.Статья = "СтатьяРасходов";
	
	ПараметрыВыбора.ТипСтатьи = "ТипСтатьи";
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("РасхожденияСтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("РасхожденияАналитикаРасходов");
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = КорректировкаПриобретенияУслугПрочихАктивов;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("РасхожденияАналитикаАктивовПассивов");
	
	ПараметрыВыбора.ВыборСтатьиДоходов = КорректировкаПриобретенияУслугПрочихАктивов;
	ПараметрыВыбора.АналитикаДоходов = "АналитикаДоходов";
	ПараметрыВыбора.ЭлементыФормы.АналитикаДоходов.Добавить("РасхожденияАналитикаДоходов");
	
	ПараметрыВыбора.УсловияДоступностиСтатьиВСтроках.Вставить("СписатьНаРасходы", Истина);
	
	МассивПаметровВыбора.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	КорректировкаПриобретенияЛокализация.ДополнитьПараметрыВыбораСтатейИАналитик(МассивПаметровВыбора, ПараметрыОпределенияДоступности);
	
	Возврат МассивПаметровВыбора;
	
КонецФункции

// Возвращает параметры механизма взаиморасчетов.
//
// Параметры:
// 	ДанныеЗаполнения - ДокументОбъект, СправочникОбъект, ДокументСсылка, СправочникСсылка, Структура, ДанныеФормыСтруктура - Объект или коллекция для
//              расчета параметров взаиморасчетов.
//
// Возвращаемое значение:
// 	Массив из см. ВзаиморасчетыСервер.ПараметрыМеханизма - Массив параметров функций механизма взаиморасчетов
//
Функция ПараметрыВзаиморасчеты(ДанныеЗаполнения = Неопределено) Экспорт
	
	ЭтоКорректировкаЗадолженности = Ложь;
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(ДанныеЗаполнения) Тогда
		СтруктураДанныеЗаполнения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДанныеЗаполнения, 
			"ВидКорректировки");
		ЭтоКорректировкаЗадолженности = 
			СтруктураДанныеЗаполнения.ВидКорректировки = Перечисления.ХозяйственныеОперации.КорректировкаПриобретенияУвеличениеЗадолженностиСводно
			ИЛИ СтруктураДанныеЗаполнения.ВидКорректировки = Перечисления.ХозяйственныеОперации.КорректировкаПриобретенияУменьшениеЗадолженностиСводно;
	ИначеЕсли ДанныеЗаполнения <> Неопределено Тогда
		ЭтоКорректировкаЗадолженности = 
			ДанныеЗаполнения.ВидКорректировки = Перечисления.ХозяйственныеОперации.КорректировкаПриобретенияУвеличениеЗадолженностиСводно
			ИЛИ ДанныеЗаполнения.ВидКорректировки = Перечисления.ХозяйственныеОперации.КорректировкаПриобретенияУменьшениеЗадолженностиСводно;
	КонецЕсли;
	
	СтруктураПараметров = ВзаиморасчетыСервер.ПараметрыМеханизма();
	
	#Область ОбязательныеПараметры
	
	// Если все опции ложь, то это платеж или служебный документ.
	СтруктураПараметров.ЭтоЗаказ                         = Ложь;
	СтруктураПараметров.ЭтоСправочник                    = Ложь;
	СтруктураПараметров.ЭтоПродажаЗакупка                = Истина;
	
	// Определяет какой регистр двигают параметры, какие общие формы, перечисления и справочники использовать.
	СтруктураПараметров.ТипРасчетов                      = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
	
	// Дата отражения документа в системе.
	// Используется для получения остатков просроченной задолженности для функции ограничения задолженности.
	// Используется для получения курсов валют документа. 
	// Используется для заполнения этапов оплаты и расшифровки платежа по умолчанию.
	СтруктураПараметров.Дата                             = "Объект.Дата"; 
	
	// Валюта и сумма операции. Обязательно путь к реквизитам объекта.
	СтруктураПараметров.ВалютаДокумента                  = "Объект.Валюта";
	СтруктураПараметров.СуммаДокумента                   = "";
	
	// Используются для генерации объектов расчетов и аналитики.
	СтруктураПараметров.Организация                      = "Объект.Организация";
	СтруктураПараметров.Партнер                          = "Объект.Партнер";
	СтруктураПараметров.Контрагент                       = "Объект.Контрагент";
	
	// Для поиска и генерации объекта
	Если ЭтоКорректировкаЗадолженности Тогда
		// Генерация объектов не требуется, они уже выбраны в табличной части
		СтруктураПараметров.Ссылка                       = "";
	Иначе
		СтруктураПараметров.Ссылка                       = "Объект.ДокументОснование";
	КонецЕсли;
	
	#КонецОбласти
	
	#Область НеобязательныеПараметры
	
	// Отличные от валюты и суммы документа реквизиты. Если не заполнен, то для чтения будет взята валюта документа.
	СтруктураПараметров.ВалютаВзаиморасчетов             = "Объект.ВалютаВзаиморасчетов";
	СтруктураПараметров.СуммаВзаиморасчетов              = "Объект.СуммаВзаиморасчетов";
	СтруктураПараметров.СуммаВзаиморасчетовПоТаре        = "";
	
	// Используется для генерации аналитики проведения и объекта расчетов, определения графика исполнения договора.
	Если ЭтоКорректировкаЗадолженности Тогда
		СтруктураПараметров.Договор                          = "";
	Иначе
		СтруктураПараметров.Договор                          = "Объект.Договор";
	КонецЕсли;
	// Используется для генерации аналитики проведения и объекта расчетов.
	Если ЭтоКорректировкаЗадолженности Тогда
		СтруктураПараметров.НаправлениеДеятельности          = "";
	Иначе
		СтруктураПараметров.НаправлениеДеятельности          = "Объект.НаправлениеДеятельности";
	КонецЕсли;
	// Порядок расчетов документа.
	Если ЭтоКорректировкаЗадолженности Тогда
		СтруктураПараметров.ПорядокРасчетов              = "";
	Иначе
		СтруктураПараметров.ПорядокРасчетов              = "Объект.ПорядокРасчетов";
	КонецЕсли;
	
	// Сумма всего с залоговой тарой. Путь к итоговому показателю формы (реквизиту).
	// Используется для отображения гиперссылок, редактирования и заполнения графика плановых оплат
	// редактирования расшифровки платежа и т.д.
	СтруктураПараметров.СуммаДокументаФорма              = "";
	
	// Реквизит формы итоговых показателей, содержащий сумму залога за тару в валюте документа.
	// Используется в заполнении этапов графика оплаты.
	СтруктураПараметров.СуммаЗалогаЗаТаруФорма            = ""; 

	// Табличная часть, по которой распределяется сумма взаиморасчетов и есть поле "заказ".
	// Используется для отображения гиперссылок, редактирования и заполнения графика плановых оплат
	// редактирования расшифровки платежа, если не заполнен параметр СуммаДокументаФорма.
	// В ТЧ заполняются объекты расчетов по заказам.
	Если ЭтоКорректировкаЗадолженности Тогда
		СтруктураПараметров.ПутьКДаннымТЧ                    = "Объект.КорректировкаЗадолженности";
		// Имя реквизита суммы с ндс табличной части.
		СтруктураПараметров.ИмяРеквизитаТЧСуммаСНДС          = "СуммаКорректировки";
		// Имя реквизита тч, содержащего заказ.
		СтруктураПараметров.ИмяРеквизитаТЧЗаказ              = "";
	Иначе
		СтруктураПараметров.ПутьКДаннымТЧ                    = "Объект.Расхождения";
		// Имя реквизита суммы с ндс табличной части.
		СтруктураПараметров.ИмяРеквизитаТЧСуммаСНДС          = "СуммаСНДС";
		// Имя реквизита тч, содержащего заказ.
		СтруктураПараметров.ИмяРеквизитаТЧЗаказ              = "ЗаказПоставщику";
	КонецЕсли;
	
	// Путь к табличной части Расшифровка платежа.
	// Используется в функция зачета оплат, распределения взаиморасчетов, распределения оплаты.
	СтруктураПараметров.ПутьКДаннымТЧРасшифровкаПлатежа  = "";
	
	// Используется для заполнения значений по умолчанию, заполнения графика плановых оплат и даты платежа.
	Если ЭтоКорректировкаЗадолженности Тогда
		СтруктураПараметров.Соглашение                   = "";
	Иначе
		СтруктураПараметров.Соглашение                   = "Объект.Соглашение";
	КонецЕсли;
	// Используются для определения значения ОплатаВВалюте и в форме редактирования правил оплаты.
	СтруктураПараметров.БанковскийСчетОрганизации        = "";
	СтруктураПараметров.БанковскийСчетКонтрагента        = "";
	СтруктураПараметров.Касса                            = "";
	СтруктураПараметров.ФормаОплаты                      = "Объект.ФормаОплаты";
	СтруктураПараметров.ОплатаВВалюте                    = "";
	// Используется в форме правил оплаты и для подбора в расшифровку платежа объектов расчетов.
	СтруктураПараметров.ИдентификаторПлатежа             = "";
	
	// Реквизиты для объекта расчетов, используются в проведении.
	Если ЭтоКорректировкаЗадолженности Тогда
		// В этом случае нет необходимости генерировать объект расчетов, он выбирается в табличной части документа из ранее созданных.
		СтруктураПараметров.ГруппаФинансовогоУчета           = "";
		СтруктураПараметров.ОбъектРасчетов                   = "";
		СтруктураПараметров.Подразделение                    = "";
		СтруктураПараметров.Менеджер                         = "";
		СтруктураПараметров.НомерВходящегоДокумента          = "";
		СтруктураПараметров.ДатаВходящегоДокумента           = "";
		СтруктураПараметров.НаименованиеПервичногоДокумента  = "";
	Иначе
		СтруктураПараметров.ГруппаФинансовогоУчета           = "";
		СтруктураПараметров.ОбъектРасчетов                   = "Объект.Расхождения.ОбъектРасчетов";
		СтруктураПараметров.Подразделение                    = "Объект.Подразделение";
		СтруктураПараметров.Менеджер                         = "Объект.Менеджер";
		СтруктураПараметров.НомерВходящегоДокумента          = "Объект.НомерВходящегоДокумента";
		СтруктураПараметров.ДатаВходящегоДокумента           = "Объект.ДатаВходящегоДокумента";
		СтруктураПараметров.НаименованиеПервичногоДокумента  = "Объект.НаименованиеВходящегоДокумента";
	КонецЕсли;
	
	// Используется для определения объекта расчетов.
	Если ЭтоКорректировкаЗадолженности Тогда
		СтруктураПараметров.НакладнаяПоЗаказам               = "";
	Иначе
		СтруктураПараметров.НакладнаяПоЗаказам               = "Объект.ПоступлениеПоЗаказам";
	КонецЕсли;
	
	// Используется при определении порядка расчетов по умолчанию и списка доступных порядков для выбора.
	// Используется для определения объекта расчетов.
	СтруктураПараметров.ЗаказОснование                   = ""; 
	
	// Имя кнопки, открывающей помощник зачета оплат для текущего набора параметров.
	СтруктураПараметров.ЭлементыФормы.ЗачетОплаты                      = "";
	// Имя элемента таблицы формы, отображающей сумму взаиморасчетов.
	// Используется для установки условного оформления.
	// Следует заполнить если документ поддерживает построчное ручное редактирование сумм взаиморасчетов.
	Если ЭтоКорректировкаЗадолженности Тогда
		СтруктураПараметров.ЭлементыФормы.СуммаВзаиморасчетовТЧ      = "КорректировкаЗадолженностиСуммаВзаиморасчетов";
	Иначе
		СтруктураПараметров.ЭлементыФормы.СуммаВзаиморасчетовТЧ      = "РасхожденияСуммаВзаиморасчетов";
	КонецЕсли;
	
	#КонецОбласти
	
	#Область РедактированиеВалютИВалютныхСуммДокумента
	
	// Курс и кратность документа - одна пара для документа.
	// Используются для расчета суммы взаиморасчетов и в форме редактирования валют и курсов документа.
	СтруктураПараметров.КурсЧислитель                          = "Объект.КурсЧислитель";
	СтруктураПараметров.КурсЗнаменатель                        = "Объект.КурсЗнаменатель";
	
	// Имя гиперссылки, отображающей текущий курс взаиморасчетов документа и открывающей соответствующую форму.
	СтруктураПараметров.ЭлементыФормы.НадписьВалюты                    = "ДекорацияВалюты";
	
	// Используются в условии через ИЛИ с правом отклонения от условий продаж/закупок.
	Если ЭтоКорректировкаЗадолженности Тогда
		СтруктураПараметров.ВалютаДокументаТолькоПросмотр                  = Ложь;
		СтруктураПараметров.ВалютаВзаиморасчетовТолькоПросмотр             = Ложь;
	Иначе
		СтруктураПараметров.ВалютаДокументаТолькоПросмотр                  = Истина;
		СтруктураПараметров.ВалютаВзаиморасчетовТолькоПросмотр             = Истина;
	КонецЕсли;
	
	// Если не следует показывать сумму и валюту взаиморасчетов исходя из данных документа.
	СтруктураПараметров.НеПоказыватьРасчеты                            = Ложь;
	// Если не требуется предлагать пересчет суммы документа.
	СтруктураПараметров.НеПересчитыватьСуммуДокумента                  = Ложь;
	
	#КонецОбласти
	
	#Область ГрафикПлановойОплатыИДатаПлатежа
	
	// Функция Этапы оплаты и дата платежа
	СтруктураПараметров.ПутьКДаннымТЧЭтапыОплаты         = "";
	СтруктураПараметров.ДатаПлатежа                      = "Объект.ДатаПлатежа";
	СтруктураПараметров.НадписьЭтапыОплаты               = "Форма.НадписьЭтапыОплаты";
	СтруктураПараметров.ИсточникСуммТабличнаяЧасть       = Истина;
	СтруктураПараметров.ГрафикОплаты                     = "";
	
	// Имя гиперссылки, отображающей текущие правила оплаты документа и открывающей форму Правила оплаты.
	СтруктураПараметров.ЭлементыФормы.НадписьЭтапы             = "ДекорацияЭтапыОплаты";
	
	// Если требуется ограничить редактирование плавил оплаты.
	СтруктураПараметров.ЭтапыОплатыТолькоПросмотр                      = Ложь;
	// Если истина и накладная по заказам то будет отображена расширенная форма графика оплаты, несмотря на функциональную опцию "Упрощенная форма оплаты".
	СтруктураПараметров.ВозможнаНакладнаяПоНесколькимЗаказам           = Ложь;
	#КонецОбласти
	
	#Область СостояниеВзаиморасчетов
	
	// Имя гиперссылки, отображающей состояние расчетов и открывающей соответствующий отчет,
	СтруктураПараметров.ЭлементыФормы.НадписьРасчеты                   = "";
	
	#КонецОбласти
	
	#Область ОграниченияЗадолженностиПоДоговору
	
	// Гиперссылка отображающая состояние ограничения задолженности
	СтруктураПараметров.ЭлементыФормы.ОграничениеЗадолженностиТекст    = "";
	// Картинка отображающая запрет отгрузки
	СтруктураПараметров.ЭлементыФормы.ОграничениеЗадолженностиКартинка = ""; 
	
	#КонецОбласти
	
	Возврат СтруктураПараметров;
КонецФункции

// Возвращает структуру параметров для заполнения налогообложения НДС закупки.
//
// Параметры:
//  Объект - ДокументОбъект.КорректировкаПриобретения,
//  		 ДокументСсылка.КорректировкаПриобретения,
//  		 ДанныеФормыСтруктура - документ, по которому необходимо сформировать параметры.
//
// Возвращаемое значение:
//  Структура - Параметры заполнения, описание параметров см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСЗакупки();
//
Функция ПараметрыЗаполненияНалогообложенияНДСЗакупки(Объект) Экспорт

	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияНалогообложенияНДСЗакупки();
	
	Если ТипЗнч(Объект) = Тип("ДокументОбъект.КорректировкаПриобретения")
		Или ТипЗнч(Объект) = Тип("ДанныеФормыСтруктура") Тогда
		ДанныеОбъекта = Объект;
	ИначеЕсли ТипЗнч(Объект) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		ДанныеОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект, "Контрагент,Договор,ХозяйственнаяОперация");
	КонецЕсли;
	
	ПараметрыЗаполнения.Контрагент = ДанныеОбъекта.Контрагент;
	ПараметрыЗаполнения.Договор = ДанныеОбъекта.Договор;
	
		ПараметрыЗаполнения.ПриобретениеТоваров = Истина;
		ПараметрыЗаполнения.ПриобретениеРабот   = Истина;
	
	ПараметрыЗаполнения.ПриобретениеНаСтатьи = Истина;
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

// Возвращает структуру параметров для заполнения вида деятельности НДС.
//
// Параметры:
//  Объект  - ДокументОбъект.КорректировкаПриобретения,
//  		 ДокументСсылка.КорректировкаПриобретения,
//  		 ДанныеФормыСтруктура - документ, по которому необходимо сформировать параметры.
//
// Возвращаемое значение:
//  Структура - Параметры заполнения, описание параметров см. УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС();
//
Функция ПараметрыЗаполненияВидаДеятельностиНДС(Объект) Экспорт

	ПараметрыЗаполнения = УчетНДСУПКлиентСервер.ПараметрыЗаполненияВидаДеятельностиНДС();
	ПараметрыЗаполнения.Организация = Объект.Организация;
	ПараметрыЗаполнения.Дата = Объект.Дата;
	ПараметрыЗаполнения.ПриобретениеНаСтатьи = Истина;
	
	Возврат ПараметрыЗаполнения;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Партнер)
	|	И ЗначениеРазрешено(Подразделение)
	|	И(ТипЗначения(ДокументОснование) = Тип(Документ.ПриобретениеУслугПрочихАктивов)
	|	ИЛИ ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОтчетаПереработчика)
	|	ИЛИ ЗначениеРазрешено(Склад)
	|	) ";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область Печать

// Возвращает правила печати печатной формы Задания на отбор (размещение) товаров.
//
// Возвращаемое значение:
//	Структура - состав свойств см. Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ДобавитьКомандуПечати.
//
Функция ПравилаПечатиЗаданияНаОтборРазмещение() Экспорт
	
	ПравилаПечатиЗадания = Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ПравилаПечатиЗаданияНаОтборРазмещение();
	ПравилаПечатиЗадания.ОперацияПоступления	= Истина;
	ПравилаПечатиЗадания.КорректировкаТоваров	= Истина;
	ПравилаПечатиЗадания.СкладыВТЧ				= Истина;
	ПравилаПечатиЗадания.ИмяТЧТовары			= "Расхождения";
	
	Возврат ПравилаПечатиЗадания;
	
КонецФункции

#КонецОбласти

// Возвращает структуру с наименованием табличных частей документа, хранящих информацию о товарах.
//
// Возвращаемое значение:
//	Структура - коллекция, содержащая следующие наименования табличных частей:
//		* Товары - ТаблицаЗначений, ТабличнаяЧасть, Неопределено - данные о товарах документа.
//		* Расхождения - ТаблицаЗначений, ТабличнаяЧасть, Неопределено - данные о расхождениях приобретения документа.
//	
Функция КоллекцияТабличныхЧастейТоваров() Экспорт
	
	ТаблицыДокумента = Новый Структура;
	ТаблицыДокумента.Вставить("Товары");
	ТаблицыДокумента.Вставить("Расхождения");
	
	Возврат ТаблицыДокумента;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		ПостфиксИмениДокумента = НСтр("ru = 'Корректировка приобретения'");
	
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 %2 от %3'"),
		ПостфиксИмениДокумента,
		Данные.Номер,
		Данные.Дата);
	
	ОбщегоНазначенияУТКлиентСервер.ОбработкаПолученияПредставленияВходящегоДокумента(
		Данные, Представление, СтандартнаяОбработка, "КорректировкаПриобретения");
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Очистить();
	Поля.Добавить("ХозяйственнаяОперация");
	Поля.Добавить("НаименованиеВходящегоДокумента");
	Поля.Добавить("НомерВходящегоДокумента");
	Поля.Добавить("ДатаВходящегоДокумента");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ОперацииРаздельнойЗакупки", ЗакупкиСервер.ХозяйственныеОперацииРаздельнойЗакупкиБезОтборов());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                                                              КАК Ссылка,
	|	ДанныеДокумента.Дата                                                                КАК Период,
	|	ДанныеДокумента.ДатаВходящегоДокумента                                              КАК ДатаВходящегоДокумента,
	|	ДанныеДокумента.Номер                                                               КАК Номер,
	|	ДанныеДокумента.НомерВходящегоДокумента                                             КАК НомерВходящегоДокумента,
	|	ДанныеДокумента.НаименованиеВходящегоДокумента                                      КАК НаименованиеВходящегоДокумента,
	|	ДанныеДокумента.ДокументОснование                                                   КАК ДокументОснование,
	|	ЕСТЬNULL(ДанныеДокумента.ДокументОснование.Дата, ДАТАВРЕМЯ(1,1,1))                  КАК ПериодПоступления,
	|	ДанныеДокумента.ЗакупкаПодДеятельность                                              КАК ЗакупкаПодДеятельность,
	|	ДанныеДокумента.Организация                                                         КАК Организация,
	|	ДанныеДокумента.НалогообложениеНДС                                                  КАК НалогообложениеНДС,
	|	ДанныеДокумента.Партнер                                                             КАК Партнер,
	|	ДанныеДокумента.Контрагент                                                          КАК Контрагент,
	|	ДанныеДокумента.Подразделение                                                       КАК Подразделение,
	|	ЕСТЬNULL(ДанныеДокумента.Подразделение.ВариантОбособленногоУчетаТоваров, ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПустаяСсылка)) КАК ВариантОбособленногоУчетаТоваров,
	|	ДанныеДокумента.Сделка                                                              КАК Сделка,
	|	ЕСТЬNULL(ДанныеДокумента.Сделка.ОбособленныйУчетТоваровПоСделке, ЛОЖЬ)              КАК ОбособленныйУчетТоваровПоСделке,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|		ТОГДА ДанныеДокумента.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ЕСТЬNULL(ДанныеДокумента.СтатьяРасходов.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) КАК ВариантРаспределенияРасходовРегл,
	|	ЕСТЬNULL(ДанныеДокумента.СтатьяРасходов.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО)   КАК ВариантРаздельногоУчетаНДС,
	|	ДанныеДокумента.АналитикаРасходов                                                   КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|		ТОГДА ДанныеДокумента.СтатьяДоходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяДоходов,
	|	ДанныеДокумента.АналитикаДоходов                                                    КАК АналитикаДоходов,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|		ТОГДА ДанныеДокумента.СтатьяРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяАктивов,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|		ТОГДА ДанныеДокумента.СтатьяДоходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяПассивов,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|		ТОГДА ДанныеДокумента.СтатьяРасходов.АктивПассив
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяАктивовАктивПассив,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.СтатьяДоходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|		ТОГДА ДанныеДокумента.СтатьяДоходов.АктивПассив
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяПассивовАктивПассив,
	|	ДанныеДокумента.АналитикаАктивов                                                    КАК АналитикаАктивов,
	|	ДанныеДокумента.АналитикаПассивов                                                   КАК АналитикаПассивов,
	|	ДанныеДокумента.ВалютаВзаиморасчетов                                                КАК ВалютаВзаиморасчетов,
	|	ДанныеДокумента.Валюта                                                              КАК Валюта,
	|	ДанныеДокумента.ПоступлениеПоЗаказам                                                КАК ПоступлениеПоЗаказам,
	|	ДанныеДокумента.ДатаПлатежа                                                         КАК ДатаПлатежа,
	|	ДанныеДокумента.ФормаОплаты                                                         КАК ФормаОплаты,
	|	ДанныеДокумента.Договор                                                             КАК Договор,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|			ТОГДА ВЫБОР
	|				КОГДА ЕСТЬNULL(ДанныеДокумента.ДокументОснование.ХозяйственнаяОперация, НЕОПРЕДЕЛЕНО) В
	|						(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
	|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
	|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути),
	|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки),
	|						ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути))
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                                                               КАК ИспользуетсяДокументПоступлениеТоваров,
	|	ДанныеДокумента.Соглашение                                                          КАК Соглашение,
	|	ЕСТЬNULL(ДанныеДокумента.Соглашение.ВариантПриемкиТоваров, ЗНАЧЕНИЕ(Перечисление.ВариантыПриемкиТоваров.ПустаяСсылка)) КАК ВариантПриемкиТоваров,
	|	ДанныеДокумента.ВидКорректировки                                                    КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОтчетаПереработчика)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОтчетаПереработчика)
	|		ИНАЧЕ ЕСТЬNULL(ДанныеДокумента.ДокументОснование.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика))
	|	КОНЕЦ                                                                               КАК ХозОперацияОснования,
	|	ВЫБОР
	|		КОГДА
	|			ДанныеДокумента.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|		ТОГДА
	|			ДанныеДокумента.ДокументОснование.ВернутьМногооборотнуюТару
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ВернутьМногооборотнуюТару,
	|	ВЫБОР
	|		КОГДА
	|			ДанныеДокумента.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|		ТОГДА
	|			ДанныеДокумента.ДокументОснование.ТребуетсяЗалогЗаТару
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяЗалогЗаТару,
	|	
	|	ДанныеДокумента.Менеджер КАК Менеджер,
	|	ЕСТЬNULL(ДанныеДокумента.ДокументОснование.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|	ЕСТЬNULL(ДанныеДокумента.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК ГруппаФинансовогоУчетаДоговора,
	|	ДанныеДокумента.ДокументОснование.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ДанныеДокумента.ДокументОснование.НаправлениеДеятельности.УчетЗатрат, ЛОЖЬ) КАК УчетЗатратПоНД,
	|	ДанныеДокумента.Склад                  КАК Склад,
	|	ДанныеДокумента.СуммаДокумента         КАК СуммаДокумента,
	|	ДанныеДокумента.Комментарий            КАК Комментарий,
	|	ДанныеДокумента.ПометкаУдаления        КАК ПометкаУдаления,
	|	ДанныеДокумента.Проведен               КАК Проведен,
	|	ДанныеДокумента.Автор                  КАК Автор,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДокументОснованиеЯвляетсяПриобретением,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.ДокументОснование.ХозяйственнаяОперация В (&ОперацииРаздельнойЗакупки)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                        КАК РаздельнаяЗакупка,
	|	НастройкиХозяйственныхОпераций.Ссылка        КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|		ПО ДанныеДокумента.ВидКорректировки = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|";
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихДоходовРасходов", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов")); 
	Запрос.УстановитьПараметр("ИспользоватьУчетПрочихАктивовПассивов", ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов"));
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "Организация")));
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета", Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	Запрос.УстановитьПараметр("ИдентификаторМетаданных",          ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ.КорректировкаПриобретения"));
	Запрос.УстановитьПараметр("ХозОперацииЗакупкаПоИмпорту",      ЗакупкиСервер.ХозяйственныеОперацииПоОсновной(
																	Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту));
	Запрос.УстановитьПараметр("ТипыНалогообложенияНДСУчитываетсяВСтоимости",
		УчетНДСУП.ВидыДеятельностиНДСУчитываетсяВСтоимости());
	Запрос.УстановитьПараметр("НалогообложениеОрганизации",
		УчетНДСУП.ПараметрыУчетаПоОрганизации(Реквизиты.Организация, Реквизиты.Период).ОсновноеНалогообложениеНДСПродажи);
	
	Если ЗначениеЗаполнено(Реквизиты.Договор) Тогда
		ИнформацияПоДоговору = НСтр("ru='По договору ""%Договор%""'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ИнформацияПоДоговору = СтрЗаменить(ИнформацияПоДоговору, "%Договор%", Реквизиты.Договор);
	КонецЕсли;
	Запрос.УстановитьПараметр("ИнформацияПоДоговору", ИнформацияПоДоговору);
	
	Запрос.УстановитьПараметр("ХозОперацииПоставкаПодПринципала",       ЗакупкиСервер.ХозяйственныеОперацииДляАгентскойЗакупкиПодПринципала());
	Запрос.УстановитьПараметр("ДокументОснованиеЯвляетсяПриобретением", Реквизиты.ДокументОснованиеЯвляетсяПриобретением);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);
	
КонецПроцедуры

Процедура ОформитьИзлишкиНедостачиНаСкладе(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаДокумента =
	"ВЫБРАТЬ
	|	ТаблицаРасхождения.Ссылка              КАК Ссылка,
	|	ТаблицаРасхождения.Ссылка.Дата         КАК Дата,
	|	ТаблицаРасхождения.Склад               КАК Склад,
	|	ТаблицаРасхождения.Номенклатура        КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика      КАК Характеристика,
	|	ТаблицаРасхождения.Назначение          КАК Назначение,
	|	ТаблицаРасхождения.Серия               КАК Серия,
	|	ТаблицаРасхождения.Количество          КАК Количество,
	|	ТаблицаРасхождения.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка В(&Ссылка)
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаРасхождения.ВариантОтражения В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУчестьПриИнвентаризации),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьТоварыУПартнеров),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьТоварыУПартнеров),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки)
	|	)";
	
	РегистрыТоварыКОформлениюИзлишковНедостач = Новый Структура();
	Если ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ТоварыКОформлениюИзлишковНедостач", Регистры) Тогда
		РегистрыТоварыКОформлениюИзлишковНедостач.Вставить("ТоварыКОформлениюИзлишковНедостач");
	Иначе
		РегистрыТоварыКОформлениюИзлишковНедостач.Вставить("ДвиженияНеТребуются");
	КонецЕсли;
	
	СкладыСервер.ОформитьИзлишкиНедостачиНаСкладе(Запрос,
		ТекстыЗапроса,
		РегистрыТоварыКОформлениюИзлишковНедостач,
		ТекстЗапросаДокумента);
	
	ТекстЗапросаДокумента =
	"ВЫБРАТЬ
	|	ТаблицаРасхождения.Ссылка              КАК Ссылка,
	|	ТаблицаРасхождения.Ссылка.Дата         КАК Дата,
	|	ТаблицаРасхождения.Склад               КАК Склад,
	|	ТаблицаРасхождения.Номенклатура        КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика      КАК Характеристика,
	|	ТаблицаРасхождения.Назначение          КАК Назначение,
	|	ТаблицаРасхождения.Серия               КАК Серия,
	|	ТаблицаРасхождения.Количество          КАК Количество,
	|	ТаблицаРасхождения.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка В(&Ссылка)
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаРасхождения.ВариантОтражения В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки))
	|	";
	
	РегистрыТоварыНаСкладах = Новый Структура();
	Если ПроведениеДокументов.ТребуетсяТаблицаДляДвижений("ТоварыНаСкладах", Регистры) Тогда
		РегистрыТоварыНаСкладах.Вставить("ТоварыНаСкладах");
	Иначе
		РегистрыТоварыНаСкладах.Вставить("ДвиженияНеТребуются");
	КонецЕсли;
	
	СкладыСервер.ОформитьИзлишкиНедостачиНаСкладе(Запрос,
		ТекстыЗапроса,
		РегистрыТоварыНаСкладах,
		ТекстЗапросаДокумента);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитик = Новый Запрос("
	|ВЫБРАТЬ
	|	Ключи.Номенклатура         КАК Номенклатура,
	|	Ключи.Характеристика       КАК Характеристика,
	|	Ключи.Назначение           КАК Назначение,
	|	Ключи.Серия                КАК Серия,
	|	Ключи.СтатьяКалькуляции    КАК СтатьяКалькуляции,
	|	Ключи.Склад                КАК Склад
	|ИЗ
	// Ключи расхождений без назначений
	|	(ВЫБРАТЬ
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура         КАК Номенклатура,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Характеристика       КАК Характеристика,
	|		&ПустоеНазначение          КАК Назначение,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Серия                КАК Серия,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.МестоХранения        КАК Склад,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.СтатьяКалькуляции    КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|	ГДЕ
	|		ТаблицаРасхождения.Ссылка = &Ссылка
	|		И НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|		И НЕ ТаблицаРасхождения.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеУслугПрочихАктивов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// Ключи у партнеров
	|	ВЫБРАТЬ
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура         КАК Номенклатура,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Характеристика       КАК Характеристика,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Назначение           КАК Назначение,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Серия                КАК Серия,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.МестоХранения        КАК Склад,
	|		ВЫБОР
	|			КОГДА ИСТИНА
	|				ТОГДА ТаблицаРасхождения.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
	|		КОНЕЦ                                                              КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.КорректировкаПриобретения.ВидыЗапасов КАК ТаблицаРасхождения
	|	ГДЕ
	|		ТаблицаРасхождения.Ссылка = &Ссылка
	|		И &УчитыватьСебестоимостьТоваровПоНазначениям
	|		И НЕ ТаблицаРасхождения.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеУслугПрочихАктивов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// Ключи у партнеров без назначений
	|	ВЫБРАТЬ
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура         КАК Номенклатура,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Характеристика       КАК Характеристика,
	|		&ПустоеНазначение          КАК Назначение,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Серия                КАК Серия,
	|		ТаблицаРасхождения.АналитикаУчетаНоменклатуры.МестоХранения        КАК Склад,
	|		ВЫБОР
	|			КОГДА ИСТИНА
	|				ТОГДА ТаблицаРасхождения.АналитикаУчетаНоменклатуры.СтатьяКалькуляции
	|		КОНЕЦ                                                              КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.КорректировкаПриобретения.ВидыЗапасов КАК ТаблицаРасхождения
	|	ГДЕ
	|		ТаблицаРасхождения.Ссылка = &Ссылка
	|		И НЕ &УчитыватьСебестоимостьТоваровПоНазначениям
	|		И НЕ ТаблицаРасхождения.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеУслугПрочихАктивов
	|
	|) КАК Ключи
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО Ключи.Номенклатура                  = Аналитика.Номенклатура
	|		И Ключи.Характеристика             = Аналитика.Характеристика
	|		И Ключи.СтатьяКалькуляции          = Аналитика.СтатьяКалькуляции
	|		И Ключи.Серия                      = Аналитика.Серия
	|		И Ключи.Склад                      = Аналитика.МестоХранения
	|		И Ключи.Назначение                 = Аналитика.Назначение
	|ГДЕ
	|	Аналитика.КлючАналитики ЕСТЬ NULL
	|	И НЕ Ключи.Номенклатура ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	Ключи.Номенклатура,
	|	Ключи.Характеристика,
	|	Ключи.Назначение,
	|	Ключи.СтатьяКалькуляции,
	|	Ключи.Серия,
	|	Ключи.Склад
	|");
	
	ЗапросАналитик.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросАналитик.УстановитьПараметр("Партнер", Запрос.Параметры.Партнер);
	ЗапросАналитик.УстановитьПараметр("ПустоеНазначение", Справочники.Назначения.ПустаяСсылка());
	ЗапросАналитик.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоНазначениям", Запрос.Параметры.УчитыватьСебестоимостьТоваровПоНазначениям);
	
	Выборка = ЗапросАналитик.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаНоменклатурыИнициализированы", Истина);
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУПР")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта,
		Запрос.Параметры.ВалютаВзаиморасчетов,
		Запрос.Параметры.ПериодПоступления,
		Запрос.Параметры.Организация);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУПР",           Коэффициенты.КоэффициентПересчетаВВалютуУПР);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл",          Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
КонецПроцедуры

Процедура УстановитьПараметрЗапросаДокументПоступленияНДС(Запрос) Экспорт
	
	Если Запрос.Параметры.Свойство("ДокументПоступленияНДС") Тогда
		Возврат;
	КонецЕсли;
	
	Если Запрос.Параметры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИсправлениеОшибок Тогда
	    ЗапросПараметра = Новый Запрос;
		ЗапросПараметра.Текст = 
		"ВЫБРАТЬ
		|	КорректировкаПриобретения.Ссылка КАК ДокументПоступленияНДС
		|ИЗ
		|	Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
		|ГДЕ
		|	КорректировкаПриобретения.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон)
		|	И КорректировкаПриобретения.Дата < &Дата
		|	И КорректировкаПриобретения.ДокументОснование = &ДокументОснование
		|	И КорректировкаПриобретения.Проведен
		|УПОРЯДОЧИТЬ ПО
		|	КорректировкаПриобретения.Дата УБЫВ
		|";
		ЗапросПараметра.УстановитьПараметр("Дата",              Запрос.Параметры.Период);
		ЗапросПараметра.УстановитьПараметр("ДокументОснование", Запрос.Параметры.ДокументОснование);
		
		Выборка = ЗапросПараметра.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ДокументПоступленияНДС = Выборка.ДокументПоступленияНДС;
		Иначе
			ДокументПоступленияНДС = Запрос.Параметры.ДокументОснование;
		КонецЕсли;
	Иначе
		ДокументПоступленияНДС = Запрос.Параметры.Ссылка;
	КонецЕсли; 
	
	Запрос.УстановитьПараметр("ДокументПоступленияНДС", ДокументПоступленияНДС);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса)
	
	// Создадим временную таблицу "ВтТаблицаАналитикУчетаПартий"
	
	// Определение НалогообложениеНДС и ВидЦенности аналогично ЗакупкаНДС()
	ТекстНомераСтрок = РасчетСебестоимостиПроведениеДокументов.ТекстЗапросаКодыСтрокДляАналитикиУчетаПартий(
		Метаданные.Документы.КорректировкаПриобретения.Имя,
		"Расхождения, ВидыЗапасов");
	
	ТекстВыборкаПоляАналитик = ТекстНомераСтрок + "
	|ВЫБРАТЬ
	|	""Расхождения"" 						КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки 			КАК НомерСтроки,
	|	ДанныеДокумента.Партнер					КАК Поставщик,
	|	ДанныеДокумента.Контрагент				КАК Контрагент,
	|	ТаблицаДокумента.СтавкаНДС 				КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Статьи.ВидЦенностиНДС, НЕОПРЕДЕЛЕНО) В
	|		  (ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС), ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА))
	|		  И ЕСТЬNULL(Статьи.ВариантРаспределенияРасходовРегл, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|		  И ЕСТЬNULL(Статьи.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию)
	|		КОГДА ЕСТЬNULL(Статьи.ВариантРаздельногоУчетаНДС, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение) 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ИНАЧЕ ВЫБОР КОГДА ЕСТЬNULL(ТаблицаДокумента.Назначение.ВидДеятельностиНДС,ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		 И НЕ ТаблицаДокумента.СписатьНаРасходы
	|			ТОГДА ТаблицаДокумента.Назначение.ВидДеятельностиНДС
	|			ИНАЧЕ ДанныеДокумента.ДокументОснование.ЗакупкаПодДеятельность
	|		КОНЕЦ
	|	КОНЕЦ                               	КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|		КОГДА ЕСТЬNULL(СпрНоменклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЕСТЬNULL(ГФУ.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары))
	|		КОГДА ЕСТЬNULL(СпрНоменклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		  И НЕ ТаблицаДокумента.СписатьНаРасходы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОГДА ЕСТЬNULL(Статьи.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)) <>
	|		  ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|			ТОГДА Статьи.ВидЦенностиНДС
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ                               	КАК ВидЦенности,
	|	(ВЫБОР
	|		КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОтчетаПереработчика)
	|			ТОГДА 0
	|		ИНАЧЕ КодыСтрок.КодСтроки КОНЕЦ)	КАК КодСтроки
	|ПОМЕСТИТЬ ВТПоляАналитикУчетаПартий
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК ДанныеДокумента
	|		ПО ДанныеДокумента.Ссылка = ТаблицаДокумента.Ссылка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКодыСтрокДляАналитикиУчетаПартий КАК КодыСтрок
	|		ПО ТаблицаДокумента.НомерСтроки = КодыСтрок.НомерСтроки
	|		И КодыСтрок.ИмяТабличнойЧасти = ""Расхождения""
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = ТаблицаДокумента.Номенклатура
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|		ПО ТаблицаДокумента.СтатьяРасходов = Статьи.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГФУ
	|		ПО СпрНоменклатура.ГруппаФинансовогоУчета = ГФУ.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ВидыЗапасов"" 						КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки 			КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.Партнер					КАК Поставщик,
	|	ТаблицаДокумента.Ссылка.Контрагент				КАК Контрагент,
	|	ТаблицаДокумента.СтавкаНДС 				КАК СтавкаНДС,
	|	ТаблицаДокумента.Ссылка.ДокументОснование.ЗакупкаПодДеятельность КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДСУПокупателя)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТоварыНалоговыйАгент)
	|		КОГДА ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЕСТЬNULL(ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ                               	КАК ВидЦенности,
	|	КодыСтрок.КодСтроки						КАК КодСтроки
	|ИЗ
	|	Документ.КорректировкаПриобретения.ВидыЗапасов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКодыСтрокДляАналитикиУчетаПартий КАК КодыСтрок
	|		ПО ТаблицаДокумента.НомерСтроки = КодыСтрок.НомерСтроки
	|		И КодыСтрок.ИмяТабличнойЧасти = ""ВидыЗапасов""
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""КорректировкаЗадолженности"" КАК ИмяТабличнойЧасти,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.Партнер КАК Поставщик,
	|	ТаблицаДокумента.Ссылка.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.Ссылка.ЗакупкаПодДеятельность КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги) КАК ВидЦенности,
	|	0 КАК КодСтроки
	|ИЗ
	|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
	ТекстЗапроса = Справочники.КлючиАналитикиУчетаПартий.ТекстЗапросаВтТаблицаАналитикУчетаПартий(ТекстВыборкаПоляАналитик, Запрос, ТекстыЗапроса);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтТаблицаРасхожденияТоваров";
	
	СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса);
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаАналитикУчетаПартий", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.Ссылка КАК Ссылка,
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасхождения.Номенклатура КАК Номенклатура,
	|	ТаблицаРасхождения.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.Назначение КАК Назначение,
	|	ТаблицаРасхождения.Серия КАК Серия,
	|	ТаблицаРасхождения.Склад КАК Склад,
	|	ТаблицаРасхождения.НомерГТД КАК НомерГТД,
	|	ТаблицаРасхождения.Склад.ЦеховаяКладовая КАК ЦеховаяКладовая,
	|	ТаблицаРасхождения.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаРасхождения.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ТаблицаРасхождения.КодСтроки КАК КодСтроки,
	|	ТаблицаРасхождения.ВариантОтражения КАК ВариантОтражения,
	|	ТаблицаРасхождения.Подразделение КАК Подразделение,
	|	ТаблицаРасхождения.НомерГруппыЗатрат КАК НомерГруппыЗатрат,
	|	ТаблицаРасхождения.СписатьНаРасходы КАК СписатьНаРасходы,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ТаблицаРасхождения.СтатьяРасходов 
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА ТаблицаРасхождения.СтатьяРасходов 
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяАктивовПассивов,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|			ТОГДА ТаблицаРасхождения.СтатьяРасходов 
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяДоходов,
	|	ТаблицаРасхождения.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаРасхождения.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ТаблицаРасхождения.АналитикаДоходов КАК АналитикаДоходов,
	|	ТаблицаРасхождения.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаАналитикУчетаПартий.ВидЦенности			КАК ВидЦенности,
	|	ТаблицаАналитикУчетаПартий.НалогообложениеНДС	КАК ВидДеятельностиНДС,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаРасхождения.КоличествоПоРНПТ
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|	ТаблицаРасхождения.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаРасхождения.СуммаНДС КАК СуммаНДС,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаРасхождения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|		 ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ИЛИ (ТаблицаРасхождения.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|				И ЕСТЬNULL(ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
	|					ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА НЕ ТаблицаРасхождения.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|						И ТаблицаРасхождения.НаправлениеДеятельности.УчетЗатрат ТОГДА
	|					ТаблицаРасхождения.НаправлениеДеятельности
	|					КОГДА ТаблицаРасхождения.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|						И &УчетЗатратПоНД  ТОГДА
	|					&НаправлениеДеятельности
	|				ИНАЧЕ
	|					НЕОПРЕДЕЛЕНО
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности,
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	|				И &Период >= ТаблицаРасхождения.Склад.ДатаНачалаОрдернойСхемыПриПоступлении
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОрдернуюСхемуПриПоступлении,
	|
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) КОНЕЦ) КАК СуммаСНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) КОНЕЦ) КАК СуммаБезНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) КОНЕЦ) КАК СуммаБезНДСРегл,
	|	ЕСТЬNULL(Суммы.БазаНДСРегл, 0) КАК БазаНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаСНДСРегл, 0)  КАК СуммаСНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) КАК СуммаНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСУпр, 0) КАК СуммаНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК ВременнаяРазница,
	|
	|	ТаблицаРасхождения.СуммаВзаиморасчетов КАК СуммаВзаиморасчетовСНДС,
	|	ТаблицаРасхождения.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаРасхождения.ОбъектРасчетов КАК ОбъектРасчетов,
	|	
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах)
	|			ИЛИ (ТаблицаРасхождения.СписатьНаРасходы 
	|					И ТаблицаРасхождения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСОтражениемНаПрочихДоходах) 
	|		КОГДА (ТаблицаРасхождения.СписатьНаРасходы
	|				ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				И ТаблицаРасхождения.ВариантОтражения В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара)))
	|				ИЛИ
	|					ТаблицаРасхождения.ВариантОтражения В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСоСписаниемНаРасходы)
	|		КОГДА ТаблицаРасхождения.ВариантОтражения В
	|					(ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУчестьПриИнвентаризации),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьТоварыУПартнеров),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара))
	|			ТОГДА 
	|				ВЫБОР 
	|					КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) 
	|						ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика) 
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСоСписаниемНаРасходы) 
	|				КОНЕЦ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации,
	|	ТаблицаРасхождения.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи
	|	
	|ПОМЕСТИТЬ ВтТаблицаРасхожденияТоваров
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура = АналитикаБезНазначения.Номенклатура
	|	И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Характеристика = АналитикаБезНазначения.Характеристика
	|	И ТаблицаРасхождения.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|	И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Серия = АналитикаБезНазначения.Серия
	|	И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.МестоХранения = АналитикаБезНазначения.МестоХранения
	|	И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО Суммы.Ссылка = ТаблицаРасхождения.Ссылка
	|	И Суммы.ИдентификаторСтроки = ТаблицаРасхождения.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО ТаблицаАналитикУчетаПартий.НомерСтроки      = ТаблицаРасхождения.НомерСтроки
	|	И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""Расхождения""
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И НЕ &РаздельнаяЗакупка 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.Ссылка КАК Ссылка,
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ТаблицаРасхождения.НомерГТД КАК НомерГТД,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая КАК ЦеховаяКладовая,
	|	ТаблицаРасхождения.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаРасхождения.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ТаблицаРасхождения.КодСтроки КАК КодСтроки,
	|	ТаблицаРасхождения.ВариантОтражения КАК ВариантОтражения,
	|	ТаблицаРасхождения.Подразделение КАК Подразделение,
	|	0 КАК НомерГруппыЗатрат,
	|	ТаблицаРасхождения.СписатьНаРасходы КАК СписатьНаРасходы,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ТаблицаРасхождения.СтатьяРасходов 
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяРасходов,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА ТаблицаРасхождения.СтатьяРасходов 
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяАктивовПассивов,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|			ТОГДА ТаблицаРасхождения.СтатьяРасходов 
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтатьяДоходов,
	|	ТаблицаРасхождения.АналитикаРасходов КАК АналитикаРасходов,
	|	ТаблицаРасхождения.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ТаблицаРасхождения.АналитикаДоходов КАК АналитикаДоходов,
	|	ТаблицаРасхождения.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаАналитикУчетаПартий.ВидЦенности			КАК ВидЦенности,
	|	ТаблицаАналитикУчетаПартий.НалогообложениеНДС	КАК ВидДеятельностиНДС,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	ТаблицаРасхождения.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаРасхождения.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаРасхождения.СуммаНДС КАК СуммаНДС,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	АналитикаБезНазначения.КлючАналитики КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаРасхождения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаРасхождения.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ИспользоватьОрдернуюСхемуПриПоступлении
	|				И &Период >= ТаблицаРасхождения.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ДатаНачалаОрдернойСхемыПриПоступлении
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОрдернуюСхемуПриПоступлении,
	|
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) КОНЕЦ) КАК СуммаСНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) КОНЕЦ) КАК СуммаБезНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) КОНЕЦ) КАК СуммаБезНДСРегл,
	|	ЕСТЬNULL(Суммы.БазаНДСРегл, 0) КАК БазаНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаСНДСРегл, 0)  КАК СуммаСНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) КАК СуммаНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСУпр, 0) КАК СуммаНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК ВременнаяРазница,
	|
	|	ТаблицаРасхождения.СуммаВзаиморасчетов КАК СуммаВзаиморасчетовСНДС,
	|	ТаблицаРасхождения.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаРасхождения.ОбъектРасчетов КАК ОбъектРасчетов,
	|	
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах) 
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСОтражениемНаПрочихДоходах) 
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы И
	|				ТаблицаРасхождения.ВариантОтражения В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара))
	|				ИЛИ ТаблицаРасхождения.ВариантОтражения В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСоСписаниемНаРасходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика)
	|	КОНЕЦ КАК НастройкаХозяйственнойОперации,
	|	ТаблицаРасхождения.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи
	|	
	|ИЗ
	|	Документ.КорректировкаПриобретения.ВидыЗапасов КАК ТаблицаРасхождения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Серия = АналитикаБезНазначения.Серия
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО Суммы.Ссылка = ТаблицаРасхождения.Ссылка
	|	И Суммы.ИдентификаторСтроки = ТаблицаРасхождения.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО ТаблицаАналитикУчетаПартий.НомерСтроки      = ТаблицаРасхождения.НомерСтроки
	|	И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""ВидыЗапасов""
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И &РаздельнаяЗакупка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаКорректировкаЗадолженности(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаКорректировкаЗадолженности";
	
	СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаАналитикУчетаПартий", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	КорректировкаЗадолженности.Ссылка КАК Ссылка,
	|	КорректировкаЗадолженности.НомерСтроки КАК НомерСтроки,
	|	КорректировкаЗадолженности.ОбъектРасчетов КАК ОбъектРасчетов,
	|	КорректировкаЗадолженности.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельностиРасчетов,
	|	КорректировкаЗадолженности.ОбъектРасчетов.Договор КАК Договор,
	|	КорректировкаЗадолженности.СтавкаНДС КАК СтавкаНДС,
	|
	|	ЛОЖЬ КАК НаРасходы,
	|	ЛОЖЬ КАК НаПрочиеАктивы,
	|	ВЫБОР 
	|		КОГДА &СтатьяДоходов <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаДоходы,
	|	ВЫБОР 
	|		КОГДА &СтатьяПассивов <> НЕОПРЕДЕЛЕНО 
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаПрочиеПассивы,
	|
	|	(КорректировкаЗадолженности.СуммаКорректировки - КорректировкаЗадолженности.СуммаНДСКорректировки) КАК СуммаБезНДС,
	|	(КорректировкаЗадолженности.СуммаКорректировки) КАК СуммаСНДС,
	|	(КорректировкаЗадолженности.СуммаНДСКорректировки) КАК СуммаНДС,
	|
	|	КорректировкаЗадолженности.СуммаВзаиморасчетов  КАК СуммаВзаиморасчетов,
	|	ВЫРАЗИТЬ(КорректировкаЗадолженности.СуммаВзаиморасчетов 
	|		* КорректировкаЗадолженности.СуммаНДСКорректировки 
	|		/ КорректировкаЗадолженности.СуммаКорректировки КАК ЧИСЛО(31,2))  КАК СуммаНДСВзаиморасчетов,
	|
	|	- ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0)       КАК СуммаСНДСУпр,
	|	- ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0)     КАК СуммаБезНДСУпр,
	|	- ЕСТЬNULL(Суммы.СуммаСНДСРегл, 0)      КАК СуммаСНДСРегл,
	|	- ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0)    КАК СуммаБезНДСРегл,
	|	- ЕСТЬNULL(Суммы.СуммаНДСРегл, 0)       КАК НДСРегл,
	|	- ЕСТЬNULL(Суммы.СуммаНДСУпр, 0)       КАК НДСУпр,
	|
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.ОбъектРасчетов.УникальныйИдентификатор КАК УникальныйИдентификаторОбъектаРасчетов
	|
	|ПОМЕСТИТЬ ВтТаблицаКорректировкаЗадолженности
	|ИЗ
	|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК КорректировкаЗадолженности
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Ссылка = КорректировкаЗадолженности.Ссылка
	|		И Суммы.ИдентификаторСтроки = КорректировкаЗадолженности.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО 
	|		ТаблицаАналитикУчетаПартий.НомерСтроки = КорректировкаЗадолженности.НомерСтроки
	|		И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""КорректировкаЗадолженности""
	|ГДЕ
	|	КорректировкаЗадолженности.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияУменьшениеЗадолженностиСводно)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаЗадолженности.Ссылка КАК Ссылка,
	|	КорректировкаЗадолженности.НомерСтроки КАК НомерСтроки,
	|	КорректировкаЗадолженности.ОбъектРасчетов КАК ОбъектРасчетов,
	|	КорректировкаЗадолженности.ОбъектРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельностиРасчетов,
	|	КорректировкаЗадолженности.ОбъектРасчетов.Договор КАК Договор,
	|	КорректировкаЗадолженности.СтавкаНДС КАК СтавкаНДС,
	|
	|	ВЫБОР 
	|		КОГДА &СтатьяРасходов <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаРасходы,
	|	ВЫБОР 
	|		КОГДА &СтатьяАктивов <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НаПрочиеАктивы,
	|	ЛОЖЬ КАК НаДоходы,
	|	ЛОЖЬ КАК НаПрочиеПассивы,
	|	
	|	КорректировкаЗадолженности.СуммаКорректировки - КорректировкаЗадолженности.СуммаНДСКорректировки КАК СуммаБезНДС,
	|	КорректировкаЗадолженности.СуммаКорректировки       КАК СуммаСНДС,
	|	КорректировкаЗадолженности.СуммаНДСКорректировки    КАК СуммаНДС,
	|
	|	КорректировкаЗадолженности.СуммаВзаиморасчетов  КАК СуммаВзаиморасчетов,
	|	ВЫРАЗИТЬ(КорректировкаЗадолженности.СуммаВзаиморасчетов 
	|		* КорректировкаЗадолженности.СуммаНДСКорректировки 
	|		/ КорректировкаЗадолженности.СуммаКорректировки КАК ЧИСЛО(31,2))  КАК СуммаНДСВзаиморасчетов,
	|
	|	ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) КАК СуммаСНДСУпр,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) КАК СуммаБезНДСУпр,
	|	ЕСТЬNULL(Суммы.СуммаСНДСРегл, 0) КАК СуммаСНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) КАК СуммаБезНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) КАК НДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСУпр, 0) КАК НДСУпр,
	|
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.ОбъектРасчетов.УникальныйИдентификатор КАК УникальныйИдентификаторОбъектаРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК КорректировкаЗадолженности
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Ссылка = КорректировкаЗадолженности.Ссылка
	|		И Суммы.ИдентификаторСтроки = КорректировкаЗадолженности.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО 
	|		ТаблицаАналитикУчетаПартий.НомерСтроки 		= КорректировкаЗадолженности.НомерСтроки
	|		И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""КорректировкаЗадолженности""
	|ГДЕ
	|	КорректировкаЗадолженности.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияУвеличениеЗадолженностиСводно)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВидыЗапасовОприходование";
	
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаАналитикУчетаПартий", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасхождения.Номенклатура КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.Серия КАК Серия,
	|	ТаблицаРасхождения.Назначение КАК Назначение,
	|	ТаблицаРасхождения.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаАналитикУчетаПартий.ВидЦенности			КАК ВидЦенности,
	|	ТаблицаАналитикУчетаПартий.НалогообложениеНДС	КАК ВидДеятельностиНДС,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаРасхождения.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаРасхождения.НомерГТД КАК НомерГТД,
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаРасхождения.КоличествоПоРНПТ
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|	ТаблицаРасхождения.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	АналитикаБезНазначения.КлючАналитики  КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаРасхождения.СтавкаНДС КАК СтавкаНДС,
	|
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) КОНЕЦ) КАК СуммаСНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) КОНЕЦ) КАК СуммаБезНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) КОНЕЦ) КАК СуммаБезНДСРегл,
	|	ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) КАК СуммаНДСРегл,
	|
	|	ТаблицаРасхождения.Склад КАК Склад,
	|	ТаблицаРасхождения.Склад.ЦеховаяКладовая КАК ЦеховаяКладовая,
	|	ТаблицаРасхождения.Сделка КАК Сделка,
	|	ТаблицаРасхождения.СуммаНДС КАК СуммаНДС,
	|	ТаблицаРасхождения.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаРасхождения.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаРасхождения.ЗаказПоставщику КАК ЗаказПоставщику,
	|	
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика) КАК НастройкаХозяйственнойОперации,
	|	ТаблицаРасхождения.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи
	|
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасовОприходование
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Серия = АналитикаБезНазначения.Серия
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО ТаблицаАналитикУчетаПартий.НомерСтроки 		= ТаблицаРасхождения.НомерСтроки
	|	 И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""Расхождения""
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО Суммы.Ссылка = ТаблицаРасхождения.Ссылка
	|		И Суммы.ИдентификаторСтроки = ТаблицаРасхождения.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И НЕ &РаздельнаяЗакупка
	|	И ТаблицаРасхождения.ВариантОтражения В
	|		(ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки),
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУчестьПриИнвентаризации))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаАналитикУчетаПартий.ВидЦенности			КАК ВидЦенности,
	|	ТаблицаАналитикУчетаПартий.НалогообложениеНДС	КАК ВидДеятельностиНДС,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ВидыЗапасов.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ВидыЗапасов.Количество КАК Количество,
	|	ВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ВидыЗапасов.СуммаСНДС КАК СуммаСНДС,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	АналитикаБезНазначения.КлючАналитики  КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ВидыЗапасов.СуммаСНДС * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаСНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов - ВидыЗапасов.СуммаНДСВзаиморасчетов
	|	ИНАЧЕ
	|		ВЫРАЗИТЬ(((ВидыЗапасов.СуммаСНДС-ВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуУПР) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБезНДСУпр,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов - ВидыЗапасов.СуммаНДСВзаиморасчетов
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(((ВидыЗапасов.СуммаСНДС - ВидыЗапасов.СуммаНДС) * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаБезНДСРегл,
	|
	|	ВЫБОР
	|		КОГДА &ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета И &Валюта <> &ВалютаВзаиморасчетов ТОГДА
	|			ВидыЗапасов.СуммаВзаиморасчетов
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ((ВидыЗапасов.СуммаНДС * &КоэффициентПересчетаВВалютуРегл) КАК ЧИСЛО(31,2))
	|	КОНЕЦ КАК СуммаНДСРегл,
	|
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая КАК ЦеховаяКладовая,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.СуммаНДС КАК СуммаНДС,
	|	ВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВидыЗапасов.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ВидыЗапасов.ЗаказПоставщику КАК ЗаказПоставщику,
	|	
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика) КАК НастройкаХозяйственнойОперации,
	|	ВидыЗапасов.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения.ВидыЗапасов КАК ВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия = АналитикаБезНазначения.Серия
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО ТаблицаАналитикУчетаПартий.НомерСтроки 		= ВидыЗапасов.НомерСтроки
	|	 И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""ВидыЗапасов""
	|
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И &РаздельнаяЗакупка
	|	И ВидыЗапасов.ВариантОтражения =
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьТоварыУПартнеров)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтВозвратыПоставщикам(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтКорректировкиПриобретения";
	
	// Условия проверок соответствуют области КорректировкаПриобретения в общем модуле "РасчетСебестоимостиПодготовкаДанных".
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.Ссылка.Организация КАК Организация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	(ВЫБОР
	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА АналитикаБезНазначения.КлючАналитики
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатуры КОНЕЦ) КАК АналитикаУчетаСебестоимости,
	|	ТаблицаРасхождения.ВидЗапасов,
	|	СУММА(ТаблицаРасхождения.Количество) КАК Количество
	|ПОМЕСТИТЬ ВтКорректировкиПриобретения
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.Ссылка = ТаблицаРасхождения.АналитикаУчетаНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|		ПО АналитикаБезНазначения.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаБезНазначения.Характеристика = Аналитика.Характеристика
	|		И АналитикаБезНазначения.Серия = Аналитика.Серия
	|		И АналитикаБезНазначения.МестоХранения = Аналитика.МестоХранения
	|		И АналитикаБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И АналитикаБезНазначения.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) < НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	И ТаблицаРасхождения.ВариантОтражения В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации))
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.Ссылка.Организация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	(ВЫБОР
	|		КОГДА НЕ &УчитыватьСебестоимостьТоваровПоНазначениям ТОГДА АналитикаБезНазначения.КлючАналитики
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатуры КОНЕЦ),
	|	ТаблицаРасхождения.ВидЗапасов
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	ИмяРегистра = "ВтНеНулевыеОстаткиТоваров";
	
	// Получаем таблицу ненулевых остатков товаров на начало месяца.
	// Для метода оценки "Средняя за месяц" берем остатки товаров из регистра "Товары организаций".
	// Для метода оценки "ФИФО" при партионном учета 2.2 берем остатки из регистра "Себестоимость товаров" по корректируемой партии.
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОстаткиТоваров.АналитикаУчетаНоменклатуры,
	|	ОстаткиТоваров.ВидЗапасов,
	|	ОстаткиТоваров.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВтНеНулевыеОстаткиТоваров
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ),
	|		НЕ (&ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка)
	|		И (АналитикаУчетаНоменклатуры, Организация, ВидЗапасов) В (
	|			ВЫБРАТЬ
	|				Корректировки.АналитикаУчетаНоменклатуры,
	|				Корректировки.Организация,
	|               Корректировки.ВидЗапасов
	|			ИЗ
	|				ВтКорректировкиПриобретения КАК Корректировки
	|		)
	|	) КАК ОстаткиТоваров
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	ИмяРегистра = "ВтНеНулевыеОстаткиСебестоимости";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОстаткиТоваров.АналитикаУчетаНоменклатуры,
	|	ОстаткиТоваров.ВидЗапасов,
	|	ОстаткиТоваров.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ ВтНеНулевыеОстаткиСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Остатки(НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ),
	|		(&ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка)
	|		И (АналитикаУчетаНоменклатуры, ВидЗапасов, Организация, Партия) В (
	|			ВЫБРАТЬ
	|				Корректировки.АналитикаУчетаСебестоимости,
	|               Корректировки.ВидЗапасов,
	|				Корректировки.Организация,
	|				&ДокументОснование КАК Партия
	|			ИЗ
	|				ВтКорректировкиПриобретения КАК Корректировки
	|		)
	|	) КАК ОстаткиТоваров
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	ИмяРегистра = "ВтПрошлыеКорректировки";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	СУММА(Движения.Количество) КАК Количество
	|ПОМЕСТИТЬ ВтПрошлыеКорректировки
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Движения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|		ПО КорректировкаПриобретения.Ссылка = Движения.Регистратор
	|		И КорректировкаПриобретения.ДокументОснование = &ДокументОснование
	|ГДЕ
	|	Движения.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И &Период
	|	И Движения.Регистратор <> &Ссылка
	|	И Движения.Активность
	|	И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И (Движения.АналитикаУчетаНоменклатуры, Движения.ВидЗапасов, Движения.Организация) В (
	|			ВЫБРАТЬ
	|				Корректировки.АналитикаУчетаСебестоимости,
	|				Корректировки.ВидЗапасов,
	|				Корректировки.Организация
	|			ИЗ
	|				ВтКорректировкиПриобретения КАК Корректировки
	|		)
	|СГРУППИРОВАТЬ ПО
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	ИмяРегистра = "ВтВозвратыПоставщикам";
	
 	// Формируем таблицу аналитик товаров, для которых корректировка приобретения должна отрабатывать как возврат товаров поставщику.
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Корректировки.АналитикаУчетаНоменклатуры,
	|	ИСТИНА КАК ЭтоВозвратПоставщику
	|ПОМЕСТИТЬ ВтВозвратыПоставщикам
	|ИЗ
	|	ВтКорректировкиПриобретения КАК Корректировки
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтНеНулевыеОстаткиТоваров КАК ОстаткиТоваров
	|		ПО ОстаткиТоваров.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаНоменклатуры
	|		И ОстаткиТоваров.ВидЗапасов = Корректировки.ВидЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтНеНулевыеОстаткиСебестоимости КАК ОстаткиСебестоимости
	|		ПО ОстаткиСебестоимости.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаСебестоимости
	|		И ОстаткиСебестоимости.ВидЗапасов = Корректировки.ВидЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтПрошлыеКорректировки КАК ПрошлыеКорректировки
	|		ПО ПрошлыеКорректировки.АналитикаУчетаНоменклатуры = Корректировки.АналитикаУчетаСебестоимости
	|		И ПрошлыеКорректировки.ВидЗапасов = Корректировки.ВидЗапасов
	|ГДЕ
	|	НЕ (&ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка)
	|		И ЕСТЬNULL(ОстаткиТоваров.Количество, 0) <= (-Корректировки.Количество - ЕСТЬNULL(ПрошлыеКорректировки.Количество, 0))
	|	ИЛИ (&ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка)
	|		И ЕСТЬNULL(ОстаткиСебестоимости.Количество, 0) <= (-Корректировки.Количество - ЕСТЬNULL(ПрошлыеКорректировки.Количество, 0))
	|";
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВидыЗапасовСписание";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтВозвратыПоставщикам", ТекстыЗапроса) Тогда
		ТекстЗапросаВтВозвратыПоставщикам(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиУчетаНоменклатуры(Запрос);
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаАналитикУчетаПартий", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаАналитикУчетаПартий(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасхождения.Номенклатура КАК Номенклатура,
	|	ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	ТаблицаРасхождения.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаРасхождения.Назначение КАК Назначение,
	|	ТаблицаАналитикУчетаПартий.ВидЦенности			КАК ВидЦенности,
	|	ТаблицаАналитикУчетаПартий.НалогообложениеНДС	КАК ВидДеятельностиНДС,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ТаблицаРасхождения.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаРасхождения.НомерГТД КАК НомерГТД,
	|	-ТаблицаРасхождения.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаРасхождения.КоличествоПоРНПТ
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|	-ТаблицаРасхождения.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	АналитикаБезНазначения.КлючАналитики КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ТаблицаРасхождения.СтавкаНДС КАК СтавкаНДС,
	|
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА -(Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(-Суммы.СуммаСНДСУпр, 0) КОНЕЦ) КАК СуммаСНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА -Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(-Суммы.СуммаБезНДСУпр, 0) КОНЕЦ) КАК СуммаБезНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА -Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(-Суммы.СуммаБезНДСРегл, 0) КОНЕЦ) КАК СуммаБезНДСРегл,
	|	ЕСТЬNULL(-Суммы.СуммаНДСРегл, 0) КАК СуммаНДСРегл,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК ВременнаяРазница,
	|
	|	ТаблицаРасхождения.Склад КАК Склад,
	|	ТаблицаРасхождения.Склад.ЦеховаяКладовая КАК ЦеховаяКладовая,
	|	ТаблицаРасхождения.Сделка КАК Сделка,
	|	ТаблицаРасхождения.СуммаНДС КАК СуммаНДС,
	|	ТаблицаРасхождения.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаРасхождения.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ТаблицаРасхождения.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫБОР
	|		КОГДА (НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|				ИЛИ ТаблицаРасхождения.СуммаСНДС <> 0)
	|			ИЛИ (НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|				И ТаблицаРасхождения.Количество <> 0)
	|			ТОГДА ЕСТЬNULL(ВозвратыПоставщикам.ЭтоВозвратПоставщику, ЛОЖЬ)
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЭтоВозвратПоставщику,
	|
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика) КАК НастройкаХозяйственнойОперации,
	|	ТаблицаРасхождения.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи
	|
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасовСписание
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВозвратыПоставщикам КАК ВозвратыПоставщикам
	|		ПО ВозвратыПоставщикам.АналитикаУчетаНоменклатуры = ТаблицаРасхождения.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Серия = АналитикаБезНазначения.Серия
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|		И ТаблицаРасхождения.АналитикаУчетаНоменклатуры.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО ТаблицаАналитикУчетаПартий.НомерСтроки 		= ТаблицаРасхождения.НомерСтроки
	|	 И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""Расхождения""
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Ссылка = ТаблицаРасхождения.Ссылка
	|		И Суммы.ИдентификаторСтроки = ТаблицаРасхождения.ИдентификаторСтроки
	|
	|ГДЕ
	|	ТаблицаРасхождения.Ссылка = &Ссылка
	|	И НЕ &РаздельнаяЗакупка
	|	И ТаблицаРасхождения.ВариантОтражения В
	|		(ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки),
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьТоварыУПартнеров),
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|	ТаблицаАналитикУчетаПартий.ВидЦенности			КАК ВидЦенности,
	|	ТаблицаАналитикУчетаПартий.НалогообложениеНДС	КАК ВидДеятельностиНДС,
	|	ТаблицаАналитикУчетаПартий.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ВидыЗапасов.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ВидыЗапасов.НомерГТД КАК НомерГТД,
	|	-ВидыЗапасов.Количество КАК Количество,
	|	-ВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	-ВидыЗапасов.СуммаСНДС КАК СуммаСНДС,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	АналитикаБезНазначения.КлючАналитики КАК АналитикаУчетаНоменклатурыБезНазначения,
	|	ВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА -(Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(-Суммы.СуммаСНДСУпр, 0) КОНЕЦ) КАК СуммаСНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА -Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(-Суммы.СуммаБезНДСУпр, 0) КОНЕЦ) КАК СуммаБезНДСУпр,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА -Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(-Суммы.СуммаБезНДСРегл, 0) КОНЕЦ) КАК СуммаБезНДСРегл,
	|	ЕСТЬNULL(-Суммы.СуммаНДСРегл, 0) КАК СуммаНДСРегл,
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл - Суммы.СуммаБезНДСРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК ВременнаяРазница,
	|
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.СкладскаяТерритория.ЦеховаяКладовая КАК ЦеховаяКладовая,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.СуммаНДС КАК СуммаНДС,
	|	ВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВидыЗапасов.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ВидыЗапасов.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|		 ИЛИ ВидыЗапасов.СуммаСНДС <> 0
	|	ТОГДА
	|		ЕСТЬNULL(ВозвратыПоставщикам.ЭтоВозвратПоставщику, ЛОЖЬ)
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ КАК ЭтоВозвратПоставщику,
	|
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.СторноПоступления) КАК НастройкаХозяйственнойОперации,
	|	ВидыЗапасов.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения.ВидыЗапасов КАК ВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтВозвратыПоставщикам КАК ВозвратыПоставщикам
	|		ПО ВозвратыПоставщикам.АналитикаУчетаНоменклатуры = ВидыЗапасов.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаБезНазначения
	|	ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура = АналитикаБезНазначения.Номенклатура
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика = АналитикаБезНазначения.Характеристика
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия = АналитикаБезНазначения.Серия
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = АналитикаБезНазначения.СтатьяКалькуляции
	|		И ВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения = АналитикаБезНазначения.МестоХранения
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаБезНазначения.Назначение
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаАналитикУчетаПартий КАК ТаблицаАналитикУчетаПартий
	|	ПО ТаблицаАналитикУчетаПартий.НомерСтроки 		= ВидыЗапасов.НомерСтроки
	|	 И ТаблицаАналитикУчетаПартий.ИмяТабличнойЧасти = ""ВидыЗапасов""
	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО
	|		Суммы.Ссылка = ВидыЗапасов.Ссылка
	|		И Суммы.ИдентификаторСтроки = ВидыЗапасов.ИдентификаторСтроки
	|ГДЕ
	|	ВидыЗапасов.Ссылка = &Ссылка
	|	И &РаздельнаяЗакупка
	|	И ВидыЗапасов.ВариантОтражения =
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьТоварыУПартнеров)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПринятаяВозвратнаяТара(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ПринятаяВозвратнаяТара";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки              КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаТовары.Номенклатура             КАК Номенклатура,
	|	ТаблицаТовары.Характеристика           КАК Характеристика,
	|	ТаблицаТовары.Количество               КАК Количество,
	|	ТаблицаТовары.СуммаСНДС                КАК Сумма,
	|	&Партнер                               КАК Партнер,
	|	&ДокументОснование                     КАК ДокументПоступления,
	|	&ТребуетсяЗалогЗаТару                  КАК ПредусмотренЗалог,
	|
	|	ТаблицаТовары.ИдентификаторФинЗаписи   КАК ИдентификаторФинЗаписи,
	|	ТаблицаТовары.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	И &ВернутьМногооборотнуюТару
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДатыПоступленияТоваровОрганизаций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период							  КАК ДатаПоступления,
	|	ТаблицаВидыЗапасов.Номенклатура   КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.Серия          КАК Серия,
	|	ТаблицаВидыЗапасов.Назначение     КАК Назначение,
	|	ТаблицаВидыЗапасов.ВидЗапасов     КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД       КАК НомерГТД
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|	ПО
	|		ТаблицаВидыЗапасов.ВидЗапасов		= ПоступленияТоваров.ВидЗапасов 
	|		И ТаблицаВидыЗапасов.Номенклатура	= ПоступленияТоваров.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = ПоступленияТоваров.Характеристика
	|		И ТаблицаВидыЗапасов.Серия          = ПоступленияТоваров.Серия
	|		И ТаблицаВидыЗапасов.Назначение     = ПоступленияТоваров.Назначение
	|		И ТаблицаВидыЗапасов.НомерГТД		= ПоступленияТоваров.НомерГТД 
	|
	|ГДЕ
	|	НЕ &ИспользуетсяДокументПоступлениеТоваров
	|	И ПоступленияТоваров.ДатаПоступления ЕСТЬ NULL
	|		ИЛИ ПоступленияТоваров.ДатаПоступления < &Период
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаВидыЗапасов.ВидЗапасов,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	ТаблицаВидыЗапасов.Серия,
	|	ТаблицаВидыЗапасов.Назначение,
	|	ТаблицаВидыЗапасов.НомерГТД
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ОрганизацияОтгрузки,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|	ИСТИНА КАК Первичное
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	НЕ &ИспользуетсяДокументПоступлениеТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Период,
	|	&Организация КАК Организация,
	|	&Организация КАК ОрганизацияОтгрузки,
	|	ТаблицаВидыЗапасов.Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД,
	|	ТаблицаВидыЗапасов.Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	&ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС,
	|	ЛОЖЬ КАК Первичное
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|
	|ГДЕ
	|	НЕ &ИспользуетсяДокументПоступлениеТоваров
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки           КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	НЕОПРЕДЕЛЕНО                           КАК ОрганизацияОтгрузки,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.ВидЗапасов            КАК ВидЗапасов,
	|	ТаблицаДокумента.НомерГТД              КАК НомерГТД,
	|	ТаблицаДокумента.Количество            КАК Количество,
	|	ТаблицаДокумента.КоличествоПоРНПТ      КАК КоличествоПоРНПТ,
	|	АналитикаУчетаНоменклатурыПоПартнерам.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	&ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                           КАК ВидДеятельностиНДС,
	|	ЛОЖЬ                                   КАК Первичное
	|ИЗ
	|	Документ.КорректировкаПриобретения.ВидыЗапасов КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаРасхождения.Ссылка
	|		И ТаблицаДокумента.КодСтроки = ТаблицаРасхождения.КодСтроки
	|		И ТаблицаДокумента.ЗаказПоставщику = ТаблицаРасхождения.ЗаказПоставщику
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура = ТаблицаРасхождения.Номенклатура
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика = ТаблицаРасхождения.Характеристика
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение = ТаблицаРасхождения.Назначение
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия = ТаблицаРасхождения.Серия
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг КАК ДанныеПоступления
	|		ПО ТаблицаДокумента.Ссылка.ДокументОснование = ДанныеПоступления.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатурыПоПартнерам
	|		ПО ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура = АналитикаУчетаНоменклатурыПоПартнерам.Номенклатура
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Характеристика = АналитикаУчетаНоменклатурыПоПартнерам.Характеристика
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение = АналитикаУчетаНоменклатурыПоПартнерам.Назначение
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.Серия = АналитикаУчетаНоменклатурыПоПартнерам.Серия
	|		И ДанныеПоступления.Договор = АналитикаУчетаНоменклатурыПоПартнерам.МестоХранения
	|		И ТаблицаДокумента.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = АналитикаУчетаНоменклатурыПоПартнерам.СтатьяКалькуляции
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И &ИспользуетсяДокументПоступлениеТоваров
	|	И ТаблицаДокумента.ВариантОтражения В (ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьТоварыУПартнеров),
	|									ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьТоварыУПартнеров))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеДоходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаКорректировкаЗадолженности", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКорректировкаЗадолженности(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы 
	|			ТОГДА ТаблицаРасхождения.Подразделение
	|		ИНАЧЕ &Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы 
	|			ТОГДА ТаблицаРасхождения.НаправлениеДеятельности
	|		ИНАЧЕ &НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы 
	|			ТОГДА ТаблицаРасхождения.СтатьяДоходов
	|		ИНАЧЕ &СтатьяДоходов
	|	КОНЕЦ КАК СтатьяДоходов,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы 
	|			ТОГДА ТаблицаРасхождения.АналитикаДоходов
	|		ИНАЧЕ &АналитикаДоходов
	|	КОНЕЦ КАК АналитикаДоходов,
	|
	|	-ТаблицаРасхождения.СуммаСНДСУпр КАК Сумма,
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА -ТаблицаРасхождения.СуммаБезНДСУпр
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпр,
	|	(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА -ТаблицаРасхождения.СуммаБезНДСРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияСОтражениемНаПрочихДоходах) КАК ХозяйственнаяОперация,
	|	
	|	ТаблицаРасхождения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаРасхождения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСОтражениемНаПрочихДоходах) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|
	|ГДЕ
	|	&ИспользоватьУчетПрочихДоходовРасходов
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах)
	|			И &СтатьяДоходов <> НЕОПРЕДЕЛЕНО)
	|		ИЛИ (ТаблицаРасхождения.СписатьНаРасходы
	|			И ТаблицаРасхождения.СтатьяДоходов <> НЕОПРЕДЕЛЕНО)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяДоходов КАК СтатьяДоходов,
	|	&АналитикаДоходов КАК АналитикаДоходов,
	|
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК Сумма,
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА КорректировкаЗадолженности.СуммаСНДСУпр
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпр,
	|	(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА КорректировкаЗадолженности.СуммаСНДСРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация,
	|	
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.УникальныйИдентификаторОбъектаРасчетов КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаЗадолженности) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	КорректировкаЗадолженности.НаДоходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&СтатьяДоходов КАК СтатьяДоходов,
	|	&АналитикаДоходов КАК АналитикаДоходов,
	|
	|	0 КАК Сумма,
	|	(ВЫБОР
	|		КОГДА &УправленческийУчетОрганизаций
	|			ТОГДА КорректировкаЗадолженности.НДСУпр
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаУпр,
	|	(ВЫБОР
	|		КОГДА &ИспользоватьУчетПрочихДоходовРасходовРегл
	|			ТОГДА КорректировкаЗадолженности.НДСРегл
	|		ИНАЧЕ 0 КОНЕЦ) КАК СуммаРегл,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВходящийНДСПоПриобретению) КАК ХозяйственнаяОперация,
	|	
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВходящийНДСПоПриобретению) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	КорректировкаЗадолженности.НаДоходы
	|	И &ЗакупкаПодДеятельность НЕ В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|	И (КорректировкаЗадолженности.НДСРегл <> 0
	|		ИЛИ КорректировкаЗадолженности.НДСУпр <> 0)
	|
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаКорректировкаЗадолженности", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКорректировкаЗадолженности(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период											КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			КАК ВидДвижения,
	|	&Организация									КАК Организация,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.Подразделение
	|	ИНАЧЕ
	|		&Подразделение
	|	КОНЕЦ											КАК Подразделение,
	|	СтатьиРасходов.Ссылка							КАК СтатьяРасходов,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.АналитикаРасходов
	|	ИНАЧЕ
	|		&АналитикаРасходов
	|	КОНЕЦ											КАК АналитикаРасходов,
	|	ВЫБОР КОГДА ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.НаправлениеДеятельности
	|	ИНАЧЕ
	|		&НаправлениеДеятельности
	|	КОНЕЦ											КАК НаправлениеДеятельности,
	|	ТаблицаРасхождения.ВидДеятельностиНДС 			КАК ВидДеятельностиНДС,
	|	
	|	ТаблицаРасхождения.СуммаСНДСУпр					КАК СуммаСНДС,
	|	ТаблицаРасхождения.СуммаБезНДСУпр				КАК СуммаБезНДС,
	|	ТаблицаРасхождения.СуммаБезНДСУпр				КАК СуммаБезНДСУпр,
	|
	|	ТаблицаРасхождения.СуммаСНДСРегл 				КАК СуммаСНДСРегл,
	|	ТаблицаРасхождения.СуммаБезНДСРегл 				КАК СуммаБезНДСРегл,
	|	0 												КАК ПостояннаяРазница,
	|	ТаблицаРасхождения.ВременнаяРазница				КАК ВременнаяРазница,
	|
	|	&ХозяйственнаяОперация 							КАК ХозяйственнаяОперация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|
	|	ТаблицаРасхождения.ИдентификаторСтроки          КАК ИдентификаторСтроки,
	|	ТаблицаРасхождения.ИдентификаторФинЗаписи       КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСоСписаниемНаРасходы) КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтИсходныеПрочиеРасходы
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|			ТаблицаРасхождения.СтатьяРасходов
	|		ИНАЧЕ
	|			&СтатьяРасходов
	|		КОНЕЦ = СтатьиРасходов.Ссылка
	|	
	|ГДЕ
	|	ТаблицаРасхождения.СтатьяРасходов <> НЕОПРЕДЕЛЕНО 
	|	И ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара))
	|	ИНАЧЕ
	|		ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы))
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&ЗакупкаПодДеятельность КАК ВидДеятельностиНДС,
	|	
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК СуммаСНДС,
	|	КорректировкаЗадолженности.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК СуммаБезНДСУпр,
	|
	|	КорректировкаЗадолженности.СуммаСНДСРегл КАК СуммаСНДСРегл,
	|	КорректировкаЗадолженности.СуммаСНДСРегл КАК СуммаБезНДСРегл,
	|
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)  КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО	КАК АналитикаУчетаНоменклатуры,
	|
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.УникальныйИдентификаторОбъектаРасчетов КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаЗадолженности) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	КорректировкаЗадолженности.НаРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&ЗакупкаПодДеятельность КАК ВидДеятельностиНДС,
	|	
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	КорректировкаЗадолженности.НДСУпр КАК СуммаБезНДСУпр,
	|
	|	0 КАК СуммаСНДСРегл,
	|	КорректировкаЗадолженности.НДСРегл КАК СуммаБезНДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВходящийНДСПоПриобретению) КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО	КАК АналитикаУчетаНоменклатуры,
	|
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВходящийНДСПоПриобретению) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	КорректировкаЗадолженности.НаРасходы
	|	И &ЗакупкаПодДеятельность НЕ В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|	И (КорректировкаЗадолженности.НДСРегл <> 0
	|		ИЛИ КорректировкаЗадолженности.НДСУпр <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

	
КонецФункции

Функция ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПрочиеРасходы";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаВтПрочиеРасходы("ИдентификаторСтроки");
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПрочиеРасходы.ТекстЗапросаТаблицаПрочиеРасходы("ИдентификаторСтроки");
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииТоваровОрганизаций";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовСписание", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрЗапросаДокументПоступленияНДС(Запрос);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&ДокументПоступленияНДС КАК ДокументПоступления,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|			ТОГДА 0
	|			ИНАЧЕ ТаблицаВидыЗапасов.СуммаНДСРегл
	|	КОНЕЦ КАК НДСРегл,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ИСТИНА КАК Первичное,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник
	|ИЗ
	|	ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	&ПартионныйУчетВерсии21
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&ДокументОснование КАК ДокументПоступления,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|			ТОГДА 0
	|			ИНАЧЕ ТаблицаВидыЗапасов.СуммаНДСРегл
	|	КОНЕЦ КАК НДСРегл,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ИСТИНА КАК Первичное,
	|	&ДокументОснование КАК ДокументИсточник
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	&ПартионныйУчетВерсии21
	|	И ТаблицаВидыЗапасов.ЭтоВозвратПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&ДокументОснование КАК ДокументПоступления,
	|	-ТаблицаВидыЗапасов.Количество КАК Количество,
	|	-ТаблицаВидыЗапасов.СуммаСНДСУпр КАК Стоимость,
	|	-ТаблицаВидыЗапасов.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	-ТаблицаВидыЗапасов.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	-ТаблицаВидыЗапасов.СуммаНДСРегл КАК НДСРегл,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ИСТИНА КАК Первичное,
	|	&ДокументОснование КАК ДокументИсточник
	|ИЗ
	|	ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	&ПартионныйУчетВерсии21
	|	И НЕ ТаблицаВидыЗапасов.ЭтоВозвратПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Организация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.ВидЗапасов,
	|	ТаблицаРасхождения.АналитикаУчетаПартий,
	|	&ДокументОснование,
	|	ТаблицаРасхождения.Количество,
	|	ТаблицаРасхождения.СуммаСНДСУпр,
	|	ТаблицаРасхождения.СуммаБезНДСУпр,
	|	ТаблицаРасхождения.СуммаБезНДСРегл,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|			ТОГДА 0
	|			ИНАЧЕ ТаблицаРасхождения.СуммаНДСРегл
	|	КОНЕЦ КАК НДСРегл,
	|	ТаблицаРасхождения.ВидДеятельностиНДС,
	|	ТаблицаРасхождения.Номенклатура,
	|	ТаблицаРасхождения.Характеристика,
	|	&ХозяйственнаяОперация,
	|	ИСТИНА КАК Первичное,
	|	НЕОПРЕДЕЛЕНО КАК ДокументИсточник
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	&ПартионныйУчетВерсии21
	|	И ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара)
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногоОборотнаяТара))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции 

Функция ТекстЗапросаТаблицаПартииРасходовНаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииРасходовНаСебестоимостьТоваров";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	&Период,
	|	&Организация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	&ДокументОснование КАК ДокументПоступления,
	|	ТаблицаРасхождения.ВидЗапасов,
	|	ТаблицаРасхождения.АналитикаУчетаПартий,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	ТаблицаРасхождения.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаРасхождения.СуммаНДСРегл
	|	КОНЕЦ КАК НДСРегл,
	|	ТаблицаРасхождения.ВидДеятельностиНДС КАК НалогообложениеНДС,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	&ПартионныйУчетВерсии21
	|	И ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара)
	|	И ТаблицаРасхождения.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногоОборотнаяТара))
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции 

Процедура СформироватьСуммыДокументаВВалютахУчета(Запрос, ТекстыЗапроса, Регистры = Неопределено) Экспорт

	ТекстЗапросаДанных =
	"ВЫБРАТЬ
	|	""Расхождения"" КАК ИсточникДанных,
	|	ИСТИНА КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Валюта КАК ВалютаДокумента,
	|	ТаблицаДокумента.Ссылка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаДокумента.Ссылка.ДокументОснование.Дата КАК ПериодБазыНДС,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаДокумента.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				И ТаблицаДокумента.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару
	|				И НЕ ТаблицаДокумента.Ссылка.ДокументОснование.ТребуетсяЗалогЗаТару
	|		ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОтражаетсяВРасчетах,
	|	ТаблицаДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ИСТИНА КАК ПересчитыватьПоДаннымРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И НЕ &РаздельнаяЗакупка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ВидыЗапасов"" КАК ИсточникДанных,
	|	ИСТИНА КАК РаспределятьОбщуюСумму,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	ТаблицаДокумента.Ссылка.Организация КАК Организация,
	|	ТаблицаДокумента.Ссылка.Валюта КАК ВалютаДокумента,
	|	ТаблицаДокумента.Ссылка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаДокумента.Ссылка.ДокументОснование.Дата КАК ПериодБазыНДС,
	|
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаДокумента.СуммаСНДС - ТаблицаДокумента.СуммаНДС КАК СуммаБезНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ТаблицаДокумента.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|
	|	ВЫБОР 
	|		КОГДА ТаблицаДокумента.АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				И ТаблицаДокумента.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару
	|				И НЕ ТаблицаДокумента.Ссылка.ДокументОснование.ТребуетсяЗалогЗаТару
	|		ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОтражаетсяВРасчетах,
	|	ТаблицаДокумента.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ИСТИНА КАК ПересчитыватьПоДаннымРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.ВидыЗапасов КАК ТаблицаДокумента
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И &РаздельнаяЗакупка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""УменьшениеЗадолженности"" КАК ИсточникДанных,
	|	ИСТИНА КАК РаспределятьОбщуюСумму,
	|	КорректировкаЗадолженности.Ссылка КАК Ссылка,
	|	КорректировкаЗадолженности.Ссылка.Дата КАК Дата,
	|	КорректировкаЗадолженности.Ссылка.Организация КАК Организация,
	|	КорректировкаЗадолженности.Ссылка.Валюта КАК ВалютаДокумента,
	|	КорректировкаЗадолженности.Ссылка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	КорректировкаЗадолженности.Ссылка.Дата КАК ПериодБазыНДС,
	|
	|	КорректировкаЗадолженности.НомерСтроки КАК НомерСтроки,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	-(КорректировкаЗадолженности.СуммаКорректировки - КорректировкаЗадолженности.СуммаНДСКорректировки) КАК СуммаБезНДС,
	|	КорректировкаЗадолженности.СтавкаНДС КАК СтавкаНДС,
	|	-(КорректировкаЗадолженности.СуммаНДСКорректировки) КАК СуммаНДС,
	|	-(КорректировкаЗадолженности.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	-(КорректировкаЗадолженности.СуммаНДСВзаиморасчетов) КАК СуммаНДСВзаиморасчетов,
	|
	|	ИСТИНА КАК ОтражаетсяВРасчетах,
	|	КорректировкаЗадолженности.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ИСТИНА КАК ПересчитыватьПоДаннымРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК КорректировкаЗадолженности
	|
	|ГДЕ
	|	КорректировкаЗадолженности.Ссылка В (&Ссылка)
	|	И КорректировкаЗадолженности.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияУменьшениеЗадолженностиСводно)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""УвеличениеЗадолженности"" КАК ИсточникДанных,
	|	ИСТИНА КАК РаспределятьОбщуюСумму,
	|	КорректировкаЗадолженности.Ссылка КАК Ссылка,
	|	КорректировкаЗадолженности.Ссылка.Дата КАК Дата,
	|	КорректировкаЗадолженности.Ссылка.Организация КАК Организация,
	|	КорректировкаЗадолженности.Ссылка.Валюта КАК ВалютаДокумента,
	|	КорректировкаЗадолженности.Ссылка.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	КорректировкаЗадолженности.Ссылка.Дата КАК ПериодБазыНДС,
	|
	|	КорректировкаЗадолженности.НомерСтроки КАК НомерСтроки,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.СуммаКорректировки - КорректировкаЗадолженности.СуммаНДСКорректировки КАК СуммаБезНДС,
	|	КорректировкаЗадолженности.СтавкаНДС КАК СтавкаНДС,
	|	КорректировкаЗадолженности.СуммаНДСКорректировки КАК СуммаНДС,
	|	КорректировкаЗадолженности.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	КорректировкаЗадолженности.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|
	|	ИСТИНА КАК ОтражаетсяВРасчетах,
	|	КорректировкаЗадолженности.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ИСТИНА КАК ПересчитыватьПоДаннымРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК КорректировкаЗадолженности
	|
	|ГДЕ
	|	КорректировкаЗадолженности.Ссылка В (&Ссылка)
	|	И КорректировкаЗадолженности.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияУвеличениеЗадолженностиСводно)
	|";
	
	РегистрыСведений.СуммыДокументовВВалютахУчета.СформироватьПоДаннымДокумента(
		Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаДанных);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтИсходныеПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаКорректировкаЗадолженности", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКорректировкаЗадолженности(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрЗапросаДокументПоступленияНДС(Запрос);
	
	ТекстЗапроса = "
	// Расходы на себестоимость
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаРасхождения.Подразделение КАК Подразделение,
	|	&ДокументПоступленияНДС КАК ДокументПоступленияРасходов,
	|	СтатьиРасходов.Ссылка КАК СтатьяРасходов,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|	 ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.АналитикаРасходов
	|	ИНАЧЕ
	|		&АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	ТаблицаРасхождения.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	ВЫБОР КОГДА ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.НаправлениеДеятельности
	|	ИНАЧЕ
	|		&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	ТаблицаРасхождения.СуммаНДСУпр КАК НДСУпр,
	|	ТаблицаРасхождения.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	0 КАК ПостояннаяРазница,
	|	ТаблицаРасхождения.ВременнаяРазница КАК ВременнаяРазница,
	|	ТаблицаРасхождения.СуммаНДСРегл КАК НДСРегл,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|
	|	ТаблицаРасхождения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаРасхождения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСоСписаниемНаРасходы) КАК НастройкаХозяйственнойОперации
	|
	|ПОМЕСТИТЬ ВтИсходныеПартииПрочихРасходов
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|			ТаблицаРасхождения.СтатьяРасходов
	|		ИНАЧЕ
	|			&СтатьяРасходов
	|		КОНЕЦ = СтатьиРасходов.Ссылка
	|ГДЕ
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара))
	|	ИНАЧЕ
	|		ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы))
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаЗадолженности.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	КорректировкаЗадолженности.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	&ЗакупкаПодДеятельность КАК ВидДеятельностиНДС,
	|
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК Стоимость,
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	КорректировкаЗадолженности.СуммаСНДСРегл  КАК СтоимостьРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК НДСРегл,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.УникальныйИдентификаторОбъектаРасчетов КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаПриобретенияСоСписаниемНаРасходы) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	КорректировкаЗадолженности.НаРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаЗадолженности.НомерСтроки КАК НомерСтроки,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	КорректировкаЗадолженности.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	&ЗакупкаПодДеятельность КАК ВидДеятельностиНДС,
	|
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	0 КАК НДСУпр,
	|	КорректировкаЗадолженности.НДСРегл  КАК СтоимостьРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	0 КАК НДСРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности) КАК ХозяйственнаяОперация,
	|
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаЗадолженности) КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	КорректировкаЗадолженности.НаРасходы
	|	И &ЗакупкаПодДеятельность НЕ В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|	И (КорректировкаЗадолженности.НДСРегл <> 0
	|		ИЛИ КорректировкаЗадолженности.НДСУпр <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса) Экспорт
	
	ИмяРегистра = "ВтПартииПрочихРасходов";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтИсходныеПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтИсходныеПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаВтПартииПрочихРасходов("ИдентификаторСтроки");
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрЗапросаДокументПоступленияНДС(Запрос);
	
	ТекстЗапроса = РегистрыНакопления.ПартииПрочихРасходов.ТекстЗапросаТаблицаПартииПрочихРасходов("ИдентификаторСтроки");
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "МатериалыИРаботыВПроизводстве";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Период КАК ДатаРегистратора,
	|	&Организация КАК Организация,
	|	ТаблицаРасхождения.Номенклатура,
	|	ТаблицаРасхождения.Характеристика,
	|	ТаблицаРасхождения.Назначение,
	|	ТаблицаРасхождения.Серия,
	|	ТаблицаРасхождения.Подразделение,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.Количество
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ ТаблицаРасхождения.СписатьНаРасходы
	|	И ТаблицаРасхождения.Количество <> 0
	|	И НЕ &ХозОперацияОснования = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОтчетаПереработчика)
	|	И (ТаблицаРасхождения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		ИЛИ ТаблицаРасхождения.Назначение.ТипНазначения <> ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала))
	|	И НЕ ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПартииПроизводственныхЗатрат(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ПартииПроизводственныхЗатрат";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.ВидЗапасов,
	|	ТаблицаРасхождения.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&ДокументОснование КАК ДокументПоступления,
	|
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	ТаблицаРасхождения.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаРасхождения.СуммаНДСРегл
	|	КОНЕЦ КАК НДСРегл
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ТаблицаРасхождения.Количество >= 0
	|	И НЕ ТаблицаРасхождения.СписатьНаРасходы
	|	И НЕ &ПартионныйУчетВерсии22
	|	И НЕ ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаРасхождения.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.ВидЗапасов,
	|	ТаблицаРасхождения.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	&ДокументОснование КАК ДокументПоступления,
	|
	|	-ТаблицаРасхождения.Количество КАК Количество,
	|	-ТаблицаРасхождения.СуммаСНДСУпр КАК Стоимость,
	|	-ТаблицаРасхождения.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	-ТаблицаРасхождения.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаРасхождения.СуммаНДСРегл
	|	КОНЕЦ КАК НДСРегл
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ТаблицаРасхождения.Количество < 0
	|	И НЕ ТаблицаРасхождения.СписатьНаРасходы
	|	И НЕ &ПартионныйУчетВерсии22
	|	И НЕ ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗакупки(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "Закупки";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаВидыЗапасовОприходование", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
#Область УвеличениеЗакупки
	УвеличениеЗакупки = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемкаПодПринципала)
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути)
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Менеджер КАК Менеджер,
	|
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Склад КАК Склад,
	|	ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|   ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Соглашение КАК Соглашение,
	|	&Договор КАК Договор,
	|	ВЫБОР 
	|		КОГДА &ПоступлениеПоЗаказам
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказПоставщику
	|		ИНАЧЕ &ДокументОснование
	|	КОНЕЦ КАК Заказ,
	|
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельностиНоменклатуры,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) 
	|	КОНЕЦ КАК СуммаБезНДС,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ + ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) КАК СуммаРегл,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ КАК СуммаРеглБезНДС,
	|	0 КАК СуммаСкидки,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) 
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) 
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК ДопРасходы,
	|	0 КАК ДопРасходыБезНДС,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов 					КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов - ТаблицаВидыЗапасов.СуммаНДСВзаиморасчетов	КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ ТаблицаВидыЗапасов.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ТаблицаВидыЗапасов.ОбъектРасчетов КАК ИсточникГФУРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаВидыЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО Суммы.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|		И Суммы.ИдентификаторСтроки = ТаблицаВидыЗапасов.ИдентификаторСтроки
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ВариантОтражения В
	|		(ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки),
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУчестьПриИнвентаризации))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути)
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Менеджер КАК Менеджер,
	|
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Соглашение КАК Соглашение,
	|	&Договор КАК Договор,
	|	ВЫБОР 
	|		КОГДА &ПоступлениеПоЗаказам
	|   		ТОГДА ТаблицаВидыЗапасов.ЗаказПоставщику
	|		ИНАЧЕ &ДокументОснование
	|	КОНЕЦ КАК Заказ,
	|
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельностиНоменклатуры,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) 
	|	КОНЕЦ КАК СуммаБезНДС,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ + ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) КАК СуммаРегл,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ КАК СуммаРеглБезНДС,
	|	0 КАК СуммаСкидки,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) 
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) 
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК ДопРасходы,
	|	0 КАК ДопРасходыБезНДС,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов 					КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов - ТаблицаВидыЗапасов.СуммаНДСВзаиморасчетов	КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ТаблицаВидыЗапасов.ОбъектРасчетов КАК ИсточникГФУРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО Суммы.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|		И Суммы.ИдентификаторСтроки = ТаблицаВидыЗапасов.ИдентификаторСтроки
	|
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ВариантОтражения =
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьТоварыУПартнеров)
	|";
#КонецОбласти

#Область УменьшениеЗакупки
	УменьшениеЗакупки =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноПоступления) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Менеджер КАК Менеджер,
	|
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Склад КАК Склад,
	|	ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Соглашение КАК Соглашение,
	|	&Договор КАК Договор,
	|	ВЫБОР 
	|		КОГДА &ПоступлениеПоЗаказам
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказПоставщику
	|		ИНАЧЕ &ДокументОснование
	|	КОНЕЦ КАК Заказ,
	|
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельностиНоменклатуры,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) 
	|	КОНЕЦ КАК СуммаБезНДС,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ + ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) КАК СуммаРегл,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ КАК СуммаРеглБезНДС,
	|	0 КАК СуммаСкидки,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) 
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) 
	|	КОНЕЦ КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК ДопРасходы,
	|	0 КАК ДопРасходыБезНДС,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|	
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	(ТаблицаВидыЗапасов.СуммаВзаиморасчетов - ТаблицаВидыЗапасов.СуммаНДСВзаиморасчетов) КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ ТаблицаВидыЗапасов.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ТаблицаВидыЗапасов.ОбъектРасчетов КАК ИсточникГФУРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаВидыЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО Суммы.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|		И Суммы.ИдентификаторСтроки = ТаблицаВидыЗапасов.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И ТаблицаВидыЗапасов.ВариантОтражения В
	|		(ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки),
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУчестьПриИнвентаризации))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторноПоступления) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Менеджер КАК Менеджер,
	|
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад,
	|	ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Соглашение КАК Соглашение,
	|	&Договор КАК Договор,
	|	ВЫБОР 
	|		КОГДА &ПоступлениеПоЗаказам
	|			ТОГДА ТаблицаВидыЗапасов.ЗаказПоставщику
	|		ИНАЧЕ &ДокументОснование
	|	КОНЕЦ КАК Заказ,
	|
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельностиНоменклатуры,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) 
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) 
	|	КОНЕЦ КАК СуммаБезНДС,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ + ЕСТЬNULL(Суммы.СуммаНДСРегл, 0) КАК СуммаРегл,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ КАК СуммаРеглБезНДС,
	|	0 КАК СуммаСкидки,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА (Суммы.СуммаПриведеннаяУпр + Суммы.СуммаНДСУпр)
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаСНДСУпр, 0) 
	|	КОНЕЦ КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяУпр, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяУпр
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСУпр, 0) 
	|	КОНЕЦ КАК СтоимостьБезНДС,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Суммы.СуммаПриведеннаяРегл, 0) <> 0
	|			ТОГДА Суммы.СуммаПриведеннаяРегл
	|		ИНАЧЕ ЕСТЬNULL(Суммы.СуммаБезНДСРегл, 0) 
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК ДопРасходы,
	|	0 КАК ДопРасходыБезНДС,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ТаблицаВидыЗапасов.СуммаСНДС - ТаблицаВидыЗапасов.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|	
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаВидыЗапасов.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	(ТаблицаВидыЗапасов.СуммаВзаиморасчетов - ТаблицаВидыЗапасов.СуммаНДСВзаиморасчетов) КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА ТаблицаВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ТаблицаВидыЗапасов.ОбъектРасчетов КАК ИсточникГФУРасчетов
	|ИЗ
	|	Документ.КорректировкаПриобретения.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтСуммыДокументовВВалютахУчета КАК Суммы
	|	ПО Суммы.Ссылка = ТаблицаВидыЗапасов.Ссылка
	|		И Суммы.ИдентификаторСтроки = ТаблицаВидыЗапасов.ИдентификаторСтроки
	|ГДЕ
	|	ТаблицаВидыЗапасов.Ссылка = &Ссылка
	|	И ТаблицаВидыЗапасов.ВариантОтражения =
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьТоварыУПартнеров)
	|";
#КонецОбласти

#Область ИзменениеСтоимости
	ИзменениеСтоимости = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Менеджер КАК Менеджер,
	|
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.Склад КАК Склад,
	|	ТаблицаРасхождения.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
	|   ТаблицаРасхождения.ВидЗапасов КАК ВидЗапасов,
	|	
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Соглашение КАК Соглашение,
	|	&Договор КАК Договор,
	|	ВЫБОР 
	|		КОГДА &ПоступлениеПоЗаказам
	|   		ТОГДА ТаблицаРасхождения.ЗаказПоставщику
	|		ИНАЧЕ &ДокументОснование
	|	КОНЕЦ КАК Заказ,
	|
	|	ТаблицаРасхождения.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности КАК НаправлениеДеятельностиНоменклатуры,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|
	|	0 КАК Количество,
	|
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Сумма,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	ТаблицаРасхождения.СуммаСНДСРегл КАК СуммаРегл,
	|	ТаблицаРасхождения.СуммаБезНДСРегл КАК СуммаРеглБезНДС,
	|	0 КАК СуммаСкидки,
	|
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	ТаблицаРасхождения.СуммаБезНДСРегл КАК СтоимостьРегл,
	|	0 КАК ДопРасходы,
	|	0 КАК ДопРасходыБезНДС,
	|
	|	&Валюта КАК ВалютаДокумента,
	|	ТаблицаРасхождения.СуммаСНДС КАК СуммаВВалютеДокумента,
	|	ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС КАК СуммаБезНДСВВалютеДокумента,
	|
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаРасхождения.СуммаВзаиморасчетовСНДС КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаРасхождения.СуммаВзаиморасчетовСНДС - ТаблицаРасхождения.СуммаНДСВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА ТаблицаРасхождения.ВидЗапасов
	|		ИНАЧЕ ТаблицаРасхождения.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры,
	|	ТаблицаРасхождения.ОбъектРасчетов КАК ИсточникГФУРасчетов
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	(ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара)
	|	ИЛИ ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара))
	|	И НЕ ТаблицаРасхождения.СписатьНаРасходы
	|	И (ТаблицаРасхождения.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИЛИ (ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		И НЕ &ИспользуетсяДокументПоступлениеТоваров
	|		И (ТаблицаРасхождения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		ИЛИ НЕ ТаблицаРасхождения.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала))))
	|";
#КонецОбласти

	ТекстЗапроса = УвеличениеЗакупки
		+ " ОБЪЕДИНИТЬ ВСЕ " + УменьшениеЗакупки
		+ " ОБЪЕДИНИТЬ ВСЕ " + ИзменениеСтоимости;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПрочиеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПрочиеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтПартииПрочихРасходов", ТекстыЗапроса) Тогда
		ТекстЗапросаТаблицаВтПартииПрочихРасходов(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаКорректировкаЗадолженности", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКорректировкаЗадолженности(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период											КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)			КАК ВидДвижения,
	|	&Организация									КАК Организация,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.Подразделение
	|	ИНАЧЕ
	|		&Подразделение
	|	КОНЕЦ											КАК Подразделение,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.НаправлениеДеятельности
	|	ИНАЧЕ
	|		&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаРасхождения.СтатьяАктивовПассивов		КАК Статья,
	|	ТаблицаРасхождения.АналитикаАктивовПассивов 	КАК Аналитика,
	|	
	|	ТаблицаРасхождения.СуммаСНДСУпр					КАК Сумма
	|	
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|	
	|ГДЕ
	|	ТаблицаРасхождения.СтатьяАктивовПассивов <> НЕОПРЕДЕЛЕНО 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.Подразделение
	|	ИНАЧЕ
	|		&Подразделение
	|	КОНЕЦ											КАК Подразделение,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.НаправлениеДеятельности
	|	ИНАЧЕ
	|		&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	&СтатьяАктивов КАК Статья,
	|	&АналитикаАктивов КАК Аналитика,
	|
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Сумма
	|
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара))
	|	ИНАЧЕ
	|		ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы))
	|	КОНЕЦ
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|	И &СтатьяАктивов <> НЕОПРЕДЕЛЕНО
	|	И ТаблицаРасхождения.СтатьяРасходов <> НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.Подразделение
	|	ИНАЧЕ
	|		&Подразделение
	|	КОНЕЦ											КАК Подразделение,
	|	ВЫБОР КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|		ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) ТОГДА
	|		ТаблицаРасхождения.НаправлениеДеятельности
	|	ИНАЧЕ
	|		&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	&СтатьяПассивов КАК Статья,
	|	&АналитикаПассивов КАК Аналитика,
	|
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Сумма
	|
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах)
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|	И &СтатьяПассивов <> НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|		ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|		ТОГДА &СтатьяАктивов
	|		ИНАЧЕ &СтатьяПассивов
	|	КОНЕЦ КАК Статья,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|		ТОГДА &АналитикаАктивов
	|		ИНАЧЕ &АналитикаПассивов
	|	КОНЕЦ КАК Аналитика,
	|	
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК Сумма
	|	
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	(КорректировкаЗадолженности.НаПрочиеАктивы
	|		ИЛИ КорректировкаЗадолженности.НаПрочиеПассивы)
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|";
	ТекстЗапроса = ТекстЗапроса + "ОБЪЕДИНИТЬ ВСЕ"
		+ РегистрыНакопления.ПрочиеАктивыПассивы.ТекстЗапросаТаблицаПрочиеАктивыПассивы();
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияПоПрочимАктивамПассивам(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияПоПрочимАктивамПассивам";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаКорректировкаЗадолженности", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКорректировкаЗадолженности(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.Подразделение
	|		ИНАЧЕ
	|			&Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.НаправлениеДеятельности
	|		ИНАЧЕ
	|			&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаРасхождения.СтатьяАктивовПассивов КАК Статья,
	|	ТаблицаРасхождения.АналитикаАктивовПассивов КАК Аналитика,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|	
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаРасхождения.СуммаСНДСРегл
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаРасхождения.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаРасхождения.СуммаСНДСУпр   КАК СуммаСНДС,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	
	|	ТаблицаРасхождения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ТаблицаРасхождения.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|	
	|ГДЕ
	|	ТаблицаРасхождения.СтатьяАктивовПассивов <> НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.Подразделение
	|		ИНАЧЕ
	|			&Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.НаправлениеДеятельности
	|		ИНАЧЕ
	|			&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	&СтатьяАктивов КАК Статья,
	|	&АналитикаАктивов КАК Аналитика,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет) КАК ДебетКредит,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаРасхождения.СуммаСНДСРегл
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаРасхождения.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаРасхождения.СуммаСНДСУпр   КАК СуммаСНДС,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	
	|	ТаблицаРасхождения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ТаблицаРасхождения.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы)
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|	И &СтатьяАктивов <> НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.Подразделение
	|		ИНАЧЕ
	|			&Подразделение
	|	КОНЕЦ КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.НаправлениеДеятельности
	|		ИНАЧЕ
	|			&НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	&СтатьяПассивов КАК Статья,
	|	&АналитикаПассивов КАК Аналитика,
	|	ВЫБОР
	|		КОГДА &СтатьяПассивовАктивПассив = ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
	|	КОНЕЦ КАК ДебетКредит,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаРасхождения.СуммаСНДСРегл
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.ВидДеятельностиНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаРасхождения.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаРасхождения.СуммаБезНДСУпр
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаРасхождения.СуммаСНДСУпр   КАК СуммаСНДС,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	
	|	ТаблицаРасхождения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ТаблицаРасхождения.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах)
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|	И &СтатьяПассивов <> НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|			ТОГДА &СтатьяАктивов
	|		ИНАЧЕ &СтатьяПассивов
	|	КОНЕЦ КАК Статья,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|			ТОГДА &АналитикаАктивов
	|		ИНАЧЕ &АналитикаПассивов
	|	КОНЕЦ КАК Аналитика,
	|	ВЫБОР
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
	|	КОНЕЦ КАК ДебетКредит,
	|
	|	КорректировкаЗадолженности.СуммаСНДСРегл КАК СуммаРегл, 
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК СуммаУпр,
	|	КорректировкаЗадолженности.СуммаСНДСУпр   КАК СуммаСНДС,
	|	КорректировкаЗадолженности.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	
	|	КорректировкаЗадолженности.УникальныйИдентификаторОбъектаРасчетов КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.КорректировкаЗадолженности) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	(КорректировкаЗадолженности.НаПрочиеАктивы
	|		ИЛИ КорректировкаЗадолженности.НаПрочиеПассивы) 
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|			ТОГДА &СтатьяАктивов
	|		ИНАЧЕ &СтатьяПассивов
	|	КОНЕЦ КАК Статья,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|			ТОГДА &АналитикаАктивов
	|		ИНАЧЕ &АналитикаПассивов
	|	КОНЕЦ КАК Аналитика,
	|	ВЫБОР
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Кредит)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДвиженийПрочихАктивовПассивов.Дебет)
	|	КОНЕЦ КАК ДебетКредит,
	|
	|	КорректировкаЗадолженности.НДСРегл КАК СуммаРегл, 
	|	КорректировкаЗадолженности.НДСУпр КАК СуммаУпр,
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаБезНДС,
	|	
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВходящийНДСПоПриобретению) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	(КорректировкаЗадолженности.НаПрочиеАктивы
	|		ИЛИ КорректировкаЗадолженности.НаПрочиеПассивы) 
	|	И &ИспользоватьУчетПрочихАктивовПассивов
	|	И &ЗакупкаПодДеятельность НЕ В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|	И (КорректировкаЗадолженности.НДСРегл <> 0
	|		ИЛИ КорректировкаЗадолженности.НДСУпр <> 0)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияКонтрагентДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ДвиженияКонтрагентДоходыРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаКорректировкаЗадолженности", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаКорректировкаЗадолженности(Запрос, ТекстыЗапроса);
	КонецЕсли;
		
#Область КорректировкаПриобретенияСоСписаниемНаРасходы
	КорректировкаПриобретенияСоСписаниемНаРасходы = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияСоСписаниемНаРасходы) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	ТаблицаРасхождения.Подразделение КАК Подразделение,
	|
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Договор КАК Договор,
	|	ТаблицаРасхождения.ОбъектРасчетов КАК ОбъектРасчетов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|
	|	ТаблицаРасхождения.НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ВЫБОР 
	|		КОГДА ТаблицаРасхождения.СтатьяАктивовПассивов <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ТаблицаРасхождения.СтатьяАктивовПассивов
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|				ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.СтатьяРасходов
	|		ИНАЧЕ 
	|			ВЫБОР
	|				КОГДА &СтатьяРасходов <> НЕОПРЕДЕЛЕНО
	|				ТОГДА &СтатьяРасходов
	|				ИНАЧЕ &СтатьяАктивов
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|				ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ТаблицаРасхождения.АналитикаРасходов
	|		ИНАЧЕ &АналитикаРасходов
	|	КОНЕЦ КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СтатьяАктивовПассивов <> НЕОПРЕДЕЛЕНО
	|		ТОГДА ТаблицаРасхождения.АналитикаАктивовПассивов
	|		ИНАЧЕ &АналитикаАктивов
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|	
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Сумма,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	ТаблицаРасхождения.СуммаСНДСРегл КАК СуммаРегл,
	|	ТаблицаРасхождения.СуммаБезНДСРегл КАК СуммаРеглБезНДС,
	|
	|	&Валюта КАК Валюта,
	|	ТаблицаРасхождения.СуммаСНДС КАК СуммаВВалюте,
	|	ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС КАК СуммаБезНДСВВалюте,
	|		
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаРасхождения.СуммаВзаиморасчетовСНДС КАК СуммаВВалютеВзаиморасчетов,
	|	ТаблицаРасхождения.СуммаВзаиморасчетовСНДС - ТаблицаРасхождения.СуммаНДСВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ТаблицаРасхождения.ОбъектРасчетов КАК ИсточникГФУРасчетов,
	|	ТаблицаРасхождения.ИдентификаторСтроки КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	(ТаблицаРасхождения.СуммаСНДС <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаНДС <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаСНДСУпр <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаБезНДСУпр <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаСНДСРегл <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаБезНДСРегл <> 0)
	|	И ВЫБОР 
	|		КОГДА ((ТаблицаРасхождения.СписатьНаРасходы И ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|					ИЛИ ТаблицаРасхождения.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|					ИЛИ ТаблицаРасхождения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|			И (ТаблицаРасхождения.СтатьяРасходов <> НЕОПРЕДЕЛЕНО 
	|					ИЛИ ТаблицаРасхождения.СтатьяАктивовПассивов <> НЕОПРЕДЕЛЕНО)
	|			ТОГДА
	|				ТаблицаРасхождения.ВариантОтражения В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара),
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара))
	|	ИНАЧЕ
	|		ТаблицаРасхождения.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы))
	|	КОНЕЦ
	|";
#КонецОбласти

#Область КорректировкаПриобретенияСОтражениемНаПрочихДоходах
	КорректировкаПриобретенияСОтражениемНаПрочихДоходах = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияСОтражениемНаПрочихДоходах) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	&Договор КАК Договор,
	|	ТаблицаРасхождения.ОбъектРасчетов КАК ОбъектРасчетов,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|
	|	ТаблицаРасхождения.НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ТОГДА ТаблицаРасхождения.СтатьяДоходов
	|		ИНАЧЕ 
	|			ВЫБОР
	|				КОГДА &СтатьяДоходов <> НЕОПРЕДЕЛЕНО
	|					ТОГДА &СтатьяДоходов
	|				ИНАЧЕ &СтатьяПассивов
	|			КОНЕЦ 
	|	КОНЕЦ КАК СтатьяДоходовРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.СписатьНаРасходы
	|			ТОГДА ТаблицаРасхождения.АналитикаДоходов
	|		ИНАЧЕ &АналитикаДоходов 
	|	КОНЕЦ КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	&АналитикаПассивов КАК АналитикаАктивовПассивов,
	|	
	|	-ТаблицаРасхождения.СуммаСНДСУпр КАК Сумма,
	|	-ТаблицаРасхождения.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	-ТаблицаРасхождения.СуммаСНДСРегл КАК СуммаРегл,
	|	-ТаблицаРасхождения.СуммаБезНДСРегл КАК СуммаРеглБезНДС,
	|
	|	&Валюта КАК Валюта,
	|	-ТаблицаРасхождения.СуммаСНДС КАК СуммаВВалюте,
	|	-(ТаблицаРасхождения.СуммаСНДС - ТаблицаРасхождения.СуммаНДС) КАК СуммаБезНДСВВалюте,
	|		
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	-ТаблицаРасхождения.СуммаВзаиморасчетовСНДС КАК СуммаВВалютеВзаиморасчетов,
	|	-(ТаблицаРасхождения.СуммаВзаиморасчетовСНДС - ТаблицаРасхождения.СуммаНДСВзаиморасчетов) КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	ТаблицаРасхождения.ОбъектРасчетов КАК ИсточникГФУРасчетов,
	|	ТаблицаРасхождения.ИдентификаторСтроки КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	(ТаблицаРасхождения.СуммаСНДС <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаНДС <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаСНДСУпр <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаБезНДСУпр <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаСНДСРегл <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаБезНДСРегл <> 0)
	|	И (ТаблицаРасхождения.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах)
	|		ИЛИ 
	|		(ТаблицаРасхождения.СписатьНаРасходы
	|			И ТаблицаРасхождения.СтатьяДоходов <> НЕОПРЕДЕЛЕНО))
	|";
#КонецОбласти

#Область КорректировкаПриобретенияУвеличениеЗадолженностиСводно
	КорректировкаПриобретенияУвеличениеЗадолженностиСводно = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияСоСписаниемНаРасходы) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	КорректировкаЗадолженности.Договор КАК Договор,
	|	КорректировкаЗадолженности.ОбъектРасчетов КАК ОбъектРасчетов,
	|	КорректировкаЗадолженности.НаправлениеДеятельностиРасчетов КАК НаправлениеДеятельностиКонтрагента,
	|
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ВЫБОР
	|		КОГДА КорректировкаЗадолженности.НаРасходы
	|			ТОГДА &СтатьяРасходов
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|			ТОГДА &СтатьяАктивов
	|	КОНЕЦ КАК СтатьяДоходовРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаРасходы
	|			ТОГДА &АналитикаРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходов,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеАктивы
	|			ТОГДА &АналитикаАктивов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|	
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК Сумма,
	|	КорректировкаЗадолженности.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	КорректировкаЗадолженности.СуммаСНДСРегл КАК СуммаРегл,
	|	КорректировкаЗадолженности.СуммаБезНДСРегл КАК СуммаРеглБезНДС,
	|
	|	&Валюта КАК Валюта,
	|	КорректировкаЗадолженности.СуммаСНДС КАК СуммаВВалюте,
	|	(КорректировкаЗадолженности.СуммаСНДС - КорректировкаЗадолженности.СуммаНДС) КАК СуммаБезНДСВВалюте,
	|		
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	КорректировкаЗадолженности.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	(КорректировкаЗадолженности.СуммаВзаиморасчетов - КорректировкаЗадолженности.СуммаНДСВзаиморасчетов) КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	КорректировкаЗадолженности.ОбъектРасчетов КАК ИсточникГФУРасчетов,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	КорректировкаЗадолженности.НаРасходы ИЛИ КорректировкаЗадолженности.НаПрочиеАктивы
	|";
#КонецОбласти

#Область КорректировкаПриобретенияУменьшениеЗадолженностиСводно
	КорректировкаПриобретенияУменьшениеЗадолженностиСводно = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияСОтражениемНаПрочихДоходах) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|
	|	&Партнер КАК Партнер,
	|	&Контрагент КАК Контрагент,
	|	КорректировкаЗадолженности.Договор КАК Договор,
	|	КорректировкаЗадолженности.ОбъектРасчетов КАК ОбъектРасчетов,
	|	КорректировкаЗадолженности.НаправлениеДеятельностиРасчетов КАК НаправлениеДеятельностиКонтрагента,
	|
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|	ВЫБОР
	|		КОГДА КорректировкаЗадолженности.НаДоходы
	|			ТОГДА &СтатьяДоходов
	|		КОГДА КорректировкаЗадолженности.НаПрочиеПассивы
	|			ТОГДА &СтатьяПассивов
	|	КОНЕЦ КАК СтатьяДоходовРасходов,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаДоходы
	|			ТОГДА &АналитикаДоходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ВЫБОР 
	|		КОГДА КорректировкаЗадолженности.НаПрочиеПассивы
	|			ТОГДА &АналитикаПассивов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|	
	|	КорректировкаЗадолженности.СуммаСНДСУпр КАК Сумма,
	|	КорректировкаЗадолженности.СуммаБезНДСУпр КАК СуммаБезНДС,
	|	КорректировкаЗадолженности.СуммаСНДСРегл КАК СуммаРегл,
	|	КорректировкаЗадолженности.СуммаБезНДСРегл КАК СуммаРеглБезНДС,
	|
	|	&Валюта КАК Валюта,
	|	КорректировкаЗадолженности.СуммаСНДС КАК СуммаВВалюте,
	|	(КорректировкаЗадолженности.СуммаСНДС - КорректировкаЗадолженности.СуммаНДС) КАК СуммаБезНДСВВалюте,
	|		
	|	&ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	КорректировкаЗадолженности.СуммаВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	(КорректировкаЗадолженности.СуммаВзаиморасчетов - КорректировкаЗадолженности.СуммаНДСВзаиморасчетов) КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|
	|	КорректировкаЗадолженности.ОбъектРасчетов КАК ИсточникГФУРасчетов,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторФинЗаписи
	|ИЗ
	|	ВтТаблицаКорректировкаЗадолженности КАК КорректировкаЗадолженности
	|ГДЕ
	|	КорректировкаЗадолженности.НаДоходы ИЛИ КорректировкаЗадолженности.НаПрочиеПассивы
	|";
#КонецОбласти
	ТекстЗапроса = КорректировкаПриобретенияСоСписаниемНаРасходы
		+ " ОБЪЕДИНИТЬ ВСЕ " + КорректировкаПриобретенияСОтражениемНаПрочихДоходах
		+ " ОБЪЕДИНИТЬ ВСЕ " + КорректировкаПриобретенияУвеличениеЗадолженностиСводно
		+ " ОБЪЕДИНИТЬ ВСЕ " + КорректировкаПриобретенияУменьшениеЗадолженностиСводно;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияНоменклатураДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)
	ИмяРегистра = "ДвиженияНоменклатураДоходыРасходы";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияСоСписаниемНаРасходы) КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	ТаблицаРасхождения.Подразделение КАК Подразделение,
	|	ВЫБОР КОГДА &УчитыватьСебестоимостьТоваровПоНазначениям 
	|		ТОГДА ТаблицаРасхождения.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ ТаблицаРасхождения.АналитикаУчетаНоменклатурыБезНазначения
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаРасхождения.Подразделение КАК Склад,
	|	ТаблицаРасхождения.ВидЗапасов КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) КАК ТипЗапасов,
	|
	|	ТаблицаРасхождения.СтатьяРасходов КАК СтатьяДоходовРасходов,
	|	ТаблицаРасхождения.АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|
	|	ТаблицаРасхождения.НаправлениеДеятельности КАК НаправлениеДеятельностиНоменклатуры,
	|	&НаправлениеДеятельности КАК НаправлениеДеятельностиСтатьи,
	|
	|	ТаблицаРасхождения.Количество КАК Количество,
	|	ТаблицаРасхождения.СуммаСНДСУпр КАК Стоимость,
	|	ТаблицаРасхождения.СуммаБезНДСУпр КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА ТаблицаРасхождения.ВидДеятельностиНДС В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА
	|				ТаблицаРасхождения.СуммаБезНДСРегл + ТаблицаРасхождения.СуммаНДСРегл
	|			ИНАЧЕ
	|				ТаблицаРасхождения.СуммаБезНДСРегл
	|	КОНЕЦ КАК СтоимостьРегл,
	|
	|	ВЫБОР
	|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ТОГДА ТаблицаРасхождения.ВидЗапасов
	|		ИНАЧЕ ТаблицаРасхождения.Номенклатура
	|	КОНЕЦ КАК ИсточникГФУНоменклатуры
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	(ТаблицаРасхождения.СуммаСНДС <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаНДС <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаСНДСУпр <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаБезНДСУпр <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаСНДСРегл <> 0
	|		ИЛИ ТаблицаРасхождения.СуммаБезНДСРегл <> 0)
	|	И ТаблицаРасхождения.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаРасхождения.СписатьНаРасходы
	|	И ТаблицаРасхождения.ВариантОтражения В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Ссылка                                 КАК Ссылка,
	|	&Период                                 КАК ДатаДокументаИБ,
	|	&Номер                                  КАК НомерДокументаИБ,
	|	&ИдентификаторМетаданных                КАК ТипСсылки,
	|	&Организация                            КАК Организация,
	|	ВЫБОР
	|		КОГДА &ДокументОснование <> НЕОПРЕДЕЛЕНО
	|			ТОГДА &ХозОперацияОснования
	|		ИНАЧЕ &ХозяйственнаяОперация
	|	КОНЕЦ                                   КАК ХозяйственнаяОперация,
	|	&Партнер                                КАК Партнер,
	|	&Контрагент                             КАК Контрагент,
	|	&Договор                                КАК Договор,
	|	&НаправлениеДеятельности                КАК НаправлениеДеятельности,
	|	&Склад                                  КАК МестоХранения,
	|	&Подразделение                          КАК Подразделение,
	|	&Менеджер                               КАК Ответственный,
	|	&Автор                                  КАК Автор,
	|	&Комментарий                            КАК Комментарий,
	|	&Валюта                                 КАК Валюта,
	|	&СуммаДокумента                         КАК Сумма,
	|	НЕОПРЕДЕЛЕНО                            КАК Статус,
	|	&Проведен                               КАК Проведен,
	|	&ПометкаУдаления                        КАК ПометкаУдаления,
	|	ЛОЖЬ                                    КАК ДополнительнаяЗапись,
	|	&ИнформацияПоДоговору                   КАК Дополнительно,
	|	&ДатаВходящегоДокумента                 КАК ДатаПервичногоДокумента,
	|	&НомерВходящегоДокумента                КАК НомерПервичногоДокумента,
	|	&НаименованиеВходящегоДокумента         КАК НаименованиеПервичногоДокумента,
	|	&Период                                 КАК ДатаОтраженияВУчете,
	|	НЕОПРЕДЕЛЕНО                            КАК Приоритет
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка						КАК Документ,
	|	&Период						КАК Период,
	|	ТаблицаСерии.Номенклатура	КАК Номенклатура,
	|	ТаблицаСерии.Характеристика	КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаСерии.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаСерии.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ						КАК Назначение,
	|	ТаблицаСерии.Серия			КАК Серия,
	|	ТаблицаСерии.Количество		КАК Количество,
	|	&Партнер					КАК Отправитель,
	|	ТаблицаСерии.Склад			КАК Получатель,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаОтПоставщика) КАК СкладскаяОперация,
	|	ИСТИНА						КАК ЭтоСкладскоеДвижение
	|ИЗ
	|	Документ.КорректировкаПриобретения.Товары КАК ТаблицаСерии
	|
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка
	|	И ТаблицаСерии.Количество <> 0
	|	И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента           = "Документ.КорректировкаПриобретения";
	СинонимТаблицыДокумента      = "";
	ВЗапросеЕстьИсточник         = Истина;
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие();
	
	ПереопределениеРасчетаПараметров = Новый Структура;
	ПереопределениеРасчетаПараметров.Вставить("ИнформацияПоДоговору",    """""");
	ПереопределениеРасчетаПараметров.Вставить("НомерВходящегоДокумента", """""");
	ПереопределениеРасчетаПараметров.Вставить(
		"ХозОперацияОснования",
		"ВЫБОР
		|	КОГДА ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОтчетаПереработчика)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОтчетаПереработчика)
		|	ИНАЧЕ ЕСТЬNULL(ДанныеДокумента.ДокументОснование.ХозяйственнаяОперация, ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика))
		|КОНЕЦ");
	ПереопределениеРасчетаПараметров.Вставить("ХозяйственнаяОперация", "ДанныеДокумента.ВидКорректировки");
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных",
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ПолноеИмяДокумента));
	
	Если ИмяРегистра = "РаспределениеЗапасовДвижения" Тогда
		
		ТекстыЗапроса = Новый СписокЗначений();
		СинонимТаблицыДокумента = "ТабЧасть";
		ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, ИмяРегистра);
		ТекстЗапроса = СтрСоединить(ТекстыЗапроса.ВыгрузитьЗначения(), ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	ИначеЕсли ИмяРегистра = "ДвиженияСерийТоваров" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаСерии";
		
	ИначеЕсли ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ВЗапросеЕстьИсточник,
			ПереопределениеРасчетаПараметров);
		
	Иначе
		
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса,
			ПолноеИмяДокумента,
			СинонимТаблицыДокумента,
			ПереопределениеРасчетаПараметров,
			ТекстыЗапросаВременныхТаблиц);
		
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	Результат.ТекстЗапроса = ТекстЗапроса;
	
	Возврат Результат;
	
КонецФункции

Процедура ОтразитьВУчетеНДС(Запрос, ТекстыЗапроса, Регистры)
	
	Если Не УчетНДСУП.ТребуетсяПроведениеПоРегистрамНДС(Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрЗапросаДокументПоступленияНДС(Запрос);
	
	ТекстЦенности =
	"ВЫБРАТЬ
	|	Расхождения.Ссылка.Дата КАК Период,
	|	Расхождения.Ссылка КАК Ссылка,
	|	Расхождения.Ссылка.Организация КАК Организация,
	|	Расхождения.Ссылка.Подразделение КАК Подразделение,
	|	Расхождения.Ссылка.Контрагент КАК Контрагент,
	|	Расхождения.Ссылка.Договор КАК Договор,
	|	Расхождения.Ссылка.ДокументОснование.Грузоотправитель КАК Грузоотправитель,
	|	&ДокументПоступленияНДС КАК ДокументПриобретения,
	|	Расхождения.Ссылка.ДокументОснование КАК ИсходныйТорговыйДокумент,
	|	ВЫБОР
	|		КОГДА Расхождения.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИсправлениеОшибок,
	|	ВЫБОР
	|		КОГДА Расхождения.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КорректировкаПоСогласованиюСторон,
	|	Расхождения.Ссылка КАК ДокументКорректировкиПриобретения,
	|	Расхождения.Ссылка.НалогообложениеНДС КАК НалогообложениеНДС,
	|	Расхождения.Ссылка.ДокументОснование.ЗакупкаПодДеятельность КАК ВидДеятельностиНДС,
	|	Расхождения.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР КОГДА Расхождения.СписатьНаРасходы
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Расхождения.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	Расхождения.ВидЗапасов КАК ВидЗапасов,
	|	Расхождения.НомерГТД КАК НомерГТД,
	|	Расхождения.Подразделение КАК ПодразделениеУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаРасхождения.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) ТОГДА
	|			&НаправлениеДеятельности
	|		ИНАЧЕ ТаблицаРасхождения.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	Расхождения.СписатьНаРасходы КАК СписатьНаРасходы,
	|	Расхождения.СтатьяРасходов КАК СтатьяРасходов,
	|	Расхождения.АналитикаРасходов КАК АналитикаРасходов,
	|	Расхождения.СтатьяАктивовПассивов КАК СтатьяПрочихАктивов,
	|	Расхождения.АналитикаАктивовПассивов КАК АналитикаПрочихАктивов,
	|	Расхождения.Назначение КАК Назначение,
	|	Расхождения.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Расхождения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВходящийНДСПоПриобретению) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК Расхождения
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаПриобретения.Расхождения КАК ТаблицаРасхождения
	|		ПО Расхождения.Ссылка = ТаблицаРасхождения.Ссылка
	|		И Расхождения.НомерСтроки = ТаблицаРасхождения.НомерСтроки
	|ГДЕ
	|	Расхождения.Ссылка = &Ссылка
	|	И (ВЫБОР
	|		КОГДА ЕСТЬNULL(Расхождения.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару, ЛОЖЬ)
	|			ТОГДА Расхождения.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИНАЧЕ
	|			ИСТИНА
	|		КОНЕЦ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаЗадолженности.Ссылка.Дата КАК Период,
	|	КорректировкаЗадолженности.Ссылка КАК Ссылка,
	|	КорректировкаЗадолженности.Ссылка.Организация КАК Организация,
	|	КорректировкаЗадолженности.Ссылка.Подразделение КАК Подразделение,
	|	КорректировкаЗадолженности.Ссылка.Контрагент КАК Контрагент,
	|	КорректировкаЗадолженности.Ссылка.Договор КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК Грузоотправитель,
	|	КорректировкаЗадолженности.ДокументПриобретения КАК ДокументПриобретения,
	|	КорректировкаЗадолженности.ДокументПриобретения КАК ИсходныйТорговыйДокумент,
	|	ЛОЖЬ КАК ИсправлениеОшибок,
	|	ИСТИНА КАК КорректировкаПоСогласованиюСторон,
	|	КорректировкаЗадолженности.Ссылка КАК ДокументКорректировкиПриобретения,
	|	КорректировкаЗадолженности.Ссылка.НалогообложениеНДС КАК НалогообложениеНДС,
	|	КорректировкаЗадолженности.Ссылка.ЗакупкаПодДеятельность КАК ВидДеятельностиНДС,
	|	КорректировкаЗадолженности.СтавкаНДС КАК СтавкаНДС,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
	|	КорректировкаЗадолженности.Ссылка.Подразделение КАК ПодразделениеУчета,
	|	КорректировкаЗадолженности.Ссылка.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЛОЖЬ КАК СписатьНаРасходы,
	|	КорректировкаЗадолженности.Ссылка.СтатьяРасходов КАК СтатьяРасходов,
	|	КорректировкаЗадолженности.Ссылка.АналитикаРасходов КАК АналитикаРасходов,
	|	КорректировкаЗадолженности.Ссылка.СтатьяРасходов КАК СтатьяПрочихАктивов,
	|	КорректировкаЗадолженности.Ссылка.АналитикаАктивов КАК АналитикаПрочихАктивов,
	|	НЕОПРЕДЕЛЕНО КАК Назначение,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	КорректировкаЗадолженности.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВходящийНДСПоПриобретению) КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК КорректировкаЗадолженности
	|";
	
	УчетНДСУП.ОтразитьПриобретениеУПоставщика(Запрос, ТекстыЗапроса, Регистры, ТекстЦенности);
	
КонецПроцедуры

Процедура ДобавитьТекстыОтраженияВзаиморасчетов(Запрос, ТекстыЗапроса, Регистры)
	
	#Область Закупка
	
	ТекстЗакупка = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                                      КАК Ссылка,
		|	Таблица.Ссылка.Организация                                                          КАК Организация,
		|	Таблица.Ссылка.Партнер                                                              КАК Партнер,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                                                КАК НомерРегистратора,
		|	
		|	Таблица.ОбъектРасчетов                                                              КАК ОбъектРасчетов,
		|	Таблица.Ссылка.ДатаПлатежа                                                          КАК ДатаПлатежа,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК ВариантОплаты,
		|	Таблица.ЗаказПоставщику                                                             КАК ЗаказЗакупки,
		|	Таблица.СуммаСНДС                                                                   КАК Сумма,
		|	ВЫБОР КОГДА Таблица.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|		ИЛИ НЕ ВЫБОР КОГДА Таблица.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|					ТОГДА Таблица.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|		ИЛИ ВЫБОР КОГДА Таблица.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|					ТОГДА Таблица.Ссылка.ДокументОснование.ТребуетсяЗалогЗаТару
		|				ИНАЧЕ ИСТИНА
		|			КОНЕЦ
		|			ТОГДА Таблица.СуммаВзаиморасчетов
		|		ИНАЧЕ 0
		|	КОНЕЦ                                                                               КАК СуммаВзаиморасчетов,
		|	ВЫБОР КОГДА Таблица.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|		И ВЫБОР КОГДА Таблица.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|					ТОГДА Таблица.Ссылка.ДокументОснование.ТребуетсяЗалогЗаТару
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ
		|			ТОГДА Таблица.СуммаВзаиморасчетов
		|		ИНАЧЕ 0
		|	КОНЕЦ                                                                               КАК СуммаВзаиморасчетовПоТаре,
		|
		|	Таблица.Ссылка.ПорядокРасчетов                                                      КАК ПорядокРасчетов,
		|	Таблица.Ссылка.ПоступлениеПоЗаказам                                                 КАК НакладнаяПоЗаказам,
		|	Таблица.Ссылка.ВалютаВзаиморасчетов                                                 КАК ВалютаВзаиморасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)             КАК ХозяйственнаяОперация,
		|	Таблица.Ссылка.ФормаОплаты                                                          КАК ФормаОплаты,
		|	Таблица.Ссылка.Валюта                                                               КАК ВалютаДокумента,
		|	Таблица.Ссылка.ДокументОснование.Дата                                               КАК ДатаКурса,
		|	Таблица.Ссылка.ДокументОснование                                                    КАК СвязанныйДокумент	
		|ИЗ
		|	Документ.КорректировкаПриобретения.Расхождения КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|	И Таблица.СуммаСНДС > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                                      КАК Ссылка,
		|	Таблица.Ссылка.Организация                                                          КАК Организация,
		|	Таблица.Ссылка.Партнер                                                              КАК Партнер,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                                                КАК НомерРегистратора,
		|	
		|	Таблица.ОбъектРасчетов                                                              КАК ОбъектРасчетов,
		|	Таблица.Ссылка.ДатаПлатежа                                                          КАК ДатаПлатежа,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК ВариантОплаты,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК ЗаказЗакупки,
		|	Таблица.СуммаКорректировки                                                          КАК Сумма,
		|	Таблица.СуммаВзаиморасчетов                                                         КАК СуммаВзаиморасчетов,
		|	0 КАК СуммаВзаиморасчетовПоТаре,
		|
		|	НЕОПРЕДЕЛЕНО                                                                        КАК ПорядокРасчетов,
		|	ЛОЖЬ                                                                                КАК НакладнаяПоЗаказам,
		|	Таблица.Ссылка.ВалютаВзаиморасчетов                                                 КАК ВалютаВзаиморасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)             КАК ХозяйственнаяОперация,
		|	Таблица.Ссылка.ФормаОплаты                                                          КАК ФормаОплаты,
		|	Таблица.Ссылка.Валюта                                                               КАК ВалютаДокумента,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаКурса,
		|	Таблица.ДокументПриобретения                                                        КАК СвязанныйДокумент	
		|ИЗ
		|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|	И Таблица.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияУвеличениеЗадолженностиСводно)
		|";
	
	#КонецОбласти
	
	#Область УвеличениеПланаОплаты
	
	ТекстПланОплаты = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                                      КАК Ссылка,
		|	Таблица.Ссылка.Организация                                                          КАК Организация,
		|	Таблица.Ссылка.Партнер                                                              КАК Партнер,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                                                КАК НомерРегистратора,
		|	Таблица.Ссылка.ДатаПлатежа                                                          КАК ДатаПлатежа,
		|	
		|	Таблица.ОбъектРасчетов                                                              КАК ОбъектРасчетов,
		|	Таблица.Ссылка.ПорядокРасчетов                                                      КАК ПорядокРасчетов,
		|	Таблица.Ссылка.ПоступлениеПоЗаказам                                                 КАК НакладнаяПоЗаказам,
		|	ИСТИНА                                                                              КАК СверхЗаказа,
		|	Таблица.ЗаказПоставщику                                                             КАК ЗаказЗакупки,
		|	Таблица.СуммаВзаиморасчетов                                                         КАК КОплате,
		|	Таблица.Ссылка.ВалютаВзаиморасчетов                                                 КАК ВалютаВзаиморасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)             КАК ХозяйственнаяОперация,
		|	Таблица.Ссылка.ФормаОплаты                                                          КАК ФормаОплаты,
		|	Таблица.Ссылка.Валюта                                                               КАК ВалютаДокумента,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаКурса,
		|	Неопределено                                                                        КАК ВариантОплаты,
		|	Таблица.Ссылка.ДокументОснование             	                                    КАК СвязанныйДокумент
		|ИЗ
		|	Документ.КорректировкаПриобретения.Расхождения КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|	И Таблица.СуммаСНДС > 0
		|	И (Таблица.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|		ИЛИ Таблица.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|				И ВЫБОР КОГДА Таблица.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|						ТОГДА Таблица.Ссылка.ДокументОснование.ТребуетсяЗалогЗаТару
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИЛИ НЕ ВЫБОР КОГДА Таблица.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|					ТОГДА Таблица.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару
		|				ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИЛИ Таблица.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                                      КАК Ссылка,
		|	Таблица.Ссылка.Организация                                                          КАК Организация,
		|	Таблица.Ссылка.Партнер                                                              КАК Партнер,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                                                КАК НомерРегистратора,
		|	Таблица.Ссылка.ДатаПлатежа                                                          КАК ДатаПлатежа,
		|	
		|	Таблица.ОбъектРасчетов                                                              КАК ОбъектРасчетов,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК ПорядокРасчетов,
		|	ЛОЖЬ                                                                                КАК НакладнаяПоЗаказам,
		|	ИСТИНА                                                                              КАК СверхЗаказа,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК ЗаказЗакупки,
		|	Таблица.СуммаВзаиморасчетов                                                         КАК КОплате,
		|	Таблица.Ссылка.ВалютаВзаиморасчетов                                                 КАК ВалютаВзаиморасчетов,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)             КАК ХозяйственнаяОперация,
		|	Таблица.Ссылка.ФормаОплаты                                                          КАК ФормаОплаты,
		|	Таблица.Ссылка.Валюта                                                               КАК ВалютаДокумента,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаКурса,
		|	НЕОПРЕДЕЛЕНО                                                                        КАК ВариантОплаты,
		|	Таблица.ДокументПриобретения                                                        КАК СвязанныйДокумент
		|ИЗ
		|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|	И Таблица.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияУвеличениеЗадолженностиСводно)
		|";

	#КонецОбласти
	
	ВзаиморасчетыСервер.ПроведениеЗакупки(Запрос, ТекстыЗапроса, Регистры, ТекстЗакупка, ТекстПланОплаты);
	
		ТекстОплата = "
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                                      КАК Ссылка,
		|	Таблица.Ссылка.Организация                                                          КАК Организация,
		|	Таблица.Ссылка.Партнер                                                              КАК Партнер,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                                                КАК НомерРегистратора,
		|	
		|	Таблица.Ссылка.ВалютаВзаиморасчетов                                                 КАК ВалютаВзаиморасчетов,
		|	Таблица.ОбъектРасчетов                                                              КАК ОбъектРасчетов,
		|	-Таблица.СуммаСНДС                                                                  КАК Сумма,
		|	-Таблица.СуммаВзаиморасчетов                                                        КАК СуммаВзаиморасчетов,
		|	0                                                                                   КАК УвеличениеОплачивается,
		|	0                                                                                   КАК УменьшениеОплачивается,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)             КАК ХозяйственнаяОперация,
		|	Неопределено                                                                        КАК СтатьяДвиженияДенежныхСредств,
		|	Неопределено                                                                        КАК ЗаявкаНаРасходованиеДенежныхСредств,
		|	Таблица.Ссылка.ФормаОплаты                                                          КАК ФормаОплаты,
		|	Таблица.Ссылка.Валюта                                                               КАК ВалютаДокумента,
		|	Таблица.Ссылка.ДокументОснование.Дата                                               КАК ДатаКурса,
		|	Таблица.Ссылка.ДокументОснование                                                    КАК СвязанныйДокумент,
		|	Таблица.ОбъектРасчетов.УникальныйИдентификатор                                      КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПустаяСсылка)                    КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	Документ.КорректировкаПриобретения.Расхождения КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|	И Таблица.СуммаСНДС < 0
		|	И (Таблица.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|		ИЛИ Таблица.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
		|				И ВЫБОР КОГДА Таблица.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|						ТОГДА Таблица.Ссылка.ДокументОснование.ТребуетсяЗалогЗаТару
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИЛИ НЕ ВЫБОР КОГДА Таблица.Ссылка.ДокументОснование ССЫЛКА Документ.ПриобретениеТоваровУслуг
		|					ТОГДА Таблица.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару
		|				ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ
		|		ИЛИ Таблица.Номенклатура.ТипНоменклатуры ЕСТЬ NULL)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Таблица.Ссылка                                                                      КАК Ссылка,
		|	Таблица.Ссылка.Организация                                                          КАК Организация,
		|	Таблица.Ссылка.Партнер                                                              КАК Партнер,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаРегистратора,
		|	Таблица.Ссылка.Номер                                                                КАК НомерРегистратора,
		|	
		|	Таблица.Ссылка.ВалютаВзаиморасчетов                                                 КАК ВалютаВзаиморасчетов,
		|	Таблица.ОбъектРасчетов                                                              КАК ОбъектРасчетов,
		|	Таблица.СуммаКорректировки                                                          КАК Сумма,
		|	Таблица.СуммаВзаиморасчетов                                                         КАК СуммаВзаиморасчетов,
		|	0                                                                                   КАК УвеличениеОплачивается,
		|	0                                                                                   КАК УменьшениеОплачивается,
		|
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаЗадолженности)             КАК ХозяйственнаяОперация,
		|	Неопределено                                                                        КАК СтатьяДвиженияДенежныхСредств,
		|	Неопределено                                                                        КАК ЗаявкаНаРасходованиеДенежныхСредств,
		|	Таблица.Ссылка.ФормаОплаты                                                          КАК ФормаОплаты,
		|	Таблица.Ссылка.Валюта                                                               КАК ВалютаДокумента,
		|	Таблица.Ссылка.Дата                                                                 КАК ДатаКурса,
		|	Таблица.Ссылка.ДокументОснование                                                    КАК СвязанныйДокумент,
		|	Таблица.ОбъектРасчетов.УникальныйИдентификатор                                      КАК ИдентификаторФинЗаписи,
		|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПустаяСсылка)                    КАК НастройкаХозяйственнойОперации
		|	
		|ИЗ
		|	Документ.КорректировкаПриобретения.КорректировкаЗадолженности КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В (&Ссылка)
		|	И Таблица.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияУменьшениеЗадолженностиСводно)
		|";
	
	ВзаиморасчетыСервер.ПроведениеОплатыПоставщику(Запрос, ТекстыЗапроса, Регистры, ТекстОплата);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыУслугиКОформлениюОтчетовКомитентуОЗакупках";
	
	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтТаблицаРасхожденияТоваров", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|   ТаблицаРасхождения.Номенклатура КАК Номенклатура,
	|   ТаблицаРасхождения.Характеристика КАК Характеристика,
	|	&Ссылка КАК ДокументПриобретения,
	|	ТаблицаРасхождения.Назначение КАК Назначение,
	|	СУММА(ТаблицаРасхождения.Количество) КАК Количество,
	|	СУММА(ТаблицаРасхождения.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	&ВалютаВзаиморасчетов КАК Валюта,
	|	ТаблицаРасхождения.НомерГТД КАК НомерГТД,
	|	СУММА(ТаблицаРасхождения.СуммаСНДС) КАК СуммаСНДС,
	|	СУММА(ТаблицаРасхождения.СуммаНДС) КАК СуммаНДС,
	|	ЛОЖЬ КАК Первичное,
	|	ТаблицаРасхождения.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ТаблицаРасхождения.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации
	|ИЗ
	|	ВтТаблицаРасхожденияТоваров КАК ТаблицаРасхождения
	|ГДЕ
	|	&ДокументОснованиеЯвляетсяПриобретением
	|	И &ХозОперацияОснования В (&ХозОперацииПоставкаПодПринципала)
	|	И ТаблицаРасхождения.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|	И (ВЫБОР ТаблицаРасхождения.ТипНоменклатуры 
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар) ТОГДА ИСТИНА
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА ИСТИНА
	|			КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара) ТОГДА НЕ &ВернутьМногооборотнуюТару
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ)	
	|	И НЕ &ИспользуетсяДокументПоступлениеТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРасхождения.Номенклатура,
	|	ТаблицаРасхождения.Характеристика,
	|	ТаблицаРасхождения.Назначение,
	|	ТаблицаРасхождения.НомерГТД,
	|	ТаблицаРасхождения.ИдентификаторФинЗаписи,
	|	ТаблицаРасхождения.НастройкаХозяйственнойОперации
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Процедура ОтразитьРаспределениеЗапасовДвижения(Запрос, ТекстыЗапроса, Регистры)
	
	ТекстЗапросаТабЧасть =
		"ВЫБРАТЬ
		|	ТабЧасть.Ссылка         КАК Ссылка,
		|	ТабЧасть.Ссылка.Дата    КАК Период,
		|	ТабЧасть.Номенклатура   КАК Номенклатура,
		|	ТабЧасть.Характеристика КАК Характеристика,
		|	
		|	ВЫБОР КОГДА ТабЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
		|				ТабЧасть.Подразделение
		|			ИНАЧЕ
		|				ТабЧасть.Склад
		|		КОНЕЦ КАК Склад,
		|	
		|	ТабЧасть.Назначение     КАК Назначение,
		|	ТабЧасть.Количество     КАК Количество,
		|	ЛОЖЬ                    КАК ПоГрафику,
		|	НЕОПРЕДЕЛЕНО            КАК РаспоряжениеВГрафике
		|ИЗ
		|	Документ.КорректировкаПриобретения.Расхождения КАК ТабЧасть
		|ГДЕ
		|	ТабЧасть.ВариантОтражения В(
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьЗакупкуУвеличитьСкладскиеОстатки),
		|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьЗакупкуУменьшитьСкладскиеОстатки))
		|		ИЛИ ТабЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|				И НЕ ТабЧасть.СписатьНаРасходы";
	
	РаспределениеЗапасовДвижения.ПриходЗапаса(Запрос, ТекстыЗапроса, Регистры, ТекстЗапросаТабЧасть);
	
КонецПроцедуры

#Область ПартионныйУчет

Функция ОписаниеРегистровУчетаЗатратИСебестоимости(Документ) Экспорт
	
	ОписаниеРегистров = Новый Массив;
	ОписаниеРегистров.Добавить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	
	Возврат ОписаниеРегистров;
	
КонецФункции

Функция УстановитьДополнительныеПараметрыОперацийУчетаЗатратИСебестоимости(Документ, Запрос = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Массив;
	ДополнительныеПараметры.Добавить("ДокументПоступленияНДС");
	
	Если Запрос <> Неопределено Тогда
		УстановитьПараметрЗапросаДокументПоступленияНДС(Запрос);
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция СформироватьДополнительныеТаблицыОперацийУчетаЗатратИСебестоимости(Документ, Запрос = Неопределено, ТекстыЗапроса = Неопределено) Экспорт
	
	ДополнительныеТаблицы = Новый Массив;
	ДополнительныеТаблицы.Добавить("ВтТаблицаВидыЗапасовОприходование");
	ДополнительныеТаблицы.Добавить("ВтТаблицаВидыЗапасовСписание");
	ДополнительныеТаблицы.Добавить("ВтТаблицаРасхожденияТоваров");
	
	Если Запрос <> Неопределено Тогда
	
		Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса(ДополнительныеТаблицы[0], ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаВидыЗапасовОприходование(Запрос, ТекстыЗапроса);
		КонецЕсли;
		
		Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса(ДополнительныеТаблицы[1], ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаВидыЗапасовСписание(Запрос, ТекстыЗапроса);
		КонецЕсли;
		
		Если НЕ ПроведениеДокументов.ЕстьТаблицаЗапроса(ДополнительныеТаблицы[2], ТекстыЗапроса) Тогда
			ТекстЗапросаВтТаблицаРасхожденияТоваров(Запрос, ТекстыЗапроса);
		КонецЕсли;
		
		
	КонецЕсли;
	
	Возврат ДополнительныеТаблицы;
	
КонецФункции

Функция ОписаниеОперацийУчетаСебестоимости(Документ) Экспорт
	
	ОписаниеОпераций = Новый Массив;
	
	#Область ВнешнееПоступление_Товар
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 		КАК Период,
	|	ТаблицаДокумента.Ссылка 	КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА &ДокументОснование
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 						КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО				КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 				КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация						КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка
	|	  И НЕ ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|		ТОГДА &ДокументПоступленияНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 												КАК Партия,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22
	|		ТОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 												КАК ВидДеятельностиНДС,
	|	ТаблицаДокумента.НалогообложениеНДС					КАК ВидДеятельностиНДСДокумента,
	|
	// Поля аналитики учета партий
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 											КАК АналитикаУчетаПартий,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22 И НЕ &ФИФОСкользящаяОценка
	|		ТОГДА ТаблицаВидыЗапасов.АналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 											КАК КорАналитикаУчетаПартий,
	|	ТаблицаДокумента.Партнер		КАК Поставщик,
	|	ТаблицаДокумента.Контрагент 	КАК Контрагент,
	|	ТаблицаВидыЗапасов.СтавкаНДС	КАК СтавкаНДС,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО					КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО					КАК КорВидЗапасов,
	|
	// Поля аналитики финансового учета
	|	ТаблицаДокумента.Сделка			КАК Сделка,
	|	ТаблицаДокумента.Подразделение 	КАК Подразделение,
	|	ТаблицаДокумента.Менеджер 		КАК Менеджер,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество					КАК Количество,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки			КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА 0 
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	КОНЕЦ 											КАК Стоимость,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА 0 
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|			ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ 											КАК СтоимостьРегл,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 0 
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|			ТОГДА 0
	|			ИНАЧЕ ТаблицаВидыЗапасов.СуммаНДСРегл
	|	КОНЕЦ								 			КАК НДСРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК СтоимостьУпр,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 0 
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|			ТОГДА 0
	|			ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр - ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ КАК НДСУпр,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|		ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ 											КАК СтоимостьЗабалансовая,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|		ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ 											КАК СтоимостьЗабалансоваяРегл,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	ИНАЧЕ 0 КОНЕЦ) КАК ДопРасходы,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	ИНАЧЕ 0 КОНЕЦ) КАК ДопРасходыБезНДС,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА (ВЫБОР
	|		 		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|				 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|					ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|				ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл КОНЕЦ)
	|	ИНАЧЕ 0 КОНЕЦ) КАК ДопРасходыРегл,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА (ВЫБОР
	|		 		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|				 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|					ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|				ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр КОНЕЦ)
	|	ИНАЧЕ 0 КОНЕЦ) КАК ДопРасходыУпр,
	|	0 												КАК ПостояннаяРазница,
	|	0 												КАК ВременнаяРазница,
	|	0												КАК КорСтоимость,
	|
	// Дополнительные поля
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		КОГДА ТаблицаВидыЗапасов.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ 											КАК РазделУчета,
	|
	// Прочие поля
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки)
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияПрошлогоПериода)
	|		КОГДА ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемкаПодПринципала)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|	КОНЕЦ 												КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи			КАК ИдентификаторФинЗаписи,
	|	ТаблицаВидыЗапасов.НастройкаХозяйственнойОперации	КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаВидыЗапасовОприходование КАК ТаблицаВидыЗапасов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И НЕ (НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|		И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		И ТаблицаВидыЗапасов.Количество = 0)";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнешнееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти

	#Область ВыбытиеПоФиксированнойСтоимости_ВозвратПоставщику
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 	КАК Период,
	|	ТаблицаДокумента.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА &ДокументОснование
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 					КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 			КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация 					КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры 	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов 					КАК ВидЗапасов,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (&ТипыНалогообложенияНДСУчитываетсяВСтоимости)
	|		ТОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС
	|		ИНАЧЕ &НалогообложениеОрганизации
	|	КОНЕЦ 											КАК ВидДеятельностиНДС,
	|	ТаблицаДокумента.НалогообложениеНДС				КАК ВидДеятельностиНДСДокумента,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО 								  	КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 								  	КАК КорВидЗапасов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров) КАК СтатьяРасходовАктивов,
	|	ТаблицаДокумента.Партнер 						КАК АналитикаРасходовАктивов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.РазницыСтоимостиВозвратаИФактическойСтоимостиТоваров)  КАК СтатьяДоходов,
	|	ТаблицаДокумента.Партнер 						КАК АналитикаДоходов,
	|
	// Поля аналитики финансового учета
	|	ТаблицаДокумента.Сделка 		КАК Сделка,
	|	ТаблицаДокумента.Подразделение 	КАК Подразделение,
	|	ТаблицаДокумента.Менеджер 		КАК Менеджер,
	|	НЕОПРЕДЕЛЕНО 					КАК ГруппаПродукции,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество 				КАК Количество,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки 		КАК ИдентификаторСтроки,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|		ТОГДА 0 
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	КОНЕЦ                                       КАК Стоимость,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|		ТОГДА 0 
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ                                       КАК СтоимостьБезНДС,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 0
	|		КОГДА &ЗакупкаПодДеятельность В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ                                       КАК СтоимостьРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА 0
	|		КОГДА &ЗакупкаПодДеятельность В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|			ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ                                       КАК СтоимостьУпр,
	|	0											КАК КорСтоимость,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|		ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ                                   КАК СтоимостьЗабалансовая,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|		ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ 0
	|	КОНЕЦ                                   КАК СтоимостьЗабалансоваяРегл,
	|
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.СобственныеТоварыВПути)
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НеотфактурованныеПоставки)
	|		КОГДА ТаблицаВидыЗапасов.ЦеховаяКладовая
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаХраненииСПравомПродажи)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|	КОНЕЦ 																	КАК РазделУчета,
	|
	// Прочие поля
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки)
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|		 И НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияПрошлогоПериода)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику) КОНЕЦ КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи 								КАК ИдентификаторФинЗаписи,
	|	ТаблицаВидыЗапасов.НастройкаХозяйственнойОперации						КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ТоварНаХраненииСПравомПродажи),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути))
	|	И ТаблицаВидыЗапасов.ЭтоВозвратПоставщику";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВыбытиеПоФиксированнойСтоимости,
		ТекстЗапроса);
	
	#КонецОбласти

	#Область ВнешнееПоступление_Товар_НеВозвратПоставщику
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 		КАК Период,
	|	ТаблицаДокумента.Ссылка 	КАК Ссылка,
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов В
	|		  (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке),
	|		   ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути))
	|		 И ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО				КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 				КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация						КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка
	|	 ИЛИ ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 												КАК Партия,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС				КАК ВидДеятельностиНДС,
	|	ТаблицаДокумента.НалогообложениеНДС					КАК ВидДеятельностиНДСДокумента,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорВидЗапасов,
	|
	// Поля аналитики учета партий
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК КорАналитикаУчетаПартий,
	|	ТаблицаДокумента.Партнер		КАК Поставщик,
	|	ТаблицаДокумента.Контрагент 	КАК Контрагент,
	|	ТаблицаВидыЗапасов.СтавкаНДС	КАК СтавкаНДС,
	|
	// Поля аналитики финансового учета
	|	ТаблицаДокумента.Сделка			КАК Сделка,
	|	ТаблицаДокумента.Подразделение 	КАК Подразделение,
	|	ТаблицаДокумента.Менеджер 		КАК Менеджер,
	|
	// Количественные и суммовые показатели
	|	-ТаблицаВидыЗапасов.Количество					КАК Количество,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки			КАК ИдентификаторСтроки,
	|	-ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	КОНЕЦ											КАК Стоимость,
	|	-ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ											КАК СтоимостьБезНДС,
	|	-ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		КОГДА &ЗакупкаПодДеятельность В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ 											КАК СтоимостьРегл,
	|	-ТаблицаВидыЗапасов.СуммаНДСРегл	 			КАК НДСРегл,
	|	-ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		КОГДА &ЗакупкаПодДеятельность В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ											КАК СтоимостьУпр,
	|	-(ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		- ТаблицаВидыЗапасов.СуммаБезНДСУпр) 		КАК НДСУпр,
	|	0												КАК СтоимостьЗабалансовая,
	|	0												КАК СтоимостьЗабалансоваяРегл,
	|	-ВЫБОР
	|		КОГДА НЕ ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	КОНЕЦ											КАК ДопРасходы,
	|	-ВЫБОР
	|		КОГДА НЕ ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ											КАК ДопРасходыБезНДС,
	|	-ВЫБОР
	|		КОГДА НЕ ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		КОГДА &ЗакупкаПодДеятельность В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|	  			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ 											КАК ДопРасходыРегл,
	|	-ВЫБОР
	|		КОГДА НЕ ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		КОГДА &ЗакупкаПодДеятельность В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ											КАК ДопРасходыУпр,
	|	0 												КАК ПостояннаяРазница,
	|	ТаблицаВидыЗапасов.ВременнаяРазница 			КАК ВременнаяРазница,
	|	0												КАК КорСтоимость,
	|
	// Прочие поля
	|	(ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке)
	|		 И ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки)
	|		КОГДА ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		 И ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику) КОНЕЦ) КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи							  КАК ИдентификаторФинЗаписи,
	|	ТаблицаВидыЗапасов.НастройкаХозяйственнойОперации					  КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаВидыЗапасовСписание КАК ТаблицаВидыЗапасов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути))
	|	И НЕ (НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|		И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		И ТаблицаВидыЗапасов.Количество = 0)
	|	И НЕ ТаблицаВидыЗапасов.ЭтоВозвратПоставщику";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнешнееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти

	#Область ВнешнееПоступление_Корректировка
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 		КАК Период,
	|	ТаблицаДокумента.Ссылка 	КАК Ссылка,
	|	&ДокументОснование			КАК ДокументИсточник,
	|	&ПериодПоступления			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 				КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация						КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	ВЫБОР КОГДА &ПартионныйУчетВерсии22 И &ФИФОСкользящаяОценка
	|	 ИЛИ ТаблицаВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|		ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 												КАК Партия,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС				КАК ВидДеятельностиНДС,
	|	ТаблицаДокумента.НалогообложениеНДС					КАК ВидДеятельностиНДСДокумента,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорВидЗапасов,
	|
	// Поля аналитики учета партий
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК КорАналитикаУчетаПартий,
	|	ТаблицаДокумента.Партнер		КАК Поставщик,
	|	ТаблицаДокумента.Контрагент 	КАК Контрагент,
	|	ТаблицаВидыЗапасов.СтавкаНДС	КАК СтавкаНДС,
	|
	// Поля аналитики финансового учета
	|	ТаблицаДокумента.Сделка			КАК Сделка,
	|	ТаблицаДокумента.Подразделение 	КАК Подразделение,
	|	ТаблицаДокумента.Менеджер 		КАК Менеджер,
	|
	// Количественные и суммовые показатели
	|	0												КАК Количество,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки			КАК ИдентификаторСтроки,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ НЕ &ПартионныйУчетВерсии22
	|		ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ 											КАК Стоимость,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ НЕ &ПартионныйУчетВерсии22
	|		ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ 											КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) И &ПартионныйУчетВерсии22
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|			ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ 											КАК СтоимостьРегл,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаНДСРегл
	|	КОНЕЦ								 			КАК НДСРегл,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК СтоимостьУпр,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр - ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК НДСУпр,
	|	0												КАК СтоимостьЗабалансовая,
	|	0												КАК СтоимостьЗабалансоваяРегл,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ НЕ &ПартионныйУчетВерсии22
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	КОНЕЦ 											КАК ДопРасходы,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ НЕ &ПартионныйУчетВерсии22
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК ДопРасходыБезНДС,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ НЕ &ПартионныйУчетВерсии22
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ 											КАК ДопРасходыРегл,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК ДопРасходыУпр,
	|	0 												КАК ПостояннаяРазница,
	|	ТаблицаВидыЗапасов.ВременнаяРазница 			КАК ВременнаяРазница,
	|	0												КАК КорСтоимость,
	|
	// Прочие поля
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияПрошлогоПериода)
	|	КОНЕЦ 												КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи			КАК ИдентификаторФинЗаписи,
	|	ТаблицаВидыЗапасов.НастройкаХозяйственнойОперации	КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаРасхожденияТоваров КАК ТаблицаВидыЗапасов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И (ТаблицаВидыЗапасов.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УменьшитьСтоимостьТовара)
	|		ИЛИ (ТаблицаВидыЗапасов.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара)
	|			И &ПартионныйУчетВерсии22)
	|		ИЛИ (НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути)
	|			И ТаблицаВидыЗапасов.Количество = 0
	|			И ТаблицаВидыЗапасов.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ЗакупкаУПоставщика)
	|			)
	|		)
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварПоНеотфактурованнойПоставке),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.СобственныйТоварВПути))
	|	И НЕ ТаблицаВидыЗапасов.СписатьНаРасходы";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнешнееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти

	#Область ВнешнееПоступление_Корректировка_ПУ21
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 		КАК Период,
	|	ТаблицаДокумента.Ссылка 	КАК Ссылка,
	|	НЕОПРЕДЕЛЕНО				КАК ДокументИсточник,
	|	НЕОПРЕДЕЛЕНО				КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 				КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация						КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	&ДокументОснование 									КАК Партия,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС				КАК ВидДеятельностиНДС,
	|	ТаблицаДокумента.НалогообложениеНДС					КАК ВидДеятельностиНДСДокумента,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорВидЗапасов,
	|
	// Поля аналитики учета партий
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК КорАналитикаУчетаПартий,
	|	ТаблицаДокумента.Партнер		КАК Поставщик,
	|	ТаблицаДокумента.Контрагент 	КАК Контрагент,
	|	ТаблицаВидыЗапасов.СтавкаНДС	КАК СтавкаНДС,
	|
	// Поля аналитики финансового учета
	|	ТаблицаДокумента.Сделка			КАК Сделка,
	|	ТаблицаДокумента.Подразделение 	КАК Подразделение,
	|	ТаблицаДокумента.Менеджер 		КАК Менеджер,
	|
	// Количественные и суммовые показатели
	|	0												КАК Количество,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки			КАК ИдентификаторСтроки,
	|	0 												КАК Стоимость,
	|	0 												КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|	  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|	  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|		ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ 											КАК СтоимостьРегл,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаНДСРегл
	|	КОНЕЦ								 			КАК НДСРегл,
	|	0 												КАК СтоимостьУпр,
	|	0 												КАК НДСУпр,
	|	0												КАК СтоимостьЗабалансовая,
	|	0												КАК СтоимостьЗабалансоваяРегл,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр					КАК ДопРасходы,
	|	ТаблицаВидыЗапасов.СуммаБезНДСУпр				КАК ДопРасходыБезНДС,
	|	0 												КАК ДопРасходыРегл,
	|	0 												КАК ДопРасходыУпр,
	|	0 												КАК ПостояннаяРазница,
	|	ТаблицаВидыЗапасов.ВременнаяРазница 			КАК ВременнаяРазница,
	|	0												КАК КорСтоимость,
	|
	// Прочие поля
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)	КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи						КАК ИдентификаторФинЗаписи,
	|	ТаблицаВидыЗапасов.НастройкаХозяйственнойОперации				КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаРасхожденияТоваров КАК ТаблицаВидыЗапасов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаВидыЗапасов.ВариантОтражения = ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.УвеличитьСтоимостьТовара)
	|	И ТаблицаВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И НЕ ТаблицаВидыЗапасов.СписатьНаРасходы
	|	И НЕ &ПартионныйУчетВерсии22";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнешнееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти

	#Область ВнешнееПоступление_Корректировка_Работа
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 		КАК Период,
	|	ТаблицаДокумента.Ссылка 	КАК Ссылка,
	|	&ДокументОснование			КАК ДокументИсточник,
	|	&ПериодПоступления			КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 				КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация						КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	&ДокументОснование  								КАК Партия,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС				КАК ВидДеятельностиНДС,
	|	ТаблицаДокумента.НалогообложениеНДС					КАК ВидДеятельностиНДСДокумента,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорВидЗапасов,
	|
	// Поля аналитики учета партий
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК КорАналитикаУчетаПартий,
	|	ТаблицаДокумента.Партнер		КАК Поставщик,
	|	ТаблицаДокумента.Контрагент 	КАК Контрагент,
	|	ТаблицаВидыЗапасов.СтавкаНДС	КАК СтавкаНДС,
	|
	// Поля аналитики финансового учета
	|	ТаблицаДокумента.Сделка			КАК Сделка,
	|	ТаблицаДокумента.Подразделение 	КАК Подразделение,
	|	ТаблицаДокумента.Менеджер 		КАК Менеджер,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество					КАК Количество,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки			КАК ИдентификаторСтроки,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ ТаблицаВидыЗапасов.Количество > 0
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		КОГДА ТаблицаВидыЗапасов.Количество < 0
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ 											КАК Стоимость,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ ТаблицаВидыЗапасов.Количество > 0
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|		КОГДА ТаблицаВидыЗапасов.Количество < 0
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ 											КАК СтоимостьБезНДС,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  И ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ 											КАК СтоимостьРегл,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаНДСРегл
	|	КОНЕЦ								 			КАК НДСРегл,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  И ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК СтоимостьУпр,
	|	ВЫБОР КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр - ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК НДСУпр,
	|	0												КАК СтоимостьЗабалансовая,
	|	0												КАК СтоимостьЗабалансоваяРегл,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.Количество <> 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаСНДСУпр
	|	КОНЕЦ 											КАК ДопРасходы,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.Количество <> 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК ДопРасходыБезНДС,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ ТаблицаВидыЗапасов.Количество <> 0
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаБезНДСРегл + ТаблицаВидыЗапасов.СуммаНДСРегл
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|	КОНЕЦ 											КАК ДопРасходыРегл,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ ТаблицаВидыЗапасов.Количество <> 0
	|			ТОГДА 0
	|		КОГДА ТаблицаВидыЗапасов.ВидДеятельностиНДС В (
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|		  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА ТаблицаВидыЗапасов.СуммаСНДСУпр
	|		ИНАЧЕ ТаблицаВидыЗапасов.СуммаБезНДСУпр
	|	КОНЕЦ 											КАК ДопРасходыУпр,
	|	0 												КАК ПостояннаяРазница,
	|	0 												КАК ВременнаяРазница,
	|	0												КАК КорСтоимость,
	|
	// Дополнительные поля
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты) КАК РазделУчета,
	|
	// Прочие поля
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ ТаблицаВидыЗапасов.Количество >= 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|		КОГДА ТаблицаВидыЗапасов.Количество < 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияПрошлогоПериода)
	|	КОНЕЦ 												КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи			КАК ИдентификаторФинЗаписи,
	|	ТаблицаВидыЗапасов.НастройкаХозяйственнойОперации	КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаРасхожденияТоваров КАК ТаблицаВидыЗапасов
	|	ПО ИСТИНА
	|
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И ТаблицаВидыЗапасов.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ ТаблицаВидыЗапасов.СписатьНаРасходы
	|	И (ТаблицаВидыЗапасов.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ИЛИ
	|		ТаблицаВидыЗапасов.Назначение.ТипНазначения <> ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала))
	|	И НЕ ТаблицаДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаОтчетаПереработчика)
	|	И НЕ ТаблицаВидыЗапасов.ВариантОтражения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.СписатьНаРасходы),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОтраженияКорректировокПоступлений.ОтразитьНаПрочихДоходах))
	|";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнешнееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти
	
	
	#Область ВнешнееПоступление_Корректировка_Забалансовая
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	// Описание документа
	|	ТаблицаДокумента.Дата 		КАК Период,
	|	ТаблицаДокумента.Ссылка 	КАК Ссылка,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  И ТаблицаВидыЗапасов.Количество = 0
	|		ТОГДА &ДокументОснование
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 						КАК ДокументИсточник,
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  И ТаблицаВидыЗапасов.Количество = 0
	|		ТОГДА &ПериодПоступления
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 						КАК ПериодДокументаИсточника,
	|	НЕОПРЕДЕЛЕНО 				КАК КорректируемыйДокумент,
	|
	// Поля учета номенклатуры
	|	ТаблицаДокумента.Организация						КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры		КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов						КАК ВидЗапасов,
	|	&ДокументОснование 									КАК Партия,
	|	ТаблицаВидыЗапасов.ВидДеятельностиНДС				КАК ВидДеятельностиНДС,
	|	ТаблицаДокумента.НалогообложениеНДС					КАК ВидДеятельностиНДСДокумента,
	|
	// Корреспондирующие поля
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО 								  		КАК КорВидЗапасов,
	|
	// Поля аналитики учета партий
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК АналитикаУчетаПартий,
	|	ТаблицаВидыЗапасов.АналитикаУчетаПартий			КАК КорАналитикаУчетаПартий,
	|	ТаблицаДокумента.Партнер		КАК Поставщик,
	|	ТаблицаДокумента.Контрагент 	КАК Контрагент,
	|	ТаблицаВидыЗапасов.СтавкаНДС	КАК СтавкаНДС,
	|
	// Поля аналитики финансового учета
	|	ТаблицаДокумента.Сделка			КАК Сделка,
	|	ТаблицаДокумента.Подразделение 	КАК Подразделение,
	|	ТаблицаДокумента.Менеджер 		КАК Менеджер,
	|
	// Количественные и суммовые показатели
	|	ТаблицаВидыЗапасов.Количество					КАК Количество,
	|	ТаблицаВидыЗапасов.ИдентификаторСтроки			КАК ИдентификаторСтроки,
	|	0 												КАК Стоимость,
	|	0 												КАК СтоимостьБезНДС,
	|	0 												КАК СтоимостьРегл,
	|	0								 				КАК НДСРегл,
	|	0 												КАК СтоимостьУпр,
	|	0 												КАК НДСУпр,
	|	ТаблицаВидыЗапасов.СуммаСНДСУпр					КАК СтоимостьЗабалансовая,
	|	ТаблицаВидыЗапасов.СуммаБезНДСРегл
	|		+ ТаблицаВидыЗапасов.СуммаНДСРегл			КАК СтоимостьЗабалансоваяРегл,
	|	0 												КАК ДопРасходы,
	|	0 												КАК ДопРасходыБезНДС,
	|	0 												КАК ДопРасходыРегл,
	|	0 												КАК ДопРасходыУпр,
	|	0 												КАК ПостояннаяРазница,
	|	0 												КАК ВременнаяРазница,
	|	0												КАК КорСтоимость,
	|
	// Прочие поля
	|	ВЫБОР КОГДА НАЧАЛОПЕРИОДА(&ПериодПоступления, МЕСЯЦ) = НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ)
	|	  ИЛИ ТаблицаВидыЗапасов.Количество > 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПриемкаПодПринципала)
	|		КОГДА ТаблицаВидыЗапасов.Количество < 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровПоставщику)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПриобретенияПрошлогоПериода)
	|	КОНЕЦ 												КАК ХозяйственнаяОперация,
	|	ТаблицаВидыЗапасов.ИдентификаторФинЗаписи			КАК ИдентификаторФинЗаписи,
	|	ТаблицаВидыЗапасов.НастройкаХозяйственнойОперации	КАК НастройкаХозяйственнойОперации
	|
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК ТаблицаДокумента
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаРасхожденияТоваров КАК ТаблицаВидыЗапасов
	|		ПО ИСТИНА
	|ГДЕ
	|	ТаблицаДокумента.Ссылка В (&Ссылка)
	|	И НЕ ТаблицаВидыЗапасов.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И ТаблицаВидыЗапасов.Назначение.ТипНазначения = ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ПоставкаПодПринципала)";
	
	РасчетСебестоимостиПроведениеДокументов.ДобавитьОписаниеОперацииДляОтраженияВУчетеСебестоимости(
		ОписаниеОпераций,
		Перечисления.ОперацииУчетаСебестоимости.ВнешнееПоступление,
		ТекстЗапроса);
	
	#КонецОбласти

	Возврат ОписаниеОпераций;
	
КонецФункции


#КонецОбласти

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Задание на размещение товаров
	Обработки.ПечатьЗаданияНаОтборРазмещениеТоваров.ДобавитьКомандуПечати(КомандыПечати, "ЗаданиеНаРазмещение");
	
	КорректировкаПриобретенияЛокализация.ДобавитьКомандыПечати(КомандыПечати);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьТоварыПоИсходнымДанным(ДокументОснование, Товары) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	*
	|ИЗ ИсходныеДанные КАК ИсходныеДанные
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИсходныеДанные.НомерСтроки
	|";
	
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура СформироватьВременнуюТаблицуИсходныхДанных(МенеджерВременныхТаблиц, ДокументОснование) Экспорт
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымПриобретениеТоваровУслуг();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов") Тогда
		ТекстЗапросаПоОснованию = ТекстЗапросаПоИсходнымДаннымПриобретениеУслугПрочихАктивов();
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаПриобретения.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДанныеПоследнейКорректировки
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.РеестрДокументов КАК РеестрДокументовСторноИсправление
	|	ПО
	|		КорректировкаПриобретения.Ссылка = РеестрДокументовСторноИсправление.СторнируемыйДокумент
	|		И РеестрДокументовСторноИсправление.Проведен
	|		И НЕ РеестрДокументовСторноИсправление.ДополнительнаяЗапись
	|ГДЕ
	|	КорректировкаПриобретения.Проведен
	|	И КорректировкаПриобретения.ДокументОснование = &ДокументОснование
	|	И РеестрДокументовСторноИсправление.Ссылка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаПриобретения.МоментВремени УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки		 	 КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураПартнера КАК НоменклатураПартнера,
	|	ТаблицаТовары.Номенклатура		 	 КАК Номенклатура,
	|	ТаблицаТовары.Назначение			 КАК Назначение,
	|	ТаблицаТовары.Характеристика	 	 КАК Характеристика,
	|	ТаблицаТовары.Серия				 	 КАК Серия,
	|	ТаблицаТовары.СтатусУказанияСерий	 КАК СтатусУказанияСерий,
	|	ТаблицаТовары.Содержание		 	 КАК Содержание,
	|	ТаблицаТовары.Упаковка			 	 КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок 	 КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество		 	 КАК Количество,
	|	ТаблицаТовары.КоличествоПоРНПТ	 	 КАК КоличествоПоРНПТ,
	|	ТаблицаТовары.Цена				 	 КАК Цена,
	|	ТаблицаТовары.Сумма				 	 КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС			 	 КАК СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС			 	 КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС			 	 КАК СуммаСНДС,
	|	ТаблицаТовары.ЗаказПоставщику	 	 КАК ЗаказПоставщику,
	|	ТаблицаТовары.КодСтроки			 	 КАК КодСтроки,
	|	ТаблицаТовары.Склад				 	 КАК Склад,
	|	ТаблицаТовары.НомерГТД			 	 КАК НомерГТД,
	|	ТаблицаТовары.Сделка			 	 КАК Сделка,
	|	ТаблицаТовары.Подразделение			 КАК Подразделение,
	|	ТаблицаТовары.НомерГруппыЗатрат      КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.СписатьНаРасходы		 КАК СписатьНаРасходы,
	|	ТаблицаТовары.СтатьяРасходов		 КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаРасходов		 КАК АналитикаРасходов,
	|	ТаблицаТовары.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаТовары.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ТаблицаТовары.АналитикаДоходов КАК АналитикаДоходов
	|	
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	Документ.КорректировкаПриобретения.Товары КАК ТаблицаТовары
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|	ПО ТаблицаТовары.Ссылка = ДанныеПоследнейКорректировки.Ссылка
	|";
	
	ТекстЗапроса = ТекстЗапроса + "
	|ОБЪЕДИНИТЬ ВСЕ
	|" +ТекстЗапросаПоОснованию + "
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеПоследнейКорректировки
	|";
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Выполнить();
	
КонецПроцедуры

Функция ТекстЗапросаПоИсходнымДаннымПриобретениеТоваровУслуг()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки			 КАК НомерСтроки,
	|	ТаблицаТовары.НоменклатураПартнера КАК НоменклатураПартнера,
	|	ТаблицаТовары.Номенклатура		 	 КАК Номенклатура,
	|	ТаблицаТовары.Назначение		 	 КАК Назначение,
	|	ТаблицаТовары.Характеристика	 	 КАК Характеристика,
	|	ТаблицаТовары.Серия				 	 КАК Серия,
	|	ВЫБОР КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|		ТОГДА 14
	|		ИНАЧЕ 0
	|	КОНЕЦ							 	 КАК СтатусУказанияСерий,
	|	""""							 	 КАК Содержание,
	|	ТаблицаТовары.Упаковка			 	 КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок 	 КАК КоличествоУпаковок,
	|	ТаблицаТовары.Количество		 	 КАК Количество,
	|	ТаблицаТовары.КоличествоПоРНПТ	 	 КАК КоличествоПоРНПТ,
	|	
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ТаблицаТовары.Цена * ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(31,2)) = ТаблицаТовары.Сумма
	|			ТОГДА ТаблицаТовары.Цена
	|		КОГДА ВЫРАЗИТЬ(ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(31,2)) > 0
	|			ТОГДА ВЫРАЗИТЬ(ТаблицаТовары.Сумма / ТаблицаТовары.КоличествоУпаковок КАК ЧИСЛО(31,2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Цена,
	|	
	|	ТаблицаТовары.Сумма				 	 КАК Сумма,
	|	ТаблицаТовары.СтавкаНДС			 	 КАК СтавкаНДС,
	|	ТаблицаТовары.СуммаНДС			 	 КАК СуммаНДС,
	|	ТаблицаТовары.СуммаСНДС			 	 КАК СуммаСНДС,
	|	ТаблицаТовары.ЗаказПоставщику	 	 КАК ЗаказПоставщику,
	|	ТаблицаТовары.КодСтроки			 	 КАК КодСтроки,
	|	ТаблицаТовары.Склад				 	 КАК Склад,
	|	ТаблицаТовары.НомерГТД			 	 КАК НомерГТД,
	|	ТаблицаТовары.Сделка			 	 КАК Сделка,
	|	ТаблицаТовары.Подразделение			 КАК Подразделение,
	|	0                                    КАК НомерГруппыЗатрат,
	|	ТаблицаТовары.СписатьНаРасходы		 КАК СписатьНаРасходы,
	|	ТаблицаТовары.СтатьяРасходов		 КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаРасходов		 КАК АналитикаРасходов,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаДоходов
	|ИЗ
	|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаТовары.Ссылка = &ДокументОснование
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПоИсходнымДаннымПриобретениеУслугПрочихАктивов()
	
	ТекстЗапроса = "	
	|ВЫБРАТЬ
	|	ТаблицаРасходы.НомерСтроки                                   КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)    КАК НоменклатураПартнера,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)               КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)                 КАК Назначение,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)          КАК Серия,
	|	0                                                            КАК СтатусУказанияСерий,
	|	ТаблицаРасходы.Содержание                                    КАК Содержание,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)   КАК Упаковка,
	|	ТаблицаРасходы.Количество                                    КАК КоличествоУпаковок,
	|	ТаблицаРасходы.Количество                                    КАК Количество,
	|	0                                                            КАК КоличествоПоРНПТ,
	|	ТаблицаРасходы.Цена                                          КАК Цена,
	|	ТаблицаРасходы.Сумма                                         КАК Сумма,
	|	ТаблицаРасходы.СтавкаНДС                                     КАК СтавкаНДС,
	|	ТаблицаРасходы.СуммаНДС                                      КАК СуммаНДС,
	|	ТаблицаРасходы.СуммаСНДС                                     КАК СуммаСНДС,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)              КАК ЗаказПоставщику,
	|	0                                                            КАК КодСтроки,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)                     КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)                  КАК НомерГТД,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)           КАК Сделка,
	|	ТаблицаРасходы.Подразделение                                 КАК Подразделение,
	|	0                                                            КАК НомерГруппыЗатрат,
	|	ИСТИНА                                                       КАК СписатьНаРасходы,
	|	ТаблицаРасходы.СтатьяРасходов                                КАК СтатьяРасходов,
	|	ТаблицаРасходы.АналитикаРасходов                             КАК АналитикаРасходов,
	|	ТаблицаРасходы.НаправлениеДеятельности                       КАК НаправлениеДеятельности,
	|	ТаблицаРасходы.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ТаблицаРасходы.АналитикаДоходов КАК АналитикаДоходов
	|ИЗ
	|	Документ.ПриобретениеУслугПрочихАктивов.Расходы КАК ТаблицаРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеПоследнейКорректировки КАК ДанныеПоследнейКорректировки
	|		ПО ИСТИНА
	|ГДЕ
	|	ДанныеПоследнейКорректировки.Ссылка ЕСТЬ NULL 
	|	И ТаблицаРасходы.Ссылка = &ДокументОснование
	|";
	Возврат ТекстЗапроса;
	
КонецФункции


Функция СуммыПоЗаказам(СсылкаОбъект) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Товары.СуммаСНДС           КАК Сумма,
	|	Товары.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	Товары.Номенклатура        КАК Номенклатура,
	|	&ВернутьМногооборотнуюТару КАК ВернутьМногооборотнуюТару,
	|	&ТребуетсяЗалогЗаТару      КАК ТребуетсяЗалогЗаТару
	|ПОМЕСТИТЬ ВТТовары
	|ИЗ &Таблица КАК Товары
	|ГДЕ &УсловиеСсылка
	|;
	|ВЫБРАТЬ 
	|	Неопределено                                     КАК Заказ,
	|	ДАТАВРЕМЯ(1,1,1)                                 КАК ДатаЗаказа,
	|	ДАТАВРЕМЯ(1,1,1)                                 КАК ДатаСогласования,
	|	ЛОЖЬ                                             КАК СверхЗаказа,
	|	СУММА(ВложенныйЗапрос.СуммаПлатежа)              КАК СуммаПлатежа,
	|	СУММА(ВложенныйЗапрос.СуммаВзаиморасчетов)       КАК СуммаВзаиморасчетов,
	|	СУММА(ВложенныйЗапрос.СуммаЗалогаЗаТару)         КАК СуммаЗалогаЗаТару,
	|	СУММА(ВложенныйЗапрос.СуммаВзаиморасчетовПоТаре) КАК СуммаВзаиморасчетовПоТаре
	|ИЗ (ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Товары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|					ИЛИ НЕ Товары.ВернутьМногооборотнуюТару
	|				ТОГДА Товары.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ                             КАК СуммаПлатежа,
	|		ВЫБОР
	|			КОГДА Товары.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|					ИЛИ НЕ Товары.ВернутьМногооборотнуюТару
	|				ТОГДА Товары.СуммаВзаиморасчетов
	|			ИНАЧЕ 0 
	|		КОНЕЦ                             КАК СуммаВзаиморасчетов,
	|		ВЫБОР 
	|			КОГДА Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				И Товары.ТребуетсяЗалогЗаТару
	|				ТОГДА Товары.Сумма
	|			ИНАЧЕ 0 
	|		КОНЕЦ                            КАК СуммаЗалогаЗаТару,
	|		ВЫБОР 
	|			КОГДА Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				И Товары.ТребуетсяЗалогЗаТару
	|				ТОГДА Товары.СуммаВзаиморасчетов
	|			ИНАЧЕ 0 
	|		КОНЕЦ                            КАК СуммаВзаиморасчетовПоТаре
	|	ИЗ ВТТовары КАК Товары) КАК ВложенныйЗапрос
	|;";
	
	Если ТипЗнч(СсылкаОбъект) = Тип("ДокументСсылка.КорректировкаПриобретения") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Таблица", "Документ.КорректировкаПриобретения.Расхождения");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ВернутьМногооборотнуюТару", "ЕСТЬNULL(Товары.Ссылка.ДокументОснование.ВернутьМногооборотнуюТару,ЛОЖЬ)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТребуетсяЗалогЗаТару", "ЕСТЬNULL(Товары.Ссылка.ДокументОснование.ТребуетсяЗалогЗаТару,ЛОЖЬ)");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСсылка", "Товары.Ссылка = &Ссылка");
		Запрос.УстановитьПараметр("Ссылка",СсылкаОбъект);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеСсылка", "ИСТИНА");
		
		Если ЗначениеЗаполнено(СсылкаОбъект.ДокументОснование)
			И ТипЗнч(СсылкаОбъект.ДокументОснование) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
			ВернутьМногооборотнуюТару = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаОбъект.ДокументОснование,"ВернутьМногооборотнуюТару");
			ТребуетсяЗалогЗаТару = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаОбъект.ДокументОснование,"ТребуетсяЗалогЗаТару");
		Иначе
			ВернутьМногооборотнуюТару = Ложь;
			ТребуетсяЗалогЗаТару = Ложь;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ВернутьМногооборотнуюТару", ВернутьМногооборотнуюТару);
		Запрос.УстановитьПараметр("ТребуетсяЗалогЗаТару", ТребуетсяЗалогЗаТару);
		Запрос.УстановитьПараметр("Таблица", СсылкаОбъект.Расхождения);
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Документы.КорректировкаПриобретения.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.8.28";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a3f4d416-c211-4c1d-b133-119f0364d235");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Документы.КорректировкаПриобретения.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Заполняет реквизит ""Ставка НДС"". Заполняет реквизит ""Объект расчетов"". Заполняет реквизит ""Хозяйственная операция"".
	|Заполняет реквизит ""Настройка счетов учета"".'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.КорректировкаПриобретения.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.СтавкиНДС.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Документы.КорректировкаПриобретения.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.КорректировкаПриобретения.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

#Область УТ_11_2_2_СкорректироватьРасчетыСПоставщикамиРесурсКПоступлениюОтложенно

Функция ДокументыДляПереносаДвиженийРасчетыСПоставщиками() Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщиками.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|ГДЕ
	|	РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаПриобретения
	|	И РасчетыСПоставщиками.КПоступлению <> 0
	|");
	
	Результат = Запрос.Выполнить().Выгрузить();
	Возврат Результат.ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = "Документ.КорректировкаПриобретения";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Дата УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	ОбъектыРасчетовСервер.ДополнитьДополнительныеИсточникиДанных(ПараметрыВыборки, ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаПриобретения.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаПриобретения КАК КорректировкаПриобретения
	|ГДЕ
	|	(КорректировкаПриобретения.ЗакупкаПодДеятельность = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И КорректировкаПриобретения.ДокументОснование <> НЕОПРЕДЕЛЕНО)
	|
	|	ИЛИ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Документ.КорректировкаПриобретения.Товары КАК КорректировкаПриобретенияТовары
	|		ГДЕ
	|			КорректировкаПриобретения.Ссылка = КорректировкаПриобретенияТовары.Ссылка
	|			И (КорректировкаПриобретенияТовары.УдалитьСтавкаНДС <> &ПустаяСтавкаНДС
	|			И КорректировкаПриобретенияТовары.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			))
	|	ИЛИ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Документ.КорректировкаПриобретения.Расхождения КАК КорректировкаПриобретенияРасхождения
	|		ГДЕ
	|			КорректировкаПриобретения.Ссылка = КорректировкаПриобретенияРасхождения.Ссылка
	|			И КорректировкаПриобретения.Ссылка.Проведен
	|			И (КорректировкаПриобретенияРасхождения.УдалитьСтавкаНДС <> &ПустаяСтавкаНДС
	|			И КорректировкаПриобретенияРасхождения.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ИЛИ КорректировкаПриобретенияРасхождения.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|			))
	|	ИЛИ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			Документ.КорректировкаПриобретения.ВидыЗапасов КАК КорректировкаПриобретенияВидыЗапасов
	|		ГДЕ
	|			КорректировкаПриобретения.Ссылка = КорректировкаПриобретенияВидыЗапасов.Ссылка
	|			И КорректировкаПриобретения.Ссылка.Проведен
	|			И (КорректировкаПриобретенияВидыЗапасов.УдалитьСтавкаНДС <> &ПустаяСтавкаНДС
	|			И КорректировкаПриобретенияВидыЗапасов.СтавкаНДС = ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)
	|			ИЛИ КорректировкаПриобретенияВидыЗапасов.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)))
	|	ИЛИ КорректировкаПриобретения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|";

	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());
	УчетНДСЛокализация.УстановитьПараметрЗапросаПустаяСтавкаНДСПеречислением(Запрос);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Для Каждого Документ Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Документ.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Документ.Ссылка.ПолучитьОбъект(); // ДокументОбъект
			
			ОбъектИзменен = Ложь;
			
			Если ДокументОбъект <> Неопределено Тогда
				
				Если Не ЗначениеЗаполнено(ДокументОбъект.ЗакупкаПодДеятельность) И ЗначениеЗаполнено(ДокументОбъект.ДокументОснование) Тогда
					ДокументОбъект.ЗакупкаПодДеятельность = 
						ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ДокументОснование, "ЗакупкаПодДеятельность");
					ОбъектИзменен = Истина;
				КонецЕсли;
				
				МассивТЧ = Новый Массив();
				МассивТЧ.Добавить("Товары");
				МассивТЧ.Добавить("Расхождения");
				МассивТЧ.Добавить("ВидыЗапасов");
				
				УчетНДСЛокализация.ЗаполнитьКолонкуТЧСтавкаНДС(ДокументОбъект, МассивТЧ, ОбъектИзменен);
				
				ТабЧастиВидыЗапасов = Новый Соответствие();
				ТабЧастиВидыЗапасов.Вставить("ВидыЗапасов", "ЗаказПоставщику");
				
				ОбъектыРасчетовСервер.ЗаполнитьОбъектыРасчетов(ДокументОбъект, ОбъектИзменен, ТабЧастиВидыЗапасов);
			
				Если НЕ ЗначениеЗаполнено(ДокументОбъект.ХозяйственнаяОперация) Тогда
						ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
					ОбъектИзменен = Истина;
				КонецЕсли;
			
				
				
			КонецЕсли;
			
			Если ОбъектИзменен ИЛИ ДокументОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДокументОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Документ.Ссылка);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Документ.Ссылка);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
