#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект,ДанныеЗаполнения);
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
	ПересчетТоваровЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	УстановитьПривилегированныйРежим(Истина);
	
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект,
		НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПересчетТоваров));
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Если РежимПроведения = РежимПроведенияДокумента.Оперативный Тогда
			Дата = ПолучитьОперативнуюОтметкуВремени();
		КонецЕсли;
		
		ПрошлыйСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
		
		// --> Евлахов Игорь Николаевич (Начало) 24.09.2024
		// Задача #4584
		// Начало было
		//Если (Не ЗначениеЗаполнено(ПрошлыйСтатус) 
		//		Или ПрошлыйСтатус = Перечисления.СтатусыПересчетовТоваров.Подготовлено)
		//	И (Статус = Перечисления.СтатусыПересчетовТоваров.ВнесениеРезультатов
		//		Или Статус = Перечисления.СтатусыПересчетовТоваров.Выполнено
		//			И СкладыСервер.ИспользоватьСтатусыПересчетовТоваров(Склад)) Тогда
		//		
		//	Если ЗначениеЗаполнено(Ссылка) Тогда 
		//		ТекстСообщения = НСтр("ru = 'Невозможно провести документ ""%Документ%"" в статусе ""%НовыйСтатус%"". Необходимо сначала провести документ в статусе ""%СтатусВРаботе%"".'");
		//		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Документ%", Ссылка);
		//	Иначе
		//		ТекстСообщения = НСтр("ru = 'Невозможно провести документ в статусе ""%НовыйСтатус%"". Необходимо сначала провести документ в статусе ""%СтатусВРаботе%"".'");
		//	КонецЕсли;
		//	
		//	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НовыйСтатус%", Статус);
		//	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СтатусВРаботе%", Перечисления.СтатусыПересчетовТоваров.ВРаботе);
		//	
		//	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		//	
		//	Если Отказ Тогда 
		//		Возврат;
		//	КонецЕсли;
		//	
		//КонецЕсли;
		//				
		//Если Статус = Перечисления.СтатусыПересчетовТоваров.Подготовлено
		//		И РежимЗаписи = РежимЗаписиДокумента.Проведение
		//	Или Статус = Перечисления.СтатусыПересчетовТоваров.ВРаботе
		//	Или Статус = Перечисления.СтатусыПересчетовТоваров.ВнесениеРезультатов Тогда
		//	
		//	Если СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, Дата) Тогда
		//		ПроверитьЯчейкиПередБлокировкой(Отказ);
		//	Иначе
		//		ПроверитьДругиеПересчеты(Отказ);
		//	КонецЕсли;
		//	
		//КонецЕсли;		
		// Конец было
		ИмяСвойстваОбмена = КР_УТ10_УТ11_ЗагрузкаДанныхСервер.СвойствоОбъектБылЗагружен();
		
		ЕстьОшибкаСменыСтатуса = Ложь;
		СледующийСтатус = Перечисления.СтатусыПересчетовТоваров.ВРаботе;
		
		Если Не ДополнительныеСвойства.Свойство(ИмяСвойстваОбмена) Тогда			
			Если ПрошлыйСтатус = Перечисления.СтатусыПересчетовТоваров.ВРаботе Тогда
				СледующийСтатус = Перечисления.СтатусыПересчетовТоваров.злКПроверке;
			ИначеЕсли ПрошлыйСтатус = Перечисления.СтатусыПересчетовТоваров.злКПроверке Тогда
				СледующийСтатус = Перечисления.СтатусыПересчетовТоваров.Выполнено;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ПрошлыйСтатус)
				И (Статус = Перечисления.СтатусыПересчетовТоваров.злКПроверке
				ИЛИ Статус = Перечисления.СтатусыПересчетовТоваров.Выполнено
				И СкладыСервер.ИспользоватьСтатусыПересчетовТоваров(Склад)) Тогда
				
				ЕстьОшибкаСменыСтатуса = Истина;
			ИначеЕсли (ПрошлыйСтатус = Перечисления.СтатусыПересчетовТоваров.ВРаботе)
				И (Статус = Перечисления.СтатусыПересчетовТоваров.Выполнено
					И СкладыСервер.ИспользоватьСтатусыПересчетовТоваров(Склад)) Тогда
				ЕстьОшибкаСменыСтатуса = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьОшибкаСменыСтатуса Тогда
			Если ЗначениеЗаполнено(Ссылка) Тогда
				ТекстШаблона = НСтр("ru = 'Невозможно провести документ ""%1"" в статусе ""%2"".'") + Символы.НПП
					 		 + НСтр("ru = 'Необходимо сначала провести документ в статусе ""%3"".'");
				ТекстСообщения = СтрШаблон(ТекстШаблона, Ссылка, Статус, СледующийСтатус);				
			Иначе
				ТекстШаблона = НСтр("ru = 'Невозможно провести документ в статусе ""%1"".'") + Символы.НПП
					 		 + НСтр("ru = 'Необходимо сначала провести документ в статусе ""%2"".'");
				ТекстСообщения = СтрШаблон(ТекстШаблона, Статус, СледующийСтатус);				
			КонецЕсли;
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
			Если Отказ Тогда 
				Возврат;
			КонецЕсли;	
		КонецЕсли;
				
		Если Статус = Перечисления.СтатусыПересчетовТоваров.ВРаботе
				И РежимЗаписи = РежимЗаписиДокумента.Проведение
			ИЛИ Статус = Перечисления.СтатусыПересчетовТоваров.злКПроверке Тогда
			
			ИспользоватьАдресноеХранение = Ложь;
		
			Если ЗначениеЗаполнено(Помещение) Тогда
				Если СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, Дата) Тогда
					ИспользоватьАдресноеХранение = Истина;		
				КонецЕсли;
			Иначе
				ВидСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "КР_ВидСклада");
				РежимРаботыПоВсемПомещениям = Не ЗначениеЗаполнено(Помещение)
						И ВидСклада = Перечисления.КР_ВидыСкладов.Магазин;
						
				Если РежимРаботыПоВсемПомещениям Тогда
					ИспользоватьАдресноеХранение = Истина;		
				КонецЕсли;
			КонецЕсли;
	
			Если ИспользоватьАдресноеХранение Тогда
				ПроверитьЯчейкиПередБлокировкой(Отказ);
			Иначе
				ПроверитьДругиеПересчеты(Отказ);
			КонецЕсли;			
		КонецЕсли;
		// Задача #4584
		// <-- Евлахов Игорь Николаевич (Конец) 25.09.2024
		
		Если Не УчетныеДанныеЗаполнены
			И Не Отказ Тогда
			
			ЗаполнитьКоличествоПоУчету();
			
		КонецЕсли;
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		УчетныеДанныеЗаполнены = Ложь;
	КонецЕсли;
	
	ПересчетТоваровЛокализация.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
	// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
	КР_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения);
	// >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
	СкладыСервер.ОтразитьСостоянияПересчетовЯчеек(ЭтотОбъект.Ссылка, ЭтотОбъект.Статус, Отказ);
	
	ПересчетТоваровЛокализация.ОбработкаПроведения(ЭтотОбъект, Отказ, РежимПроведения);
	
	// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
	КР_ОбработкаПроведения(Отказ);
	// >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
	ПересчетТоваровЛокализация.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ);
	
	// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
	Если Статус = Перечисления.СтатусыПересчетовТоваров.Выполнено Тогда
		КР_АвтоматическоеСозданиеСкладскихАктов.ПометитьНаУдалениеСвязанныеСкладскиеАкты(ЭтотОбъект, Отказ);
	КонецЕсли;
	// >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	УчетныеДанныеЗаполнены = Ложь;
	
	ИнициализироватьДокумент();

	ПересчетТоваровЛокализация.ПриКопировании(ЭтотОбъект, ОбъектКопирования);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// --> Евлахов Игорь Николаевич (Начало) 25.09.2024
	// Задача #4584
	// Начало было
	//Если
	//	// << 20.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-2281
	//	Не ДополнительныеСвойства.Свойство(КР_УТ10_УТ11_ЗагрузкаДанныхСервер.СвойствоОбъектБылЗагружен()) И
	//	// >> 20.09.2023 Марченко С.Н., КРОК, JIRA№A2105505-2281
	//	НЕ Проведен
	//		И (Статус = Перечисления.СтатусыПересчетовТоваров.ВнесениеРезультатов
	// 	ИЛИ Статус = Перечисления.СтатусыПересчетовТоваров.Выполнено)
	// 		И СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, Дата) Тогда
	//	
	//	ТекстСообщения = НСтр("ru = 'Нельзя проводить документ сразу в статусе ""%ТекущийСтатус%"". Необходимо сначала провести документ в предыдущем статусе.'");
	//	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекущийСтатус%",Статус);
	//	
	//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект, "Статус", "Объект", Отказ);
	//	
	//КонецЕсли;
	// Конец было
	// Задача #4584
	// <-- Евлахов Игорь Николаевич (Конец) 25.09.2024
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИменаПолейССуффиксом.Вставить("Количество", "КоличествоФакт");
	ПараметрыПроверки.ИменаПолейССуффиксом.Вставить("КоличествоУпаковок", "КоличествоУпаковокФакт");
	ПараметрыПроверки.ПроверитьВозможностьОкругления = Ложь;
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ СкладыСервер.ИспользоватьСкладскиеПомещения(Склад,Дата) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Помещение");
	КонецЕсли;
	
	Если НЕ ИспользоватьОтдельнуюЯчейкуИзлишков Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЯчейкаКонсолидацииИзлишковТоваров");
	КонецЕсли;
	Если НЕ ИспользоватьОтдельнуюЯчейкуПорчи Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЯчейкаКонсолидацииИспорченныхТоваров");
	КонецЕсли;

	// --> Евлахов Игорь Николаевич (Начало) 25.09.2024
	// Задача #4584
	// Начало было
	//ИспользоватьАдресноеХранение = СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, Дата);
	// Конец было
	ИспользоватьАдресноеХранение = Ложь;
		
	Если ЗначениеЗаполнено(Помещение) Тогда
		Если СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, Дата) Тогда
			ИспользоватьАдресноеХранение = Истина;		
		КонецЕсли;
	Иначе
		ВидСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "КР_ВидСклада");
		РежимРаботыПоВсемПомещениям = Не ЗначениеЗаполнено(Помещение)
				И ВидСклада = Перечисления.КР_ВидыСкладов.Магазин;
				
		Если РежимРаботыПоВсемПомещениям Тогда
			ИспользоватьАдресноеХранение = Истина;		
		КонецЕсли;
	КонецЕсли;
	// Задача #4584
	// <-- Евлахов Игорь Николаевич (Конец) 25.09.2024
	
	Если Не ИспользоватьАдресноеХранение Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Упаковка");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Ячейка");
		
	ИначеЕсли Не ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры") Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Упаковка");
		
		ТекстСообщения = НСтр("ru='В настройках программы не включено использование упаковок номенклатуры, 
			|поэтому нельзя оформить документ по складу с адресным хранением остатков. Обратитесь к администратору'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	Иначе
		НоменклатураСервер.ПроверитьЗаполнениеУпаковок(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ)
	КонецЕсли;
	
	Если ИспользоватьАдресноеХранение Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
		
	КонецЕсли;
	
	Если НЕ УчетныеДанныеЗаполнены Тогда
		
		КлючевыеРеквизиты = Новый Массив;
		КлючевыеРеквизиты.Добавить("Номенклатура");
		КлючевыеРеквизиты.Добавить("Характеристика");
		КлючевыеРеквизиты.Добавить("Назначение");
		КлючевыеРеквизиты.Добавить("Серия");
		КлючевыеРеквизиты.Добавить("Ячейка");
		КлючевыеРеквизиты.Добавить("Упаковка");
		
		ОбщегоНазначенияУТ.ПроверитьНаличиеДублейСтрокТЧ(ЭтотОбъект, "Товары", КлючевыеРеквизиты, Отказ)
		
	КонецЕсли;
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(
		ЭтотОбъект,
		МассивНепроверяемыхРеквизитов,
		Отказ);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
												НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПересчетТоваров),
												Отказ,
												МассивНепроверяемыхРеквизитов);
												
	// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
	КР_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	// >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	ПересчетТоваровЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент()

	Дата          = ТекущаяДатаСеанса();
	Ответственный = Пользователи.ТекущийПользователь();
	
	Склад = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Склад);
	
	ИспользуетсяАдресноеХранение = СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение);
	ИспользоватьСтатусыПересчетовТоваров = СкладыСервер.ИспользоватьСтатусыПересчетовТоваров(Склад);
	Если Не ИспользоватьСтатусыПересчетовТоваров Тогда
		Статус = Перечисления.СтатусыПересчетовТоваров.Выполнено;
	// --> Евлахов Игорь Николаевич (Начало) 24.09.2024
	// Задача #4584
	// Начало было
	//ИначеЕсли ИспользуетсяАдресноеХранение Тогда
	//	Статус = Перечисления.СтатусыПересчетовТоваров.Подготовлено;
	// Конец было
	// <-- Евлахов Игорь Николаевич (Конец) 24.09.2024
	Иначе
		Статус = Перечисления.СтатусыПересчетовТоваров.ВРаботе;
	КонецЕсли;
	
	СтруктураЯчеек = Документы.ПересчетТоваров.ПолучитьЯчейкиИзлишковИБракаПоУмолчанию(Ссылка, Склад, Помещение);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураЯчеек);

	ПечататьКоличествоПоУчету = Документы.ПересчетТоваров.ПолучитьЗначениеПризнакаПечататьКоличествоПоУчетуПоУмолчанию(Ссылка, Ответственный);
	
КонецПроцедуры

Процедура ЗаполнитьКоличествоПоУчету()
	
	Запрос = Новый Запрос;
	
	// --> Евлахов Игорь Николаевич (Начало) 25.07.2024
	// Задача #4485
	ИспользоватьАдресноеХранение = Ложь;
		
	Если ЗначениеЗаполнено(Помещение) Тогда
		Если СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, Дата) Тогда
			ИспользоватьАдресноеХранение = Истина;		
		КонецЕсли;
	Иначе
		ВидСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "КР_ВидСклада");
		РежимРаботыПоВсемПомещениям = Не ЗначениеЗаполнено(Помещение)
				И ВидСклада = Перечисления.КР_ВидыСкладов.Магазин;
				
		Если РежимРаботыПоВсемПомещениям Тогда
			ИспользоватьАдресноеХранение = Истина;		
		КонецЕсли;
	КонецЕсли;
	
	Если ИспользоватьАдресноеХранение Тогда
	// Начало было
	// Если СкладыСервер.ИспользоватьАдресноеХранение(Склад, Помещение, Дата) Тогда
	// Конец было
	// Задача #4485
	// <-- Евлахов Игорь Николаевич (Конец) 25.07.2024
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Упаковка КАК Упаковка,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Серия КАК Серия,
		|	Таблица.Ячейка КАК Ячейка,
		|	Таблица.КоличествоФакт КАК КоличествоФакт,
		|	Таблица.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт,
		|	Таблица.НомерСтроки
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ТаблицаНоменклатуры КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваровСОстатками.Ячейка,
		|	ТаблицаТоваровСОстатками.Номенклатура,
		|	ТаблицаТоваровСОстатками.Характеристика,
		|	ТаблицаТоваровСОстатками.Назначение,
		|	ТаблицаТоваровСОстатками.Упаковка,
		|	ТаблицаТоваровСОстатками.Серия КАК Серия,
		|	СУММА(ТаблицаТоваровСОстатками.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаТоваровСОстатками.Количество) КАК Количество,
		|	СУММА(ТаблицаТоваровСОстатками.КоличествоФакт) КАК КоличествоФакт,
		|	СУММА(ТаблицаТоваровСОстатками.КоличествоУпаковокФакт) КАК КоличествоУпаковокФакт
		|ПОМЕСТИТЬ ТаблицаТоваровСОстатками
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаНоменклатуры.Ячейка КАК Ячейка,
		|		ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|		ТаблицаНоменклатуры.Характеристика КАК Характеристика,
		|		ТаблицаНоменклатуры.Назначение КАК Назначение,
		|		ТаблицаНоменклатуры.Упаковка КАК Упаковка,
		|		ТаблицаНоменклатуры.Серия КАК Серия,
		|		0 КАК КоличествоУпаковок,
		|		0 КАК Количество,
		|		ТаблицаНоменклатуры.КоличествоФакт КАК КоличествоФакт,
		|		ТаблицаНоменклатуры.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт
		|	ИЗ
		|		ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|	ГДЕ
		|		ТаблицаНоменклатуры.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		ТаблицаНоменклатуры.Ячейка,
		|		ЕСТЬNULL(ТоварыВЯчейкахОстатки.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
		|		ЕСТЬNULL(ТоварыВЯчейкахОстатки.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
		|		ЕСТЬNULL(ТоварыВЯчейкахОстатки.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)),
		|		ЕСТЬNULL(ТоварыВЯчейкахОстатки.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)),
		|		ЕСТЬNULL(ТоварыВЯчейкахОстатки.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)),
		|		ЕСТЬNULL(ТоварыВЯчейкахОстатки.ВНаличииОстаток, 0),
		|		ВЫБОР
		|			КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 0) = 0
		|				ТОГДА ЕСТЬNULL(ТоварыВЯчейкахОстатки.ВНаличииОстаток, 0)
		|			ИНАЧЕ ЕСТЬNULL(ТоварыВЯчейкахОстатки.ВНаличииОстаток, 0) * &ТекстЗапросаКоэффициентУпаковки
		|		КОНЕЦ,
		|		0,
		|		0
		|	ИЗ
		|		ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВЯчейках.Остатки(&ДатаОстатков, ) КАК ТоварыВЯчейкахОстатки
		|			ПО (ТоварыВЯчейкахОстатки.Ячейка = ТаблицаНоменклатуры.Ячейка)
		|				И (ВЫБОР
		|					КОГДА ТаблицаНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ТоварыВЯчейкахОстатки.Номенклатура = ТаблицаНоменклатуры.Номенклатура
		|				КОНЕЦ)
		|				И (ВЫБОР
		|					КОГДА ТаблицаНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ТоварыВЯчейкахОстатки.Характеристика = ТаблицаНоменклатуры.Характеристика
		|				КОНЕЦ)
		|				И (ВЫБОР
		|					КОГДА ТаблицаНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ТоварыВЯчейкахОстатки.Назначение = ТаблицаНоменклатуры.Назначение
		|				КОНЕЦ)
		|				И (ВЫБОР
		|					КОГДА ТаблицаНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ТоварыВЯчейкахОстатки.Серия = ТаблицаНоменклатуры.Серия
		|				КОНЕЦ)
		|				И (ВЫБОР
		|					КОГДА ТаблицаНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|						ТОГДА ИСТИНА
		|					ИНАЧЕ ТоварыВЯчейкахОстатки.Упаковка = ТаблицаНоменклатуры.Упаковка
		|				КОНЕЦ)
		|	ГДЕ
		|		(ТаблицаНоменклатуры.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|				ИЛИ ТаблицаНоменклатуры.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|					И НЕ (ТоварыВЯчейкахОстатки.Номенклатура ЕСТЬ NULL
		|							И ТоварыВЯчейкахОстатки.Характеристика ЕСТЬ NULL
		|							И ТоварыВЯчейкахОстатки.Назначение ЕСТЬ NULL
		|							И ТоварыВЯчейкахОстатки.Упаковка ЕСТЬ NULL
		|							И ТоварыВЯчейкахОстатки.Серия ЕСТЬ NULL))) КАК ТаблицаТоваровСОстатками
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваровСОстатками.Ячейка,
		|	ТаблицаТоваровСОстатками.Упаковка,
		|	ТаблицаТоваровСОстатками.Характеристика,
		|	ТаблицаТоваровСОстатками.Назначение,
		|	ТаблицаТоваровСОстатками.Номенклатура,
		|	ТаблицаТоваровСОстатками.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваровСОстатками.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваровСОстатками.Характеристика КАК Характеристика,
		|	ТаблицаТоваровСОстатками.Назначение КАК Назначение,
		|	ТаблицаТоваровСОстатками.Упаковка КАК Упаковка,
		|	ТаблицаТоваровСОстатками.Ячейка КАК Ячейка,
		|	ТаблицаТоваровСОстатками.Серия КАК Серия,
		|	ТаблицаТоваровСОстатками.КоличествоУпаковок,
		|	ТаблицаТоваровСОстатками.Количество,
		|	ТаблицаТоваровСОстатками.КоличествоФакт,
		|	ТаблицаТоваровСОстатками.КоличествоУпаковокФакт,
		|	ЕСТЬNULL(ТаблицаНоменклатуры.НомерСтроки, &МаксимальноВозможноеКоличествоСтрокВТЧ) КАК НомерСтроки,
		|	ЛОЖЬ КАК ИзлишекПорча
		|ИЗ
		|	ТаблицаТоваровСОстатками КАК ТаблицаТоваровСОстатками
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ПО ТаблицаТоваровСОстатками.Номенклатура = ТаблицаНоменклатуры.Номенклатура
		|			И ТаблицаТоваровСОстатками.Характеристика = ТаблицаНоменклатуры.Характеристика
		|			И ТаблицаТоваровСОстатками.Назначение = ТаблицаНоменклатуры.Назначение
		|			И ТаблицаТоваровСОстатками.Упаковка = ТаблицаНоменклатуры.Упаковка
		|			И ТаблицаТоваровСОстатками.Ячейка = ТаблицаНоменклатуры.Ячейка
		|			И ТаблицаТоваровСОстатками.Серия = ТаблицаНоменклатуры.Серия
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки,
		|	ВЫРАЗИТЬ(ТаблицаТоваровСОстатками.Ячейка КАК Справочник.СкладскиеЯчейки).РабочийУчасток,
		|	ВЫРАЗИТЬ(ТаблицаТоваровСОстатками.Ячейка КАК Справочник.СкладскиеЯчейки).ПорядокОбхода,
		|	ВЫРАЗИТЬ(ТаблицаТоваровСОстатками.Ячейка КАК Справочник.СкладскиеЯчейки).Код,
		|	ТаблицаТоваровСОстатками.Серия.Номер,
		|	ТаблицаТоваровСОстатками.Серия.ГоденДо";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
									"&ТекстЗапросаКоэффициентУпаковки",
									Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
										"ТоварыВЯчейкахОстатки.Упаковка",
										"ТоварыВЯчейкахОстатки.Номенклатура"));
		
		Запрос.УстановитьПараметр("МаксимальноВозможноеКоличествоСтрокВТЧ", 100000);
		
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Назначение КАК Назначение,
		|	Таблица.Серия КАК Серия,
		|	Таблица.КоличествоФакт КАК КоличествоФакт,
		|	Таблица.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт,
		|	Таблица.НомерСтроки
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ТаблицаНоменклатуры КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваровСОстатками.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваровСОстатками.Характеристика КАК Характеристика,
		|	ТаблицаТоваровСОстатками.Назначение КАК Назначение,
		|	ТаблицаТоваровСОстатками.Серия КАК Серия,
		|	СУММА(ТаблицаТоваровСОстатками.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ТаблицаТоваровСОстатками.Количество) КАК Количество,
		|	СУММА(ТаблицаТоваровСОстатками.КоличествоФакт) КАК КоличествоФакт,
		|	СУММА(ТаблицаТоваровСОстатками.КоличествоУпаковокФакт) КАК КоличествоУпаковокФакт
		|ПОМЕСТИТЬ ТаблицаТоваровСОстатками
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаНоменклатуры.Номенклатура КАК Номенклатура,
		|		ТаблицаНоменклатуры.Характеристика КАК Характеристика,
		|		ТаблицаНоменклатуры.Назначение КАК Назначение,
		|		ТаблицаНоменклатуры.Серия КАК Серия,
		|		0 КАК КоличествоУпаковок,
		|		0 КАК Количество,
		|		ТаблицаНоменклатуры.КоличествоФакт КАК КоличествоФакт,
		|		ТаблицаНоменклатуры.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт
		|	ИЗ
		|		ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТоварыНаСкладахОстатки.Номенклатура,
		|		ТоварыНаСкладахОстатки.Характеристика,
		|		ТоварыНаСкладахОстатки.Назначение,
		|		ТоварыНаСкладахОстатки.Серия,
		|		ТоварыНаСкладахОстатки.ВНаличииОстаток,
		|		ТоварыНаСкладахОстатки.ВНаличииОстаток,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.ТоварыНаСкладах.Остатки(
		|				&ДатаОстатков,
		|				(Номенклатура, Характеристика, Назначение, Склад, Помещение) В
		|					(ВЫБРАТЬ
		|						Таблица.Номенклатура,
		|						Таблица.Характеристика,
		|						Таблица.Назначение,
		|						&Склад,
		|						&Помещение
		|					ИЗ
		|						ТаблицаНоменклатуры КАК Таблица)) КАК ТоварыНаСкладахОстатки) КАК ТаблицаТоваровСОстатками
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваровСОстатками.Номенклатура,
		|	ТаблицаТоваровСОстатками.Характеристика,
		|	ТаблицаТоваровСОстатками.Назначение,
		|	ТаблицаТоваровСОстатками.Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваровСОстатками.Номенклатура,
		|	ТаблицаТоваровСОстатками.Характеристика,
		|	ТаблицаТоваровСОстатками.Назначение,
		|	ТаблицаТоваровСОстатками.Серия КАК Серия,
		|	ТаблицаТоваровСОстатками.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаТоваровСОстатками.Количество КАК Количество,
		|	ТаблицаТоваровСОстатками.КоличествоФакт КАК КоличествоФакт,
		|	ТаблицаТоваровСОстатками.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт,
		|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
		|	ЛОЖЬ КАК ИзлишекПорча
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваровСОстатками КАК ТаблицаТоваровСОстатками
		|		ПО ТаблицаНоменклатуры.Номенклатура = ТаблицаТоваровСОстатками.Номенклатура
		|			И ТаблицаНоменклатуры.Характеристика = ТаблицаТоваровСОстатками.Характеристика
		|			И ТаблицаНоменклатуры.Серия = ТаблицаТоваровСОстатками.Серия
		|			И ТаблицаНоменклатуры.Назначение = ТаблицаТоваровСОстатками.Назначение
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки,
		|	ТаблицаТоваровСОстатками.Серия.Номер,
		|	ТаблицаТоваровСОстатками.Серия.ГоденДо";
		
		Запрос.УстановитьПараметр("Ссылка",		Ссылка);
		Запрос.УстановитьПараметр("Склад",		Склад);
		Запрос.УстановитьПараметр("Помещение",	Помещение);
		
	КонецЕсли;
	
	ДатаОстатков = Новый Граница(Дата, ВидГраницы.Включая);
	
	Запрос.УстановитьПараметр("ДатаОстатков",			ДатаОстатков);
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры",	Товары.Выгрузить());
	
	Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект,
		НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПересчетТоваров));
	
	УчетныеДанныеЗаполнены = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ПроверитьЯчейкиПередБлокировкой(Отказ)
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.БлокировкиСкладскихЯчеек");
	ЭлементБлокировки.Режим          = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ячейка", "Ячейка");
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыВЯчейках");
	ЭлементБлокировки.Режим          = РежимБлокировкиДанных.Исключительный;   
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Ячейка", "Ячейка");
	
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаНоменклатуры.Ячейка КАК Ячейка,
	|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ТаблицаЯчеекДляЗапроса
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ТаблицаЯчеекДляЗапроса.Ячейка КАК Справочник.СкладскиеЯчейки) КАК Ячейка,
	|	МИНИМУМ(ТаблицаЯчеекДляЗапроса.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ТаблицаЯчеек
	|ИЗ
	|	ТаблицаЯчеекДляЗапроса КАК ТаблицаЯчеекДляЗапроса
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЯчеекДляЗапроса.Ячейка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЯчеек.Ячейка КАК Ячейка,
	|	ТаблицаЯчеек.Ячейка.ТипСкладскойЯчейки КАК ТипСкладскойЯчейки,
	|	ТаблицаЯчеек.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыВЯчейкахОстатки.КОтборуОстаток, 0) <> 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьЗаданияНаОтбор,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТоварыВЯчейкахОстатки.КРазмещениюОстаток, 0) <> 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьЗаданияНаРазмещение,
	|	ВЫБОР
	|		КОГДА БлокировкиСкладскихЯчеек.ТипБлокировки ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьБлокировкиПоЯчейке
	|ИЗ
	|	ТаблицаЯчеек КАК ТаблицаЯчеек
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыВЯчейках.Остатки(
	|				,
	|				Ячейка В
	|					(ВЫБРАТЬ
	|						ТаблицаЯчеек.Ячейка КАК Ячейка
	|					ИЗ
	|						ТаблицаЯчеек КАК ТаблицаЯчеек)) КАК ТоварыВЯчейкахОстатки
	|		ПО ТаблицаЯчеек.Ячейка = ТоварыВЯчейкахОстатки.Ячейка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БлокировкиСкладскихЯчеек КАК БлокировкиСкладскихЯчеек
	|		ПО (ТаблицаЯчеек.Ячейка = БлокировкиСкладскихЯчеек.Ячейка
	|				И БлокировкиСкладскихЯчеек.Регистратор <> &Ссылка)
	|ГДЕ
	|	(ЕСТЬNULL(ТоварыВЯчейкахОстатки.КОтборуОстаток, 0) <> 0
	|			ИЛИ ЕСТЬNULL(ТоварыВЯчейкахОстатки.КРазмещениюОстаток, 0) <> 0
	|			ИЛИ (НЕ БлокировкиСкладскихЯчеек.ТипБлокировки ЕСТЬ NULL ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ячейка";
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", Товары.Выгрузить());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = НСтр("ru='Невозможно заблокировать %ТипЯчейки% %Ячейка%:
		|%Причина%'");
		Если Выборка.ЕстьБлокировкиПоЯчейке Тогда
			ТекстПричины = НСтр("ru='ячейка заблокирована другим документом'");
		Иначе
			ТекстПричины = "";
			Если Выборка.ЕстьЗаданияНаОтбор <> 0 Тогда
				ТекстПричины = НСтр("ru='есть невыполненные задания на отбор товара из ячейки'");
			КонецЕсли;
			Если Выборка.ЕстьЗаданияНаРазмещение <> 0 Тогда
				Если ЗначениеЗаполнено(ТекстПричины) Тогда
					ТекстПричины = ТекстПричины + ";
						|"
				КонецЕсли;
				ТекстПричины = ТекстПричины + НСтр("ru='есть невыполненные задания на размещение в ячейку'");
				Если Выборка.ТипСкладскойЯчейки = Перечисления.ТипыСкладскихЯчеек.Отгрузка Тогда
					ТекстПричины = ТекстПричины + " " + НСтр("ru='или есть расходные ордера (ордера на перемещение) в неокончательном статусе с неотгружаемыми товарами, размещаемыми в эту зону отгрузки'");
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТипЯчейки%",
			?(Выборка.ТипСкладскойЯчейки = Перечисления.ТипыСкладскихЯчеек.Отгрузка, НСтр("ru='зону отгрузки'"),
				?(Выборка.ТипСкладскойЯчейки = Перечисления.ТипыСкладскихЯчеек.Приемка, НСтр("ru='зону приемки'"),
					НСтр("ru='ячейку'"))));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ТекстПричины);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ячейка%", Выборка.Ячейка);
		
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Ячейка");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект,Поле,"Объект",Отказ);
	КонецЦикла;
КонецПроцедуры

Процедура ПроверитьДругиеПересчеты(Отказ)
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыКОформлениюИзлишковНедостач");
	
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.УстановитьЗначение("Склад", Склад);
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Серия", "Серия");
	
	СтатусБыл = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
	
	Если Статус = Перечисления.СтатусыПересчетовТоваров.Выполнено
		ИЛИ СтатусБыл = Перечисления.СтатусыПересчетовТоваров.Выполнено Тогда
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	Иначе
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	КонецЕсли;
	
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.НомерСтроки
	|ПОМЕСТИТЬ ВТТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТаблицаТовары.НомерСтроки
	|ИЗ
	|	Документ.ПересчетТоваров.Товары КАК ПересчетТоваровТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТТаблицаТовары КАК ВТТаблицаТовары
	|		ПО (ВТТаблицаТовары.Номенклатура = ПересчетТоваровТовары.Номенклатура)
	|			И (ВТТаблицаТовары.Характеристика = ПересчетТоваровТовары.Характеристика)
	|			И (ВТТаблицаТовары.Серия = ПересчетТоваровТовары.Серия)
	|ГДЕ
	|	ПересчетТоваровТовары.Ссылка.Склад = &Склад
	|	И ПересчетТоваровТовары.Ссылка.Помещение = &Помещение
	|	И ПересчетТоваровТовары.Ссылка.Проведен
	|	И НЕ ПересчетТоваровТовары.Ссылка = &Ссылка
	// --> Евлахов Игорь Николаевич (Начало) 25.09.2024
	// Задача #4584
	// Начало было
	//|	И ПересчетТоваровТовары.Ссылка.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.ВРаботе), ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.ВнесениеРезультатов))
	// Конец было
	|	И ПересчетТоваровТовары.Ссылка.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.ВРаботе), ЗНАЧЕНИЕ(Перечисление.СтатусыПересчетовТоваров.злКПроверке))
	// <-- Евлахов Игорь Николаевич (Конец) 25.09.2024
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаТовары.НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Помещение", Помещение);
	Запрос.УстановитьПараметр("ТаблицаТовары", Товары.Выгрузить());
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	Результат = МассивРезультатовЗапроса[1]; // РезультатЗапроса
	ВыборкаПересчеты = Результат.Выбрать();
	Пока ВыборкаПересчеты.Следующий() Цикл
		ТекстСообщения = НСтр("ru = 'Товар в строке %НомерСтроки% уже включен в пересчеты товаров в статусе ""В работе"" или ""Внесение результатов"" на этом же складе (помещении).'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", ВыборкаПересчеты.НомерСтроки);
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", ВыборкаПересчеты.НомерСтроки, "НомерСтроки");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,ЭтотОбъект,Поле,"Объект",Отказ);
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область КР_ДополнительныеПроцедурыИФункции

// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
Процедура КР_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов)
	
	ВидТекущегоСклада = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "КР_ВидСклада");
	Если ВидТекущегоСклада = Перечисления.КР_ВидыСкладов.Магазин Тогда

		// << 17.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1567
		Если Не ДополнительныеСвойства.Свойство(КР_УТ10_УТ11_ЗагрузкаДанныхСервер.СвойствоОбъектБылЗагружен()) Тогда 
		// >> 17.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1567
		
			ПроверяемыеРеквизиты.Добавить("КР_ПриказНаИнвентаризациюТоваров");
            // --> Евлахов Игорь Николаевич (Начало) 24.09.2024
			// Задача #4584
			ПроверяемыеРеквизиты.Добавить("Ответственный");
			ПроверяемыеРеквизиты.Добавить("КР_ЗональныеПересчеты");
			
			Если ЗначениеЗаполнено(КР_ПриказНаИнвентаризациюТоваров) Тогда
				ИменаРеквизитов = "ДатаНачала, ДатаОкончания";
				РеквизитыПриказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КР_ПриказНаИнвентаризациюТоваров, ИменаРеквизитов);
				
				Если Дата < НачалоДня(РеквизитыПриказа.ДатаНачала)
					ИЛИ Дата > КонецДня(РеквизитыПриказа.ДатаОкончания) Тогда
					ТекстШаблона = НСтр("ru = '""Дата"" документа, находится за границей периода с [%1] по [%2], указанного в документе ""%3""'");
					ТекстСообщения = СтрШаблон(ТекстШаблона, 
											   Формат(РеквизитыПриказа.ДатаНачала, "ДЛФ=D"), 
											   Формат(РеквизитыПриказа.ДатаОкончания, "ДЛФ=D"), 
											   КР_ПриказНаИнвентаризациюТоваров);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Дата", "Объект", Отказ);	
				КонецЕсли;
			КонецЕсли;
			// Задача #4584
			// <-- Евлахов Игорь Николаевич (Конец) 24.09.2024
			
		// << 17.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1567
		КонецЕсли; 
		// >> 17.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1567  
		
		МассивНепроверяемыхРеквизитов.Добавить("Помещение");
	КонецЕсли;
	
	// << 26.04.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1449
	Если КР_УпорядочиваниеТоваровВЯчейках Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КР_ПриказНаИнвентаризациюТоваров");
	КонецЕсли;
	// >> 26.04.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1449
	
КонецПроцедуры // >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
Процедура КР_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	// << 25.04.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1449
	Если КР_УпорядочиваниеТоваровВЯчейках Тогда
		КР_ЗональныеПересчеты.Очистить();
	Иначе
		КР_ПротоколУпорядочиванияТоваровВЯчейках = "";
	КонецЕсли;
	
	Документы.ПересчетТоваров.КР_ПроверитьДублиЗональныхПересчетовВСтрокахТЧ(КР_ЗональныеПересчеты.Выгрузить(), Отказ);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		КР_ПроверитьПолнотуПокрытияЗонПересчета();
		КР_ПроверитьИзлишкиНедостачиВРежимеУпорядочиванияВЯчейках(Отказ);
	КонецЕсли;
	// >> 25.04.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1449
	
	РеквизитыСсылки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Проведен, Статус");
	КР_ПроверкаНаДублирующийДокументПоИнвентаризации(РежимЗаписи, Отказ);
	КР_ПроверкаНаОграниченияПоПравам(РеквизитыСсылки, РежимЗаписи, Отказ);
	
КонецПроцедуры // >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
Процедура КР_ПроверкаНаДублирующийДокументПоИнвентаризации(РежимЗаписи, Отказ)
	
	Если РежимЗаписи <> РежимЗаписиДокумента.Проведение Тогда
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(КР_ПриказНаИнвентаризациюТоваров) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПересчетТоваров.Ссылка КАК Ссылка,
	|	ПересчетТоваров.Представление КАК Представление
	|ИЗ
	|	Документ.ПересчетТоваров КАК ПересчетТоваров
	|ГДЕ
	|	ПересчетТоваров.Проведен
	|	И ПересчетТоваров.КР_ПриказНаИнвентаризациюТоваров = &КР_ПриказНаИнвентаризациюТоваров
	|	И ПересчетТоваров.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("КР_ПриказНаИнвентаризациюТоваров",	КР_ПриказНаИнвентаризациюТоваров);
	Запрос.УстановитьПараметр("Ссылка",								Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		Шаблон = НСтр("ru='Для %1 существует проведенный %2'");
		Сообщение = СтрШаблон(Шаблон, КР_ПриказНаИнвентаризациюТоваров, ВыборкаДетальныеЗаписи.Представление);
		ОбщегоНазначения.СообщитьПользователю(Сообщение, , , , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры // >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
Процедура КР_ПроверкаНаОграниченияПоПравам(РеквизитыСсылки, РежимЗаписи, Отказ)
	
	// Режим записи "Запись" (или первичное проведение) - проверка не требуется
	Если РежимЗаписи = РежимЗаписиДокумента.Запись Или Не Проведен Тогда
		Возврат;
	КонецЕсли;

	ЕстьОграничениеПоИзменениюДокумента = РольДоступна("КР_ОграниченноеДобавлениеПересчетовТоваров") И
		Не Пользователи.РолиДоступны("ДобавлениеИзменениеПересчетовТоваров", , Ложь);
	
	Если Не ЕстьОграничениеПоИзменениюДокумента Тогда
		Возврат;
	КонецЕсли;
	
	// Режим записи "Отмена проведения" - запрещено, если ЕстьОграничениеПоДобавлениюДокумента
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ТекстСообщения = НСтр("ru = 'Не достаточно прав для отмены проведения документа'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	// --> Евлахов Игорь Николаевич (Начало) 26.09.2024
	// Задача #4584
	// Начало было
	//Если РеквизитыСсылки.Статус = Перечисления.СтатусыПересчетовТоваров.ВнесениеРезультатов Тогда
	// Конец было
	Если РеквизитыСсылки.Статус = Перечисления.СтатусыПересчетовТоваров.злКПроверке Тогда
	// <-- Евлахов Игорь Николаевич (Конец) 26.09.2024
		// Режим записи "Проведение" - запрещено, если ЕстьОграничениеПоДобавлениюДокумента и статус понижен на более младший
		ЭтоПонижениеСтатуса = КР_ЭтоПонижениеСтатуса(РеквизитыСсылки.Статус, Статус);
		Если ЭтоПонижениеСтатуса Тогда
			Шаблон = НСтр("ru='Повторное проведение документа %1 не возможно с изменением статуса на более младший'");
			Сообщение = СтрШаблон(Шаблон, Ссылка);
			ОбщегоНазначения.СообщитьПользователю(Сообщение, , , , Отказ);
		КонецЕсли;
	Иначе
		// Режим записи "Проведение" - запрещено, если ЕстьОграничениеПоДобавлениюДокумента и статус не повышается
		ЭтоПовышениеСтатуса = КР_ЭтоПовышениеСтатуса(РеквизитыСсылки.Статус, Статус);
		Если Не ЭтоПовышениеСтатуса Тогда
			Шаблон = НСтр("ru='Повторное проведение документа %1 возможно только с изменением статуса на более старший'");
			Сообщение = СтрШаблон(Шаблон, Ссылка);
			ОбщегоНазначения.СообщитьПользователю(Сообщение, , , , Отказ);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
Функция КР_ЭтоПовышениеСтатуса(НачальныйСтатус, НовыйСтатус)
	
	ЭтоПовышениеСтатуса = (КР_СтаршенствоСтатуса(НовыйСтатус) > КР_СтаршенствоСтатуса(НачальныйСтатус));
	Возврат ЭтоПовышениеСтатуса;
	
КонецФункции // >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
Функция КР_ЭтоПонижениеСтатуса(НачальныйСтатус, НовыйСтатус)
	
	КР_ЭтоПонижениеСтатуса = (КР_СтаршенствоСтатуса(НовыйСтатус) < КР_СтаршенствоСтатуса(НачальныйСтатус));
	Возврат КР_ЭтоПонижениеСтатуса;
	
КонецФункции // >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
Функция КР_СтаршенствоСтатуса(ТекущийСтатус)
	
	СтатусыПересчетовТоваров = Перечисления.СтатусыПересчетовТоваров;
	
	// --> Евлахов Игорь Николаевич (Начало) 26.09.2024
	// Задача #4584
	// Начало было
	//Если ТекущийСтатус = СтатусыПересчетовТоваров.Подготовлено Тогда
	//	Порядок = 1;
	//ИначеЕсли ТекущийСтатус = СтатусыПересчетовТоваров.ВРаботе Тогда
	//	Порядок = 2;
	//ИначеЕсли ТекущийСтатус = СтатусыПересчетовТоваров.ВнесениеРезультатов Тогда
	//	Порядок = 3;
	//ИначеЕсли ТекущийСтатус = СтатусыПересчетовТоваров.Выполнено Тогда
	//	Порядок = 4;
	//Иначе
	//	Порядок = 0;
	//КонецЕсли;
	// Конец было
	Если ТекущийСтатус = СтатусыПересчетовТоваров.ВРаботе Тогда
		Порядок = 1;
	ИначеЕсли ТекущийСтатус = СтатусыПересчетовТоваров.злКПроверке Тогда
		Порядок = 2;
	ИначеЕсли ТекущийСтатус = СтатусыПересчетовТоваров.Выполнено Тогда
		Порядок = 3;
	Иначе
		Порядок = 0;
	КонецЕсли;
	// <-- Евлахов Игорь Николаевич (Конец) 26.09.2024
	
	Возврат Порядок;
	
КонецФункции // >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

// << 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860
Процедура КР_ОбработкаПроведения(Отказ)
	
	Если Отказ Или КР_НеФормироватьДвижения
		// << 16.05.2023 Марченко С.Н., КРОК, JIRA№A2105505-1241
		Или ДополнительныеСвойства.Свойство(КР_УТ11_ЦБ_Магазин_ЗагрузкаДанныхСервер.СвойствоОбъектБылЗагружен())
		// >> 16.05.2023 Марченко С.Н., КРОК, JIRA№A2105505-1241
		Тогда
		Возврат;
	КонецЕсли;
	
	ПредыдущийСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
	Если Статус = Перечисления.СтатусыПересчетовТоваров.Выполнено Тогда
		КР_АвтоматическоеСозданиеСкладскихАктов.СоздатьАктуализироватьСкладскиеАкты(ЭтотОбъект, Отказ);
	Иначе
		КР_АвтоматическоеСозданиеСкладскихАктов.ПометитьНаУдалениеСвязанныеСкладскиеАкты(ЭтотОбъект, Отказ);
	КонецЕсли;
	
КонецПроцедуры // >> 03.02.2023 Федоров Д.Е., КРОК, JIRA№A2105505-860

// << 26.04.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1449
Процедура КР_ПроверитьПолнотуПокрытияЗонПересчета()
	
	Если Не КР_ЗональныеПересчеты.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ЗоныПересчетаДокумента = ОбщегоНазначения.ВыгрузитьКолонку(ОбщегоНазначения.ЗначениеРеквизитаОбъектов(
		КР_ЗональныеПересчеты.ВыгрузитьКолонку("ЗональныйПересчет"), "ЗонаПересчета"), "Значение", Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ПомещениеЗаполнено", ЗначениеЗаполнено(Помещение));
	Запрос.УстановитьПараметр("Помещение", Помещение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗоныПересчетаТоваров.Ссылка КАК ЗонаПересчета
	|ИЗ
	|	Справочник.КР_ЗоныПересчетаТоваров КАК ЗоныПересчетаТоваров
	|ГДЕ
	|	ЗоныПересчетаТоваров.Владелец = &Склад
	|	И ВЫБОР
	|			КОГДА &ПомещениеЗаполнено
	|				ТОГДА ЗоныПересчетаТоваров.Помещение = &Помещение
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И НЕ ЗоныПересчетаТоваров.НеИспользуется
	|	И НЕ ЗоныПересчетаТоваров.ПометкаУдаления";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗоныПересчетаДокумента.Найти(Выборка.ЗонаПересчета) = Неопределено Тогда
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Отсутствует зональный пересчет по зоне ""%1""'"), Выборка.ЗонаПересчета));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // >> 26.04.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1449

// << 03.05.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1449
Процедура КР_ПроверитьИзлишкиНедостачиВРежимеУпорядочиванияВЯчейках(Отказ)
	
	Если Не КР_УпорядочиваниеТоваровВЯчейках Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаТовары", Товары.Выгрузить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.КоличествоУпаковокФакт КАК КоличествоУпаковокФакт
	|ПОМЕСТИТЬ врт_ТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врт_ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	врт_ТаблицаТовары.Характеристика КАК Характеристика,
	|	врт_ТаблицаТовары.Упаковка КАК Упаковка,
	|	ВЫРАЗИТЬ(врт_ТаблицаТовары.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	МАКСИМУМ(ШтрихкодыНоменклатуры.Штрихкод) КАК Штрихкод,
	|	СУММА(врт_ТаблицаТовары.КоличествоУпаковокФакт) - СУММА(врт_ТаблицаТовары.КоличествоУпаковок) КАК Отклонение
	|ИЗ
	|	врт_ТаблицаТовары КАК врт_ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО врт_ТаблицаТовары.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И врт_ТаблицаТовары.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|			И врт_ТаблицаТовары.Упаковка = ШтрихкодыНоменклатуры.Упаковка
	|
	|СГРУППИРОВАТЬ ПО
	|	врт_ТаблицаТовары.Упаковка,
	|	врт_ТаблицаТовары.Характеристика,
	|	врт_ТаблицаТовары.Номенклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(врт_ТаблицаТовары.КоличествоУпаковокФакт) <> СУММА(врт_ТаблицаТовары.КоличествоУпаковок)";
	
	ШаблонСообщения = НСтр("ru = '%1 по [%2] %3 / %4 в количестве %5 %6 недопустимы, т.к. документ проводится
		|в режиме ""Упорядочивание товаров в ячейках"". Используйте команду ""Убрать расхождения на уровне товара""'");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОтклонениеМодуль = Макс(-Выборка.Отклонение, Выборка.Отклонение);
		ИзлишекНедостача = ?(Выборка.Отклонение > 0, НСтр("ru = 'Излишки'"), НСтр("ru = 'Недостачи'"));
		ТекстСообщения = СтрШаблон(ШаблонСообщения,
			ИзлишекНедостача,
			Выборка.Штрихкод,
			Выборка.Номенклатура,
			Выборка.Характеристика,
			ОтклонениеМодуль,
			Выборка.ЕдиницаИзмерения);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
