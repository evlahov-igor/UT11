
#Область ОписаниеПеременных

&НаКлиенте
Перем ПодтверждениеЗакрытияФормы;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТабличныйДокумент = ОбщегоНазначенияУТ.МакетПоИмени("Документ.ЗаказКлиента.КР_ШаблонЗагрузкаШтрихкодовWB");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ПодтверждениеЗакрытияФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Отказ Или (ПодтверждениеЗакрытияФормы <> Истина);
	Если ЗавершениеРаботы Тогда
		ТекстПредупреждения = НСтр("ru = 'Введенные данные не будут записаны.'");
		Возврат;
	КонецЕсли;
		
	Если Отказ Тогда
		Оповещение = Новый ОписаниеОповещения("ЗакрытиеФормыЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Введенные данные не будут записаны. Закрыть форму?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьШтрихкоды(Команда)
	
	МассивШтрихкодов = РаспознатьТабличныйДокумент();
	
	ПодтверждениеЗакрытияФормы = Истина;
	
	Закрыть(МассивШтрихкодов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ЗакрытиеФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗагрузитьШтрихкоды(Неопределено);
	Иначе
		ПодтверждениеЗакрытияФормы = Истина;
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РаспознатьТабличныйДокумент()
	
	ВсегоСтрок = ТабличныйДокумент.ВысотаТаблицы;
	МассивШтрихкодов = Новый Массив;
	
	Для Строка = 2 По ВсегоСтрок Цикл
		ТекстЯчейки = СокрЛП(ТабличныйДокумент.Область(Строка, 1).Текст);
		Если ПустаяСтрока(ТекстЯчейки)
		   Или Не МассивШтрихкодов.Найти(ТекстЯчейки) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		МассивШтрихкодов.Добавить(ТекстЯчейки);
	КонецЦикла;
	
	Возврат МассивШтрихкодов;
	
КонецФункции

#КонецОбласти
