
////////////////////////////////////////////////////
//// Модуль объекта документа "КР_ЗональныйПересчетТоваров"
//// Создан: 14.04.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-1449
//// Разработка по ФДР С11.009, Документы инвентаризации товаров

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ПроверитьВхождениеЯчеекВЗонуПересчета(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	НастройкаХранения = Неопределено;
	Если ЗначениеЗаполнено(ЗонаПересчета) Тогда
		Помещение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗонаПересчета, "Помещение");
		Если ЗначениеЗаполнено(Помещение) Тогда
			НастройкаХранения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Помещение, "НастройкаАдресногоХранения");
		КонецЕсли;
	КонецЕсли;
	Если НастройкаХранения = Перечисления.НастройкиАдресногоХранения.ЯчейкиОстатки Тогда
		ПроверяемыеРеквизиты.Добавить("Товары.Ячейка");
	КонецЕсли;
	
	НоменклатураСервер.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ);
	
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(Знач ДанныеЗаполнения)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ДанныеЗаполнения.Свойство("Товары") Тогда
		ЗаполнитьТоварыПоТаблице(ДанныеЗаполнения.Товары);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьТоварыПоТаблице(ДанныеЗаполнения)
	
	Если ДанныеЗаполнения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДанныхЗаполнения Из ДанныеЗаполнения Цикл
		
		Строка = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, СтрокаДанныхЗаполнения);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьВхождениеЯчеекВЗонуПересчета(Отказ)
	
	МассивЯчеек = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗонаПересчета, "Ячейки").Выгрузить().ВыгрузитьКолонку("Ячейка");
	Если Не МассивЯчеек.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	ШаблонСообщения = НСтр("ru = 'В строке %1 ячейка ""%2"" не входит в зону пересчета ""%3""'");
	
	Для Каждого ТекСтрока Из Товары Цикл
		Если МассивЯчеек.Найти(ТекСтрока.Ячейка) = Неопределено Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ТекСтрока.НомерСтроки, ТекСтрока.Ячейка, ЗонаПересчета);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
			Если Не ЭтоПолноправныйПользователь Тогда
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
