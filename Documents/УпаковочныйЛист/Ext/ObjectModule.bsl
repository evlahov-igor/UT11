#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказНаПеремещение")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПоставщику")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПеремещениеТоваров")
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда
		
		Вид = Перечисления.ВидыУпаковочныхЛистов.Входящий;
		
		ЗаполнитьНаОсновании(ДанныеЗаполнения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// << 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	// >> 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если Вид = Перечисления.ВидыУпаковочныхЛистов.Исходящий Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Код");
	КонецЕсли;
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ВыводитьНомераСтрок = Истина;
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект,МассивНепроверяемыхРеквизитов,Отказ,ПараметрыПроверки);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.УпаковочныйЛист), Отказ);
	УпаковочныеЛистыСервер.ПроверитьЗаполнениеТЧСУпаковочнымиЛистами(ЭтотОбъект, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов, Отказ);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	// << 08.08.2022, Глухов А.Н., КРОК, Jira№ A2105505-314
	// Дополнительная проверка обработки заполнения
	КР_ОбработкаПроверкиЗаполненияДополнительно(Отказ, ПроверяемыеРеквизиты);
	// >> 08.08.2022, Глухов А.Н., КРОК, Jira№ A2105505-314
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	// << 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	// >> 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
	
	Если Не ЗначениеЗаполнено(Код) Тогда
		Код = Документы.УпаковочныйЛист.ПолучитьКодНового(
		// << 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
		Дата // Дата документа, требуется для генерации кода в разрезе годов
		// >> 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751	
		);
	КонецЕсли;
	
	ВсегоМест = УпаковочныеЛистыСервер.КоличествоМестВТЧ(Товары);
	
	// << 08.08.2022, Глухов А.Н., КРОК, Jira№ A2105505-314
	// Проверка уникальности кода
	Если Вид = Перечисления.ВидыУпаковочныхЛистов.Входящий Тогда
		КР_ПроверкаУникальностиКода(Отказ);
	КонецЕсли;
	// >> 08.08.2022, Глухов А.Н., КРОК, Jira№ A2105505-314
	
	// << 18.05.2023, Федоров Д.Е., КРОК, Jira№A2105505-1664
	КР_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения);
	// >> 18.05.2023, Федоров Д.Е., КРОК, Jira№A2105505-1664

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОсновании(ДокументОснование)
	
	Запрос = Новый Запрос;
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Номенклатура              КАК Номенклатура,
	|	Товары.Характеристика            КАК Характеристика,
	|	Товары.Упаковка                  КАК Упаковка,
	|	Товары.Серия                     КАК Серия,
	|	Товары.СтатусУказанияСерий       КАК СтатусУказанияСерий,
	|	СУММА(Товары.Количество)         КАК Количество,
	|	СУММА(Товары.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	&ИсточникДанных КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|		ИЛИ Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Упаковка,
	|	Товары.Серия,
	|	Товары.СтатусУказанияСерий";
	
	ИсточникДанных = "";
	ИмяТЧ          = "Товары";
	
	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказНаПеремещение") Тогда
		ИсточникДанных = Метаданные.Документы.ЗаказНаПеремещение.ПолноеИмя() + "." + ИмяТЧ;
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
		ИсточникДанных = Метаданные.Документы.ЗаказПоставщику.ПолноеИмя() + "." + ИмяТЧ;
		ТекстЗапроса   = ТекстЗапросаПоОснованиюЗаказПоставщику();
	ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		ИсточникДанных = Метаданные.Документы.ПеремещениеТоваров.ПолноеИмя() + "." + ИмяТЧ;
	Иначе
		ИсточникДанных = Метаданные.Документы.ПриобретениеТоваровУслуг.ПолноеИмя() + "." + ИмяТЧ;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИсточникДанных", ИсточникДанных);
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Товары.Загрузить(РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

Функция ТекстЗапросаПоОснованиюЗаказПоставщику()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Номенклатура              КАК Номенклатура,
	|	Товары.Характеристика            КАК Характеристика,
	|	Товары.Упаковка                  КАК Упаковка,
	|	СУММА(Товары.Количество)         КАК Количество,
	|	СУММА(Товары.КоличествоУпаковок) КАК КоличествоУпаковок
	|ИЗ
	|	&ИсточникДанных КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &ДокументОснование
	|	И (Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|		ИЛИ Товары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Упаковка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область КР_ДополнительныеПроцедурыИФункции

// << 08.08.2022, Глухов А.Н., КРОК, Jira№ A2105505-314
// Дополнительная проверка обработки заполнения
Процедура КР_ОбработкаПроверкиЗаполненияДополнительно(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если Вид = Перечисления.ВидыУпаковочныхЛистов.Исходящий Тогда
		МассивНепроверяемыхРеквизитов.Добавить("КР_Партнер");
		МассивНепроверяемыхРеквизитов.Добавить("КР_ТипНаполнения");
		ПроверяемыеРеквизиты.Добавить("СкладУпаковки");
	КонецЕсли;
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры // >> 08.08.2022, Глухов А.Н., КРОК, Jira№ A2105505-314

// << 08.08.2022, Глухов А.Н., КРОК, Jira№ A2105505-314
// Проверка уникальности кода
Процедура КР_ПроверкаУникальностиКода(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	// << 15.02.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1254
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УпаковочныйЛист.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.УпаковочныйЛист КАК УпаковочныйЛист
	|ГДЕ
	|	УпаковочныйЛист.Код = &Код
	|	И УпаковочныйЛист.Вид = ЗНАЧЕНИЕ(Перечисление.ВидыУпаковочныхЛистов.Входящий)
	|	И УпаковочныйЛист.Ссылка <> &Ссылка";
	// >> 15.02.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1254
	
	Запрос.УстановитьПараметр("Код", Код);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = НСтр("ru = 'Код не уникален!'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , "Объект.Код", Отказ);
	КонецЕсли;		
	
КонецПроцедуры // >> 08.08.2022, Глухов А.Н., КРОК, Jira№ A2105505-314

// << 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);

КонецПроцедуры // >> 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751

// << 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры // >> 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751  

// << 18.05.2023, Федоров Д.Е., КРОК, Jira№A2105505-1664
Процедура КР_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	КР_ЗаполнитьШтрихкодКороба(Отказ);
	
КонецПроцедуры // >> 18.05.2023, Федоров Д.Е., КРОК, Jira№A2105505-1664

// << 18.05.2023, Федоров Д.Е., КРОК, Jira№A2105505-1664
Процедура КР_ЗаполнитьШтрихкодКороба(Отказ)

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КР_Штрихкод) Тогда
		// Уникальный идентификатор документа не должен меняться, следовательно перерасчет не требуется
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		СсылкаНовогоДокумента = ЭтотОбъект.ПолучитьСсылкуНового();
		Если НЕ ЗначениеЗаполнено(СсылкаНовогоДокумента) Тогда
			СсылкаНовогоДокумента = Документы.УпаковочныйЛист.ПолучитьСсылку();
			УстановитьСсылкуНового(СсылкаНовогоДокумента);
		КонецЕсли;
		ГУИД = СсылкаНовогоДокумента.УникальныйИдентификатор();
	Иначе
		ГУИД = Ссылка.УникальныйИдентификатор();
	КонецЕсли;

	КР_Штрихкод = КР_РаботаСТСД.ПреобразоватьGUID_78(ГУИД);

КонецПроцедуры // >> 18.05.2023, Федоров Д.Е., КРОК, Jira№A2105505-1664

// << 28.07.2023, Фомичев А.Е., КРОК, Jira№A2105505-2050
Процедура ПриКопировании(ОбъектКопирования)
	КР_Штрихкод = "";
КонецПроцедуры

#КонецОбласти

#КонецЕсли

