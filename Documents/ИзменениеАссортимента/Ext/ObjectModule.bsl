#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если НЕ АссортиментСервер.ВидыЦенИзмененияАссортиментаСоответствуютПравилам(ЭтотОбъект) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ЗначениеЗаполнено(Этап) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Стадия");
	КонецЕсли;
	
	Если Не Истина Тогда // << 26.12.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-948
	ИспользоватьФорматыМагазинов = ПолучитьФункциональнуюОпцию("ИспользоватьФорматыМагазинов");
	
	Если НЕ ЗначениеЗаполнено(ОбъектПланирования) 
		ИЛИ ИспользоватьФорматыМагазинов И ТипЗнч(ОбъектПланирования) = Тип("СправочникСсылка.Склады")
		ИЛИ НЕ ИспользоватьФорматыМагазинов И ТипЗнч(ОбъектПланирования) = Тип("СправочникСсылка.ФорматыМагазинов") Тогда
		
		ШаблонОшибкиОбъектаПланирования = НСтр("ru = 'Поле ""%ОбъектаПланирования%"" не заполнено'");
		Если ИспользоватьФорматыМагазинов Тогда
			ПредставлениеОбъектаПланирования = НСтр("ru = 'Формат магазина'");
		Иначе
			ПредставлениеОбъектаПланирования = НСтр("ru = 'Магазин/склад'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрЗаменить(ШаблонОшибкиОбъектаПланирования, "%ОбъектаПланирования%", ПредставлениеОбъектаПланирования),
			ЭтотОбъект,
			"ОбъектПланирования",
			,
			Отказ);
	
	КонецЕсли;
	КонецЕсли; // >> 26.12.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-948
	
	Если Операция <> Перечисления.ОперацииИзмененияАссортимента.УправлениеКоллекцией Тогда
	
		МассивНепроверяемыхРеквизитов.Добавить("КоллекцияНоменклатуры");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаНачалаЗакупок");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаЗапретаЗакупки");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаНачалаПродаж");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаЗапретаПродажи");
	
	КонецЕсли; 
	
	Если Операция <> Перечисления.ОперацииИзмененияАссортимента.ИзменениеВАссортименте Тогда
	
		МассивНепроверяемыхРеквизитов.Добавить("Этап");
		МассивНепроверяемыхРеквизитов.Добавить("Стадия");
		
	КонецЕсли; 
	
	Если Операция = Перечисления.ОперацииИзмененияАссортимента.УправлениеКоллекцией Тогда
		Если ЗначениеЗаполнено(ДатаНачалаЗакупок) 
			И ЗначениеЗаполнено(ДатаЗапретаЗакупки) 
			И ДатаНачалаЗакупок = ДатаЗапретаЗакупки Тогда
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Период закупок должен быть больше одного дня.'"),
				ЭтотОбъект,
				"ДатаЗапретаЗакупки",
				,
				Отказ);
		
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(ДатаНачалаПродаж) 
			И ЗначениеЗаполнено(ДатаЗапретаПродажи) 
			И ДатаНачалаПродаж = ДатаЗапретаПродажи Тогда
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Период продаж должен быть больше одного дня.'"),
				ЭтотОбъект,
				"ДатаЗапретаПродажи",
				,
				Отказ);
		
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДатаНачалаЗакупок) 
			И ЗначениеЗаполнено(ДатаЗапретаЗакупки) 
			И ДатаНачалаЗакупок > ДатаЗапретаЗакупки Тогда
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Дата окончания закупок меньше чем дата начала.'"),
				ЭтотОбъект,
				"ДатаЗапретаЗакупки",
				,
				Отказ);
		
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДатаНачалаПродаж) 
			И ЗначениеЗаполнено(ДатаЗапретаПродажи) 
			И ДатаНачалаПродаж > ДатаЗапретаПродажи Тогда
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Дата окончания продаж меньше чем дата начала.'"),
				ЭтотОбъект,
				"ДатаЗапретаПродажи",
				,
				Отказ);
		
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ЭтоНовый() Тогда
		ИнициализироватьДокумент(ДанныеЗаполнения);
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный		 = ПользователиКлиентСервер.ТекущийПользователь();
	ДатаНачалаДействия	 = НачалоДня(ТекущаяДатаСеанса());
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеДокументов.ПередЗаписьюДокумента(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	Если Операция = Перечисления.ОперацииИзмененияАссортимента.УправлениеКоллекцией Тогда
		
		Этап = Перечисления.ЭтапыИзмененияАссортимента.ПустаяСсылка();
		Стадия = Перечисления.СтадииАссортимента.ПустаяСсылка();
		
	Иначе
		КоллекцияНоменклатуры = Справочники.КоллекцииНоменклатуры.ПустаяСсылка();
		ДатаНачалаЗакупок = Неопределено;
		ДатаНачалаПродаж = Неопределено;
	КонецЕсли;
	
	// << 26.12.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-948
	КР_ПередЗаписьюДополнительно(Отказ, РежимЗаписи, РежимПроведения);
	// >> 26.12.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-948
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ПриЗаписиДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеДокументов.ОбработкаПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	ПроведениеДокументов.ОбработкаУдаленияПроведенияДокумента(ЭтотОбъект, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//    Таблица - см. УправлениеДоступом.ТаблицаНаборыЗначенийДоступа
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	// Логика ограничения следующая:
	// объект доступен, если доступны все виды цен.
	
	ВидыЦен = Товары.Выгрузить();
	ВидыЦен.Свернуть("ВидЦены");
	
	Для Каждого СтрокаТаблицы Из ВидыЦен Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ВидЦены) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаб = Таблица.Добавить();
		СтрокаТаб.ЗначениеДоступа = СтрокаТаблицы.ВидЦены;
		
	КонецЦикла;
	
	Если Таблица.Количество() = 0 Тогда
		СтрокаТаб = Таблица.Добавить();
		СтрокаТаб.ЗначениеДоступа = Справочники.ВидыЦен.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

// Инициализирует документ
//
Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено)
	
	Ответственный		 = Пользователи.ТекущийПользователь();
	ДатаНачалаДействия	 = НачалоДня(ТекущаяДатаСеанса());
	ОбъектПланирования	 = ЗначениеНастроекПовтИсп.ПолучитьОбъектПланированияПоУмолчанию(ОбъектПланирования);
	
КонецПроцедуры

#КонецОбласти

#Область КР_ДобавленныеПроцедурыИФункции

#Область КР_ОбработчикиСобытий

// << 26.12.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-948
Процедура КР_ПередЗаписьюДополнительно(Отказ, РежимЗаписи, РежимПроведения)
	
	// << 08.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2459
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения
		И Не Пользователи.ЭтоПолноправныйПользователь()
		И РольДоступна("КР_ОграниченноеДобавлениеДокументаИзмененияАссортимента") Тогда 
		ТекстСообщения = 
			НСтр("ru = 'Вам разрешено только добавление/проведение новых документов ""Изменение ассортимента""'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;	
	// >> 08.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2459
	
	Если Этап = Перечисления.ЭтапыИзмененияАссортимента.КР_Заморозка
		// << 25.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
		Или Этап = Перечисления.ЭтапыИзмененияАссортимента.КР_ПереводНаВременнуюКонсервацию
		Или Этап = Перечисления.ЭтапыИзмененияАссортимента.КР_РазблокировкаВременнойКонсервации
		// >> 25.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
		Или Этап = Перечисления.ЭтапыИзмененияАссортимента.КР_Приостановка
		// << 10.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2403
		Или Этап = Перечисления.ЭтапыИзмененияАссортимента.КР_СнятиеЗаморозки
		// >> 10.11.2023 Марченко С.Н., КРОК, JIRA№A2105505-2403
		Тогда
		Товары.Очистить();
	Иначе
		//КР_ТоварыСХарактеристикой.Очистить();
		КР_ЗаполнитьТоварыСХарактеристикойИзТовары();
	КонецЕсли;
	
	Если Этап = Перечисления.ЭтапыИзмененияАссортимента.ВводВАссортимент
			Или Этап = Перечисления.ЭтапыИзмененияАссортимента.КР_ЗапретОтгрузок Тогда
		// A2105505-1015 - отказ от контроля уникальности
		//Если ЗначениеЗаполнено(КР_Контейнер)
		//		И РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		//	КР_КонтрольУникальностиДокумента(Отказ);
		//КонецЕсли;
	Иначе
		КР_Контейнер = Справочники.КР_Контейнеры.ПустаяСсылка();
	КонецЕсли;
	
	// << 25.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
	КР_КонтрольТоваровРазблокировка(Отказ, РежимЗаписи, РежимПроведения);
	// >> 25.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
	
КонецПроцедуры

#КонецОбласти

#Область КР_СлужебныеПроцедурыИФункции

// << 17.05.2023 Федотов А.М., КРОК, Jira№A2105505-1666
Процедура КР_ЗаполнитьТоварыСХарактеристикойИзТовары()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.РольАссортимента КАК РольАссортимента
		|ПОМЕСТИТЬ ВТТовары
		|ИЗ
		|	&Товары КАК Товары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТТовары.Номенклатура КАК Номенклатура,
		|	ВТТовары.РольАссортимента КАК РольАссортимента,
		|	ВЫБОР
		|		КОГДА СпрНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
		|			ТОГДА СпрНоменклатура.Ссылка
		|		КОГДА СпрНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
		|			ТОГДА СпрНоменклатура.ВидНоменклатуры
		|		КОГДА СпрНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
		|			ТОГДА СпрНоменклатура.ВладелецХарактеристик
		|		КОГДА СпрНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать)
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ВладалецХарактеристик
		|ПОМЕСТИТЬ ВТВладелецХарактеристик
		|ИЗ
		|	ВТТовары КАК ВТТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО ВТТовары.Номенклатура = СпрНоменклатура.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТВладелецХарактеристик.Номенклатура КАК Номенклатура,
		|	ВТВладелецХарактеристик.РольАссортимента КАК РольАссортимента,
		|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, НЕОПРЕДЕЛЕНО) КАК Характеристика,
		|	0 КАК НормаПотребления
		|ИЗ
		|	ВТВладелецХарактеристик КАК ВТВладелецХарактеристик
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
		|		ПО ВТВладелецХарактеристик.ВладалецХарактеристик = ХарактеристикиНоменклатуры.Владелец
		|			И (НЕ ХарактеристикиНоменклатуры.ПометкаУдаления)";
	
	Запрос.УстановитьПараметр("Товары", Товары.Выгрузить());
	РезультатЗапроса = Запрос.Выполнить();
	КР_ТоварыСХарактеристикой.Загрузить(РезультатЗапроса.Выгрузить());	
	
КонецПроцедуры // >> 17.05.2023 Федотов А.М., КРОК, Jira№A2105505-1666

Процедура КР_КонтрольУникальностиДокумента(Отказ)
	
	// Прим. разработчика
	// По требованию ФДР документы контролируются без огрнаичений по периоду
	// Потенциально запрос к таблице документов не оптимален,
	// однако запрос к 2 регистрам сведений и их связь по регистратору
	// также потенциально снизит производительность
	//
	// Прим. разработчика 2
	// Порядок условий изменен так, что предположительно улучшит
	// производительность, если включить индексирование КР_Контейнер
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ОбъектПланирования", ОбъектПланирования);
	Запрос.УстановитьПараметр("Контейнер", КР_Контейнер);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИзменениеАссортимента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ИзменениеАссортимента КАК ИзменениеАссортимента
	|ГДЕ
	|	ИзменениеАссортимента.КР_Контейнер = &Контейнер
	|	И ИзменениеАссортимента.ОбъектПланирования = &ОбъектПланирования
	|	И ИзменениеАссортимента.Проведен
	|	И ИзменениеАссортимента.Ссылка <> &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстОшибки = НСтр("ru = 'Документ ""%1"" с выбранным контейнером №%2 по объекту планирования ""%3"" уже проведен.
								 |%4'");
		ТекстОшибки = СтрШаблон(ТекстОшибки,
			Метаданные().Синоним,
			КР_Контейнер,
			ОбъектПланирования,
			Выборка.Ссылка);
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры // >> 26.12.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-948

// << 25.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
Процедура КР_КонтрольТоваровРазблокировка(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		Возврат;
	КонецЕсли;
	
	// A2105505-2037
	// Добавлена логика полных прав
	ЛокальныйОтказ = Ложь; 
	//
	
	КР_КонтрольТоваровСтрогаяКонсервация(ЛокальныйОтказ);
	КР_КонтрольТоваровВременняКонсервация(ЛокальныйОтказ);    
	
	// A2105505-2037
	Если ЛокальныйОтказ 
		И Не Пользователи.ЭтоПолноправныйПользователь() Тогда 
		Отказ = ЛокальныйОтказ;
	КонецЕсли;	
	//
	
КонецПроцедуры // >> 25.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553

// << 27.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
Процедура КР_КонтрольТоваровСтрогаяКонсервация(Отказ)
	
	Если Не Этап = Перечисления.ЭтапыИзмененияАссортимента.КР_РазблокировкаСтрогойКонсервации Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаТовары 		= КР_ТоварыСХарактеристикой.Выгрузить(, "Номенклатура,Характеристика");
	ГруппаАксессуары 	= КР_ДополнительныеНастройкиПовтИсп.Значение("ГруппаВидовНоменклатурыАксессуаров");
	
	Запрос = Новый Запрос;
	Запрос.Текст = КР_ТекстЗапросаКонтрольТоваровСтрогаяКонсервация();
	Запрос.УстановитьПараметр("ТаблицаТовары", 		ТаблицаТовары);
	Запрос.УстановитьПараметр("Период", 			Дата);
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	Запрос.УстановитьПараметр("Склад", 				ОбъектПланирования);
	Запрос.УстановитьПараметр("ГруппаАксессуары", 	ГруппаАксессуары);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Артикулы = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Артикулы.Добавить(Выборка.Артикул);
	КонецЦикла;
	
	АртикулыОписание = СтрСоединить(Артикулы, ", ");
	            
	Шаблон = НСтр("ru = 'Данный товар (%1) не заблокирован. Для проведения документа данные строки необходимо удалить.'");
	ТекстОшибки = СтрШаблон(Шаблон, АртикулыОписание);
	ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
	
КонецПроцедуры
// >> 27.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553

// << 27.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
Процедура КР_КонтрольТоваровВременняКонсервация(Отказ)
	
	Если Не Этап = Перечисления.ЭтапыИзмененияАссортимента.КР_РазблокировкаВременнойКонсервации Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаТовары 		= КР_ТоварыСХарактеристикой.Выгрузить(, "Номенклатура, Характеристика");
	
	Запрос = Новый Запрос;
	Запрос.Текст = КР_ТекстЗапросаКонтрольТоваровВременняКонсервация();
	Запрос.УстановитьПараметр("ТаблицаТовары", 		ТаблицаТовары);
	Запрос.УстановитьПараметр("Период", 			Дата);
	Запрос.УстановитьПараметр("Склад", 				ОбъектПланирования);
	Запрос.УстановитьПараметр("Ссылка", 			Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Артикулы = Новый Массив;
	
	Пока Выборка.Следующий() Цикл
		Артикулы.Добавить(Выборка.Артикул);
	КонецЦикла;
	
	АртикулыОписание = СтрСоединить(Артикулы, ", ");
	
	Шаблон = НСтр("ru = 'Данный товар (%1) не заблокирован. Для проведения документа данные строки необходимо удалить.'");
	ТекстОшибки = СтрШаблон(Шаблон, АртикулыОписание);
	ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
	
КонецПроцедуры
// >> 27.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553

// << 25.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
Функция КР_ТекстЗапросаКонтрольТоваровСтрогаяКонсервация()
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	&Склад КАК Склад
	|ПОМЕСТИТЬ втТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникНоменклатура.Ссылка КАК Номенклатура,
	|	втТаблицаТовары.Характеристика КАК Характеристика,
	|	втТаблицаТовары.Склад КАК Склад,
	|	СправочникНоменклатура.ВидНоменклатуры КАК ТоварнаяГруппа,
	|	СправочникНоменклатура.ВидНоменклатуры.Родитель = &ГруппаАксессуары КАК ЭтоАксессуар
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	втТаблицаТовары КАК втТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО втТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИсторияИзмененияФорматовМагазинов.ФорматМагазина КАК ФорматМагазина,
	|	ИсторияИзмененияФорматовМагазинов.КР_ФорматМагазинаАксессуары КАК ФорматМагазинаАксессуары
	|ПОМЕСТИТЬ втФорматыМагазинов
	|ИЗ
	|	РегистрСведений.ИсторияИзмененияФорматовМагазинов.СрезПоследних(&Период, Склад = &Склад) КАК ИсторияИзмененияФорматовМагазинов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТовары.Склад КАК Склад,
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА втТовары.ЭтоАксессуар
	|			ТОГДА втФорматыМагазинов.ФорматМагазинаАксессуары
	|		ИНАЧЕ втФорматыМагазинов.ФорматМагазина
	|	КОНЕЦ КАК ФорматМагазина
	|ПОМЕСТИТЬ втФормат
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ втФорматыМагазинов КАК втФорматыМагазинов
	|		ПО (ИСТИНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(КР_РасширенныйАссортимент.Период) КАК Период,
	|	КР_РасширенныйАссортимент.Номенклатура КАК Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика КАК Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования КАК ОбъектПланирования
	|ПОМЕСТИТЬ втРасширенныйАссортиментПред
	|ИЗ
	|	втФормат КАК втФормат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КР_РасширенныйАссортимент КАК КР_РасширенныйАссортимент
	|		ПО втФормат.ФорматМагазина = КР_РасширенныйАссортимент.ОбъектПланирования
	|			И втФормат.Номенклатура = КР_РасширенныйАссортимент.Номенклатура
	|			И втФормат.Характеристика = КР_РасширенныйАссортимент.Характеристика
	|ГДЕ
	|	КР_РасширенныйАссортимент.Период <= &Период
	|	И НЕ КР_РасширенныйАссортимент.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КР_РасширенныйАссортимент.Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КР_РасширенныйАссортимент.Номенклатура КАК Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика КАК Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования КАК ФорматМагазина,
	|	МАКСИМУМ(КР_РасширенныйАссортимент.РольАссортимента) КАК РольАссортимента,
	|	МАКСИМУМ(КР_РасширенныйАссортимент.РазрешеныПродажи) КАК РазрешеныПродажи
	|ПОМЕСТИТЬ втРасширенныйАссортимент
	|ИЗ
	|	втРасширенныйАссортиментПред КАК втРасширенныйАссортиментПред
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КР_РасширенныйАссортимент КАК КР_РасширенныйАссортимент
	|		ПО втРасширенныйАссортиментПред.Период = КР_РасширенныйАссортимент.Период
	|			И втРасширенныйАссортиментПред.Номенклатура = КР_РасширенныйАссортимент.Номенклатура
	|			И втРасширенныйАссортиментПред.Характеристика = КР_РасширенныйАссортимент.Характеристика
	|			И втРасширенныйАссортиментПред.ОбъектПланирования = КР_РасширенныйАссортимент.ОбъектПланирования
	|ГДЕ
	|	КР_РасширенныйАссортимент.Период <= &Период
	|	И НЕ КР_РасширенныйАссортимент.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КР_РасширенныйАссортимент.Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втФормат.Склад КАК Склад,
	|	втФормат.Номенклатура КАК Номенклатура,
	|	втФормат.Характеристика КАК Характеристика,
	|	втФормат.ФорматМагазина КАК ФорматМагазина,
	|	втРасширенныйАссортимент.РольАссортимента КАК РольАссортимента,
	|	втРасширенныйАссортимент.РазрешеныПродажи КАК РазрешеныПродажи
	|ПОМЕСТИТЬ втСостоянияПоФормату
	|ИЗ
	|	втФормат КАК втФормат
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРасширенныйАссортимент КАК втРасширенныйАссортимент
	|		ПО втФормат.Номенклатура = втРасширенныйАссортимент.Номенклатура
	|			И втФормат.Характеристика = втРасширенныйАссортимент.Характеристика
	|			И втФормат.ФорматМагазина = втРасширенныйАссортимент.ФорматМагазина
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(КР_РасширенныйАссортимент.Период) КАК Период,
	|	КР_РасширенныйАссортимент.Номенклатура КАК Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика КАК Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования КАК ОбъектПланирования
	|ПОМЕСТИТЬ втРасширенныйАссортиментСкладПред
	|ИЗ
	|	втФормат КАК втФормат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КР_РасширенныйАссортимент КАК КР_РасширенныйАссортимент
	|		ПО втФормат.Склад = КР_РасширенныйАссортимент.ОбъектПланирования
	|			И втФормат.Номенклатура = КР_РасширенныйАссортимент.Номенклатура
	|			И втФормат.Характеристика = КР_РасширенныйАссортимент.Характеристика
	|ГДЕ
	|	НЕ КР_РасширенныйАссортимент.Этап В (ЗНАЧЕНИЕ(Перечисление.ЭтапыИзмененияАссортимента.КР_ПереводНаВременнуюКонсервацию), ЗНАЧЕНИЕ(Перечисление.ЭтапыИзмененияАссортимента.КР_РазблокировкаВременнойКонсервации))
	|	И НЕ КР_РасширенныйАссортимент.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КР_РасширенныйАссортимент.Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КР_РасширенныйАссортимент.Номенклатура КАК Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика КАК Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования КАК Склад,
	|	МАКСИМУМ(КР_РасширенныйАссортимент.РольАссортимента) КАК РольАссортимента,
	|	МАКСИМУМ(КР_РасширенныйАссортимент.РазрешеныПродажи) КАК РазрешеныПродажи
	|ПОМЕСТИТЬ втРасширенныйАссортиментСклад
	|ИЗ
	|	втРасширенныйАссортиментСкладПред КАК втРасширенныйАссортиментСкладПред
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КР_РасширенныйАссортимент КАК КР_РасширенныйАссортимент
	|		ПО втРасширенныйАссортиментСкладПред.Период = КР_РасширенныйАссортимент.Период
	|			И втРасширенныйАссортиментСкладПред.Номенклатура = КР_РасширенныйАссортимент.Номенклатура
	|			И втРасширенныйАссортиментСкладПред.Характеристика = КР_РасширенныйАссортимент.Характеристика
	|			И втРасширенныйАссортиментСкладПред.ОбъектПланирования = КР_РасширенныйАссортимент.ОбъектПланирования
	|ГДЕ
	|	НЕ КР_РасширенныйАссортимент.Этап В (ЗНАЧЕНИЕ(Перечисление.ЭтапыИзмененияАссортимента.КР_ПереводНаВременнуюКонсервацию), ЗНАЧЕНИЕ(Перечисление.ЭтапыИзмененияАссортимента.КР_РазблокировкаВременнойКонсервации))
	|
	|СГРУППИРОВАТЬ ПО
	|	КР_РасширенныйАссортимент.Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втСостоянияПоФормату.Склад КАК Склад,
	|	втСостоянияПоФормату.Номенклатура КАК Номенклатура,
	|	втСостоянияПоФормату.Характеристика КАК Характеристика,
	|	втСостоянияПоФормату.ФорматМагазина КАК ФорматМагазина,
	|	ЕСТЬNULL(втРасширенныйАссортиментСклад.РольАссортимента, втСостоянияПоФормату.РольАссортимента) КАК РольАссортимента,
	|	ЕСТЬNULL(втРасширенныйАссортиментСклад.РазрешеныПродажи, втСостоянияПоФормату.РазрешеныПродажи) КАК РазрешеныПродажи
	|ПОМЕСТИТЬ втСостояния
	|ИЗ
	|	втСостоянияПоФормату КАК втСостоянияПоФормату
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРасширенныйАссортиментСклад КАК втРасширенныйАссортиментСклад
	|		ПО втСостоянияПоФормату.Номенклатура = втРасширенныйАссортиментСклад.Номенклатура
	|			И втСостоянияПоФормату.Характеристика = втРасширенныйАссортиментСклад.Характеристика
	|			И втСостоянияПоФормату.Склад = втРасширенныйАссортиментСклад.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСостояния.Номенклатура.Артикул КАК Артикул
	|ИЗ
	|	втСостояния КАК втСостояния
	|ГДЕ
	|	(втСостояния.РазрешеныПродажи = ИСТИНА
	|			ИЛИ втСостояния.РазрешеныПродажи ЕСТЬ NULL
	|				И втСостояния.РольАссортимента ЕСТЬ NULL
	|			ИЛИ НЕ втСостояния.РольАссортимента = ЗНАЧЕНИЕ(Перечисление.РолиАссортимента.ДополнительныйАссортимент))";
	
	Возврат Текст;
	
КонецФункции // >> 25.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553

// << 27.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553
Функция КР_ТекстЗапросаКонтрольТоваровВременняКонсервация()
	
	Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	&Склад КАК Склад
	|ПОМЕСТИТЬ втТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(КР_РасширенныйАссортимент.Период) КАК Период,
	|	КР_РасширенныйАссортимент.Номенклатура КАК Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика КАК Характеристика,
	|	КР_РасширенныйАссортимент.ОбъектПланирования КАК ОбъектПланирования
	|ПОМЕСТИТЬ втРасширенныйАссортиментПред
	|ИЗ
	|	втТаблицаТовары КАК втТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КР_РасширенныйАссортимент КАК КР_РасширенныйАссортимент
	|		ПО втТаблицаТовары.Номенклатура = КР_РасширенныйАссортимент.Номенклатура
	|			И втТаблицаТовары.Характеристика = КР_РасширенныйАссортимент.Характеристика
	|			И втТаблицаТовары.Склад = КР_РасширенныйАссортимент.ОбъектПланирования
	|ГДЕ
	|	НЕ КР_РасширенныйАссортимент.Этап В (ЗНАЧЕНИЕ(Перечисление.ЭтапыИзмененияАссортимента.КР_ПереводНаСтрогуюКонсервацию), ЗНАЧЕНИЕ(Перечисление.ЭтапыИзмененияАссортимента.КР_РазблокировкаСтрогойКонсервации))
	|	И КР_РасширенныйАссортимент.Период <= &Период
	|	И НЕ КР_РасширенныйАссортимент.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КР_РасширенныйАссортимент.ОбъектПланирования,
	|	КР_РасширенныйАссортимент.Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КР_РасширенныйАссортимент.ОбъектПланирования КАК ФорматМагазина,
	|	КР_РасширенныйАссортимент.Номенклатура КАК Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика КАК Характеристика,
	|	КР_РасширенныйАссортимент.РольАссортимента КАК РольАссортимента,
	|	КР_РасширенныйАссортимент.РазрешеныПродажи КАК РазрешеныПродажи
	|ПОМЕСТИТЬ втРасширенныйАссортимент
	|ИЗ
	|	втРасширенныйАссортиментПред КАК втРасширенныйАссортиментПред
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КР_РасширенныйАссортимент КАК КР_РасширенныйАссортимент
	|		ПО втРасширенныйАссортиментПред.Период = КР_РасширенныйАссортимент.Период
	|			И втРасширенныйАссортиментПред.Номенклатура = КР_РасширенныйАссортимент.Номенклатура
	|			И втРасширенныйАссортиментПред.Характеристика = КР_РасширенныйАссортимент.Характеристика
	|			И втРасширенныйАссортиментПред.ОбъектПланирования = КР_РасширенныйАссортимент.ОбъектПланирования
	|ГДЕ
	|	НЕ КР_РасширенныйАссортимент.Этап В (ЗНАЧЕНИЕ(Перечисление.ЭтапыИзмененияАссортимента.КР_ПереводНаСтрогуюКонсервацию), ЗНАЧЕНИЕ(Перечисление.ЭтапыИзмененияАссортимента.КР_РазблокировкаСтрогойКонсервации))
	|	И КР_РасширенныйАссортимент.Период <= &Период
	|	И НЕ КР_РасширенныйАссортимент.Регистратор = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КР_РасширенныйАссортимент.ОбъектПланирования,
	|	КР_РасширенныйАссортимент.Номенклатура,
	|	КР_РасширенныйАссортимент.Характеристика,
	|	КР_РасширенныйАссортимент.РольАссортимента,
	|	КР_РасширенныйАссортимент.РазрешеныПродажи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СправочникНоменклатура.Артикул КАК Артикул
	|ИЗ
	|	втТаблицаТовары КАК втТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ втРасширенныйАссортимент КАК втРасширенныйАссортимент
	|		ПО втТаблицаТовары.Склад = втРасширенныйАссортимент.ФорматМагазина
	|			И втТаблицаТовары.Номенклатура = втРасширенныйАссортимент.Номенклатура
	|			И втТаблицаТовары.Характеристика = втРасширенныйАссортимент.Характеристика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО втТаблицаТовары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	(втРасширенныйАссортимент.РазрешеныПродажи = ИСТИНА
	|			ИЛИ втРасширенныйАссортимент.РазрешеныПродажи ЕСТЬ NULL
	|				И втРасширенныйАссортимент.РольАссортимента ЕСТЬ NULL
	|			ИЛИ НЕ втРасширенныйАссортимент.РольАссортимента = ЗНАЧЕНИЕ(Перечисление.РолиАссортимента.ДополнительныйАссортимент))";
	
	Возврат Текст;
	
КонецФункции // >> 27.04.2023, Гашников А.П., КРОК, Jira№ A2105505-1553

#КонецОбласти

#КонецОбласти

#КонецЕсли