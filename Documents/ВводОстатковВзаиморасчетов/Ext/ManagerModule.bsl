#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


#Область Проведение

// Возвращает таблицы для движений, необходимые для проведения документа по регистрам учетных механизмов.
//
// Параметры:
//  Документ - ДокументСсылка - ссылка на документ, по которому необходимо получить данные
//  Регистры - Структура - список имен регистров, для которых необходимо получить таблицы
//  ДопПараметры - Структура - дополнительные параметры для получения данных, определяющие контекст проведения.
//
// Возвращаемое значение:
//  Структура - коллекция элементов:
//     * Таблица_ИмяРегистра - ТаблицаЗначений - таблица данных для отражения в регистр (вместо _ИмяРегистра подставляется имя самого регистра).
//
Функция ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, Документ);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры);
	Если Документ.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам Тогда
		ДействующиеПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитик("УчетнаяПолитикаФинансовогоУчета", Документ.Организация, Документ.Дата);
		Дисконт = ДействующиеПараметрыУчетнойПолитики.УчетДисконтированнойКредиторскойЗадолженностиПоставщикам;
		Если Дисконт Тогда
			ТекстЗапросаТаблицаПроцентныеРасходыДисконтирования(Запрос, ТекстыЗапроса, Регистры);
		КонецЕсли;
	КонецЕсли;
	ВводОстатковЛокализация.ВводОстатковВзаиморасчетовДополнитьТекстыЗапросовПроведения(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеДокументов.ДобавитьЗапросыСторноДвижений(Запрос, ТекстыЗапроса, Регистры, ПустаяСсылка().Метаданные());
	
	////////////////////////////////////////////////////////////////////////////
	// Получим таблицы для движений
	
	Возврат ПроведениеДокументов.ИнициализироватьДанныеДокументаДляПроведения(Запрос, ТекстыЗапроса, ДопПараметры);
	
КонецФункции

// Описывает учетные механизмы используемые в документе для регистрации в механизме проведения.
//
// Параметры:
//  МеханизмыДокумента - Массив - список имен учетных механизмов, для которых будет выполнена
//              регистрация в механизме проведения.
//
Процедура ЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента) Экспорт
	
	МеханизмыДокумента.Добавить("Взаиморасчеты");
	МеханизмыДокумента.Добавить("УчетПрочихАктивовПассивов");
	МеханизмыДокумента.Добавить("РеестрДокументов");
	
	ВводОстатковЛокализация.ВводОстатковВзаиморасчетовЗарегистрироватьУчетныеМеханизмы(МеханизмыДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеНаОсновании

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	ИсправлениеДокументов.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, ПустаяСсылка().Метаданные());
	
	ВводОстатковЛокализация.ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры);
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	ВводОстатковЛокализация.ВводОстатковВзаиморасчетовДобавитьКомандыОтчетов(КомандыОтчетов, Параметры);
КонецПроцедуры

#КонецОбласти


#Область ДляВызоваИзДругихПодсистем

// Добавляет команду создания документа на основании.
//
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
// Возвращаемое значение:
//	КомандаФормы - добавляемая команда.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Возврат СозданиеНаОсновании.ДобавитьКомандуСозданияНаОсновании(КомандыСозданияНаОсновании, Метаданные.Документы.ВводОстатковВзаиморасчетов);
	
КонецФункции

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация) ";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)

	ВыбраннаяОперация = Неопределено;
	
	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		ВыбраннаяОперация = Параметры.Ключ.ХозяйственнаяОперация;
		
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") Тогда
		
		Параметры.ЗначенияЗаполнения.Свойство("ХозяйственнаяОперация", ВыбраннаяОперация);
		
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") Тогда
		
		ВыбраннаяОперация = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Параметры.ЗначениеКопирования,"ХозяйственнаяОперация");
		
	ИначеЕсли Параметры.Свойство("ОтборПоТипамОпераций") И Параметры.ОтборПоТипамОпераций.Количество() = 1 Тогда
		
		ВыбраннаяОперация = Параметры.ОтборПоТипамОпераций[0].Значение;
		
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("ХозяйственнаяОперация", ВыбраннаяОперация);
		
		Параметры.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		Если Параметры.Свойство("Организация") Тогда
			ЗначенияЗаполнения.Вставить("Организация", Параметры.Организация);
		КонецЕсли;
		
		Если Параметры.Свойство("ОтражатьВОперативномУчете") Тогда
			ЗначенияЗаполнения.Вставить("ОтражатьВОперативномУчете", Параметры.ОтражатьВОперативномУчете);
		КонецЕсли;
		
		Если Параметры.Свойство("ОтражатьВБУиНУ") Тогда
			ЗначенияЗаполнения.Вставить("ОтражатьВБУиНУ", Параметры.ОтражатьВБУиНУ);
		КонецЕсли;
		
		Если Параметры.Свойство("ОтражатьВУУ") Тогда
			ЗначенияЗаполнения.Вставить("ОтражатьВУУ", Параметры.ОтражатьВУУ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбраннаяОперация) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		СоответствиеТиповОперацийФормам = Новый Соответствие;
		СоответствиеТиповОперацийФормам.Вставить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов"),                   "ФормаРасчетыСПартнерами");
		СоответствиеТиповОперацийФормам.Вставить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам"),                "ФормаРасчетыСПартнерами");
		СоответствиеТиповОперацийФормам.Вставить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВводОстатковАвансовКлиентов"),                         "ФормаРасчетыСПартнерами");
		СоответствиеТиповОперацийФормам.Вставить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам"),                      "ФормаРасчетыСПартнерами");
		
		ВыбраннаяФорма = СоответствиеТиповОперацийФормам[ВыбраннаяОперация];
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("ХозяйственнаяОперация");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Представление = ВводОстатковВызовСервера.ЗаголовокДокументаВводОстатковПоХозяйственнойОперации(Данные,
		Данные.Номер,
		Данные.Дата,
		Данные.ХозяйственнаяОперация);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных; 
КонецФункции

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
		|	ДанныеДокумента.Дата КАК Период,
		|	ДанныеДокумента.Номер КАК Номер,
		|	ДанныеДокумента.Ссылка КАК Ссылка,
		|	ДанныеДокумента.Организация КАК Организация,
		|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеДокумента.ОтражатьВОперативномУчете КАК ОтражатьВОперативномУчете,
		|	ДанныеДокумента.ОтражатьВБУиНУ КАК ОтражатьВБУиНУ,
		|	ДанныеДокумента.ОтражатьВУУ КАК ОтражатьВУУ,
		|	ДанныеДокумента.Ответственный КАК Ответственный,
		|	ПервичныйДокумент.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ВЫРАЗИТЬ(ДанныеДокумента.Комментарий КАК СТРОКА(100)) КАК Комментарий,
		|	ДанныеДокумента.ПометкаУдаления КАК ПометкаУдаления,
		|	ДанныеДокумента.Проведен КАК Проведен,
		|	ДанныеДокумента.Исправление КАК Исправление,
		|	ДанныеДокумента.СторнируемыйДокумент КАК СторнируемыйДокумент,
		|	ДанныеДокумента.ИсправляемыйДокумент КАК ИсправляемыйДокумент,
		|	НастройкиХозяйственныхОпераций.Ссылка КАК НастройкаХозяйственнойОперации,
		|	СУММА(ЕСТЬNULL(ДанныеТабличнойЧастиРасчетыСПартнерами.Сумма, 0)) КАК СуммаДокумента
		|ИЗ
		|	Документ.ВводОстатковВзаиморасчетов КАК ДанныеДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетов.РасчетыСПартнерами КАК ДанныеТабличнойЧастиРасчетыСПартнерами
		|		ПО ДанныеДокумента.Ссылка = ДанныеТабличнойЧастиРасчетыСПартнерами.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
		|		ПО ДанныеДокумента.ХозяйственнаяОперация = НастройкиХозяйственныхОпераций.ХозяйственнаяОперация
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПервичныйДокумент КАК ПервичныйДокумент
		|		ПО ДанныеТабличнойЧастиРасчетыСПартнерами.ДокументРасчетов = ПервичныйДокумент.Ссылка
		|ГДЕ
		|	ДанныеДокумента.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДокумента.Дата,
		|	ДанныеДокумента.Номер,
		|	ДанныеДокумента.Ссылка,
		|	ДанныеДокумента.Организация,
		|	ДанныеДокумента.ХозяйственнаяОперация,
		|	ДанныеДокумента.ОтражатьВОперативномУчете,
		|	ДанныеДокумента.ОтражатьВБУиНУ,
		|	ДанныеДокумента.ОтражатьВУУ,
		|	ДанныеДокумента.Ответственный,
		|	ВЫРАЗИТЬ(ДанныеДокумента.Комментарий КАК СТРОКА(100)),
		|	ДанныеДокумента.ПометкаУдаления,
		|	ПервичныйДокумент.НаправлениеДеятельности, 
		|	ДанныеДокумента.Проведен,
		|	ДанныеДокумента.Исправление,
		|	ДанныеДокумента.СторнируемыйДокумент,
		|	ДанныеДокумента.ИсправляемыйДокумент,
		|	НастройкиХозяйственныхОпераций.Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();

	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Номер",  Реквизиты.Номер);
	
	СписокТиповЗаказов = Новый СписокЗначений;
	СписокТиповЗаказов.Добавить(Тип("СправочникСсылка.ДоговорыКонтрагентов"));
	СписокТиповЗаказов.Добавить(Тип("СправочникСсылка.ДоговорыМеждуОрганизациями"));
	СписокТиповЗаказов.Добавить(Тип("ДокументСсылка.ЗаказКлиента"));
	СписокТиповЗаказов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
	СписокТиповЗаказов.Добавить(Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента"));
	Запрос.УстановитьПараметр("СписокТиповЗаказов", СписокТиповЗаказов);
	
	Запрос.УстановитьПараметр("Организация",                    Реквизиты.Организация);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",          Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Реквизиты.Организация));
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",     Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ОтражатьВОперативномУчете",      Реквизиты.ОтражатьВОперативномУчете);
	Запрос.УстановитьПараметр("ОтражатьВБУиНУ",                 Реквизиты.ОтражатьВБУиНУ);
	Запрос.УстановитьПараметр("ОтражатьВУУ",                    Реквизиты.ОтражатьВУУ);
	Запрос.УстановитьПараметр("Ответственный",                  Реквизиты.Ответственный);
	Запрос.УстановитьПараметр("Комментарий",                    Реквизиты.Комментарий);
	Запрос.УстановитьПараметр("ПометкаУдаления",                Реквизиты.ПометкаУдаления);
	Запрос.УстановитьПараметр("Проведен",                       Реквизиты.Проведен);
	Запрос.УстановитьПараметр("Исправление",                    Реквизиты.Исправление);
	Запрос.УстановитьПараметр("СторнируемыйДокумент",           Реквизиты.СторнируемыйДокумент);
	Запрос.УстановитьПараметр("ИсправляемыйДокумент",           Реквизиты.ИсправляемыйДокумент);
	Запрос.УстановитьПараметр("СуммаДокумента",                 Реквизиты.СуммаДокумента);
	Запрос.УстановитьПараметр("НастройкаХозяйственнойОперации", Реквизиты.НастройкаХозяйственнойОперации);
	Запрос.УстановитьПараметр("ИдентификаторМетаданных", 
		ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Документ."+Метаданные.Документы.ВводОстатковВзаиморасчетов.Имя));
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиПоПартнерам(Запрос)
	Если Запрос.Параметры.Свойство("КлючиАналитикиПоПартнерамИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитик = Новый Запрос;
	ЗапросАналитик.УстановитьПараметр("Ссылка"     ,               Запрос.Параметры.Ссылка);
	ЗапросАналитик.УстановитьПараметр("Организация",               Запрос.Параметры.Организация);
	ЗапросАналитик.УстановитьПараметр("ХозяйственнаяОперация",     Запрос.Параметры.ХозяйственнаяОперация);
	ЗапросАналитик.УстановитьПараметр("ОтражатьВОперативномУчете", Запрос.Параметры.ОтражатьВОперативномУчете);
	
	ЗапросАналитик.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
	ЗапросАналитик.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРасчетыСПартнерами.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСПартнерами.ОбъектРасчетовОтправитель КАК ОбъектРасчетовОтправитель,
	|	ВЫБОР
	|		КОГДА ОбъектыРасчетов.Организация.ГоловнаяОрганизация = &Организация
	|			ТОГДА ОбъектыРасчетов.Организация
	|		ИНАЧЕ &Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам))
	|					И ЕСТЬNULL(Направления.УчетРасчетовСПоставщиками, ЛОЖЬ)
	|				ИЛИ &ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовКлиентов))
	|					И ЕСТЬNULL(Направления.УчетДоходов, ЛОЖЬ)
	|			ТОГДА ТаблицаРасчетыСПартнерами.ОбъектРасчетовОтправитель.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВтТаблицаОбъектовРасчетов
	|ИЗ
	|	Документ.ВводОстатковВзаиморасчетов.РасчетыСПартнерами КАК ТаблицаРасчетыСПартнерами
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ТаблицаРасчетыСПартнерами.ОбъектРасчетовОтправитель = ОбъектыРасчетов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК Направления
	|		ПО ТаблицаРасчетыСПартнерами.ОбъектРасчетовОтправитель.НаправлениеДеятельности = Направления.Ссылка
	|ГДЕ
	|	ТаблицаРасчетыСПартнерами.Ссылка = &Ссылка
	|	И &ОтражатьВОперативномУчете
	|	И &ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовКлиентов), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОбъектовРасчетов.Организация КАК Организация,
	|	ТаблицаРасчетыСПартнерами.Партнер КАК Партнер,
	|	ТаблицаРасчетыСПартнерами.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТаблицаРасчетыСПартнерами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыКонтрагентов
	|				ИЛИ ТаблицаРасчетыСПартнерами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|			ТОГДА ТаблицаРасчетыСПартнерами.ОбъектРасчетов
	|		КОГДА ТаблицаРасчетыСПартнерами.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|			ТОГДА ЕСТЬNULL(ТаблицаРасчетыСПартнерами.ОбъектРасчетов.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка))
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаРасчетыСПартнерами.ОбъектРасчетов.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|	КОНЕЦ КАК Договор,
	|	ТаблицаОбъектовРасчетов.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	Документ.ВводОстатковВзаиморасчетов.РасчетыСПартнерами КАК ТаблицаРасчетыСПартнерами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаОбъектовРасчетов КАК ТаблицаОбъектовРасчетов
	|		ПО ТаблицаРасчетыСПартнерами.ОбъектРасчетов = ТаблицаОбъектовРасчетов.ОбъектРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РегистрАналитикаУчетаПоПартнерам
	|		ПО (ТаблицаОбъектовРасчетов.Организация = РегистрАналитикаУчетаПоПартнерам.Организация)
	|			И ТаблицаРасчетыСПартнерами.Партнер = РегистрАналитикаУчетаПоПартнерам.Партнер
	|			И ТаблицаРасчетыСПартнерами.Контрагент = РегистрАналитикаУчетаПоПартнерам.Контрагент
	|			И (ВЫБОР
	|				КОГДА ТаблицаРасчетыСПартнерами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыКонтрагентов
	|						ИЛИ ТаблицаРасчетыСПартнерами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|					ТОГДА ТаблицаРасчетыСПартнерами.ОбъектРасчетов
	|				КОГДА ТаблицаРасчетыСПартнерами.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|					ТОГДА ЕСТЬNULL(ТаблицаРасчетыСПартнерами.ОбъектРасчетов.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка))
	|				ИНАЧЕ ЕСТЬNULL(ТаблицаРасчетыСПартнерами.ОбъектРасчетов.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|			КОНЕЦ = РегистрАналитикаУчетаПоПартнерам.Договор)
	|			И (ТаблицаОбъектовРасчетов.НаправлениеДеятельности = РегистрАналитикаУчетаПоПартнерам.НаправлениеДеятельности)
	|ГДЕ
	|	ТаблицаРасчетыСПартнерами.Ссылка = &Ссылка
	|	И РегистрАналитикаУчетаПоПартнерам.КлючАналитики ЕСТЬ NULL
	|	И &ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовКлиентов), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам))";
	
	Выборка = ЗапросАналитик.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаПоПартнерам.ЗначениеКлючаАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиПоПартнерамИнициализированы", Истина);
	
КонецПроцедуры

Функция ТекстЗапросаВтКурсыВалют(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтКурсыВалют";
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.КурсЧислитель / КурсыВалютСрезПоследних.КурсЗнаменатель КАК КоэффициентПересчета
	|	
	|ПОМЕСТИТЬ ВтКурсыВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
	|			&Период,
	|			Валюта В
	|				(ВЫБРАТЬ
	|					&ВалютаУправленческогоУчета
	|				
	|				ОБЪЕДИНИТЬ ВСЕ
	|				
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ВводОстатковРасчетыСПартнерами.ВалютаВзаиморасчетов
	|				ИЗ
	|					Документ.ВводОстатковВзаиморасчетов.РасчетыСПартнерами КАК ВводОстатковРасчетыСПартнерами
	|				ГДЕ
	|					ВводОстатковРасчетыСПартнерами.Ссылка = &Ссылка
	|				) И БазоваяВалюта = &ВалютаРегламентированногоУчета
	|	) КАК КурсыВалютСрезПоследних";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтРасчетыСПартнерами(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтРасчетыСПартнерами";
	
	ИнициализироватьКлючиАналитикиПоПартнерам(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаРасчетыСПартнерами.НомерСтроки КАК НомерСтроки,
	|	ТаблицаРасчетыСПартнерами.ДатаПлатежа КАК ДатаПлатежа,
	|	ТаблицаРасчетыСПартнерами.ОбъектРасчетовОтправитель КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСПартнерами.Сумма КАК Сумма,
	|	ТаблицаРасчетыСПартнерами.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ТаблицаРасчетыСПартнерами.СуммаРегл КАК СуммаРегл,
	|	ТаблицаРасчетыСПартнерами.СуммаУпр КАК СуммаУпр,
	|	ТаблицаРасчетыСПартнерами.СуммаДисконтирования КАК СуммаДисконтирования,
	|	ТаблицаРасчетыСПартнерами.СуммаДисконтированияРегл КАК СуммаДисконтированияРегл,
	|	ТаблицаРасчетыСПартнерами.СуммаДисконтированияУпр КАК СуммаДисконтированияУпр,
	|	ЕСТЬNULL(Аналитика.КлючАналитики, Неопределено) КАК КлючАналитики,
	|	ТаблицаРасчетыСПартнерами.ДокументРасчетов КАК РасчетныйДокумент,
	|	ЕСТЬNULL(ТаблицаПервичногоДокумента.НаправлениеДеятельности,
	|		ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(ТаблицаРасчетыСПартнерами.ДокументРасчетов.Дата, &Период) КАК ДатаРасчетныйДокумент
	|ПОМЕСТИТЬ ВтРасчетыСПартнерами
	|ИЗ
	|	Документ.ВводОстатковВзаиморасчетов.РасчетыСПартнерами КАК ТаблицаРасчетыСПартнерами
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПервичныйДокумент КАК ТаблицаПервичногоДокумента
	|		ПО ТаблицаРасчетыСПартнерами.ДокументРасчетов = ТаблицаПервичногоДокумента.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТаблицаОбъектовРасчетов КАК ТаблицаОбъектовРасчетов
	|		ПО ТаблицаРасчетыСПартнерами.ОбъектРасчетов = ТаблицаОбъектовРасчетов.ОбъектРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО ТаблицаОбъектовРасчетов.Организация = Аналитика.Организация
	|		И ТаблицаРасчетыСПартнерами.Контрагент = Аналитика.Контрагент
	|		И ТаблицаРасчетыСПартнерами.Партнер = Аналитика.Партнер
	|		И (ВЫБОР
	|			КОГДА ТаблицаРасчетыСПартнерами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыКонтрагентов
	|			ИЛИ ТаблицаРасчетыСПартнерами.ОбъектРасчетов ССЫЛКА Справочник.ДоговорыМеждуОрганизациями
	|				ТОГДА ТаблицаРасчетыСПартнерами.ОбъектРасчетов
	|			КОГДА ТаблицаРасчетыСПартнерами.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|				ТОГДА ЕСТЬNULL(ТаблицаРасчетыСПартнерами.ОбъектРасчетов.Договор,
	|					ЗНАЧЕНИЕ(Справочник.ДоговорыМеждуОрганизациями.ПустаяСсылка))
	|			ИНАЧЕ ЕСТЬNULL(ТаблицаРасчетыСПартнерами.ОбъектРасчетов.Договор,
	|				ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка))
	|		КОНЕЦ) = Аналитика.Договор
	|		И ТаблицаОбъектовРасчетов.НаправлениеДеятельности = Аналитика.НаправлениеДеятельности
	|ГДЕ
	|	ТаблицаРасчетыСПартнерами.Ссылка = &Ссылка
	|	И &ОтражатьВОперативномУчете
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	РасчетныйДокумент,
	|	ОбъектРасчетов,
	|	КлючАналитики,
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентами(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСКлиентами";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтРасчетыСПартнерами", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРасчетыСПартнерами(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтКурсыВалют", ТекстыЗапроса) Тогда
		ТекстЗапросаВтКурсыВалют(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	1                                              КАК Порядок,
	|	ТаблицаРасчетыСКлиентами.НомерСтроки           КАК НомерСтроки,
	|	&Период                                        КАК Период,
	|	ТаблицаРасчетыСКлиентами.ДатаРасчетныйДокумент КАК ДатаРегистратора,
	|	&Номер                                         КАК НомерРегистратора,
	|	ТаблицаРасчетыСКлиентами.ДатаПлатежа           КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)         КАК ВидДвижения,
	|	&Организация                                   КАК Организация,
	|	ТаблицаРасчетыСКлиентами.ОбъектРасчетов        КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСКлиентами.ВалютаВзаиморасчетов  КАК Валюта,
	|	ТаблицаРасчетыСКлиентами.КлючАналитики         КАК АналитикаУчетаПоПартнерам,
	|	ИСТИНА                                         КАК ИсключатьПриКонтроле,
	|	ТаблицаРасчетыСКлиентами.Сумма                 КАК Сумма,
	|	0                                              КАК КОплате,
	|	ТаблицаРасчетыСКлиентами.СуммаРегл             КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаРасчетыСКлиентами.СуммаУпр <> 0
	|			ТОГДА ТаблицаРасчетыСКлиентами.СуммаУпр
	|		КОГДА ТаблицаРасчетыСКлиентами.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасчетыСКлиентами.Сумма
	|		ИНАЧЕ
	|			ТаблицаРасчетыСКлиентами.Сумма * ЕСТЬNULL(КурсыВалютВзаиморасчетов.КоэффициентПересчета, 0) / ЕСТЬNULL(КурсыВалютУпр.КоэффициентПересчета, 1)
	|	КОНЕЦ КАК СуммаУпр,
	|	ТаблицаРасчетыСКлиентами.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов) КАК ХозяйственнаяОперация,
	|	&ВалютаРегламентированногоУчета КАК ВалютаДокумента,
	|	ТаблицаРасчетыСКлиентами.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации               КАК НастройкаХозяйственнойОперации,
	|	""1""                                         КАК Вид,
	|	ТаблицаРасчетыСКлиентами.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ВтРасчетыСПартнерами КАК ТаблицаРасчетыСКлиентами
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютВзаиморасчетов
	|		ПО КурсыВалютВзаиморасчетов.Валюта = ТаблицаРасчетыСКлиентами.ВалютаВзаиморасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов)
	|	И &ОтражатьВОперативномУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                                                               КАК Порядок,
	|	ТаблицаРасчетыСКлиентами.НомерСтроки                                            КАК НомерСтроки,
	|	ВЫБОР КОГДА КОНЕЦПЕРИОДА(ТаблицаРасчетыСКлиентами.ДатаПлатежа, День) < &Период
	|		ТОГДА &Период
	|		ИНАЧЕ КОНЕЦПЕРИОДА(ТаблицаРасчетыСКлиентами.ДатаПлатежа, День)
	|	КОНЕЦ                                                                           КАК Период,
	|	ТаблицаРасчетыСКлиентами.ДатаРасчетныйДокумент                                  КАК ДатаРегистратора,
	|	&Номер                                                                          КАК НомерРегистратора,
	|	ТаблицаРасчетыСКлиентами.ДатаПлатежа                                            КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                                          КАК ВидДвижения,
	|	&Организация                                                                    КАК Организация,
	|	ТаблицаРасчетыСКлиентами.ОбъектРасчетов                                         КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСКлиентами.ВалютаВзаиморасчетов                                   КАК Валюта,
	|	ТаблицаРасчетыСКлиентами.КлючАналитики                                          КАК АналитикаУчетаПоПартнерам,
	|	ИСТИНА                                                                          КАК ИсключатьПриКонтроле,
	|	0                                                                               КАК Сумма,
	|	ТаблицаРасчетыСКлиентами.Сумма                                                  КАК КОплате,
	|	0                                                                               КАК СуммаРегл,
	|	0                                                                               КАК СуммаУпр,
	|	ТаблицаРасчетыСКлиентами.РасчетныйДокумент                                      КАК РасчетныйДокумент,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов) КАК ХозяйственнаяОперация,
	|	&ВалютаРегламентированногоУчета                                                 КАК ВалютаДокумента,
	|	ТаблицаРасчетыСКлиентами.ОбъектРасчетов.УникальныйИдентификатор                 КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации                                                 КАК НастройкаХозяйственнойОперации,
	|	""1""                                                                           КАК Вид,
	|	ТаблицаРасчетыСКлиентами.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ВтРасчетыСПартнерами КАК ТаблицаРасчетыСКлиентами
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов)
	|	И &ОтражатьВОперативномУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3                                              КАК Порядок,
	|	ТаблицаРасчетыСКлиентами.НомерСтроки           КАК НомерСтроки,
	|	&Период                                        КАК Период,
	|	ТаблицаРасчетыСКлиентами.ДатаРасчетныйДокумент КАК ДатаРегистратора,
	|	&Номер                                         КАК НомерРегистратора,
	|	NULL                                           КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)         КАК ВидДвижения,
	|	&Организация                                   КАК Организация,
	|	ТаблицаРасчетыСКлиентами.ОбъектРасчетов        КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСКлиентами.ВалютаВзаиморасчетов  КАК ВалютаВзаиморасчетов,
	|	ТаблицаРасчетыСКлиентами.КлючАналитики         КАК АналитикаУчетаПоПартнерам,
	|	ИСТИНА                                         КАК ИсключатьПриКонтроле,
	|	ТаблицаРасчетыСКлиентами.Сумма                 КАК Сумма,
	|	ТаблицаРасчетыСКлиентами.Сумма                 КАК КОплате,
	|	ТаблицаРасчетыСКлиентами.СуммаРегл             КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаРасчетыСКлиентами.СуммаУпр <> 0
	|			ТОГДА ТаблицаРасчетыСКлиентами.СуммаУпр
	|		КОГДА ТаблицаРасчетыСКлиентами.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасчетыСКлиентами.Сумма
	|		ИНАЧЕ
	|			ТаблицаРасчетыСКлиентами.Сумма * ЕСТЬNULL(КурсыВалютВзаиморасчетов.КоэффициентПересчета, 0) / ЕСТЬNULL(КурсыВалютУпр.КоэффициентПересчета, 1)
	|	КОНЕЦ                                         КАК СуммаУпр,
	|	ТаблицаРасчетыСКлиентами.РасчетныйДокумент    КАК РасчетныйДокумент,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовКлиентов) КАК ХозяйственнаяОперация,
	|	&ВалютаРегламентированногоУчета               КАК ВалютаДокумента,
	|	ТаблицаРасчетыСКлиентами.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации               КАК НастройкаХозяйственнойОперации,
	|	""1""                                         КАК Вид,
	|	ТаблицаРасчетыСКлиентами.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ВтРасчетыСПартнерами КАК ТаблицаРасчетыСКлиентами
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютВзаиморасчетов
	|		ПО КурсыВалютВзаиморасчетов.Валюта = ТаблицаРасчетыСКлиентами.ВалютаВзаиморасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовКлиентов)
	|	И &ОтражатьВОперативномУчете
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрАналитикаВзаиморасчетовПоставщик(Запрос)
	
	Если Запрос.Параметры.Свойство("АналитикаВзаиморасчетовПоставщик") Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиПоПартнерам(Запрос);
	
	АналитикаВзаиморасчетовПоставщик = Неопределено;
	
	Запрос.УстановитьПараметр("АналитикаВзаиморасчетовПоставщик", АналитикаВзаиморасчетовПоставщик);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаРасчетыСПоставщиками(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РасчетыСПоставщиками";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтРасчетыСПартнерами", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРасчетыСПартнерами(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтКурсыВалют", ТекстыЗапроса) Тогда
		ТекстЗапросаВтКурсыВалют(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрАналитикаВзаиморасчетовПоставщик(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	1                                                 КАК Порядок,
	|	ТаблицаРасчетыСПоставщиками.НомерСтроки           КАК НомерСтроки,
	|	&Период                                           КАК Период,
	|	ТаблицаРасчетыСПоставщиками.ДатаРасчетныйДокумент КАК ДатаРегистратора,
	|	&Номер                                            КАК НомерРегистратора,
	|	ТаблицаРасчетыСПоставщиками.ДатаПлатежа           КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)            КАК ВидДвижения,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов        КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСПоставщиками.КлючАналитики         КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов  КАК Валюта,
	|	ТаблицаРасчетыСПоставщиками.Сумма                 КАК Сумма,
	|	0                                                 КАК КОплате,
	|	ТаблицаРасчетыСПоставщиками.СуммаРегл             КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаРасчетыСПоставщиками.СуммаУпр <> 0
	|			ТОГДА ТаблицаРасчетыСПоставщиками.СуммаУпр
	|		КОГДА ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасчетыСПоставщиками.Сумма
	|		ИНАЧЕ
	|			ТаблицаРасчетыСПоставщиками.Сумма * ЕСТЬNULL(КурсыВалютВзаиморасчетов.КоэффициентПересчета, 0) / ЕСТЬNULL(КурсыВалютУпр.КоэффициентПересчета, 1)
	|	КОНЕЦ                                            КАК СуммаУпр,
	|	ТаблицаРасчетыСПоставщиками.РасчетныйДокумент    КАК РасчетныйДокумент,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам) КАК ХозяйственнаяОперация,
	|	&ВалютаРегламентированногоУчета                 КАК ВалютаДокумента,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации                 КАК НастройкаХозяйственнойОперации,
	|	""1""                                           КАК Вид
	|ИЗ
	|	ВтРасчетыСПартнерами КАК ТаблицаРасчетыСПоставщиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютВзаиморасчетов
	|		ПО КурсыВалютВзаиморасчетов.Валюта = ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам)
	|	И &ОтражатьВОперативномУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2                                                           КАК Порядок,
	|	ТаблицаРасчетыСПоставщиками.НомерСтроки                     КАК НомерСтроки,
	|	ВЫБОР КОГДА КОНЕЦПЕРИОДА(ТаблицаРасчетыСПоставщиками.ДатаПлатежа, День) < &Период
	|		ТОГДА &Период
	|		ИНАЧЕ КОНЕЦПЕРИОДА(ТаблицаРасчетыСПоставщиками.ДатаПлатежа, День)
	|	КОНЕЦ                                                       КАК Период,
	|	ТаблицаРасчетыСПоставщиками.ДатаРасчетныйДокумент           КАК ДатаРегистратора,
	|	&Номер                                                      КАК НомерРегистратора,
	|	ТаблицаРасчетыСПоставщиками.ДатаПлатежа                     КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)                      КАК ВидДвижения,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов                  КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСПоставщиками.КлючАналитики                   КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов            КАК Валюта,
	|	0                                                           КАК Сумма,
	|	ТаблицаРасчетыСПоставщиками.Сумма                           КАК КОплате,
	|	0                                                           КАК СуммаРегл,
	|	0                                                           КАК СуммаУпр,
	|	ТаблицаРасчетыСПоставщиками.РасчетныйДокумент               КАК РасчетныйДокумент,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам) КАК ХозяйственнаяОперация,
	|	&ВалютаРегламентированногоУчета                             КАК ВалютаДокумента,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации                             КАК НастройкаХозяйственнойОперации,
	|	""1""                                                       КАК Вид
	|ИЗ
	|	ВтРасчетыСПартнерами КАК ТаблицаРасчетыСПоставщиками
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам)
	|	И &ОтражатьВОперативномУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3                                                 КАК Порядок,
	|	ТаблицаРасчетыСПоставщиками.НомерСтроки           КАК НомерСтроки,
	|	&Период                                           КАК Период,
	|	ТаблицаРасчетыСПоставщиками.ДатаРасчетныйДокумент КАК ДатаРегистратора,
	|	&Номер                                            КАК НомерРегистратора,
	|	NULL                                              КАК ДатаПлатежа,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов        КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСПоставщиками.КлючАналитики         КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов  КАК ВалютаВзаиморасчетов,
	|	ТаблицаРасчетыСПоставщиками.Сумма                 КАК Сумма,
	|	ТаблицаРасчетыСПоставщиками.Сумма                 КАК КОплате,
	|	ТаблицаРасчетыСПоставщиками.СуммаРегл             КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаРасчетыСПоставщиками.СуммаУпр <> 0
	|			ТОГДА ТаблицаРасчетыСПоставщиками.СуммаУпр
	|		КОГДА ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасчетыСПоставщиками.Сумма
	|		ИНАЧЕ
	|			ТаблицаРасчетыСПоставщиками.Сумма * ЕСТЬNULL(КурсыВалютВзаиморасчетов.КоэффициентПересчета, 0) / ЕСТЬNULL(КурсыВалютУпр.КоэффициентПересчета, 1)
	|	КОНЕЦ                                            КАК СуммаУпр,
	|	ТаблицаРасчетыСПоставщиками.РасчетныйДокумент    КАК РасчетныйДокумент,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам) КАК ХозяйственнаяОперация,
	|	&ВалютаРегламентированногоУчета                  КАК ВалютаДокумента,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи,
	|	&НастройкаХозяйственнойОперации                  КАК НастройкаХозяйственнойОперации,
	|	""1""                                            КАК Вид
	|ИЗ
	|	ВтРасчетыСПартнерами КАК ТаблицаРасчетыСПоставщиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютВзаиморасчетов
	|		ПО КурсыВалютВзаиморасчетов.Валюта = ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам)
	|	И &ОтражатьВОперативномУчете
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПроцентныеРасходыДисконтирования(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПроцентныеРасходыДисконтирования";
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтРасчетыСПартнерами", ТекстыЗапроса) Тогда
		ТекстЗапросаВтРасчетыСПартнерами(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.ЕстьТаблицаЗапроса("ВтКурсыВалют", ТекстыЗапроса) Тогда
		ТекстЗапросаВтКурсыВалют(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	УстановитьПараметрАналитикаВзаиморасчетовПоставщик(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	1                                                 КАК Порядок,
	|	&Период                                           КАК Период,
	|	ТаблицаРасчетыСПоставщиками.ДатаРасчетныйДокумент КАК ДатаРегистратора,
	|	&Номер                                            КАК НомерРегистратора,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)            КАК ВидДвижения,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов        КАК ОбъектРасчетов,
	|	ТаблицаРасчетыСПоставщиками.КлючАналитики         КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов  КАК Валюта,
	|	СУММА(ТаблицаРасчетыСПоставщиками.СуммаДисконтирования) КАК СуммаДисконтирования,
	|	СУММА(ТаблицаРасчетыСПоставщиками.СуммаДисконтированияРегл) КАК СуммаДисконтированияРегл,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаРасчетыСПоставщиками.СуммаДисконтированияУпр <> 0
	|			ТОГДА ТаблицаРасчетыСПоставщиками.СуммаДисконтированияУпр
	|		КОГДА ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасчетыСПоставщиками.СуммаДисконтирования
	|		ИНАЧЕ
	|			ТаблицаРасчетыСПоставщиками.СуммаДисконтирования * ЕСТЬNULL(КурсыВалютВзаиморасчетов.КоэффициентПересчета, 0) / ЕСТЬNULL(КурсыВалютУпр.КоэффициентПересчета, 1)
	|	КОНЕЦ)                                           КАК СуммаДисконтированияУпр,
	|	ТаблицаРасчетыСПоставщиками.РасчетныйДокумент    КАК РасчетныйДокумент,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеПроцентовПоДисконтированию) КАК ХозяйственнаяОперация,
	|	&ВалютаРегламентированногоУчета                 КАК ВалютаДокумента,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов.УникальныйИдентификатор КАК ИдентификаторФинЗаписи,
	|	ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.НачислениеПроцентовПоДисконтированию) КАК НастройкаХозяйственнойОперации,
	|	""1""                                           КАК Вид,
	|	ДоговорыКонтрагентов.СтавкаДисконтирования      КАК СтавкаДисконтирования,
	|	СУММА(ТаблицаРасчетыСПоставщиками.Сумма / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100)) КАК ФактическаяЗадолженностьБезНДС,
	|	СУММА(ТаблицаРасчетыСПоставщиками.Сумма / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100) - ТаблицаРасчетыСПоставщиками.СуммаДисконтирования) КАК ДисконтированнаяЗадолженностьБезНДС,
	|	СУММА(ТаблицаРасчетыСПоставщиками.СуммаДисконтирования) КАК РассчитанныйОстатокПроцентов,
	|	СУММА(ТаблицаРасчетыСПоставщиками.СуммаРегл / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100)) КАК ФактическаяЗадолженностьБезНДСРегл,
	|	СУММА(ТаблицаРасчетыСПоставщиками.СуммаРегл / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100) - ТаблицаРасчетыСПоставщиками.СуммаДисконтированияРегл) КАК ДисконтированнаяЗадолженностьБезНДСРегл,
	|	СУММА(ТаблицаРасчетыСПоставщиками.СуммаДисконтированияРегл) КАК РассчитанныйОстатокПроцентовРегл,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаРасчетыСПоставщиками.СуммаУпр <> 0
	|			ТОГДА ТаблицаРасчетыСПоставщиками.СуммаУпр / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100)
	|		КОГДА ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасчетыСПоставщиками.Сумма / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100)
	|		ИНАЧЕ
	|			ТаблицаРасчетыСПоставщиками.Сумма * ЕСТЬNULL(КурсыВалютВзаиморасчетов.КоэффициентПересчета, 0) / ЕСТЬNULL(КурсыВалютУпр.КоэффициентПересчета, 1) / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100)
	|	КОНЕЦ) КАК ФактическаяЗадолженностьБезНДСУпр,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаРасчетыСПоставщиками.СуммаДисконтированияУпр <> 0 И ТаблицаРасчетыСПоставщиками.СуммаУпр <> 0 
	|			ТОГДА ТаблицаРасчетыСПоставщиками.СуммаУпр / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100) - ТаблицаРасчетыСПоставщиками.СуммаДисконтированияУпр
	|		КОГДА ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасчетыСПоставщиками.Сумма / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100) - ТаблицаРасчетыСПоставщиками.СуммаДисконтирования
	|		ИНАЧЕ
	|			ТаблицаРасчетыСПоставщиками.Сумма / (1 + ЕСТЬNULL(СтавкиНДС.Ставка, 0) / 100) - ТаблицаРасчетыСПоставщиками.СуммаДисконтирования * ЕСТЬNULL(КурсыВалютВзаиморасчетов.КоэффициентПересчета, 0) / ЕСТЬNULL(КурсыВалютУпр.КоэффициентПересчета, 1)
	|	КОНЕЦ) КАК ДисконтированнаяЗадолженностьБезНДСУпр,
	|	СУММА(ВЫБОР
	|		КОГДА ТаблицаРасчетыСПоставщиками.СуммаДисконтированияУпр <> 0
	|			ТОГДА ТаблицаРасчетыСПоставщиками.СуммаДисконтированияУпр
	|		КОГДА ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов = &ВалютаУправленческогоУчета
	|			ТОГДА ТаблицаРасчетыСПоставщиками.СуммаДисконтирования
	|		ИНАЧЕ
	|			ТаблицаРасчетыСПоставщиками.СуммаДисконтирования * ЕСТЬNULL(КурсыВалютВзаиморасчетов.КоэффициентПересчета, 0) / ЕСТЬNULL(КурсыВалютУпр.КоэффициентПересчета, 1)
	|	КОНЕЦ) КАК РассчитанныйОстатокПроцентовУпр
	|ИЗ
	|	ВтРасчетыСПартнерами КАК ТаблицаРасчетыСПоставщиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютВзаиморасчетов
	|		ПО КурсыВалютВзаиморасчетов.Валюта = ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтКурсыВалют КАК КурсыВалютУпр
	|		ПО КурсыВалютУпр.Валюта = &ВалютаУправленческогоУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитикиУчетаПоПартнерам
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО КлючиАналитикиУчетаПоПартнерам.Договор = ДоговорыКонтрагентов.Ссылка
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
	|				ПО ДоговорыКонтрагентов.СтавкаНДС = СтавкиНДС.Ссылка
	|		ПО ТаблицаРасчетыСПоставщиками.КлючАналитики = КлючиАналитикиУчетаПоПартнерам.Ссылка
	|		
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам)
	|	И &ОтражатьВОперативномУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоговорыКонтрагентов.СтавкаДисконтирования,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов.УникальныйИдентификатор,
	|	ТаблицаРасчетыСПоставщиками.РасчетныйДокумент,
	|	ТаблицаРасчетыСПоставщиками.ВалютаВзаиморасчетов,
	|	ТаблицаРасчетыСПоставщиками.КлючАналитики,
	|	ТаблицаРасчетыСПоставщиками.ОбъектРасчетов,
	|	ТаблицаРасчетыСПоставщиками.ДатаРасчетныйДокумент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "РеестрДокументов";
	
	Если Не ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	&ИдентификаторМетаданных КАК ТипСсылки,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Партнер,
	|	НЕОПРЕДЕЛЕНО КАК МестоХранения,
	|	НЕОПРЕДЕЛЕНО КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК Подразделение,
	|	&Период КАК ДатаДокументаИБ,
	|	&Ссылка КАК Ссылка,
	|	&Номер КАК НомерДокументаИБ,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	&Ответственный КАК Ответственный,
	|	ЛОЖЬ КАК ДополнительнаяЗапись,
	|	НЕОПРЕДЕЛЕНО КАК Дополнительно,
	|	&Комментарий КАК Комментарий,
	|	&Проведен КАК Проведен,
	|	&ПометкаУдаления КАК ПометкаУдаления,
	|	НЕОПРЕДЕЛЕНО КАК ДатаПервичногоДокумента,
	|	НЕОПРЕДЕЛЕНО КАК НомерПервичногоДокумента,
	|	СУММА(ЕСТЬNULL(ДанныеТабличнойЧастиРасчетыСПартнерами.Сумма, 0)) КАК Сумма,
	|	Организации.ВалютаРегламентированногоУчета КАК Валюта,
	|	НЕОПРЕДЕЛЕНО КАК Договор,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельности,
	|	&Период КАК ДатаОтраженияВУчете,
	|	&Исправление КАК СторноИсправление,
	|	&СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	&ИсправляемыйДокумент КАК ИсправляемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК Приоритет
	|ИЗ
	|	Документ.ВводОстатковВзаиморасчетов КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВзаиморасчетов.РасчетыСПартнерами КАК ДанныеТабличнойЧастиРасчетыСПартнерами
	|		ПО ДанныеДокумента.Ссылка = ДанныеТабличнойЧастиРасчетыСПартнерами.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|			ПО Организации.Ссылка = &Организация
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Ссылка,
	|	Организации.ВалютаРегламентированногоУчета";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.ВводОстатковВзаиморасчетов";
	
	ЗначенияПараметров = Новый Структура;
	ЗначенияПараметров.Вставить("ИдентификаторМетаданных", ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.Документы.ВводОстатковВзаиморасчетов));
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ТекстЗапросаТаблицаРеестрДокументов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ДанныеДокумента";
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Если ИмяРегистра = "РеестрДокументов" Тогда
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросПроведенияПоНезависимомуРегистру(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента, Истина);
	Иначе
		ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(
			ТекстЗапроса, ПолноеИмяДокумента, СинонимТаблицыДокумента);
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ТекстЗапроса = ТекстЗапроса;
	Результат.ЗначенияПараметров = ЗначенияПараметров;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ХозяйственнаяОперацияДоговораПоТипуОперации(ХозяйственнаяОперация) Экспорт
	
	ХозяйственнаяОперацияДоговора = Перечисления.ХозяйственныеОперации.ПустаяСсылка();
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковАвансовКлиентов Тогда
		ХозяйственнаяОперацияДоговора = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам Тогда
		ХозяйственнаяОперацияДоговора = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
	КонецЕсли;
	
	Возврат ХозяйственнаяОперацияДоговора;
	
КонецФункции



// Метод подбирает документ расчетов для подстановки в документ.
// 
// Параметры:
// 	Объект - ДокументОбъект.ВводОстатковВзаиморасчетов - объект, в который подбирается документ расчетов.
// 	ТекущиеДанные - СтрокаТабличнойЧасти - строка табличной части, в которую необходимо подобрать документ расчетов.
// 	Отказ - Булево - признак отказа от выполнения подбора документа.
//
Процедура ПодобратьДокументРасчетов(Объект, ТекущиеДанные, Отказ = Ложь) Экспорт
	
	ИмяТЧ = ИмяТабличнойЧастиРасчетовПоХозяйственнойОперации(Объект.ХозяйственнаяОперация);
	
	Если ТекущиеДанные.ОбъектРасчетов = Неопределено Тогда
		
		ТекущиеДанные.ОбъектРасчетов = ТипДокументаРасчетовПоХозяйственнойОперации(Объект.ХозяйственнаяОперация);
		
		Если ТекущиеДанные.ОбъектРасчетов = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	МетаданныеРасчетногоДокумента = Метаданные.НайтиПоТипу(ТипЗнч(ТекущиеДанные.ОбъектРасчетов));
	
	Если ТипЗнч(ТекущиеДанные.ОбъектРасчетов) = Тип("ПеречислениеСсылка.ТипыДоговоров") Тогда
		ПредставлениеОбъекта = НСтр("ru='Договор контрагента'");
	ИначеЕсли ТипЗнч(ТекущиеДанные.ОбъектРасчетов) = Тип("ПеречислениеСсылка.ТипыДоговоровМеждуОрганизациями") Тогда
		ПредставлениеОбъекта = НСтр("ru='Договор между организациями'");
	Иначе
		ПредставлениеОбъекта = ?(ЗначениеЗаполнено(МетаданныеРасчетногоДокумента.ПредставлениеОбъекта),МетаданныеРасчетногоДокумента.ПредставлениеОбъекта,МетаданныеРасчетногоДокумента.Синоним);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Если ТипЗнч(ТекущиеДанные.ОбъектРасчетов) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДокументРасчетов.Ссылка,
		|	ДокументРасчетов.Контрагент,
		|	ДокументРасчетов.Партнер,
		|	ДокументРасчетов.Организация,
		|	ДокументРасчетов.ВалютаВзаиморасчетов КАК Валюта,
		|	ДокументРасчетов.Дата,
		|	ДокументРасчетов.Номер
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДокументРасчетов
		|ГДЕ
		|	ДокументРасчетов.Номер = &Номер
		|	И ДокументРасчетов.Дата = &Дата
		|	И ДокументРасчетов.Партнер = &Партнер
		|	И ДокументРасчетов.Контрагент = &Контрагент
		|	И ДокументРасчетов.Организация = &Организация
		|	И ДокументРасчетов.ОплатаВВалюте = ЛОЖЬ";
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Номер",СокрЛП(ТекущиеДанные.НомерРасчетногоДокумента));
		Запрос.УстановитьПараметр("Дата",ТекущиеДанные.ДатаРасчетногоДокумента);
		Запрос.УстановитьПараметр("Партнер",ТекущиеДанные.Партнер);
		Запрос.УстановитьПараметр("Контрагент",ТекущиеДанные.Контрагент);
		Запрос.УстановитьПараметр("Организация",Объект.Организация);
		
	ИначеЕсли ТипЗнч(ТекущиеДанные.ОбъектРасчетов) = Тип("ПеречислениеСсылка.ТипыДоговоров") Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДокументРасчетов.Ссылка,
		|	ДокументРасчетов.Контрагент,
		|	ДокументРасчетов.Партнер,
		|	ДокументРасчетов.Организация,
		|	ДокументРасчетов.ВалютаВзаиморасчетов КАК Валюта,
		|	ДокументРасчетов.Дата,
		|	ДокументРасчетов.Номер
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДокументРасчетов
		|ГДЕ
		|	ДокументРасчетов.Номер = &Номер
		|	И ДокументРасчетов.Дата = &Дата
		|	И ДокументРасчетов.Партнер = &Партнер
		|	И ДокументРасчетов.Контрагент = &Контрагент
		|	И ДокументРасчетов.Организация = &Организация
		|	И ДокументРасчетов.ТипДоговора = &ТипДоговора";
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Номер",СокрЛП(ТекущиеДанные.НомерРасчетногоДокумента));
		Запрос.УстановитьПараметр("Дата",ТекущиеДанные.ДатаРасчетногоДокумента);
		Запрос.УстановитьПараметр("Партнер",ТекущиеДанные.Партнер);
		Запрос.УстановитьПараметр("Контрагент",ТекущиеДанные.Контрагент);
		Запрос.УстановитьПараметр("Организация",Объект.Организация);
		Запрос.УстановитьПараметр("ТипДоговора",ТекущиеДанные.ОбъектРасчетов);
	Иначе
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДокументРасчетов.Ссылка,
		|	ДокументРасчетов.Контрагент,
		|	ДокументРасчетов.Партнер,
		|	ДокументРасчетов.Организация,
		|	ДокументРасчетов.Валюта,
		|	ДокументРасчетов.Дата,
		|	ДокументРасчетов.Номер
		|ИЗ
		|	&ДокументРасчетов КАК ДокументРасчетов
		|ГДЕ
		|	ДокументРасчетов.Номер = &Номер
		|	И ГОД(ДокументРасчетов.Дата) = &Год";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДокументРасчетов",  "Документ."+МетаданныеРасчетногоДокумента.Имя);
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Номер",ТекущиеДанные.НомерРасчетногоДокумента);
		Запрос.УстановитьПараметр("Год",Год(ТекущиеДанные.ДатаРасчетногоДокумента));
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Если Выборка.Организация <> Объект.Организация Тогда
			
			ТекстСообщения = НСтр("ru='В найденном документе ""%ТипДокумента%"" № %Номер% организация отличается от указанной во вводе остатков'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ТипДокумента%",ПредставлениеОбъекта);
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Номер%",ТекущиеДанные.НомерРасчетногоДокумента);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект."+ИмяТЧ+"["+Формат(ТекущиеДанные.НомерСтроки-1,"ЧГ=0")+"].НомерРасчетногоДокумента",,Отказ);
			ТекущиеДанные.ОбъектРасчетов = ТипДокументаРасчетовПоХозяйственнойОперации(Объект.ХозяйственнаяОперация);
			
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Партнер)
			И Выборка.Партнер <> ТекущиеДанные.Партнер Тогда
			
			ТекстСообщения = НСтр("ru='В найденном документе ""%ТипДокумента%"" № %Номер% партнер отличается от указанного во вводе остатков'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ТипДокумента%",ПредставлениеОбъекта);
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Номер%",ТекущиеДанные.НомерРасчетногоДокумента);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект."+ИмяТЧ+"["+Формат(ТекущиеДанные.НомерСтроки-1,"ЧГ=0")+"].Партнер",,Отказ);
			ТекущиеДанные.ОбъектРасчетов = ТипДокументаРасчетовПоХозяйственнойОперации(Объект.ХозяйственнаяОперация);
			
			Возврат;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Контрагент)
			И Выборка.Контрагент <> ТекущиеДанные.Контрагент Тогда
			
			ТекстСообщения = НСтр("ru='В найденном документе ""%ТипДокумента%"" № %Номер% контрагент отличается от указанного во вводе остатков'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%ТипДокумента%",ПредставлениеОбъекта);
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Номер%",ТекущиеДанные.НомерРасчетногоДокумента);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,"Объект."+ИмяТЧ+"["+Формат(ТекущиеДанные.НомерСтроки-1,"ЧГ=0")+"].Контрагент",,Отказ);
			ТекущиеДанные.ОбъектРасчетов = ТипДокументаРасчетовПоХозяйственнойОперации(Объект.ХозяйственнаяОперация);
			
			Возврат;
		КонецЕсли;
		ТекущиеДанные.Контрагент = Выборка.Контрагент;
		ТекущиеДанные.Партнер	 = Выборка.Партнер;
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ВалютаВзаиморасчетов) Тогда
			ТекущиеДанные.ВалютаВзаиморасчетов = Выборка.Валюта;
		КонецЕсли;
		
		ТекущиеДанные.ОбъектРасчетов			= Выборка.Ссылка;
		ТекущиеДанные.ДатаРасчетногоДокумента	= Выборка.Дата;
		ТекущиеДанные.НомерРасчетногоДокумента	= Выборка.Номер;
		
	Иначе
		
		Если ТипЗнч(ТекущиеДанные.ОбъектРасчетов) = Тип("СправочникСсылка.ДоговорыКонтрагентов")
			Или ТипЗнч(ТекущиеДанные.ОбъектРасчетов) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") Тогда
			ТекущиеДанные.ОбъектРасчетов = ТекущиеДанные.ОбъектРасчетов.ТипДоговора;
		ИначеЕсли ТипЗнч(ТекущиеДанные.ОбъектРасчетов) <> Тип("ПеречислениеСсылка.ТипыДоговоров")
			И ТипЗнч(ТекущиеДанные.ОбъектРасчетов) <> Тип("ПеречислениеСсылка.ТипыДоговоровМеждуОрганизациями") Тогда
			МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипЗнч(ТекущиеДанные.ОбъектРасчетов));
			ТекущиеДанные.ОбъектРасчетов = Документы[МетаданныеДокумента.Имя].ПустаяСсылка();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Определяет видимость группы формы, содержащей флажки видов учета.
// Используется в формах, в которых доступны все виды учета.
// 
// Параметры:
// 	Объект - ДокументОбъект - Документ ввода начальных остатков.
// Возвращаемое значение:
// 	Булево - определяет видимость группы вида учета (оперативный, регламентированный и управленческий)
Функция ВидимостьГруппУчета(Объект) Экспорт
	
	ВидимостьГруппУчета = ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет")
		Или РасчетСебестоимостиПовтИсп.ФормироватьДвиженияПоРегистрамСебестоимости(НачалоМесяца(Объект.Дата), Истина);
	
	Возврат ВидимостьГруппУчета;
	
КонецФункции

Функция ТипДокументаРасчетовПоХозяйственнойОперации(ХозяйственнаяОперация) Экспорт
	
	ОбъектРасчетов = Неопределено;
	
	Возврат ОбъектРасчетов;
	
КонецФункции

Функция ИмяТабличнойЧастиРасчетовПоХозяйственнойОперации(ХозяйственнаяОперация) Экспорт
	
	Возврат "РасчетыСПартнерами";
	
КонецФункции

Процедура ЗаполнитьОбъектыРасчетов(Объект, РежимЗаписи) Экспорт
	
	Объекты = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
				Объект.РасчетыСПартнерами.ВыгрузитьКолонку("ОбъектРасчетовОтправитель"),
				"Объект",
				Истина);
	
	Для Каждого СтрокаТЧ Из Объект.РасчетыСПартнерами Цикл
		
		Если СтрокаТЧ.ОбъектРасчетовОтправитель = Справочники.ОбъектыРасчетов.ПустаяСсылка() 
			Или Объекты.Получить(СтрокаТЧ.ОбъектРасчетовОтправитель) <> Неопределено
				И СтрокаТЧ.ОбъектРасчетов <> Объекты.Получить(СтрокаТЧ.ОбъектРасчетовОтправитель).Объект Тогда
			
			ИсточникОбъектаРасчетов = СтрокаТЧ.ОбъектРасчетов;
			
			Если ЗначениеЗаполнено(ИсточникОбъектаРасчетов) Тогда
				Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковАвансовПоставщикам
					Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиПоставщикам Тогда
						ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
				ИначеЕсли Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковАвансовКлиентов
					Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВводОстатковЗадолженностиКлиентов Тогда
						ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
				КонецЕсли;
				
				БлокировкаИсточникОбъектаРасчетов = Новый БлокировкаДанных;
				
				ЭлементБлокировки = БлокировкаИсточникОбъектаРасчетов.Добавить(ИсточникОбъектаРасчетов.Метаданные().ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ИсточникОбъектаРасчетов);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
				
				БлокировкаИсточникОбъектаРасчетов.Заблокировать();
			
				//Комиссия между организациями
				ДоговорКомиссии = ТипЗнч(ИсточникОбъектаРасчетов) = Тип("СправочникСсылка.ДоговорыМеждуОрганизациями") 
					И ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(ИсточникОбъектаРасчетов, "ТипДоговора") = Перечисления.ТипыДоговоровМеждуОрганизациями.Комиссионный;
					
				ОтчетПоКомиссии = ТипЗнч(ИсточникОбъектаРасчетов) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями");
					
				Если ТипЗнч(СтрокаТЧ.Контрагент) = Тип("СправочникСсылка.Организации")
					И (ДоговорКомиссии Или ОтчетПоКомиссии) Тогда
						ОрганизацияОбъектаРасчетов = ОбщегоНазначенияУТКлиентСервер.ДанныеПоПути(ИсточникОбъектаРасчетов, "Организация");
				Иначе
					ОрганизацияОбъектаРасчетов = Объект.Организация;
				КонецЕсли;
				
				ДополнительныеКритерииПоиска = ОбъектыРасчетовСервер.ДополнительныеКритерииПоиска();
				ДополнительныеКритерииПоиска.ОбновлениеИБ = Истина;
				ДополнительныеКритерииПоиска.Партнер = СтрокаТЧ.Партнер;
				
				Если ТипЗнч(ИсточникОбъектаРасчетов) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
					СлужебныеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсточникОбъектаРасчетов, "РазрешенаРаботаСДочернимиПартнерами, Партнер");;
					Если СлужебныеРеквизиты.РазрешенаРаботаСДочернимиПартнерами 
						И ЗначениеЗаполнено(СлужебныеРеквизиты.Партнер) Тогда
							ДополнительныеКритерииПоиска.Партнер = СлужебныеРеквизиты.Партнер;
					КонецЕсли; 
				КонецЕсли;
				
				ДополнительныеКритерииПоиска.Контрагент = СтрокаТЧ.Контрагент;
				ДополнительныеКритерииПоиска.ВалютаВзаиморасчетов = СтрокаТЧ.ВалютаВзаиморасчетов;
				
				СтрокаТЧ.ОбъектРасчетовОтправитель = ОбъектыРасчетовСервер.ПолучитьОбъектРасчетовПоСсылке(
					ИсточникОбъектаРасчетов,
					ОрганизацияОбъектаРасчетов,
					ТипРасчетов,
					ДополнительныеКритерииПоиска);
					
				Если НЕ ЗначениеЗаполнено(СтрокаТЧ.ОбъектРасчетовОтправитель) Тогда
					Если ТипЗнч(ИсточникОбъектаРасчетов) = Тип("ДокументСсылка.ПервичныйДокумент") Тогда
						СтрокаТЧ.ОбъектРасчетовОтправитель = Документы.ПервичныйДокумент.СгенерироватьОбъектРасчетов(ИсточникОбъектаРасчетов.ПолучитьОбъект());
					Иначе
						МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоСсылке(ИсточникОбъектаРасчетов);
						ПараметрыВзаиморасчеты = МенеджерОбъекта.ПараметрыВзаиморасчеты();
						Если ТипЗнч(ПараметрыВзаиморасчеты) = Тип("Массив") Тогда
							ПараметрыВзаиморасчеты = ПараметрыВзаиморасчеты[0];
						КонецЕсли;
						РеквизитыОбъекта = ОбъектыРасчетовСервер.РеквизитыОбъектаРасчетовПоСтруктуре(
											ИсточникОбъектаРасчетов.ПолучитьОбъект(),
											ИсточникОбъектаРасчетов,
											ПараметрыВзаиморасчеты,
											РежимЗаписи);
						СтрокаТЧ.ОбъектРасчетовОтправитель = ОбъектыРасчетовСервер.ПроверитьСоздатьОбъектРасчетов(РеквизитыОбъекта);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	ВводОстатковЛокализация.ВводОстатковВзаиморасчетовДобавитьКомандыПечати(КомандыПечати);
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#КонецЕсли
