
////////////////////////////////////////////////////
//// Модуль команды документа "КР_УстановкаСпециальнойЦены"
//// Создан: 13.01.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-1102
//// Разработка по ФДР С31.012, Обработка Установка специальной цены

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВидыЦен");
	ДополнительныеПараметры.Вставить("Источник");
	ДополнительныеПараметры.Вставить("НавигационнаяСсылка");
	ДополнительныеПараметры.Вставить("Окно");
	ДополнительныеПараметры.Вставить("Параметры");
	ДополнительныеПараметры.Вставить("Уникальность");
	
	ДокументПроведен = ДокументПроведен(ПараметрКоманды, ДополнительныеПараметры);
	
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ПараметрыВыполненияКоманды);
	ДополнительныеПараметры.Вставить("Ключ", ПараметрКоманды);
	
	Если ДокументПроведен Тогда
		ОткрытьФормуСпискаСкидокНаценок(ДополнительныеПараметры);
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПровестиДокументОтвет", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(
			ОписаниеОповещения,
			НСтр("ru = 'Для установки специальных цен документ должен быть проведен.
						|Провести документ?'"),
			РежимДиалогаВопрос.ДаНет, ,
			КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПровестиДокументОтвет(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Не Ответ = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПроведенияДокумента", ЭтотОбъект, ДополнительныеПараметры);
	
	ФормаВызова = ДополнительныеПараметры.Источник;
	Если СтрНайти(ФормаВызова.ИмяФормы, "ФормаДокумента") Тогда
		ФормаДокумента = ФормаВызова;
	Иначе
		ФормаДокумента = ПолучитьФорму("Документ.УстановкаЦенНоменклатуры.ФормаОбъекта",
			Новый Структура("Ключ", ДополнительныеПараметры.Ключ));
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиент.Провести(ФормаДокумента, , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроведенияДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ОткрытьФормуСпискаСкидокНаценок(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСпискаСкидокНаценок(ПараметрыВыполненияКоманды)
	
	ВидыЦенДокумента = Неопределено;
	Если Не ПараметрыВыполненияКоманды.Свойство("ВидыЦен", ВидыЦенДокумента) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'В документе не установлен список специальных цен.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("СпособПредоставления",
		ПредопределенноеЗначение("Перечисление.СпособыПредоставленияСкидокНаценок.ВидЦены"));
	ПараметрыОтбора.Вставить("ВидЦены", ВидыЦенДокумента);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
	
	ФормаСкидкиНаценки = ПолучитьФорму("Справочник.СкидкиНаценки.ФормаСписка",
		ПараметрыФормы,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно,
		ПараметрыВыполненияКоманды.НавигационнаяСсылка);
		
	ФормаСкидкиНаценки.ИспользованиеНаСкладахВариантОтображенияСкидокНаценок = "Все";
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ФормаСкидкиНаценки.ИспользованиеНаСкладах,
		"Статус",
		ПредопределенноеЗначение("Перечисление.СтатусыДействияСкидок.Действует"),
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Ложь);
	
	ФормаСкидкиНаценки.Элементы.Список.НачальноеОтображениеДерева =
		НачальноеОтображениеДерева.РаскрыватьВсеУровни;
	
	ФормаСкидкиНаценки.Открыть();
	
КонецПроцедуры

&НаСервере
Функция ДокументПроведен(Документ, ДополнительныеПараметры)
	
	// Сократим количество серверных вызовов - сразу запросим таблицу видов цен 
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, "Проведен, ВидыЦен");
	
	ДополнительныеПараметры.Вставить("ВидыЦен",
		Реквизиты.ВидыЦен.Выгрузить().ВыгрузитьКолонку("ВидЦены"));
	
	Возврат Реквизиты.Проведен;
	
КонецФункции
