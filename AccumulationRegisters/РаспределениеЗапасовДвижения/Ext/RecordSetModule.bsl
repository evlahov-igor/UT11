#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОтключитьРасчетИтогов() Экспорт
	
	ДополнительныеСвойства.Вставить("ОтключитьРасчетИтогов");
	
КонецПроцедуры

Процедура ВключитьБлокировкуРегистраСведенийПоНабору() Экспорт
	
	ДополнительныеСвойства.Вставить("ВключитьБлокировкуРегистраСведенийПоНабору");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		И Не (ДополнительныеСвойства.Свойство("ЗаменаСсылок") И ДополнительныеСвойства.ЗаменаСсылок) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ДополнительныеСвойства.Свойство("ОтключитьРасчетИтогов") Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицДляЗаписиРегистраСведений", МенеджерВременныхТаблиц);
		Запрос = Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("НаборЗаписейРаспределениеЗапасовДвижения", Выгрузить());
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		РаспределениеЗапасовДвижения.ДвиженияПриЗаписиВоВременнуюТаблицу(Запрос, Истина);
		РаспределениеЗапасовДвижения.РаспределениеЗапасовДвиженияИзменениеВоВременнуюТаблицу(Запрос);
		
		УстановитьБлокировки(Запрос);
		
	КонецЕсли;
	Если ДополнительныеСвойства.Свойство("ВключитьБлокировкуРегистраСведенийПоНабору") Тогда
		
		УстановитьБлокировкиПоНабору();
		
	КонецЕсли;
	
	Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		СформироватьТаблицуИсходныхДвижений();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		И Не (ДополнительныеСвойства.Свойство("ЗаменаСсылок") И ДополнительныеСвойства.ЗаменаСсылок) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ДосчитыватьРегистрРегламентнымЗаданием = РаспределениеЗапасов.ДосчитыватьРегистрРегламентнымЗаданием();
	Если Не ДополнительныеСвойства.Свойство("ОтключитьРасчетИтогов") Тогда
		
		МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицДляЗаписиРегистраСведений;
		Запрос = Новый Запрос();
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		// Содержит изменения документа по состоянию "Остаток на складе" по ресурсам ВНаличии,Резерв,Запас
		// а также ресурс Свободно по товарам для которых произошло уменьшение остатка и есть контроль остатков (может быть отрицательным).
		// далее в ЗаписатьСведенияПоЗаказамНаПоступление этот ресурс будет пересчитан, если он не отрицательный.
		// если отрицательный, то документ не проведется и это значение будет выдано пользователю в сообщении контроля остатков.
		ВременнаяТаблицаОстатокНаСкладе(Запрос); // вызов обязательно до ЗаписатьСведенияПоЗаказамНаОтгрузку, так как читаются данные до изменений.
		
		ЗаписатьСведенияПоЗаказамНаОтгрузку(Запрос);
		ЗаписатьСведенияПоЗаказамНаПоступление(Запрос);

		ЗаписатьРаспределениеЗапасовНаПотребности(Запрос);
		
		Если ДосчитыватьРегистрРегламентнымЗаданием Тогда
			ЗаписатьЗадания(Запрос);
		Иначе
			
			Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
				ЗапросСостояний = Новый Запрос();
				ЗапросСостояний.МенеджерВременныхТаблиц =  ДополнительныеСвойства.МенеджерВременныхТаблиц;
				СформироватьТаблицуТоварыДляРасчетаСостояний();
				РаспределениеЗапасов.ДобавитьВременныеТаблицыАктуализацииСостояний(ЗапросСостояний, "ТоварыДляРасчетаСостояний", "ПередЗаписью");
			КонецЕсли;
			
			ЗаписатьРаспределениеЗапасов(Запрос);
			
			Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
				РаспределениеЗапасов.ДобавитьВременныеТаблицыАктуализацииСостояний(ЗапросСостояний, "ТоварыДляРасчетаСостояний", "ПриЗаписи");
				РаспределениеЗапасов.ТаблицаИзмененийРаспределениеЗапасовДляРасчетаСостояний(ЗапросСостояний);
				УничтожитьТаблицуТоварыДляРасчетаСостояний();
			КонецЕсли;
			
		КонецЕсли;
		Запрос.Текст = "УНИЧТОЖИТЬ ДвиженияПриЗаписи; УНИЧТОЖИТЬ РаспределениеЗапасовДвиженияИзменение";
		
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		СформироватьТаблицуИзмененийДвижений();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТаблицыИзмененийРегистра

Процедура СформироватьТаблицуИсходныхДвижений()
	
	// Текущее состояние набора помещается во временную таблицу "ДвиженияПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Таблица.Номенклатура                           КАК Номенклатура,
		|	Таблица.Характеристика                         КАК Характеристика,
		|	Таблица.Склад                                  КАК Склад,
		|	Таблица.Назначение                             КАК Назначение,
		|	Таблица.ЗаказНаОтгрузку                        КАК ЗаказНаОтгрузку,
		|	Таблица.ЖелаемаяДатаОтгрузки                   КАК ЖелаемаяДатаОтгрузки,
		|	Таблица.ЗаказНаПоступление                     КАК ЗаказНаПоступление,
		|	Таблица.ДатаПоступления                        КАК ДатаПоступления,
		|	Таблица.Отгрузить                              КАК Отгрузить,
		|	Таблица.Резервировать                          КАК Резервировать,
		|	Таблица.РезервироватьПоМереПоступления         КАК РезервироватьПоМереПоступления,
		|	Таблица.КОбеспечениюБезРезерва                 КАК КОбеспечению,
		|	Таблица.НеОбеспечивать                         КАК НеОбеспечивать,
		|	Таблица.Поступило                              КАК Поступило,
		|	Таблица.ПоступитКДате                          КАК ПоступитКДате,
		|	Таблица.ПоставкаНаСогласовании                 КАК ПоставкаНаСогласовании,
		|	Таблица.ЗакрытьГрафикПоступления               КАК ЗакрытьГрафикПоступления,
		|	Таблица.ИгнорироватьРезервыПриКонтролеОстатков КАК ИгнорироватьРезервыПриКонтролеОстатков
		|ПОМЕСТИТЬ РаспределениеЗапасовДвиженияПередЗаписью
		|ИЗ
		|	РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
		|ГДЕ
		|	Таблица.Регистратор = &Регистратор
		|	И НЕ &ЭтоНовый
		|	И Таблица.Активность";
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СформироватьТаблицуИзмененийДвижений()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаИзменений.Номенклатура                                                   КАК Номенклатура,
		|	ТаблицаИзменений.Характеристика                                                 КАК Характеристика,
		|	ТаблицаИзменений.Склад                                                          КАК Склад,
		|	ТаблицаИзменений.Назначение                                                     КАК Назначение,
		|	ТаблицаИзменений.ЗаказНаОтгрузку                                                КАК ЗаказНаОтгрузку,
		|	ТаблицаИзменений.ЖелаемаяДатаОтгрузки                                           КАК ЖелаемаяДатаОтгрузки,
		|	ТаблицаИзменений.ЗаказНаПоступление                                             КАК ЗаказНаПоступление,
		|	ТаблицаИзменений.ДатаПоступления                                                КАК ДатаПоступления,
		|	СУММА(ТаблицаИзменений.Отгрузить)                                               КАК Отгрузить,
		|	СУММА(ТаблицаИзменений.Резервировать)                                           КАК Резервировать,
		|	СУММА(ТаблицаИзменений.РезервироватьПоМереПоступления)                          КАК РезервироватьПоМереПоступления,
		|	СУММА(ТаблицаИзменений.КОбеспечению)                                            КАК КОбеспечению,
		|	СУММА(ТаблицаИзменений.НеОбеспечивать)                                          КАК НеОбеспечивать,
		|	СУММА(ТаблицаИзменений.Поступило)                                               КАК Поступило,
		|	СУММА(ТаблицаИзменений.ПоступитКДате)                                           КАК ПоступитКДате,
		|	СУММА(ТаблицаИзменений.ПоставкаНаСогласовании)                                  КАК ПоставкаНаСогласовании,
		|	СУММА(ТаблицаИзменений.ЗакрытьГрафикПоступления)                                КАК ЗакрытьГрафикПоступления,
		|	ЛОЖЬ                                                                            КАК ИгнорироватьРезервыПриКонтролеОстатков
		|	
		|ПОМЕСТИТЬ ДвиженияРаспределениеЗапасовДвиженияИзменение
		|ИЗ(
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура                           КАК Номенклатура,
		|		Таблица.Характеристика                         КАК Характеристика,
		|		Таблица.Склад                                  КАК Склад,
		|		Таблица.Назначение                             КАК Назначение,
		|		Таблица.ЗаказНаОтгрузку                        КАК ЗаказНаОтгрузку,
		|		Таблица.ЖелаемаяДатаОтгрузки                   КАК ЖелаемаяДатаОтгрузки,
		|		Таблица.ЗаказНаПоступление                     КАК ЗаказНаПоступление,
		|		Таблица.ДатаПоступления                        КАК ДатаПоступления,
		|		-Таблица.Отгрузить                             КАК Отгрузить,
		|		-Таблица.Резервировать                         КАК Резервировать,
		|		-Таблица.РезервироватьПоМереПоступления        КАК РезервироватьПоМереПоступления,
		|		-Таблица.КОбеспечению                          КАК КОбеспечению,
		|		-Таблица.НеОбеспечивать                        КАК НеОбеспечивать,
		|		-Таблица.Поступило                             КАК Поступило,
		|		-Таблица.ПоступитКДате                         КАК ПоступитКДате,
		|		-Таблица.ПоставкаНаСогласовании                КАК ПоставкаНаСогласовании,
		|		-Таблица.ЗакрытьГрафикПоступления              КАК ЗакрытьГрафикПоступления,
		|		Таблица.ИгнорироватьРезервыПриКонтролеОстатков КАК ИгнорироватьРезервыПриКонтролеОстатков
		|	ИЗ
		|		РаспределениеЗапасовДвиженияПередЗаписью КАК Таблица
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Таблица.Номенклатура                           КАК Номенклатура,
		|		Таблица.Характеристика                         КАК Характеристика,
		|		Таблица.Склад                                  КАК Склад,
		|		Таблица.Назначение                             КАК Назначение,
		|		Таблица.ЗаказНаОтгрузку                        КАК ЗаказНаОтгрузку,
		|		Таблица.ЖелаемаяДатаОтгрузки                   КАК ЖелаемаяДатаОтгрузки,
		|		Таблица.ЗаказНаПоступление                     КАК ЗаказНаПоступление,
		|		Таблица.ДатаПоступления                        КАК ДатаПоступления,
		|		Таблица.Отгрузить                              КАК Отгрузить,
		|		Таблица.Резервировать                          КАК Резервировать,
		|		Таблица.РезервироватьПоМереПоступления         КАК РезервироватьПоМереПоступления,
		|		Таблица.КОбеспечениюБезРезерва                 КАК КОбеспечению,
		|		Таблица.НеОбеспечивать                         КАК НеОбеспечивать,
		|		Таблица.Поступило                              КАК Поступило,
		|		Таблица.ПоступитКДате                          КАК ПоступитКДате,
		|		Таблица.ПоставкаНаСогласовании                 КАК ПоставкаНаСогласовании,
		|		Таблица.ЗакрытьГрафикПоступления               КАК ЗакрытьГрафикПоступления,
		|		Таблица.ИгнорироватьРезервыПриКонтролеОстатков КАК ИгнорироватьРезервыПриКонтролеОстатков
		|	ИЗ
		|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
		|	ГДЕ
		|		Таблица.Регистратор = &Регистратор
		|			И Таблица.Активность) КАК ТаблицаИзменений
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.Номенклатура,
		|	ТаблицаИзменений.Характеристика,
		|	ТаблицаИзменений.Склад,
		|	ТаблицаИзменений.Назначение,
		|	ТаблицаИзменений.ЗаказНаОтгрузку,
		|	ТаблицаИзменений.ЖелаемаяДатаОтгрузки,
		|	ТаблицаИзменений.ЗаказНаПоступление,
		|	ТаблицаИзменений.ДатаПоступления
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаИзменений.Отгрузить) <> 0
		|		ИЛИ СУММА(ТаблицаИзменений.Резервировать) <> 0
		|		ИЛИ СУММА(ТаблицаИзменений.РезервироватьПоМереПоступления) <> 0
		|		ИЛИ СУММА(ТаблицаИзменений.КОбеспечению) <> 0
		|		ИЛИ СУММА(ТаблицаИзменений.НеОбеспечивать) <> 0
		|		ИЛИ СУММА(ТаблицаИзменений.Поступило) <> 0
		|		ИЛИ СУММА(ТаблицаИзменений.ПоступитКДате) <> 0
		|		ИЛИ СУММА(ТаблицаИзменений.ПоставкаНаСогласовании) <> 0
		|		ИЛИ СУММА(ТаблицаИзменений.ЗакрытьГрафикПоступления) <> 0
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РаспределениеЗапасовДвиженияПередЗаписью";
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияРаспределениеЗапасовДвиженияИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

Процедура УстановитьБлокировки(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененияДвижений.Номенклатура       КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика     КАК Характеристика,
		|	ИзмененияДвижений.Склад              КАК Склад,
		|	ИзмененияДвижений.Назначение         КАК Назначение,
		|	НЕОПРЕДЕЛЕНО                         КАК ЗаказНаОтгрузку,
		|	НЕОПРЕДЕЛЕНО                         КАК ЗаказНаПоступление,
		|	1                                    КАК ТипЗаписиРаспределенияЗапасов
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|;
		|
		|/////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененияДвижений.Номенклатура       КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика     КАК Характеристика,
		|	ИзмененияДвижений.Склад              КАК Склад,
		|	НЕОПРЕДЕЛЕНО                         КАК ЗаказНаОтгрузку,
		|	НЕОПРЕДЕЛЕНО                         КАК ЗаказНаПоступление,
		|	1                                    КАК ТипЗаписиРаспределенияЗапасов
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|ГДЕ
		|	ИзмененияДвижений.Поступило <> 0 ИЛИ ИзмененияДвижений.Отгрузить <> 0 ИЛИ ИзмененияДвижений.Резервировать <> 0
		|;
		|
		|/////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененияДвижений.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку,
		|	НЕОПРЕДЕЛЕНО                      КАК ЗаказНаПоступление,
		|	1                                 КАК ТипЗаписиРаспределенияЗапасов
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|ГДЕ
		|	ИзмененияДвижений.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО
		|;
		|
		|/////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененияДвижений.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	НЕОПРЕДЕЛЕНО                         КАК ЗаказНаОтгрузку,
		|	1                                    КАК ТипЗаписиРаспределенияЗапасов
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|ГДЕ
		|	ИзмененияДвижений.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО";

	ПакетРезультатов = Запрос.ВыполнитьПакет();
	
	ДосчитыватьРегистрРегламентнымЗаданием = РаспределениеЗапасов.ДосчитыватьРегистрРегламентнымЗаданием();
	БлокировкаДанных = Новый БлокировкаДанных();
	
	// см. ВременнаяТаблицаОстатокНаСкладе и ЗаписатьРаспределениеЗапасовНаПотребности
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РаспределениеЗапасов");
	ЭлементБлокировки.ИсточникДанных = ПакетРезультатов[0];
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура",                  "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика",                "Характеристика");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад",                         "Склад");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Назначение",                    "Назначение");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаОтгрузку",               "ЗаказНаОтгрузку");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПоступление",            "ЗаказНаПоступление");
	Если ДосчитыватьРегистрРегламентнымЗаданием Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипЗаписиРаспределенияЗапасов", "ТипЗаписиРаспределенияЗапасов");
	КонецЕсли;
	
	// см. ВременнаяТаблицаОстатокНаСкладе
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РаспределениеЗапасов");
	ЭлементБлокировки.ИсточникДанных = ПакетРезультатов[1];
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура",                  "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика",                "Характеристика");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад",                         "Склад");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаОтгрузку",               "ЗаказНаОтгрузку");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПоступление",            "ЗаказНаПоступление");
	Если ДосчитыватьРегистрРегламентнымЗаданием Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипЗаписиРаспределенияЗапасов", "ТипЗаписиРаспределенияЗапасов");
	КонецЕсли;
	
	// см. ЗаписатьСведенияПоЗаказамНаОтгрузку
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.РаспределениеЗапасовДвижения");
	ЭлементБлокировки.ИсточникДанных = ПакетРезультатов[2];
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаОтгрузку", "ЗаказНаОтгрузку");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПоступление", "ЗаказНаПоступление");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РаспределениеЗапасов");
	ЭлементБлокировки.ИсточникДанных = ПакетРезультатов[2];
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаОтгрузку", "ЗаказНаОтгрузку");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПоступление", "ЗаказНаПоступление");
	Если ДосчитыватьРегистрРегламентнымЗаданием Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипЗаписиРаспределенияЗапасов", "ТипЗаписиРаспределенияЗапасов");
	КонецЕсли;
	
	// см. ЗаписатьСведенияПоЗаказамНаПоступление
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.РаспределениеЗапасовДвижения");
	ЭлементБлокировки.ИсточникДанных = ПакетРезультатов[3];
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПоступление", "ЗаказНаПоступление");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаОтгрузку", "ЗаказНаОтгрузку");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РаспределениеЗапасов");
	ЭлементБлокировки.ИсточникДанных = ПакетРезультатов[3];
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПоступление", "ЗаказНаПоступление");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаОтгрузку", "ЗаказНаОтгрузку");
	Если ДосчитыватьРегистрРегламентнымЗаданием Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ТипЗаписиРаспределенияЗапасов", "ТипЗаписиРаспределенияЗапасов");
	КонецЕсли;
	
	БлокировкаДанных.Заблокировать();
	
КонецПроцедуры

Процедура УстановитьБлокировкиПоНабору()
	
	БлокировкаДанных = Новый БлокировкаДанных();
	
	Таблица = Выгрузить(, "Номенклатура,Характеристика,Склад");
	Таблица.Колонки.Добавить("ЗаказНаОтгрузку", Метаданные.ОпределяемыеТипы.ОжидаемаяОтгрузка.Тип);
	Таблица.Колонки.Добавить("ЗаказНаПоступление", Метаданные.ОпределяемыеТипы.ОжидаемоеПоступление.Тип);
	
	ЗаказыНаПоступление = Выгрузить(, "ЗаказНаПоступление");
	ЗаказыНаПоступление.Свернуть("ЗаказНаПоступление");
	НайденныеСтроки = ЗаказыНаПоступление.НайтиСтроки(Новый Структура("ЗаказНаПоступление", Неопределено));
	Если НайденныеСтроки.Количество() > 0 Тогда
		ЗаказыНаПоступление.Удалить(НайденныеСтроки[0]);
	КонецЕсли;
	ЗаказыНаОтгрузку = Выгрузить(, "ЗаказНаОтгрузку");
	ЗаказыНаОтгрузку.Свернуть("ЗаказНаОтгрузку");
	НайденныеСтроки = ЗаказыНаОтгрузку.НайтиСтроки(Новый Структура("ЗаказНаОтгрузку", Неопределено));
	Если НайденныеСтроки.Количество() > 0 Тогда
		ЗаказыНаОтгрузку.Удалить(НайденныеСтроки[0]);
	КонецЕсли;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РаспределениеЗапасов");
	ЭлементБлокировки.ИсточникДанных = Таблица;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаОтгрузку", "ЗаказНаОтгрузку");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПоступление", "ЗаказНаПоступление");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.РаспределениеЗапасовДвижения");
	ЭлементБлокировки.ИсточникДанных = ЗаказыНаПоступление;
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПоступление", "ЗаказНаПоступление");
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.РаспределениеЗапасовДвижения");
	ЭлементБлокировки.ИсточникДанных = ЗаказыНаОтгрузку;
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаОтгрузку", "ЗаказНаОтгрузку");
	
	БлокировкаДанных.Заблокировать();
	
КонецПроцедуры

Процедура ЗаписатьСведенияПоЗаказамНаОтгрузку(Запрос)
	
	Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Изменения.Номенклатура           КАК Номенклатура,
		|	Изменения.Характеристика         КАК Характеристика,
		|	Изменения.Склад                  КАК Склад,
		|	Изменения.Назначение             КАК Назначение
		|ПОМЕСТИТЬ РазличныеТоварыСНазначениями
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК Изменения
		|ГДЕ
		|	Изменения.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|/////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Изменения.Номенклатура           КАК Номенклатура,
		|	Изменения.Характеристика         КАК Характеристика,
		|	Изменения.Склад                  КАК Склад
		|ПОМЕСТИТЬ РазличныеТоварыБезНазначений
		|ИЗ
		|	РазличныеТоварыСНазначениями КАК Изменения
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|/////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура КАК Номенклатура,
		|	Товары.Характеристика КАК Характеристика,
		|	Товары.Склад КАК Склад,
		|	РасчетПереопределяемый.ГраницаПериода КАК ГраницаПериода
		|ПОМЕСТИТЬ ГраницыПериодаОбеспечения
		|ИЗ
		|	РазличныеТоварыБезНазначений КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА
		|		
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|/////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Изменения.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
		|ПОМЕСТИТЬ ЗаказыНаОтгрузку
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК Изменения
		|ГДЕ
		|	Изменения.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаОтгрузку
		|;
		|
		|//////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГруппировкаЗаписей.ЗаказНаОтгрузку       КАК ЗаказНаОтгрузку,
		|	ГруппировкаЗаписей.Номенклатура          КАК Номенклатура,
		|	ГруппировкаЗаписей.Характеристика        КАК Характеристика,
		|	ГруппировкаЗаписей.Склад                 КАК Склад,
		|	ГруппировкаЗаписей.Назначение            КАК Назначение,
		|	ГруппировкаЗаписей.ЖелаемаяДатаОтгрузки  КАК ЖелаемаяДатаОтгрузки,
		|	ГруппировкаЗаписей.Резервировать         КАК Резервировать,
		|	ГруппировкаЗаписей.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	ГруппировкаЗаписей.КОбеспечению          КАК КОбеспечению,
		|	ВЫБОР КОГДА ГруппировкаЗаписей.НеОбеспечивать < ГруппировкаЗаписей.ЗакрытьГрафикОтгрузки ТОГДА
		|				0
		|			ИНАЧЕ
		|				ГруппировкаЗаписей.НеОбеспечивать - ГруппировкаЗаписей.ЗакрытьГрафикОтгрузки
		|		КОНЕЦ КАК НеОбеспечивать
		|ПОМЕСТИТЬ ДвиженияПоЗаказамНаОтгрузку
		|ИЗ(
		|	ВЫБРАТЬ
		|		ЗаказыНаОтгрузку.ЗаказНаОтгрузку                   КАК ЗаказНаОтгрузку,
		|		Движения.Номенклатура                              КАК Номенклатура,
		|		Движения.Характеристика                            КАК Характеристика,
		|		Движения.Склад                                     КАК Склад,
		|		Движения.Назначение                                КАК Назначение,
		|		Движения.ЖелаемаяДатаОтгрузки                      КАК ЖелаемаяДатаОтгрузки,
		|		СУММА(Движения.Резервировать)                      КАК Резервировать,
		|		СУММА(Движения.РезервироватьПоМереПоступления)     КАК РезервироватьПоМереПоступления,
		|		СУММА(Движения.КОбеспечениюБезРезерва)             КАК КОбеспечению,
		|		СУММА(Движения.НеОбеспечивать)                     КАК НеОбеспечивать,
		|		СУММА(Движения.ЗакрытьГрафикОтгрузки)              КАК ЗакрытьГрафикОтгрузки
		|	ИЗ
		|		ЗаказыНаОтгрузку КАК ЗаказыНаОтгрузку
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
		|			ПО Движения.ЗаказНаОтгрузку = ЗаказыНаОтгрузку.ЗаказНаОтгрузку
		|			 И Движения.Активность
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ РазличныеТоварыСНазначениями КАК Фильтр
		|			ПО Фильтр.Номенклатура   = Движения.Номенклатура
		|			 И Фильтр.Характеристика = Движения.Характеристика
		|			 И Фильтр.Склад          = Движения.Склад
		|			 И Фильтр.Назначение     = Движения.Назначение
		|	ГДЕ
		|		НЕ Фильтр.Номенклатура ЕСТЬ NULL
		|	СГРУППИРОВАТЬ ПО
		|		Движения.Номенклатура,
		|		Движения.Характеристика,
		|		Движения.Склад,
		|		Движения.Назначение,
		|		ЗаказыНаОтгрузку.ЗаказНаОтгрузку,
		|		Движения.ЖелаемаяДатаОтгрузки
		|	ИМЕЮЩИЕ
		|		СУММА(Движения.Резервировать) <> 0
		|			ИЛИ СУММА(Движения.КОбеспечениюБезРезерва) <> 0
		|			ИЛИ СУММА(Движения.РезервироватьПоМереПоступления) <> 0
		|			ИЛИ СУММА(Движения.НеОбеспечивать) <> 0) ГруппировкаЗаписей
		|ГДЕ
		|	ГруппировкаЗаписей.Резервировать <> 0
		|		ИЛИ ГруппировкаЗаписей.РезервироватьПоМереПоступления <> 0
		|		ИЛИ ГруппировкаЗаписей.КОбеспечению  <> 0
		|		ИЛИ ВЫБОР КОГДА ГруппировкаЗаписей.НеОбеспечивать < ГруппировкаЗаписей.ЗакрытьГрафикОтгрузки ТОГДА
		|					0
		|				ИНАЧЕ
		|					ГруппировкаЗаписей.НеОбеспечивать - ГруппировкаЗаписей.ЗакрытьГрафикОтгрузки
		|			КОНЕЦ  <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаОтгрузку
		|;
		|
		|//////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сведения.Состояние                     КАК Состояние,
		|	Сведения.Номенклатура                  КАК Номенклатура,
		|	Сведения.Характеристика                КАК Характеристика,
		|	Сведения.Склад                         КАК Склад,
		|	Сведения.Назначение                    КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку               КАК ЗаказНаОтгрузку
		|ИЗ
		|	ЗаказыНаОтгрузку КАК ЗаказыНаОтгрузку
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.ЗаказНаОтгрузку = ЗаказыНаОтгрузку.ЗаказНаОтгрузку
		|		 И Сведения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать))
		|ГДЕ
		|	НЕ Сведения.Номенклатура ЕСТЬ NULL
		|;
		|
		|//////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка) КАК Состояние,
		|	1                             КАК ТипЗаписиРаспределенияЗапасов,
		|	Движения.Номенклатура         КАК Номенклатура,
		|	Движения.Характеристика       КАК Характеристика,
		|	Движения.Склад                КАК Склад,
		|	Движения.Назначение           КАК Назначение,
		|	Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	Движения.Резервировать        КАК Резервировать,
		|	ВЫБОР КОГДА Границы.ГраницаПериода = ДАТАВРЕМЯ(1,1,1)
		|					ИЛИ Границы.ГраницаПериода >= Движения.ЖелаемаяДатаОтгрузки ТОГДА
		|				Движения.РезервироватьПоМереПоступления
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК РезервироватьПоМереПоступления,
		|	ВЫБОР КОГДА Границы.ГраницаПериода = ДАТАВРЕМЯ(1,1,1)
		|					ИЛИ Границы.ГраницаПериода >= Движения.ЖелаемаяДатаОтгрузки ТОГДА
		|				0
		|			ИНАЧЕ
		|				Движения.РезервироватьПоМереПоступления
		|		КОНЕЦ КАК ОтложитьРезервирование,
		|	Движения.КОбеспечению         КАК КОбеспечениюБезРезерва,
		|	Движения.НеОбеспечивать       КАК НеОбеспечивать,
		|	0                             КАК Зарезервировано,
		|	0                             КАК НеОбеспечено
		|ИЗ
		|	ДвиженияПоЗаказамНаОтгрузку КАК Движения
		|		ЛЕВОЕ СОЕДИНЕНИЕ ГраницыПериодаОбеспечения КАК Границы
		|		ПО Границы.Номенклатура = Движения.Номенклатура
		|		 И Границы.Характеристика = Движения.Характеристика
		|		 И Границы.Склад = Движения.Склад
		|ГДЕ
		|	Движения.КОбеспечению <> 0
		|		ИЛИ Движения.РезервироватьПоМереПоступления <> 0
		|		ИЛИ Движения.Резервировать <> 0
		|		ИЛИ Движения.НеОбеспечивать <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве) КАК Состояние,
		|	1                             КАК ТипЗаписиРаспределенияЗапасов,
		|	Движения.Номенклатура         КАК Номенклатура,
		|	Движения.Характеристика       КАК Характеристика,
		|	Движения.Склад                КАК Склад,
		|	Движения.Назначение           КАК Назначение,
		|	Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	0                             КАК Резервировать,
		|	0                             КАК РезервироватьПоМереПоступления,
		|	0                             КАК ОтложитьРезервирование,
		|	0                             КАК КОбеспечениюБезРезерва,
		|	0                             КАК НеОбеспечивать,
		|	Движения.Резервировать        КАК Зарезервировано,
		|	0                             КАК НеОбеспечено
		|ИЗ
		|	ДвиженияПоЗаказамНаОтгрузку КАК Движения
		|ГДЕ
		|	Движения.Резервировать <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать) КАК Состояние,
		|	1                             КАК ТипЗаписиРаспределенияЗапасов,
		|	Движения.Номенклатура         КАК Номенклатура,
		|	Движения.Характеристика       КАК Характеристика,
		|	Движения.Склад                КАК Склад,
		|	Движения.Назначение           КАК Назначение,
		|	Движения.ЗаказНаОтгрузку      КАК ЗаказНаОтгрузку,
		|	Движения.ЖелаемаяДатаОтгрузки КАК ЖелаемаяДатаОтгрузки,
		|	НЕОПРЕДЕЛЕНО                  КАК ЗаказНаПоступление,
		|	ДАТАВРЕМЯ(1, 1, 1)            КАК ДатаПоступления,
		|	0                             КАК Запас,
		|	0                             КАК Свободно,
		|	0                             КАК Резервировать,
		|	0                             КАК РезервироватьПоМереПоступления,
		|	0                             КАК ОтложитьРезервирование,
		|	0                             КАК КОбеспечениюБезРезерва,
		|	0                             КАК НеОбеспечивать,
		|	0                             КАК Зарезервировано,
		|	Движения.НеОбеспечивать       КАК НеОбеспечено
		|ИЗ
		|	ДвиженияПоЗаказамНаОтгрузку КАК Движения
		|ГДЕ
		|	Движения.НеОбеспечивать <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Сведения.Состояние                     КАК Состояние,
		|	Сведения.ТипЗаписиРаспределенияЗапасов КАК ТипЗаписиРаспределенияЗапасов,
		|	Сведения.Номенклатура                  КАК Номенклатура,
		|	Сведения.Характеристика                КАК Характеристика,
		|	Сведения.Склад                         КАК Склад,
		|	Сведения.Назначение                    КАК Назначение,
		|	Сведения.ЗаказНаОтгрузку               КАК ЗаказНаОтгрузку,
		|	Сведения.ЖелаемаяДатаОтгрузки          КАК ЖелаемаяДатаОтгрузки,
		|	Сведения.ЗаказНаПоступление            КАК ЗаказНаПоступление,
		|	Сведения.ДатаПоступления               КАК ДатаПоступления,
		|	Сведения.Запас                         КАК Запас,
		|	Сведения.Свободно                      КАК Свободно,
		|	Сведения.Резервировать                 КАК Резервировать,
		|	Сведения.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
		|	Сведения.ОтложитьРезервирование        КАК ОтложитьРезервирование,
		|	Сведения.КОбеспечениюБезРезерва        КАК КОбеспечениюБезРезерва,
		|	Сведения.НеОбеспечивать                КАК НеОбеспечивать,
		|	Сведения.Зарезервировано               КАК Зарезервировано,
		|	Сведения.НеОбеспечено                  КАК НеОбеспечено
		|ИЗ
		|	ЗаказыНаОтгрузку КАК ЗаказыНаОтгрузку
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.ЗаказНаОтгрузку = ЗаказыНаОтгрузку.ЗаказНаОтгрузку
		|		 И Сведения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ВРезерве),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.НеОбеспечивать))
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РазличныеТоварыСНазначениями КАК Фильтр
		|		ПО Фильтр.Номенклатура   = Сведения.Номенклатура
		|		 И Фильтр.Характеристика = Сведения.Характеристика
		|		 И Фильтр.Склад          = Сведения.Склад
		|		 И Фильтр.Назначение     = Сведения.Назначение
		|ГДЕ
		|	НЕ Сведения.Номенклатура ЕСТЬ NULL И Фильтр.Номенклатура ЕСТЬ NULL
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаОтгрузку, Номенклатура, Характеристика, Склад, Назначение, ЖелаемаяДатаОтгрузки
		|;
		|
		|//////////////////////////////////////
		|УНИЧТОЖИТЬ ДвиженияПоЗаказамНаОтгрузку;
		|УНИЧТОЖИТЬ ЗаказыНаОтгрузку;
		|УНИЧТОЖИТЬ ГраницыПериодаОбеспечения;
		|УНИЧТОЖИТЬ РазличныеТоварыСНазначениями;
		|УНИЧТОЖИТЬ РазличныеТоварыБезНазначений";
	
	Подстановки = ОбеспечениеВДокументахСервер.ПодстановкиГраницыОбеспечиваемогоПериода(
		"Товары.Номенклатура", "Товары.Характеристика", "Товары.Склад", "&НачалоТекущегоДня", "ИСТИНА");
	
	Текст = СтрЗаменить(Текст, "РасчетПереопределяемый.ГраницаПериода",             Подстановки.Поле);
	Текст = СтрЗаменить(Текст, "ЛЕВОЕ СОЕДИНЕНИЕ РасчетПереопределяемый ПО ИСТИНА", Подстановки.Соединения);
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	
	Запрос.Текст = Текст;
	Пакет = Запрос.ВыполнитьПакет();
	
	Выборка = Пакет[5].Выбрать();
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		Набор.Отбор.Состояние.Установить(Выборка.Состояние);
		Набор.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
		Набор.Отбор.Характеристика.Установить(Выборка.Характеристика);
		Набор.Отбор.Склад.Установить(Выборка.Склад);
		Набор.Отбор.Назначение.Установить(Выборка.Назначение);
		Набор.Отбор.ЗаказНаОтгрузку.Установить(Выборка.ЗаказНаОтгрузку);
		Набор.Записать();
	КонецЦикла;
	
	Выборка = Пакет[6].Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ЗаказНаОтгрузку") Цикл
		
		Пока Выборка.СледующийПоЗначениюПоля("Номенклатура") Цикл
			
			Пока Выборка.СледующийПоЗначениюПоля("Характеристика") Цикл
				
				Пока Выборка.СледующийПоЗначениюПоля("Склад") Цикл
					
					Пока Выборка.СледующийПоЗначениюПоля("Назначение") Цикл
						
						Итог = 0;
						Пока Выборка.Следующий() Цикл
							
							Набор.Отбор.Состояние.Установить(Выборка.Состояние);
							Набор.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
							Набор.Отбор.Характеристика.Установить(Выборка.Характеристика);
							Набор.Отбор.Склад.Установить(Выборка.Склад);
							Набор.Отбор.Назначение.Установить(Выборка.Назначение);
							Набор.Отбор.ЗаказНаОтгрузку.Установить(Выборка.ЗаказНаОтгрузку);
							Набор.Отбор.ЖелаемаяДатаОтгрузки.Установить(Выборка.ЖелаемаяДатаОтгрузки);
							
							НоваяСтрока = Набор.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
							
							Если Выборка.РезервироватьПоМереПоступления > 0 Тогда
								Итог = Итог + Выборка.РезервироватьПоМереПоступления;
								НоваяСтрока.РезервироватьПоМереПоступленияИтогНаДату = Итог;
							КонецЕсли;
							Набор.Записать();
							Набор.Очистить();
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьСведенияПоЗаказамНаПоступление(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Изменения.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Изменения.Номенклатура КАК Номенклатура,
		|	Изменения.Характеристика КАК Характеристика,
		|	Изменения.Склад КАК Склад,
		|	Изменения.Назначение КАК Назначение
		|ПОМЕСТИТЬ ИзмененияЗаказовНаПоступление
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК Изменения
		|ГДЕ
		|	Изменения.ЗаказНаПоступление <> НЕОПРЕДЕЛЕНО
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПоступление, Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Изменения.ЗаказНаПоступление КАК ЗаказНаПоступление
		|ПОМЕСТИТЬ ЗаказыНаПоступление
		|ИЗ
		|	ИзмененияЗаказовНаПоступление КАК Изменения
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПоступление
		|;
		|
		|///////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыНаПоступление.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	Движения.Номенклатура                  КАК Номенклатура,
		|	Движения.Характеристика                КАК Характеристика,
		|	Движения.Склад                         КАК Склад,
		|	Движения.Назначение                    КАК Назначение,
		|	
		|	СУММА(Движения.ПоставкаНаСогласовании
		|				+ Движения.ПоступитКДате
		|				- Движения.ЗакрытьГрафикПоступления) КАК Количество
		|	
		|ПОМЕСТИТЬ ОстаткиПланируемыхПоступлений
		|ИЗ
		|	ИзмененияЗаказовНаПоступление КАК ЗаказыНаПоступление
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
		|		ПО Движения.ЗаказНаПоступление = ЗаказыНаПоступление.ЗаказНаПоступление
		|		 И Движения.Номенклатура       = ЗаказыНаПоступление.Номенклатура
		|		 И Движения.Характеристика     = ЗаказыНаПоступление.Характеристика
		|		 И Движения.Склад              = ЗаказыНаПоступление.Склад
		|		 И Движения.Назначение         = ЗаказыНаПоступление.Назначение
		|		 И Движения.Активность
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыНаПоступление.ЗаказНаПоступление,
		|	Движения.Номенклатура,
		|	Движения.Характеристика,
		|	Движения.Склад,
		|	Движения.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(Движения.ПоставкаНаСогласовании
		|			+ Движения.ПоступитКДате
		|			- Движения.ЗакрытьГрафикПоступления) > 0
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПоступление, Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИСТИНА                     КАК НуженРасчет,
		|	Остатки.Номенклатура       КАК Номенклатура,
		|	Остатки.Характеристика     КАК Характеристика,
		|	Остатки.Склад              КАК Склад,
		|	Остатки.Назначение         КАК Назначение,
		|	Остатки.ЗаказНаПоступление КАК ЗаказНаПоступление,
		|	1                          КАК ТипЗаписиРаспределенияЗапасов,
		|
		|	ВЫБОР КОГДА Движения.ДатаПоступления <> ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление)
		|			ИНАЧЕ
		|				ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеподтвержденное)
		|		КОНЕЦ КАК Состояние,
		|	
		|	Движения.ДатаПоступления КАК ДатаПоступления,
		|	МАКСИМУМ(Остатки.Количество) КАК КоличествоВсего,
		|	СУММА(Движения.ПоставкаНаСогласовании + Движения.ПоступитКДате) КАК Запас,
		|	0 КАК Свободно
		|ИЗ
		|	ОстаткиПланируемыхПоступлений КАК Остатки
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РаспределениеЗапасовДвижения КАК Движения
		|		ПО Движения.Номенклатура         = Остатки.Номенклатура
		|		 И Движения.Характеристика       = Остатки.Характеристика
		|		 И Движения.Склад                = Остатки.Склад
		|		 И Движения.Назначение           = Остатки.Назначение
		|		 И Движения.ЗаказНаОтгрузку      = НЕОПРЕДЕЛЕНО       // для использования индекса
		|		 И Движения.ЖелаемаяДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1) // для использования индекса
		|		 И Движения.ЗаказНаПоступление   = Остатки.ЗаказНаПоступление
		|		 И Движения.Активность
		|СГРУППИРОВАТЬ ПО
		|	Остатки.ЗаказНаПоступление,
		|	Остатки.Номенклатура,
		|	Остатки.Характеристика,
		|	Остатки.Склад,
		|	Остатки.Назначение,
		|	Движения.ДатаПоступления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ                                   КАК НуженРасчет,
		|	Сведения.Номенклатура                  КАК Номенклатура,
		|	Сведения.Характеристика                КАК Характеристика,
		|	Сведения.Склад                         КАК Склад,
		|	Сведения.Назначение                    КАК Назначение,
		|	Сведения.ЗаказНаПоступление            КАК ЗаказНаПоступление,
		|	Сведения.ТипЗаписиРаспределенияЗапасов КАК ТипЗаписиРаспределенияЗапасов,
		|	Сведения.Состояние                     КАК Состояние,
		|	Сведения.ДатаПоступления               КАК ДатаПоступления,
		|	0                                      КАК КоличествоВсего,
		|	Сведения.Запас                         КАК Запас,
		|	Сведения.Свободно                      КАК Свободно
		|ИЗ
		|	ЗаказыНаПоступление КАК Заказы
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.ЗаказНаПоступление = Заказы.ЗаказНаПоступление
		|		 И Сведения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИзмененияЗаказовНаПоступление КАК Изменения
		|		ПО Изменения.Номенклатура         = Сведения.Номенклатура
		|		 И Изменения.Характеристика       = Сведения.Характеристика
		|		 И Изменения.Склад                = Сведения.Склад
		|		 И Изменения.Назначение           = Сведения.Назначение
		|		 И Изменения.ЗаказНаПоступление   = Сведения.ЗаказНаПоступление
		|ГДЕ
		|	Изменения.Номенклатура ЕСТЬ NULL
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаПоступление, Номенклатура, Характеристика, Склад, Назначение, НуженРасчет УБЫВ, ДатаПоступления УБЫВ
		|;
		|
		|/////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сведения.Номенклатура                  КАК Номенклатура,
		|	Сведения.Характеристика                КАК Характеристика,
		|	Сведения.Склад                         КАК Склад,
		|	Сведения.Назначение                    КАК Назначение,
		|	Сведения.ЗаказНаПоступление            КАК ЗаказНаПоступление,
		|	Сведения.Состояние                     КАК Состояние
		|ИЗ
		|	ЗаказыНаПоступление КАК Заказы
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК Сведения
		|		ПО Сведения.ЗаказНаПоступление = Заказы.ЗаказНаПоступление
		|		 И Сведения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемоеПоступление),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ПоступлениеНеПодтвержденное))
		|;
		|
		|/////////////////////////////////////////
		|УНИЧТОЖИТЬ ИзмененияЗаказовНаПоступление;
		|УНИЧТОЖИТЬ ЗаказыНаПоступление;
		|УНИЧТОЖИТЬ ОстаткиПланируемыхПоступлений";
	Пакет = Запрос.ВыполнитьПакет();
	
	Выборка = Пакет[4].Выбрать();
	Пока Выборка.Следующий() Цикл
		Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
		Набор.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
		Набор.Отбор.Характеристика.Установить(Выборка.Характеристика);
		Набор.Отбор.Склад.Установить(Выборка.Склад);
		Набор.Отбор.Назначение.Установить(Выборка.Назначение);
		Набор.Отбор.Состояние.Установить(Выборка.Состояние);
		Набор.Отбор.ЗаказНаОтгрузку.Значение = Неопределено;
		Набор.Отбор.ЗаказНаОтгрузку.Использование = Истина;
		Набор.Отбор.ЖелаемаяДатаОтгрузки.Установить('00010101');
		Набор.Отбор.ЗаказНаПоступление.Установить(Выборка.ЗаказНаПоступление);
		Набор.Записать();
	КонецЦикла;
	
	Выборка = Пакет[3].Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ЗаказНаПоступление") Цикл
		
		Пока Выборка.СледующийПоЗначениюПоля("Номенклатура") Цикл
			
			Пока Выборка.СледующийПоЗначениюПоля("Характеристика") Цикл
				
				Пока Выборка.СледующийПоЗначениюПоля("Склад") Цикл
					
					Пока Выборка.СледующийПоЗначениюПоля("Назначение") Цикл
						
						Распределить = Выборка.КоличествоВсего;
						Пока Выборка.Следующий() Цикл
							
							Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
							Набор.Отбор.Номенклатура.Установить(Выборка.Номенклатура);
							Набор.Отбор.Характеристика.Установить(Выборка.Характеристика);
							Набор.Отбор.Склад.Установить(Выборка.Склад);
							Набор.Отбор.Назначение.Установить(Выборка.Назначение);
							Набор.Отбор.Состояние.Установить(Выборка.Состояние);
							Набор.Отбор.ЗаказНаОтгрузку.Значение = Неопределено;
							Набор.Отбор.ЗаказНаОтгрузку.Использование = Истина;
							Набор.Отбор.ЖелаемаяДатаОтгрузки.Установить('00010101');
							Набор.Отбор.ЗаказНаПоступление.Установить(Выборка.ЗаказНаПоступление);
							Набор.Отбор.ДатаПоступления.Установить(Выборка.ДатаПоступления);
							Если Выборка.НуженРасчет Тогда
								
								Запас = Макс(Мин(Распределить, Выборка.Запас), 0);
								Распределить = Распределить - Запас;
								
								Если Запас <> 0 Тогда
									НоваяСтрока = Набор.Добавить();
									ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
									НоваяСтрока.Запас = Запас;
								КонецЕсли;
								
							Иначе
								НоваяСтрока = Набор.Добавить();
								ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
							КонецЕсли;
							
							Набор.Записать();
							Набор.Очистить();
							
						КонецЦикла;
						
					КонецЦикла;
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстатокНаСкладе(Запрос)
	
	КонтрольОстатковВДокументеВключен = ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства);
	Запрос.УстановитьПараметр("КонтрольОстатковВДокументеВключен", КонтрольОстатковВДокументеВключен);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененияДвижений.Номенклатура   КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика КАК Характеристика,
		|	ИзмененияДвижений.Склад          КАК Склад
		|ПОМЕСТИТЬ ТоварыОстатокКоторыхУменьшится
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|ГДЕ
		|	ИзмененияДвижений.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|		И НЕ ИзмененияДвижений.ИгнорироватьРезервыПриКонтролеОстатков
		|		И (ИзмененияДвижений.Отгрузить <> 0
		|			ИЛИ ИзмененияДвижений.Резервировать <> 0
		|			ИЛИ ИзмененияДвижений.Поступило <> 0)
		|		И &КонтрольОстатковВДокументеВключен
		|СГРУППИРОВАТЬ ПО
		|	ИзмененияДвижений.Номенклатура,
		|	ИзмененияДвижений.Характеристика,
		|	ИзмененияДвижений.Склад,
		|	ИзмененияДвижений.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать) > 0
		|		И СУММА(ИзмененияДвижений.Отгрузить + ИзмененияДвижений.Резервировать - ИзмененияДвижений.Поступило) > 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Товары.Номенклатура                 КАК Номенклатура,
		|	Товары.Характеристика               КАК Характеристика,
		|	Товары.Склад                        КАК Склад
		|ПОМЕСТИТЬ ТоварыДляКонтроляОстатков
		|ИЗ
		|	ТоварыОстатокКоторыхУменьшится КАК Товары
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспеченияХарактеристика
		|		ПО НастройкаКонтроляОбеспеченияХарактеристика.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура = Товары.Номенклатура
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Характеристика = Товары.Характеристика
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспеченияНоменклатура
		|		ПО НастройкаКонтроляОбеспеченияНоменклатура.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Номенклатура = Товары.Номенклатура
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура ЕСТЬ NULL
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаКонтроляОбеспечения
		|		ПО НастройкаКонтроляОбеспечения.Склад = Товары.Склад
		|		 И НастройкаКонтроляОбеспечения.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспечения.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаКонтроляОбеспеченияХарактеристика.Номенклатура ЕСТЬ NULL
		|		 И НастройкаКонтроляОбеспеченияНоменклатура.Номенклатура ЕСТЬ NULL
		|ГДЕ
		|	ЕСТЬNULL(НастройкаКонтроляОбеспеченияХарактеристика.КонтролироватьСвободныеОстатки,
		|		ЕСТЬNULL(НастройкаКонтроляОбеспеченияНоменклатура.КонтролироватьСвободныеОстатки,
		|			НастройкаКонтроляОбеспечения.КонтролироватьСвободныеОстатки))
		|;
		|
		|//////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененияДвижений.Номенклатура    КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика  КАК Характеристика,
		|	ИзмененияДвижений.Склад           КАК Склад,
		|	ИзмененияДвижений.Назначение      КАК Назначение,
		|	ИзмененияДвижений.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
		|ПОМЕСТИТЬ ЗаказыТоваровДляКонтроляОстатков
		|ИЗ
		|	ТоварыДляКонтроляОстатков КАК Товары
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|		ПО ИзмененияДвижений.Номенклатура = Товары.Номенклатура
		|		 И ИзмененияДвижений.Характеристика = Товары.Характеристика
		|		 И ИзмененияДвижений.Склад = Товары.Склад
		|		 И ИзмененияДвижений.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО
		|		 И (ИзмененияДвижений.Отгрузить <> 0 ИЛИ ИзмененияДвижений.Резервировать <> 0)
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|/////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СведенияДоИзменения.Номенклатура        КАК Номенклатура,
		|	СведенияДоИзменения.Характеристика      КАК Характеристика,
		|	СведенияДоИзменения.Склад               КАК Склад,
		|	СведенияДоИзменения.Назначение          КАК Назначение,
		|	СведенияДоИзменения.ЗаказНаОтгрузку     КАК ЗаказНаОтгрузку,
		// Получаем распределенное на складе количество по заказу, но так как это количество формируется фоновым заданием
		// а не в транзакции проведения документа, то оно может быть завышено, если заказ на отгрузку уменьшен, а фоновое
		// задание еще не отработало. В этом случае были бы получены завышенные данные о доступном для отгрузки товаре для заказа.
		// Ситуация недопустима для контроля остатков, поэтому распределенное количество корректируется с учетом потребности заказа.
		// Таким образом синхронизруются данные рассчитываемые фоновым заданием и рассчитываемые в транзации проведения документов.
		|	ВЫБОР КОГДА СведенияДоИзменения.РезервироватьПоМереПоступления < СведенияДоИзменения.Зарезервировано ТОГДА
		|			СведенияДоИзменения.РезервироватьПоМереПоступления
		|		ИНАЧЕ
		|			СведенияДоИзменения.Зарезервировано
		|		КОНЕЦ КАК Зарезервировано
		|ПОМЕСТИТЬ ЗарезервированоНаСкладеПоЗаказамТоваровДляКонтроляОстатков
		|ИЗ(
		|	ВЫБРАТЬ
		|		СведенияДоИзменения.Номенклатура        КАК Номенклатура,
		|		СведенияДоИзменения.Характеристика      КАК Характеристика,
		|		СведенияДоИзменения.Склад               КАК Склад,
		|		СведенияДоИзменения.Назначение          КАК Назначение,
		|		СведенияДоИзменения.ЗаказНаОтгрузку     КАК ЗаказНаОтгрузку,
		|		СУММА(СведенияДоИзменения.Зарезервировано) КАК Зарезервировано,
		|		СУММА(СведенияДоИзменения.РезервироватьПоМереПоступления) КАК РезервироватьПоМереПоступления
		|	ИЗ
		|		ЗаказыТоваровДляКонтроляОстатков КАК Товары
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК СведенияДоИзменения
		|			ПО СведенияДоИзменения.Номенклатура = Товары.Номенклатура
		|			 И СведенияДоИзменения.Характеристика = Товары.Характеристика
		|			 И СведенияДоИзменения.Склад = Товары.Склад
		|			 И СведенияДоИзменения.Назначение = Товары.Назначение
		|			 И СведенияДоИзменения.ЗаказНаОтгрузку = Товары.ЗаказНаОтгрузку
		|	ГДЕ
		|		СведенияДоИзменения.Состояние В(
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОжидаемаяОтгрузка),
		|			ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОбеспеченНаСкладе))
		|	СГРУППИРОВАТЬ ПО
		|		СведенияДоИзменения.Номенклатура,
		|		СведенияДоИзменения.Характеристика,
		|		СведенияДоИзменения.Склад,
		|		СведенияДоИзменения.Назначение,
		|		СведенияДоИзменения.ЗаказНаОтгрузку) КАК СведенияДоИзменения
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|//////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзмененияДвижений.Номенклатура                                               КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика                                             КАК Характеристика,
		|	ИзмененияДвижений.Склад                                                      КАК Склад,
		|	ИзмененияДвижений.Назначение                                                 КАК Назначение,
		|	ИзмененияДвижений.ЗаказНаОтгрузку                                            КАК ЗаказНаОтгрузку,
		|	-СУММА(ИзмененияДвижений.Отгрузить) - СУММА(ИзмененияДвижений.Резервировать) КАК Свободно,
		|	СУММА(ИзмененияДвижений.Поступило)                                           КАК Поступило
		|ПОМЕСТИТЬ ИзменениеОстаткаПоТоварамДляКонтроляОстатков
		|ИЗ
		|	ТоварыДляКонтроляОстатков КАК Товары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|		ПО ИзмененияДвижений.Номенклатура = Товары.Номенклатура
		|		 И ИзмененияДвижений.Характеристика = Товары.Характеристика
		|		 И ИзмененияДвижений.Склад = Товары.Склад
		|СГРУППИРОВАТЬ ПО
		|	ИзмененияДвижений.Номенклатура,
		|	ИзмененияДвижений.Характеристика,
		|	ИзмененияДвижений.Склад,
		|	ИзмененияДвижений.Назначение,
		|	ИзмененияДвижений.ЗаказНаОтгрузку
		|ИМЕЮЩИЕ
		|	СУММА(ИзмененияДвижений.Отгрузить) <> 0
		|		ИЛИ СУММА(ИзмененияДвижений.Резервировать) <> 0
		|		ИЛИ СУММА(ИзмененияДвижений.Поступило) <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаОтгрузку
		|;
		|
		|/////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзмененияДвижений.Номенклатура                                              КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика                                            КАК Характеристика,
		|	ИзмененияДвижений.Склад                                                     КАК Склад,
		|	ИзмененияДвижений.Назначение                                                КАК Назначение,
		|	СУММА(ВЫБОР КОГДА ИзмененияДвижений.Свободно >= 0 ТОГДА
		|				ИзмененияДвижений.Свободно
		|			ИНАЧЕ
		|				ВЫБОР КОГДА ИзмененияДвижений.Свободно + ЕСТЬNULL(Сведения.Зарезервировано, 0) > 0 ТОГДА
		|					0
		|						ИНАЧЕ
		|							ИзмененияДвижений.Свободно + ЕСТЬNULL(Сведения.Зарезервировано, 0)
		|					КОНЕЦ
		|		КОНЕЦ
		|			+ ИзмененияДвижений.Поступило) КАК Свободно
		|ПОМЕСТИТЬ УменьшениеСвободногоОстаткаДляТоваровСКонтролемОстатка
		|ИЗ
		|	ИзменениеОстаткаПоТоварамДляКонтроляОстатков КАК ИзмененияДвижений
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЗарезервированоНаСкладеПоЗаказамТоваровДляКонтроляОстатков КАК Сведения
		|		ПО Сведения.Номенклатура = ИзмененияДвижений.Номенклатура
		|		 И Сведения.Характеристика = ИзмененияДвижений.Характеристика
		|		 И Сведения.Склад = ИзмененияДвижений.Склад
		|		 И Сведения.Назначение = ИзмененияДвижений.Назначение
		|		 И Сведения.ЗаказНаОтгрузку = ИзмененияДвижений.ЗаказНаОтгрузку
		|СГРУППИРОВАТЬ ПО
		|	ИзмененияДвижений.Номенклатура,
		|	ИзмененияДвижений.Характеристика,
		|	ИзмененияДвижений.Склад,
		|	ИзмененияДвижений.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР КОГДА ИзмененияДвижений.Свободно >= 0 ТОГДА
		|				ИзмененияДвижений.Свободно
		|			ИНАЧЕ
		|				ВЫБОР КОГДА ИзмененияДвижений.Свободно + ЕСТЬNULL(Сведения.Зарезервировано, 0) > 0 ТОГДА
		|					0
		|						ИНАЧЕ
		|							ИзмененияДвижений.Свободно + ЕСТЬNULL(Сведения.Зарезервировано, 0)
		|					КОНЕЦ
		|		КОНЕЦ
		|			+ ИзмененияДвижений.Поступило) < 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|/////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзмененияДвижений.Номенклатура          КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика        КАК Характеристика,
		|	ИзмененияДвижений.Склад                 КАК Склад,
		|	ИзмененияДвижений.Назначение            КАК Назначение,
		|	СУММА(ИзмененияДвижений.Отгрузить)      КАК Отгрузить,
		|	СУММА(ИзмененияДвижений.Резервировать)  КАК Резервировать,
		|	СУММА(ИзмененияДвижений.Поступило)      КАК Поступило
		|ПОМЕСТИТЬ ИзмененияПоТовару
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений
		|СГРУППИРОВАТЬ ПО
		|	ИзмененияДвижений.Номенклатура,
		|	ИзмененияДвижений.Характеристика,
		|	ИзмененияДвижений.Склад,
		|	ИзмененияДвижений.Назначение
		|ИМЕЮЩИЕ
		|	СУММА(ИзмененияДвижений.Отгрузить) <> 0
		|		ИЛИ СУММА(ИзмененияДвижений.Резервировать) <> 0
		|		ИЛИ СУММА(ИзмененияДвижений.Поступило) <> 0
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение
		|;
		|
		|/////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Номенклатура                  КАК Номенклатура,
		|	Таблица.Характеристика                КАК Характеристика,
		|	Таблица.Склад                         КАК Склад,
		|	СУММА(Таблица.Поступило - Таблица.Отгрузить) КАК ВНаличии,
		|	СУММА(ВЫБОР КОГДА Таблица.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|				Таблица.Поступило - Таблица.Отгрузить
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ) КАК Запас,
		|	СУММА(ВЫБОР КОГДА Таблица.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
		|				Таблица.Резервировать
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ) КАК Резерв,
		|	МАКСИМУМ(Таблица.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК ЕстьИзмененияБезНазначения
		|ПОМЕСТИТЬ ИзмененияБезНазначения
		|ИЗ
		|	ИзмененияПоТовару КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Номенклатура,
		|	Таблица.Характеристика,
		|	Таблица.Склад
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|/////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
		|	1                           КАК ТипЗаписиРаспределенияЗапасов,
		|	Таблица.Номенклатура        КАК Номенклатура,
		|	Таблица.Характеристика      КАК Характеристика,
		|	Таблица.Склад               КАК Склад,
		|	Таблица.Назначение          КАК Назначение,
		|	0                           КАК ВНаличии,
		|	Таблица.Поступило - Таблица.Отгрузить + ЕСТЬNULL(ДанныеИБ.Запас, 0) КАК Запас,
		|	Таблица.Резервировать + ЕСТЬNULL(ДанныеИБ.Резерв, 0) КАК Резерв,
		|	ВЫБОР КОГДА ТоварыДляКонтроля.Номенклатура ЕСТЬ NULL ТОГДА
		|				0
		|			ИНАЧЕ
		|				ТоварыДляКонтроля.Свободно + ЕСТЬNULL(ДанныеИБ.Свободно, 0)
		|		КОНЕЦ КАК Свободно
		|ПОМЕСТИТЬ ТаблицаОстатокНаскладе
		|ИЗ
		|	ИзмененияПоТовару КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ДанныеИБ
		|		ПО ДанныеИБ.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		|		 И ДанныеИБ.Номенклатура   = Таблица.Номенклатура
		|		 И ДанныеИБ.Характеристика = Таблица.Характеристика
		|		 И ДанныеИБ.Склад          = Таблица.Склад
		|		 И ДанныеИБ.Назначение     = Таблица.Назначение
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ УменьшениеСвободногоОстаткаДляТоваровСКонтролемОстатка КАК ТоварыДляКонтроля
		|		ПО ТоварыДляКонтроля.Номенклатура   = Таблица.Номенклатура
		|		 И ТоварыДляКонтроля.Характеристика = Таблица.Характеристика
		|		 И ТоварыДляКонтроля.Склад          = Таблица.Склад
		|		 И ТоварыДляКонтроля.Назначение     = Таблица.Назначение
		|ГДЕ
		|	Таблица.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе) КАК Состояние,
		|	1                                                 КАК ТипЗаписиРаспределенияЗапасов,
		|	Таблица.Номенклатура                              КАК Номенклатура,
		|	Таблица.Характеристика                            КАК Характеристика,
		|	Таблица.Склад                                     КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)      КАК Назначение,
		|	Таблица.ВНаличии + ЕСТЬNULL(ДанныеИБ.ВНаличии, 0) КАК ВНаличии,
		|	Таблица.Запас + ЕСТЬNULL(ДанныеИБ.Запас, 0)       КАК Запас,
		|	Таблица.Резерв + ЕСТЬNULL(ДанныеИБ.Резерв, 0)     КАК Резерв,
		|	ВЫБОР КОГДА ТоварыДляКонтроля.Номенклатура ЕСТЬ NULL ТОГДА
		|				ВЫБОР КОГДА Таблица.ЕстьИзмененияБезНазначения ТОГДА
		|							0
		|						ИНАЧЕ
		|							ЕСТЬNULL(ДанныеИБ.Свободно, 0)
		|					КОНЕЦ
		|			ИНАЧЕ
		|				ТоварыДляКонтроля.Свободно + ЕСТЬNULL(ДанныеИБ.Свободно, 0)
		|		КОНЕЦ КАК Свободно
		|ИЗ
		|	ИзмененияБезНазначения КАК Таблица
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаспределениеЗапасов КАК ДанныеИБ
		|		ПО ДанныеИБ.Состояние = ЗНАЧЕНИЕ(Перечисление.РаспределениеЗапасовСостояния.ОстатокНаСкладе)
		|		 И ДанныеИБ.Номенклатура   = Таблица.Номенклатура
		|		 И ДанныеИБ.Характеристика = Таблица.Характеристика
		|		 И ДанныеИБ.Склад          = Таблица.Склад
		|		 И ДанныеИБ.Назначение     = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ УменьшениеСвободногоОстаткаДляТоваровСКонтролемОстатка КАК ТоварыДляКонтроля
		|		ПО ТоварыДляКонтроля.Номенклатура   = Таблица.Номенклатура
		|		 И ТоварыДляКонтроля.Характеристика = Таблица.Характеристика
		|		 И ТоварыДляКонтроля.Склад          = Таблица.Склад
		|		 И ТоварыДляКонтроля.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,Характеристика,Склад,Назначение
		|;
		|
		|/////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ИзмененияПоТовару;
		|УНИЧТОЖИТЬ ИзмененияБезНазначения;
		|УНИЧТОЖИТЬ ЗаказыТоваровДляКонтроляОстатков;
		|УНИЧТОЖИТЬ ЗарезервированоНаСкладеПоЗаказамТоваровДляКонтроляОстатков;
		|УНИЧТОЖИТЬ ИзменениеОстаткаПоТоварамДляКонтроляОстатков;
		|УНИЧТОЖИТЬ ТоварыДляКонтроляОстатков;
		|УНИЧТОЖИТЬ ТоварыОстатокКоторыхУменьшится;
		|УНИЧТОЖИТЬ УменьшениеСвободногоОстаткаДляТоваровСКонтролемОстатка";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаписатьРаспределениеЗапасовНаПотребности(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РазличныеТовары.Номенклатура   КАК Номенклатура,
		|	РазличныеТовары.Характеристика КАК Характеристика,
		|	РазличныеТовары.Склад          КАК Склад,
		|	РазличныеТовары.Назначение     КАК Назначение,
		|	СУММА(РазличныеТовары.Отгрузить) <> 0
		|		ИЛИ СУММА(РазличныеТовары.Резервировать) <> 0
		|		ИЛИ СУММА(РазличныеТовары.Поступило) <> 0 КАК РаспределятьЕслиОстатокНеотрицательный
		|ПОМЕСТИТЬ РазличныеТовары
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК РазличныеТовары
		|СГРУППИРОВАТЬ ПО
		|	РазличныеТовары.Номенклатура,
		|	РазличныеТовары.Характеристика,
		|	РазличныеТовары.Склад,
		|	РазличныеТовары.Назначение
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	Запрос.Выполнить();
	
	РаспределениеЗапасовДвижения.ЗаписатьРаспределениеЗапасовНаПотребностиПоТаблицеРазличныеТоварыПриПроведении(Запрос);
	Запрос.Текст = "УНИЧТОЖИТЬ РазличныеТовары";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаписатьЗадания(Запрос)
	
	Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененияДвижений.Номенклатура    КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика  КАК Характеристика,
		|	ИзмененияДвижений.Склад           КАК Склад,
		|	ИзмененияДвижений.Назначение      КАК Назначение,
		|	ИзмененияДвижений.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений";
	
	Если ДополнительныеСвойства.Свойство("ИзменилосьМестоВОчередиРаспределенияЗапасов") Тогда
		
		ТекстыЗапросов = Новый Массив();
		ТекстыЗапросов.Добавить(Текст);
		
		Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Товары.Номенклатура    КАК Номенклатура,
			|	Товары.Характеристика  КАК Характеристика,
			|	Товары.Склад           КАК Склад,
			|	Товары.Назначение      КАК Назначение,
			|	Товары.ЗаказНаОтгрузку КАК ЗаказНаОтгрузку
			|ИЗ
			|	ДвиженияПриЗаписи КАК Товары";
			
		ТекстыЗапросов.Добавить(Текст);
		Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
		
	КонецЕсли;
	
	Запрос.Текст = Текст;
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Таблица.Колонки.Добавить("РазделительЗаписи");
	РазделительЗаписи = Новый УникальныйИдентификатор();
	Таблица.ЗаполнитьЗначения(РазделительЗаписи, "РазделительЗаписи");
	Таблица.Колонки.Добавить("ДатаЗаписи");
	Таблица.ЗаполнитьЗначения(ТекущаяДатаСеанса(), "ДатаЗаписи");
	
	Набор = РегистрыСведений.ЗаданияКРаспределениюЗапасов.СоздатьНаборЗаписей();
	Набор.Загрузить(Таблица);
	Набор.Отбор.РазделительЗаписи.Установить(РазделительЗаписи);
	Набор.Записать(Ложь);
	
КонецПроцедуры

Процедура ЗаписатьРаспределениеЗапасов(Запрос)
	
	Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененияДвижений.Номенклатура    КАК Номенклатура,
		|	ИзмененияДвижений.Характеристика  КАК Характеристика,
		|	ИзмененияДвижений.Склад           КАК Склад,
		|	ИзмененияДвижений.Назначение      КАК Назначение
		|ИЗ
		|	РаспределениеЗапасовДвиженияИзменение КАК ИзмененияДвижений";
	
	Если ДополнительныеСвойства.Свойство("ИзменилосьМестоВОчередиРаспределенияЗапасов") Тогда
		
		ТекстыЗапросов = Новый Массив();
		ТекстыЗапросов.Добавить(Текст);
		
		Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Товары.Номенклатура    КАК Номенклатура,
			|	Товары.Характеристика  КАК Характеристика,
			|	Товары.Склад           КАК Склад,
			|	Товары.Назначение      КАК Назначение
			|ИЗ
			|	ДвиженияПриЗаписи КАК Товары";
			
		ТекстыЗапросов.Добавить(Текст);
		Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении(Истина));
		
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Набор.Номенклатура    КАК Номенклатура,
		|	Набор.Характеристика  КАК Характеристика,
		|	Набор.Склад           КАК Склад,
		|	Набор.Назначение      КАК Назначение
		|ПОМЕСТИТЬ ТоварыДляРаспределенияЗапасов
		|ИЗ
		|	&Набор КАК Набор
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад, Назначение";
	
	Текст = СтрЗаменить(ТекстЗапроса, "&Набор", СтрШаблон("(%1)",Текст));
	Запрос.Текст = Текст;
	
	Запрос.Выполнить();
	
	Результат = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	Результат.Колонки.Добавить("Пустая", Новый ОписаниеТипов("Булево"));
	Запрос.Текст = РаспределениеЗапасов.ТекстЗапросаРаспределенияЗапасов();
	Запрос.УстановитьПараметр("НачалоТекущегоДня", НачалоДня(ТекущаяДатаСеанса()));
	Пакет = Запрос.ВыполнитьПакет();
	Таблица = Пакет[8].Выгрузить();
	График = Пакет[9].Выгрузить();
	
	РаспределениеЗапасов.РаспределитьЗапасыВТаблицу(Результат, Таблица, График, Истина);
	ВсегоСтрок = Результат.Количество();
	
	Номенклатура   = Неопределено;
	Характеристика = Неопределено;
	Склад          = Неопределено;
	Назначение     = Неопределено;
	
	Набор = РегистрыСведений.РаспределениеЗапасов.СоздатьНаборЗаписей();
	
	Для Индекс = 0 По ВсегоСтрок - 1 Цикл
		
		ТекСтрока = Результат[Индекс];
		Если Номенклатура <> ТекСтрока.Номенклатура
				Или Характеристика <> ТекСтрока.Характеристика
				Или Склад <> ТекСтрока.Склад
				Или Назначение <> ТекСтрока.Назначение Тогда
					
					Если Номенклатура <> Неопределено Тогда
						Набор.Записать();
						Набор.Очистить();
					КонецЕсли;

					Номенклатура   = ТекСтрока.Номенклатура;
					Характеристика = ТекСтрока.Характеристика;
					Склад          = ТекСтрока.Склад;
					Назначение     = ТекСтрока.Назначение;
					
					Набор.Отбор.ТипЗаписиРаспределенияЗапасов.Установить(0);
					Набор.Отбор.Номенклатура.Установить(Номенклатура);
					Набор.Отбор.Характеристика.Установить(Характеристика);
					Набор.Отбор.Склад.Установить(Склад);
					Набор.Отбор.Назначение.Установить(Назначение);
		КонецЕсли;
		
		Если Не ТекСтрока.Пустая Тогда
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), ТекСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Номенклатура <> Неопределено Тогда
		Набор.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьТаблицуТоварыДляРасчетаСостояний()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Если ДополнительныеСвойства.Свойство("ИзменилосьМестоВОчередиРаспределенияЗапасов") Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Таблица.Номенклатура   КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Склад          КАК Склад,
			|	Таблица.Назначение     КАК Назначение
			|ПОМЕСТИТЬ ТоварыДляРасчетаСостояний
			|ИЗ
			|	РаспределениеЗапасовДвиженияПередЗаписью КАК Таблица
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Таблица.Номенклатура   КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Склад          КАК Склад,
			|	Таблица.Назначение     КАК Назначение
			|ИЗ
			|	РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|ГДЕ
			|	Таблица.Регистратор = &Регистратор
			|		И Таблица.Активность";
		
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ТаблицаИзменений.Номенклатура   КАК Номенклатура,
			|	ТаблицаИзменений.Характеристика КАК Характеристика,
			|	ТаблицаИзменений.Склад          КАК Склад,
			|	ТаблицаИзменений.Назначение     КАК Назначение
			|ПОМЕСТИТЬ ТоварыДляРасчетаСостояний
			|ИЗ(
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура             КАК Номенклатура,
			|		Таблица.Характеристика           КАК Характеристика,
			|		Таблица.Склад                    КАК Склад,
			|		Таблица.Назначение               КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку          КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки     КАК ЖелаемаяДатаОтгрузки,
			|		Таблица.ЗаказНаПоступление       КАК ЗаказНаПоступление,
			|		Таблица.ДатаПоступления          КАК ДатаПоступления,
			|		Таблица.Отгрузить                КАК Отгрузить,
			|		Таблица.Резервировать            КАК Резервировать,
			|		Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
			|		Таблица.КОбеспечению             КАК КОбеспечению,
			|		Таблица.НеОбеспечивать           КАК НеОбеспечивать,
			|		Таблица.Поступило                КАК Поступило,
			|		Таблица.ПоступитКДате            КАК ПоступитКДате,
			|		Таблица.ПоставкаНаСогласовании   КАК ПоставкаНаСогласовании,
			|		Таблица.ЗакрытьГрафикПоступления КАК ЗакрытьГрафикПоступления
			|	ИЗ
			|		РаспределениеЗапасовДвиженияПередЗаписью КАК Таблица
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Таблица.Номенклатура              КАК Номенклатура,
			|		Таблица.Характеристика            КАК Характеристика,
			|		Таблица.Склад                     КАК Склад,
			|		Таблица.Назначение                КАК Назначение,
			|		Таблица.ЗаказНаОтгрузку           КАК ЗаказНаОтгрузку,
			|		Таблица.ЖелаемаяДатаОтгрузки      КАК ЖелаемаяДатаОтгрузки,
			|		Таблица.ЗаказНаПоступление        КАК ЗаказНаПоступление,
			|		Таблица.ДатаПоступления           КАК ДатаПоступления,
			|		-Таблица.Отгрузить                КАК Отгрузить,
			|		-Таблица.Резервировать            КАК Резервировать,
			|		- Таблица.РезервироватьПоМереПоступления КАК РезервироватьПоМереПоступления,
			|		-Таблица.КОбеспечениюБезРезерва   КАК КОбеспечению,
			|		-Таблица.НеОбеспечивать           КАК НеОбеспечивать,
			|		-Таблица.Поступило                КАК Поступило,
			|		-Таблица.ПоступитКДате            КАК ПоступитКДате,
			|		-Таблица.ПоставкаНаСогласовании   КАК ПоставкаНаСогласовании,
			|		-Таблица.ЗакрытьГрафикПоступления КАК ЗакрытьГрафикПоступления
			|	ИЗ
			|		РегистрНакопления.РаспределениеЗапасовДвижения КАК Таблица
			|	ГДЕ
			|		Таблица.Регистратор = &Регистратор
			|			И Таблица.Активность) КАК ТаблицаИзменений
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаИзменений.Номенклатура,
			|	ТаблицаИзменений.Характеристика,
			|	ТаблицаИзменений.Склад,
			|	ТаблицаИзменений.Назначение,
			|	ТаблицаИзменений.ЗаказНаОтгрузку,
			|	ТаблицаИзменений.ЖелаемаяДатаОтгрузки,
			|	ТаблицаИзменений.ЗаказНаПоступление,
			|	ТаблицаИзменений.ДатаПоступления
			|ИМЕЮЩИЕ
			|	СУММА(ТаблицаИзменений.Отгрузить) <> 0
			|		ИЛИ СУММА(ТаблицаИзменений.Резервировать) <> 0
			|		ИЛИ СУММА(ТаблицаИзменений.РезервироватьПоМереПоступления) <> 0
			|		ИЛИ СУММА(ТаблицаИзменений.КОбеспечению) <> 0
			|		ИЛИ СУММА(ТаблицаИзменений.НеОбеспечивать) <> 0
			|		ИЛИ СУММА(ТаблицаИзменений.Поступило) <> 0
			|		ИЛИ СУММА(ТаблицаИзменений.ПоступитКДате) <> 0
			|		ИЛИ СУММА(ТаблицаИзменений.ПоставкаНаСогласовании) <> 0
			|		ИЛИ СУММА(ТаблицаИзменений.ЗакрытьГрафикПоступления) <> 0";
		
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура УничтожитьТаблицуТоварыДляРасчетаСостояний()
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст = "УНИЧТОЖИТЬ ТоварыДляРасчетаСостояний";
	Запрос.Выполнить();
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

