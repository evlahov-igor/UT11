////////////////////////////////////////////////////
//// Модуль менеджера регистра накопления "КР_КоробаНаСкладах"
//// Создан: 01.11.2022 Марченко С.Н., КРОК, JIRA№A2105505-751
//// Разработка по ФДР С11.032 Документы "Отбор (размещение) товаров" и "Перемещение товаров"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.КР_КоробаНаСкладах);
		
	КонецЕсли;
	
	// Контроль
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.КР_КоробаНаСкладах);

	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	#Область КР_КоробаНаСкладах

	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "КР_ДвиженияКоробаНаСкладахИзменение") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	КоробаНаСкладахОстатки.Склад КАК Склад,
		|	КоробаНаСкладахОстатки.Короб КАК Короб,
		// << 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
		//|	КоробаНаСкладахОстатки.ВНаличииОстаток КАК Количество
		|	КоробаНаСкладахОстатки.ВНаличииОстаток КАК ВНаличии,
		|	КоробаНаСкладахОстатки.КОтгрузкеОстаток КАК КОтгрузке,
		|	КоробаНаСкладахОстатки.ВНаличииОстаток - КоробаНаСкладахОстатки.КОтгрузкеОстаток КАК Количество
		// >> 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
		|ИЗ
		|	РегистрНакопления.КР_КоробаНаСкладах.Остатки(
		|			,
		|			Склад.КР_КонтролироватьОстаткиКоробов
		|				И (Склад, Короб) В
		|					(ВЫБРАТЬ
		|						Таблица.Склад,
		|						Таблица.Короб
		|					ИЗ
		|						КР_ДвиженияКоробаНаСкладахИзменение КАК Таблица)) КАК КоробаНаСкладахОстатки
		|ГДЕ
		// << 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
		//|	КоробаНаСкладахОстатки.ВНаличииОстаток < 0";
		|	КоробаНаСкладахОстатки.ВНаличииОстаток - КоробаНаСкладахОстатки.КОтгрузкеОстаток < 0
		// >> 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
		|";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиКоробаНаСкладах");
		
	КонецЕсли;

	#КонецОбласти

КонецПроцедуры

// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	
	#Область КР_КоробаНаСкладах
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "КР_ДвиженияКоробаНаСкладахИзменение") Тогда
		
		ШаблонСообщения = НСтр("ru = 'Короб %1
			|Превышен оперативный складской остаток на складе ""%2"" на %3 шт'");
		// << 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
		//ШаблонСообщенияОшибкаОтгрузки = НСтр("ru = 'Короб %1 
		//	|не доступен для отгрузки на складе ""%2"": В наличии %3, из них зарегистрировано к отгрузке %4'");
		// >> 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
		
		Для каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиКоробаНаСкладах Цикл
			
			// << 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
			//Если СтрокаОшибки.Количество < 0 Тогда
			// >> 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
			
				ТекстСообщения = СтрШаблон(ШаблонСообщения, СтрокаОшибки.Короб,
					СтрокаОшибки.Склад, -СтрокаОшибки.Количество);
			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Документ, , , Отказ);
			
			// << 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
			//ИначеЕсли СтрокаОшибки.ОшибкаОтгрузки Тогда
			//	
			//	ТекстСообщения = СтрШаблон(ШаблонСообщенияОшибкаОтгрузки, СтрокаОшибки.Короб, СтрокаОшибки.Склад,
			//		СтрокаОшибки.Количество);
			//	
			//	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Документ, , , Отказ);
			//	
			//КонецЕсли;
			// >> 28.11.2022,  Федоров Д.Е.,  КРОК,  Jira№A2105505-933
			
		КонецЦикла;
		
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

// Процедура формирования движений по регистру.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "КР_КоробаНаСкладах");
	
КонецПроцедуры

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие - Соответствие полного имени регистра тексту запроса сторнирования
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ТекстыЗапросов = Новый Соответствие();
	
	Возврат ТекстыЗапросов;
	
КонецФункции

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие();
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
