
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс


#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.7.246";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("da61b003-8fb1-4d3b-93c0-0f8c05596f2a");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Заполняет измерение ""Объект расчетов"". Исправляет движения в регистре взаиморасчетов.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "ОбъектыРасчетовСервер.ПереформироватьНекорректныеОнлайнДвижения";
	НоваяСтрока.Порядок = "До";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыКонтрагентов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.ДоговорыМеждуОрганизациями.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "Справочники.Контрагенты.СгенерироватьОбъектыРасчетов";
	НоваяСтрока.Порядок = "После";

КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = СоздатьНаборЗаписей().Метаданные().ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.ОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|		И НЕ ДанныеРегистра.АналитикаУчетаПоПартнерам = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка)
	|		ИЛИ НЕ ДанныеРегистра.КорОбъектРасчетов.ВалютаВзаиморасчетов ЕСТЬ NULL
	|		И  ДанныеРегистра.КорОбъектРасчетов.ВалютаВзаиморасчетов <> ДанныеРегистра.Валюта
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК ДанныеРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК КорОР
	|			ПО ДанныеРегистра.КорОбъектРасчетов = КорОР.Ссылка
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КорКлючиАналитики
	|			ПО ДанныеРегистра.КорАналитикаУчетаПоПартнерам = КорКлючиАналитики.Ссылка
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КлючиАналитики
	|			ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = КлючиАналитики.Ссылка
	|ГДЕ
	|	ДанныеРегистра.КорОбъектРасчетов <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|	И НЕ КорОР.Договор ЕСТЬ NULL
	|	И ЕСТЬNULL(КорКлючиАналитики.Договор, КлючиАналитики.Договор) <> КорОР.Договор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы,
		ПолноеИмяРегистра);
		
	Если Регистраторы.Количество() > 0 Тогда
		ВзаиморасчетыВызовСервера.ОтключитьРаспределениеВзаиморасчетов();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеРегистра.Период КАК Период,
		|	ДанныеРегистра.Регистратор КАК Регистратор,
		|	ДанныеРегистра.Активность КАК Активность,
		|	ДанныеРегистра.ВидДвижения КАК ВидДвижения,
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ДанныеРегистра.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ДанныеРегистра.Валюта КАК Валюта,
		|	ДанныеРегистра.РасчетныйДокумент КАК РасчетныйДокумент,
		|	ДанныеРегистра.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
		|	ДанныеРегистра.ДатаВозникновения КАК ДатаВозникновения,
		|	ДанныеРегистра.УдалитьОбъектРасчетов КАК УдалитьОбъектРасчетов,
		|	ДанныеРегистра.Предоплата КАК Предоплата,
		|	ДанныеРегистра.ПредоплатаРегл КАК ПредоплатаРегл,
		|	ДанныеРегистра.ПредоплатаУпр КАК ПредоплатаУпр,
		|	ДанныеРегистра.Долг КАК Долг,
		|	ДанныеРегистра.ДолгРегл КАК ДолгРегл,
		|	ДанныеРегистра.ДолгУпр КАК ДолгУпр,
		|	ДанныеРегистра.ПорядокЗачета КАК ПорядокЗачета,
		|	ДанныеРегистра.ПорядокОперации КАК ПорядокОперации,
		|	ДанныеРегистра.ВалютаДокумента КАК ВалютаДокумента,
		|	ДанныеРегистра.СвязанныйДокумент КАК СвязанныйДокумент,
		|	ДанныеРегистра.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДанныеРегистра.УдалитьКорОбъектРасчетов КАК УдалитьКорОбъектРасчетов,
		|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ДанныеРегистра.Сторно КАК Сторно,
		|	ДанныеРегистра.ДокументРегистратор КАК ДокументРегистратор,
		|	ДанныеРегистра.КорАналитикаУчетаПоПартнерам КАК КорАналитикаУчетаПоПартнерам,
		|	ДанныеРегистра.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|	ДанныеРегистра.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|	ДанныеРегистра.МоментВремени КАК МоментВремени,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.КорОбъектРасчетов.Объект = НЕОПРЕДЕЛЕНО
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДанныеРегистра.КорОбъектРасчетов
		|	КОНЕЦ КАК КорОбъектРасчетов
		|ПОМЕСТИТЬ ВТДанныеРегистра
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.Регистратор В (&Регистраторы)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРегистра.Период,
		|	ДанныеРегистра.Регистратор,
		|	ДанныеРегистра.Активность,
		|	ДанныеРегистра.ВидДвижения,
		|	ДанныеРегистра.АналитикаУчетаПоПартнерам,
		|	ДанныеРегистра.ОбъектРасчетов,
		|	ДанныеРегистра.Валюта,
		|	ДанныеРегистра.РасчетныйДокумент,
		|	ДанныеРегистра.ДатаПлановогоПогашения,
		|	ДанныеРегистра.ДатаВозникновения,
		|	ДанныеРегистра.УдалитьОбъектРасчетов,
		|	ДанныеРегистра.Предоплата,
		|	ДанныеРегистра.ПредоплатаРегл,
		|	ДанныеРегистра.ПредоплатаУпр,
		|	ДанныеРегистра.Долг,
		|	ДанныеРегистра.ДолгРегл,
		|	ДанныеРегистра.ДолгУпр,
		|	ДанныеРегистра.ПорядокЗачета,
		|	ДанныеРегистра.ПорядокОперации,
		|	ДанныеРегистра.ВалютаДокумента,
		|	ДанныеРегистра.СвязанныйДокумент,
		|	ДанныеРегистра.СтатьяДвиженияДенежныхСредств,
		|	ДанныеРегистра.УдалитьКорОбъектРасчетов,
		|	ДанныеРегистра.ХозяйственнаяОперация,
		|	ДанныеРегистра.Сторно,
		|	ДанныеРегистра.ДокументРегистратор,
		|	ДанныеРегистра.КорАналитикаУчетаПоПартнерам,
		|	ДанныеРегистра.НастройкаХозяйственнойОперации,
		|	ДанныеРегистра.ИдентификаторФинЗаписи,
		|	ДанныеРегистра.МоментВремени,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.КорОбъектРасчетов.Объект = НЕОПРЕДЕЛЕНО
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ ДанныеРегистра.КорОбъектРасчетов
		|	КОНЕЦ
		|;
        |
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.ОбъектРасчетов <> &ОбъектРасчетовПустаяСсылка
		|			ТОГДА ДанныеРегистра.ОбъектРасчетов
		|		ИНАЧЕ ЕСТЬNULL(ОбъектыРасчетов.Ссылка, &ОбъектРасчетовПустаяСсылка)
		|	КОНЕЦ КАК ОбъектРасчетов,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.КорОбъектРасчетов <> &ОбъектРасчетовПустаяСсылка
		|			ТОГДА ДанныеРегистра.КорОбъектРасчетов
		|		ИНАЧЕ ЕСТЬNULL(КорОбъектыРасчетов.Ссылка, &ОбъектРасчетовПустаяСсылка)
		|	КОНЕЦ КАК КорОбъектРасчетов,
		|	&ДополнительныеПоля КАК ДополнительныеПоля,
		|	ДанныеРегистра.Регистратор КАК Регистратор,
		|	ДанныеРегистра.УдалитьОбъектРасчетов КАК ИсточникОбъектаРасчетов
		|ИЗ
		|	ВТДанныеРегистра КАК ДанныеРегистра
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|			ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
		|			
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК Договора
		|			ПО Договора.Ссылка = Аналитика.Договор
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КорАналитика
		|			ПО ДанныеРегистра.КорАналитикаУчетаПоПартнерам = КорАналитика.Ссылка
		|			
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК КорДоговора
		|			ПО КорДоговора.Ссылка = КорАналитика.Договор
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|			ПО ДанныеРегистра.УдалитьОбъектРасчетов 				= ОбъектыРасчетов.Объект
		|				И ОбъектыРасчетов.ТипРасчетов						= ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|				И ОбъектыРасчетов.Организация						= Аналитика.Организация
		|				И ОбъектыРасчетов.Контрагент						= Аналитика.Контрагент
		|				И ОбъектыРасчетов.ВалютаВзаиморасчетов				= ДанныеРегистра.Валюта
		|				И (ВЫБОР
		|					КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.УдалитьОбъектРасчетов) = ТИП(Справочник.ДоговорыКонтрагентов)
		|							И Договора.РазрешенаРаботаСДочернимиПартнерами
		|						ТОГДА Договора.Партнер
		|					ИНАЧЕ Аналитика.Партнер
		|				КОНЕЦ = ОбъектыРасчетов.Партнер)
		|			
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК КорОбъектыРасчетов
		|			ПО ДанныеРегистра.УдалитьКорОбъектРасчетов				= КорОбъектыРасчетов.Объект
		|				И КорОбъектыРасчетов.ТипРасчетов					= ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|				И КорОбъектыРасчетов.Организация					= ЕСТЬNULL(КорАналитика.Организация, Аналитика.Организация)
		|				И КорОбъектыРасчетов.Контрагент						= ЕСТЬNULL(КорАналитика.Контрагент, Аналитика.Контрагент)
		|				И КорОбъектыРасчетов.Партнер						= ЕСТЬNULL(КорАналитика.Партнер, Аналитика.Партнер)
		|				И КорОбъектыРасчетов.Договор						= ЕСТЬNULL(КорАналитика.Договор, Аналитика.Договор)
		|				И КорОбъектыРасчетов.ВалютаВзаиморасчетов			= ДанныеРегистра.Валюта
		|				И НЕ ДанныеРегистра.УдалитьКорОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
		|ГДЕ
		|	ДанныеРегистра.Регистратор В (&Регистраторы)
		|	И НЕ ДанныеРегистра.УдалитьОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.ОбъектРасчетов <> &ОбъектРасчетовПустаяСсылка
		|			ТОГДА ДанныеРегистра.ОбъектРасчетов
		|		ИНАЧЕ ЕСТЬNULL(ОбъектыРасчетов.Ссылка, &ОбъектРасчетовПустаяСсылка)
		|	КОНЕЦ КАК ОбъектРасчетов,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.КорОбъектРасчетов <> &ОбъектРасчетовПустаяСсылка
		|			ТОГДА ДанныеРегистра.КорОбъектРасчетов
		|		ИНАЧЕ ЕСТЬNULL(КорОбъектыРасчетов.Ссылка, &ОбъектРасчетовПустаяСсылка)
		|	КОНЕЦ КАК КорОбъектРасчетов,
		|	&ДополнительныеПоля КАК ДополнительныеПоля,
		|	ДанныеРегистра.Регистратор КАК Регистратор,
		|	ДанныеРегистра.УдалитьОбъектРасчетов КАК ИсточникОбъектаРасчетов
		|ИЗ
		|	ВТДанныеРегистра КАК ДанныеРегистра
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Аналитика
		|			ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.Ссылка
		|			
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК КорАналитика
		|			ПО ДанныеРегистра.КорАналитикаУчетаПоПартнерам = КорАналитика.Ссылка
		|			
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК КорДоговора
		|			ПО КорДоговора.Ссылка = КорАналитика.Договор
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|			ПО ОбъектыРасчетов.ТипРасчетов								= ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|				И ОбъектыРасчетов.Организация							= Аналитика.Организация
		|				И ОбъектыРасчетов.Контрагент							= Аналитика.Контрагент
		|				И ОбъектыРасчетов.Договор								= Аналитика.Договор
		|				И ОбъектыРасчетов.НаправлениеДеятельности				= Аналитика.НаправлениеДеятельности
		|				И ОбъектыРасчетов.Партнер								= Аналитика.Партнер
		|				И ОбъектыРасчетов.Объект								= НЕОПРЕДЕЛЕНО
		|				И ОбъектыРасчетов.ВалютаВзаиморасчетов					= ДанныеРегистра.Валюта
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК КорОбъектыРасчетов
		|			ПО ДанныеРегистра.УдалитьКорОбъектРасчетов = КорОбъектыРасчетов.Объект
		|				И КорОбъектыРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|				И КорОбъектыРасчетов.Организация						= ЕСТЬNULL(КорАналитика.Организация, Аналитика.Организация)
		|				И КорОбъектыРасчетов.Контрагент 						= ЕСТЬNULL(КорАналитика.Контрагент, Аналитика.Контрагент)
		|				И КорОбъектыРасчетов.Партнер							= 
		|				ЕСТЬNULL(ВЫБОР
		|						КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.УдалитьОбъектРасчетов) = ТИП(Справочник.ДоговорыКонтрагентов)
		|								И КорДоговора.РазрешенаРаботаСДочернимиПартнерами
		|							ТОГДА КорДоговора.Партнер
		|						ИНАЧЕ КорАналитика.Партнер
		|					КОНЕЦ, Аналитика.Партнер)
		|				И КорОбъектыРасчетов.Договор							= ЕСТЬNULL(КорАналитика.Договор, Аналитика.Договор)
		|				И КорОбъектыРасчетов.ВалютаВзаиморасчетов 				= ДанныеРегистра.Валюта
		|				И НЕ ДанныеРегистра.УдалитьКорОбъектРасчетов В (&ПустыеЗначенияОбъектовРасчетов)
		|ГДЕ
		|	ДанныеРегистра.Регистратор В (&Регистраторы)
		|	И ДанныеРегистра.УдалитьОбъектРасчетов В(&ПустыеЗначенияОбъектовРасчетов)
		|ИТОГИ ПО 
		|	Регистратор";
	
	Запрос.УстановитьПараметр("ПустыеЗначенияОбъектовРасчетов", ОбъектыРасчетовСервер.ПустыеЗначенияОбъектРасчетов());
	Запрос.УстановитьПараметр("ПустыеЗначенияКонтрагент", ОбъектыРасчетовСервер.ПустыеЗначенияКонтрагент());
	Запрос.УстановитьПараметр("ОбъектРасчетовПустаяСсылка", Справочники.ОбъектыРасчетов.ПустаяСсылка());
	ОбъектыРасчетовСервер.ДополнитьЗапросПрочимиПолями(Запрос, МетаданныеРегистра);
	
	ВсеОбъектыРасчетовСгенерированы = ОбъектыРасчетовСервер.ВсеОбъектыРасчетовСгенерированы(Параметры.Очередь);
	
	Для Каждого ПорцияДанных Из ОбновляемыеДанные Цикл
	
		НачатьТранзакцию();
		
		Попытка
			ОбрабатываемыйДокумент = ПорцияДанных.Регистратор;
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ОбрабатываемыйДокумент);
			Блокировка.Заблокировать();
			
			Запрос.УстановитьПараметр("Регистраторы", ОбрабатываемыйДокумент);
			РезультатЗапроса = Запрос.Выполнить();
				
			ВыборкаРегистратор = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаРегистратор.Следующий() Цикл
				ВыборкаДетальныеЗаписи = ВыборкаРегистратор.Выбрать();
				НаборЗаписей = СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаРегистратор.Регистратор);
				ЕстьОшибкаЗапонения = Ложь;
				
				Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
					НоваяСтрокаНабора = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаНабора, ВыборкаДетальныеЗаписи);
					Если ВыборкаДетальныеЗаписи.ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПустаяСсылка() Тогда
						Если ОбъектыРасчетовСервер.ЕстьБитыеСсылкиВИсточникеОбъектаРасчетов(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов)
							ИЛИ ОбъектыРасчетовСервер.ЕстьБитыеСсылкиВИсточникеОбъектаРасчетов(ВыборкаДетальныеЗаписи.АналитикаУчетаПоПартнерам) Тогда
							ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Не удалось заполнить объект расчетов в наборе записей регистра накопления %1, строка № %2
								|в источнике данных объекта расчетов обнаружена ссылка на несуществующий элемент.'"),
								ПолноеИмяРегистра,
								ВыборкаРегистратор.Регистратор);
							ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
								УровеньЖурналаРегистрации.Ошибка,
								МетаданныеРегистра,
								,
								ТекстСообщения);
						ИначеЕсли НоваяСтрокаНабора.АналитикаУчетаПоПартнерам = Справочники.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка()
								И Не ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов) Тогда
								ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Не удалось заполнить объект расчетов наборе записей регистра накопления %1, строка № %2
									|Не заполнена аналитика учета по партнерам.'"),
									ВыборкаРегистратор.Регистратор,
									ВыборкаДетальныеЗаписи.НомерСтроки);
								ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Ошибка,
									МетаданныеРегистра,
									,
									ТекстСообщения);
						Иначе
							Если ОбновлениеИнформационнойБазы.ОбъектОбработан(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов).Обработан Тогда
								Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов) Тогда
									ОбъектРасчетов = Неопределено;
									Если Не ТипЗнч(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов) = Тип("СправочникСсылка.КлючиАналитикиУчетаПоПартнерам") Тогда
										ОбъектыРасчетовСервер.ДогенерироватьОбъектыРасчетов(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов);
										
										ДопПараметрыПоиска 						= ОбъектыРасчетовСервер.ДополнительныеКритерииПоиска();
										Аналитика 								= ВыборкаДетальныеЗаписи.АналитикаУчетаПоПартнерам;
										ДопПараметрыПоиска.Контрагент 			= Аналитика.Контрагент;
										ДопПараметрыПоиска.Партнер				= Аналитика.Партнер;
										ДопПараметрыПоиска.ВалютаВзаиморасчетов = ВыборкаДетальныеЗаписи.Валюта;
										ДопПараметрыПоиска.ОбновлениеИБ			= Истина;
										
										ОбъектРасчетов = ОбъектыРасчетовСервер.ПолучитьОбъектРасчетовПоСсылке(
											ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов,
											Аналитика.Организация,
											Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком,
											ДопПараметрыПоиска);
									ИначеЕсли Не ВсеОбъектыРасчетовСгенерированы Тогда 
										ЕстьОшибкаЗапонения = Истина;
										Прервать;
									КонецЕсли;
									
									Если ЗначениеЗаполнено(ОбъектРасчетов) Тогда
										НоваяСтрокаНабора.ОбъектРасчетов = ОбъектРасчетов;
									Иначе
										ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'Не удалось заполнить объект расчетов наборе записей регистра накопления %1, 
											|по источнику %2.'"),
											ПолноеИмяРегистра,
											ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов);
										ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
											УровеньЖурналаРегистрации.Ошибка,
											МетаданныеРегистра,
											,
											ТекстСообщения);
									КонецЕсли;
								КонецЕсли;
							Иначе
								ЕстьОшибкаЗапонения = Истина;
								Прервать;
							КонецЕсли;
						КонецЕсли;
						
						Если Не ЗначениеЗаполнено(НоваяСтрокаНабора.ОбъектРасчетов)
								И Не ОбъектыРасчетовСервер.ЕстьБитыеСсылкиВИсточникеОбъектаРасчетов(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов)
								И Не ОбъектыРасчетовСервер.ЕстьБитыеСсылкиВИсточникеОбъектаРасчетов(ВыборкаДетальныеЗаписи.АналитикаУчетаПоПартнерам) Тогда
									ЕстьОшибкаЗапонения = Истина;
									Прервать;
						КонецЕсли;
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(НоваяСтрокаНабора.ОбъектРасчетов)
						И Не ОбъектыРасчетовСервер.ЕстьБитыеСсылкиВИсточникеОбъектаРасчетов(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов)
						И Не ОбъектыРасчетовСервер.ЕстьБитыеСсылкиВИсточникеОбъектаРасчетов(ВыборкаДетальныеЗаписи.АналитикаУчетаПоПартнерам)
						И ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ИсточникОбъектаРасчетов) Тогда
							ЕстьОшибкаЗапонения = Истина;
							Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если ЕстьОшибкаЗапонения Тогда
					Продолжить;
				КонецЕсли;
				
				Если НаборЗаписей.Модифицированность() Тогда
					ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				Иначе
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
				КонецЕсли;
			
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
		
			ОтменитьТранзакцию();
			
			Шаблон = НСтр("ru = 'Не удалось записать данные в регистр %1 , по причине: %2'");
			ТекстСообщения = СтрШаблон(Шаблон,
				ПолноеИмяРегистра,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеРегистра,
				,
				ТекстСообщения);
		
		КонецПопытки;
	
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена =  НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
	Если Параметры.ОбработкаЗавершена Тогда
		ОперативныеВзаиморасчетыСервер.ПослеОбновленияРегистровВзаиморасчетов();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли