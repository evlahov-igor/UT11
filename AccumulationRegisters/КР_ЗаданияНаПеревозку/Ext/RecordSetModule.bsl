
////////////////////////////////////////////////////
//// Модуль набора записей регистра накопления "КР_ЗаданияНаПеревозку"
//// Создан: 19.10.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-376
//// Разработка по ФДР 21.001 Доработка документа "Задание на перевозку"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// << 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524
Процедура ПередЗаписью(Отказ, Замещение)
	
	ОбщегоНазначенияУТ.СвернутьНаборЗаписей(ЭтотОбъект);
	
	Если ОбменДанными.Загрузка Или 
		Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
				
	// Текущее состояние набора помещается во временную таблицу "ДвиженияПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый", ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	Т.Отправитель КАК Отправитель,
	|	Т.Получатель КАК Получатель,
	|	Т.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Т.ДатаОтгрузки КАК ДатаОтгрузки,
	|	ВЫБОР
	|		КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Т.КВыполнению
	|		ИНАЧЕ -Т.КВыполнению
	|	КОНЕЦ КАК КВыполнению
	|ПОМЕСТИТЬ ДвиженияПередЗаписью
	|ИЗ
	|	РегистрНакопления.КР_ЗаданияНаПеревозку КАК Т
	|ГДЕ
	|	Т.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	
	Запрос.Выполнить();
	
КонецПроцедуры // >> 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524

// << 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка 
		Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	ТаблицаИзменений.Отправитель КАК Отправитель,
	|	ТаблицаИзменений.Получатель КАК Получатель,
	|	ТаблицаИзменений.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ТаблицаИзменений.ДатаОтгрузки КАК ДатаОтгрузки,
	|	СУММА(ТаблицаИзменений.КВыполнению) КАК КВыполнению
	|ПОМЕСТИТЬ КР_ДвиженияЗаданияНаПеревозкуИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|		Т.Отправитель КАК Отправитель,
	|		Т.Получатель КАК Получатель,
	|		Т.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|		Т.ДатаОтгрузки КАК ДатаОтгрузки,
	|		Т.КВыполнению КАК КВыполнению
	|	ИЗ
	|		ДвиженияПередЗаписью КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.ЗаданиеНаПеревозку,
	|		Т.Отправитель,
	|		Т.Получатель,
	|		Т.ИдентификаторСтроки,
	|		Т.ДатаОтгрузки,
	|		-ВЫБОР
	|			КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА Т.КВыполнению
	|			ИНАЧЕ -Т.КВыполнению
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.КР_ЗаданияНаПеревозку КАК Т
	|	ГДЕ
	|		Т.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ЗаданиеНаПеревозку,
	|	ТаблицаИзменений.Отправитель,
	|	ТаблицаИзменений.Получатель,
	|	ТаблицаИзменений.ИдентификаторСтроки,
	|	ТаблицаИзменений.ДатаОтгрузки
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КВыполнению) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияПередЗаписью";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет()[0]; // РезультатЗапроса
	Выборка = РезультатЗапроса.Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"КР_ДвиженияЗаданияНаПеревозкуИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры // >> 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524

#КонецОбласти

#КонецЕсли