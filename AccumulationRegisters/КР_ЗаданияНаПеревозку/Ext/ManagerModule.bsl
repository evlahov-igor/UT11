
////////////////////////////////////////////////////
//// Модуль менеджера регистра накопления "КР_РасходыНаПеревозку"
//// Создан: 19.10.2022, Маскаев П.Ю., КРОК, JIRA№ A2105505-376
//// Разработка по ФДР 21.001 Доработка документа "Задание на перевозку"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.КР_ЗаданияНаПеревозку);
		
	КонецЕсли;    
	
	// << 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524
	// Контроль
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		
		Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.КР_ЗаданияНаПеревозку);

	КонецЕсли;
	// >> 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524
	
	Возврат Параметры;
	
КонецФункции     

// << 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524  
// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	#Область КР_ЗаданияНаПеревозку

	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "КР_ДвиженияЗаданияНаПеревозкуИзменение") Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Т.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
		|	Т.Отправитель КАК Отправитель,
		|	Т.Получатель КАК Получатель,
		|	Т.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Т.ДатаОтгрузки КАК ДатаОтгрузки,
		|	Т.КВыполнениюОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.КР_ЗаданияНаПеревозку.Остатки(
		|			,
		|			(ЗаданиеНаПеревозку, Отправитель, Получатель, ИдентификаторСтроки, ДатаОтгрузки) В
		|				(ВЫБРАТЬ
		|					Т.ЗаданиеНаПеревозку,
		|					Т.Отправитель,
		|					Т.Получатель,
		|					Т.ИдентификаторСтроки,
		|					Т.ДатаОтгрузки
		|				ИЗ
		|					КР_ДвиженияЗаданияНаПеревозкуИзменение КАК Т)) КАК Т
		|ГДЕ
		|	Т.КВыполнениюОстаток < 0";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиЗаданияНаПеревозку");
		
	КонецЕсли;

	#КонецОбласти

КонецПроцедуры // >> 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524

// << 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524
// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	
	#Область КР_ЗаданияНаПеревозку
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "КР_ДвиженияЗаданияНаПеревозкуИзменение") Тогда
		
		ШаблонСообщения = НСтр("ru = 'По документу %1 "
			+ "(направление ""%2"" - ""%3"", дата отгрузки %4, идентификатор ""%5"") превышен остаток на %6 шт'");	
			
		Для каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиЗаданияНаПеревозку Цикл
			
			
			ТекстСообщения = СтрШаблон(ШаблонСообщения, 
				СтрокаОшибки.ЗаданиеНаПеревозку, СтрокаОшибки.Отправитель, СтрокаОшибки.Получатель,
				Формат(СтрокаОшибки.ДатаОтгрузки, "ДФ=dd.MM.yyyy"), СтрокаОшибки.ИдентификаторСтроки,
				-СтрокаОшибки.Количество);
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Документ, , , Отказ);
						
		КонецЦикла;
		
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры // >> 06.04.2023 Марченко С.Н., КРОК, JIRA№A2105505-1524

// Процедура формирования движений по регистру.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "КР_ЗаданияНаПеревозку");
	
КонецПроцедуры

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие - Соответствие полного имени регистра тексту запроса сторнирования
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ТекстыЗапросов = Новый Соответствие();
	
	Возврат ТекстыЗапросов;
	
КонецФункции

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие();
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
