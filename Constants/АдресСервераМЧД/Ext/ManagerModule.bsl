#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает в константу адрес сервера.
// 
// Параметры:
//  СтруктураАдреса - Структура см. МашиночитаемыеДоверенности.СтруктураАдресаСервераМЧД
//  
Процедура ЗаписатьАдрес(СтруктураАдреса) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, СтруктураАдреса);
	АдресСервераСтрокой = ЗаписьJSON.Закрыть();
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.АдресСервераМЧД.Установить(АдресСервераСтрокой);
	УстановитьПривилегированныйРежим(Ложь);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

// Читает из константы адрес сервера МЧД
// 
// Возвращаемое значение:
//  СтруктураАдреса - Структура см. МашиночитаемыеДоверенности.СтруктураАдресаСервераМЧД
//  
Функция ПрочитатьАдрес() Экспорт

	СтруктураАдреса = МашиночитаемыеДоверенности.СтруктураАдресаСервераМЧД();
	АдресСервера = Константы.АдресСервераМЧД.Получить();
	
	Если ПустаяСтрока(АдресСервера) Тогда
		ЗаписатьАдрес(СтруктураАдреса);
	Иначе
		
		Попытка
			
			ЧтениеJSON = Новый ЧтениеJSON;
			ЧтениеJSON.УстановитьСтроку(АдресСервера);
			РезультатЧтения = ПрочитатьJSON(ЧтениеJSON);
			 			 
			Для Каждого ЭлементСтруктуры Из СтруктураАдреса Цикл
				ИмяПоля = ЭлементСтруктуры.Ключ;
				Если РезультатЧтения.Свойство(ИмяПоля) Тогда
					СтруктураАдреса[ИмяПоля] = РезультатЧтения[ИмяПоля];
				КонецЕсли;
			КонецЦикла;
			
		Исключение
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Чтение настроек сервера МЧД'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЕсли;

	Возврат СтруктураАдреса;
	
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработкаСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ВыбраннаяФорма = "ОбщаяФорма.АдресСервераМЧД";
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти
