
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Отбор.Свойство("Владелец") Тогда
		
		ВладелецОтбор = Параметры.Отбор.Владелец;
		Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
		Элементы.ВладелецОтбор.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецОтборПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Владелец", ВладелецОтбор, , ,
																			ЗначениеЗаполнено(ВладелецОтбор));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	СтандартнаяОбработка = Ложь;
	
	Для Каждого СтрокаМассива Из ПараметрыПеретаскивания.Значение Цикл
		
		Если ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение Тогда
			ПеренестиИсточникВПриемник(СтрокаМассива, Строка);
		Иначе
			СкопироватьИсточникВПриемник(СтрокаМассива, Строка);
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет проверку перемещения и перемещение элементов в справочнике.
//
// Параметры:
//	Источник - СправочникСсылка.ЭквайринговыеТерминалы - перемещаемый элемент справочника.
//	Приемник - СправочникСсылка.ЭквайринговыеТерминалы - элемент (группа) справочника, в который выполняется перемещение
// источника.
//
&НаСервереБезКонтекста
Процедура ПеренестиИсточникВПриемник(Источник, Приемник)
	
	Если ЗначениеЗаполнено(Приемник.Владелец)
		И Приемник.Владелец <> Источник.Владелец Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Невозможно переместить элемент %1:
							|	договор эквайринга группы не соответствует перемещаемому элементу'"), Строка(Источник.Наименование));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Источник);
		
	Иначе
		
		Если Источник.Родитель <> Приемник Тогда
			
			ИсточникОбъект = Источник.ПолучитьОбъект();
			ИсточникОбъект.Родитель = Приемник;
			ИсточникОбъект.Записать();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Выполняет копирование элементов в справочнике. В случае, если источник и приемник подченены различным договорам, то 
// новый элемент создается с владельцем по приемнику.
//
// Параметры:
//	Источник - СправочникСсылка.ЭквайринговыеТерминалы - копируемый элемент справочника.
//	Приемник - СправочникСсылка.ЭквайринговыеТерминалы - элемент (группа) справочника, в который выполняется копирование
// источника.
//
&НаСервереБезКонтекста
Процедура СкопироватьИсточникВПриемник(Источник, Приемник)
	
	НовыйЭлемент = Справочники.ЭквайринговыеТерминалы.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовыйЭлемент, Источник, , "Родитель, Владелец");
	НовыйЭлемент.Родитель = Приемник;
	НовыйЭлемент.Владелец = Приемник.Владелец;
	НовыйЭлемент.Записать();
	
КонецПроцедуры

#КонецОбласти