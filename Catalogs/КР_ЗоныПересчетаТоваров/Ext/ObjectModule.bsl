
////////////////////////////////////////////////////
//// Модуль объекта справочника "КР_ЗоныПересчетаТоваров"
//// Создан: 14.04.2023, Маскаев П.Ю., КРОК, JIRA№ A2105505-1449
//// Разработка по ФДР С11.009, Документы инвентаризации товаров

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Помещение) Тогда
		РеквизитыПомещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Помещение, "НастройкаАдресногоХранения, КР_ТипСкладскогоПомещения");
		Если Не РеквизитыПомещения.НастройкаАдресногоХранения = Перечисления.НастройкиАдресногоХранения.ЯчейкиОстатки Тогда
			Ячейки.Очистить();
		КонецЕсли;
		Если Не РеквизитыПомещения.КР_ТипСкладскогоПомещения = Перечисления.КР_ТипыСкладскихПомещений.ТорговыйЗал Тогда
			МестаРазмещения.Очистить();
		КонецЕсли;
		злУдалитьДублирующиеСтроки();						// #4558.. Фомин Д.Ю. 21.08.2024.
	Иначе
		Ячейки.Очистить();
		МестаРазмещения.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ЭтоГруппа Тогда
		
		ИспользоватьСкладскиеПомещения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ИспользоватьСкладскиеПомещения");
		Если Не ИспользоватьСкладскиеПомещения = Истина Тогда
			Помещение = Справочники.СкладскиеПомещения.ПустаяСсылка();
			МассивНепроверяемыхРеквизитов.Добавить("Помещение");
		КонецЕсли;

		// << 06.08.2024 Петухов А.В., Фактор, #4526
		мсвЯчейки = Новый Массив;
		Для Каждого СтрокаЯчейки Из Ячейки Цикл
			Если ЗначениеЗаполнено(СтрокаЯчейки.Ячейка) И мсвЯчейки.Найти(СтрокаЯчейки.Ячейка) = Неопределено Тогда
				мсвЯчейки.Добавить(СтрокаЯчейки.Ячейка);
			КонецЕсли;
		КонецЦикла;
		мсвМестаРазмещения = Новый Массив;
		Для Каждого СтрокаМестаРазмещения Из МестаРазмещения Цикл
			Если ЗначениеЗаполнено(СтрокаМестаРазмещения.МестоРазмещения) И мсвМестаРазмещения.Найти(СтрокаМестаРазмещения.МестоРазмещения) = Неопределено Тогда
				мсвМестаРазмещения.Добавить(СтрокаМестаРазмещения.МестоРазмещения);
			КонецЕсли;
		КонецЦикла;
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КР_ЗоныПересчетаТоваровЯчейки.Ссылка КАК Зона,
			|	КР_ЗоныПересчетаТоваровЯчейки.Ячейка КАК Ячейка
			|ИЗ
			|	Справочник.КР_ЗоныПересчетаТоваров.Ячейки КАК КР_ЗоныПересчетаТоваровЯчейки
			|ГДЕ
			|	КР_ЗоныПересчетаТоваровЯчейки.Ячейка В(&Ячейки)
			|	И КР_ЗоныПересчетаТоваровЯчейки.Ссылка <> &Ссылка
			|ИТОГИ ПО
			|	Ячейка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КР_ЗоныПересчетаМестаРазмещения.Ссылка КАК Зона,
			|	КР_ЗоныПересчетаМестаРазмещения.МестоРазмещения КАК МестоРазмещения
			|ИЗ
			|	Справочник.КР_ЗоныПересчетаТоваров.МестаРазмещения КАК КР_ЗоныПересчетаМестаРазмещения
			|ГДЕ
			|	КР_ЗоныПересчетаМестаРазмещения.МестоРазмещения В(&МестаРазмещения)
			|	И КР_ЗоныПересчетаМестаРазмещения.Ссылка <> &Ссылка
			|ИТОГИ ПО
			|	МестоРазмещения";
		
		Запрос.УстановитьПараметр("Ячейки", мсвЯчейки);
		Запрос.УстановитьПараметр("МестаРазмещения", мсвМестаРазмещения);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		Если Не РезультатЗапроса[0].Пустой() Тогда
			Отказ = Истина;
			СтрокаОшибкиЕдЧисло = "Ячейка %1 уже находится в зоне %2.";
			СтрокаОшибкиМнЧисло = "Ячейка %1 уже находится в зонах %2.";
			мсвЗоны = Новый Массив;
			ВыборкаЯчейки = РезультатЗапроса[0].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Ячейка");
			Пока ВыборкаЯчейки.Следующий() Цикл
				Выборка = ВыборкаЯчейки.Выбрать();
				мсвЗоны.Очистить();
				Пока Выборка.Следующий() Цикл
					мсвЗоны.Добавить(Выборка.Зона);
				КонецЦикла;
				Если мсвЗоны.Количество() = 1 Тогда
					СтрокаСообщения = СтрШаблон(СтрокаОшибкиЕдЧисло, ВыборкаЯчейки.Ячейка, мсвЗоны[0]);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, мсвЗоны[0]);
				ИначеЕсли мсвЗоны.Количество() > 1 Тогда
					СтрокаСообщения = СтрШаблон(СтрокаОшибкиМнЧисло, ВыборкаЯчейки.Ячейка, СтрСоединить(мсвЗоны,", "));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;

		Если Не РезультатЗапроса[1].Пустой() Тогда
			Отказ = Истина;
			СтрокаОшибкиЕдЧисло = "Место размещения %1 уже находится в зоне %2.";
			СтрокаОшибкиМнЧисло = "Место размещения %1 уже находится в зонах %2.";
			мсвЗоны = Новый Массив;
			ВыборкаМестаРазмещения = РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "МестоРазмещения");
			Пока ВыборкаМестаРазмещения.Следующий() Цикл
				Выборка = ВыборкаМестаРазмещения.Выбрать();
				мсвЗоны.Очистить();
				Пока Выборка.Следующий() Цикл
					мсвЗоны.Добавить(Выборка.Зона);
				КонецЦикла;
				Если мсвЗоны.Количество() = 1 Тогда
					СтрокаСообщения = СтрШаблон(СтрокаОшибкиЕдЧисло, ВыборкаМестаРазмещения.МестоРазмещения, мсвЗоны[0]);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения, мсвЗоны[0]);
				ИначеЕсли мсвЗоны.Количество() > 1 Тогда
					СтрокаСообщения = СтрШаблон(СтрокаОшибкиМнЧисло, ВыборкаМестаРазмещения.МестоРазмещения, СтрСоединить(мсвЗоны,", "));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		// >> 06.08.2024 Петухов А.В., Фактор, #4526
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// #4558.. Фомин Д.Ю. 21.08.2024.
Процедура злУдалитьДублирующиеСтроки()          
	
	Если Ячейки.Количество() > 0 Тогда
		ЭтоЯчейки = Истина;
		тзТабличнаяЧасть = Ячейки.Выгрузить();
		ИменаКолонок = "Ячейка";
		ДляВывода = "ячейками";
	ИначеЕсли МестаРазмещения.Количество() > 0 Тогда	
		ЭтоЯчейки = Ложь;
		тзТабличнаяЧасть = МестаРазмещения.Выгрузить();
		ИменаКолонок = "МестоРазмещения";
		ДляВывода = "местами размещения";
	Иначе
		Возврат;
	КонецЕсли;	
		
	НачальноеКоличество = тзТабличнаяЧасть.Количество();
	тзТабличнаяЧасть.Свернуть(ИменаКолонок);
	КоличествоУдаленныхСтрок = НачальноеКоличество - тзТабличнаяЧасть.Количество();
	
	Если КоличествоУдаленныхСтрок > 0 Тогда
		
		Если ЭтоЯчейки Тогда
			Ячейки.Загрузить(тзТабличнаяЧасть);
		Иначе	
			МестаРазмещения.Загрузить(тзТабличнаяЧасть);
		КонецЕсли;
		
		ТекстСообщения = "Количество удаленных строк с повторяющимися " + ДляВывода + ": " + Формат(КоличествоУдаленныхСтрок, "ЧЦ=10; ЧДЦ=0");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект);
			
	КонецЕсли;	
	
КонецПроцедуры	
// ..#4558

#КонецОбласти

#КонецЕсли
