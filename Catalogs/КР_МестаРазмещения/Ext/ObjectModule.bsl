////////////////////////////////////////////////////
//// Модуль объекта справочника "КР_МестаРазмещения"
//// Создан: 25.10.2022 Марченко С.Н., КРОК, JIRA№A2105505-710
//// Разработка по ФДР С31.006, Разработка справочника "Места размещения"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	МассивНепроверяемыхРеквизитов.Добавить("Наименование");
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
		ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов); 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьНаименование();

	ПроверитьНаУникальность(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьНаименование()

	ШаблонНаименования = НСтр("ru = '%1 %2'");  
	Наименование = СтрШаблон(ШаблонНаименования, ТипПоверхности, Номер);
	
КонецПроцедуры

Процедура ПроверитьНаУникальность(Отказ)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КР_МестаРазмещения КАК Т
	|ГДЕ
	|	Т.Ссылка <> &Ссылка
	|	И Т.Наименование = &Наименование");

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Если Не Запрос.Выполнить().Пустой() Тогда    
		
		ТекстОшибки = НСтр("ru = 'Наименование места размещения не уникально'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
