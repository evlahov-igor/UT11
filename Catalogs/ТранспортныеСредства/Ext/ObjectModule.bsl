#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Код = ВРег(Код);
	Код = СтрЗаменить(Код, " ", "");
	
	ВместимостьПредставление = "";
	ПробелВСередине = "";
	Если ЗначениеЗаполнено(ГрузоподъемностьВТоннах) Тогда
		ВместимостьПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 т'"), Строка(ГрузоподъемностьВТоннах));
		ПробелВСередине = " ";
	КонецЕсли;
	Если ЗначениеЗаполнено(ВместимостьВКубическихМетрах) Тогда
		ВместимостьПредставление = ВместимостьПредставление + ПробелВСередине
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 м3'"), Строка(ВместимостьВКубическихМетрах));
	КонецЕсли;
	
	// << 25.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-880
	КР_ПередЗаписьюДополнительно(Отказ);
	// >> 25.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-880
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.ТипыТранспортныхСредств") Тогда
		СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения,
			"ВместимостьВКубическихМетрах, ГрузоподъемностьВТоннах");
		ВместимостьВКубическихМетрах = СтруктураРеквизитов.ВместимостьВКубическихМетрах;
		ГрузоподъемностьВТоннах = СтруктураРеквизитов.ГрузоподъемностьВТоннах;
		Тип = ДанныеЗаполнения;
	КонецЕсли;
	
	// << 25.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-880
	КР_ОбработкаЗаполненияДополнительно(ДанныеЗаполнения, СтандартнаяОбработка);
	// >> 25.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-880
	
КонецПроцедуры

#КонецОбласти

#Область КР_ДобавленныеПроцедурыИФункции

#Область КР_ОбработчикиСобытий

// << 25.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-880
Процедура КР_ПередЗаписьюДополнительно(Отказ)
	
	// Использован платформенный метод РольДоступна(), т.к. роль
	// используется как маркер выполнения логики алгоритма.
	// Использование метода Пользователи.РолиДоступны() не подходит, т.к.
	// для полноправного пользователя логика не должна выполняться.
	Если РольДоступна("КР_ДобавлениеИзменениеТранспортныхСредствРозница")
		И Не КР_ТранспортРозницы Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Недоступно изменение транспорта розницы.'"), , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура КР_ОбработкаЗаполненияДополнительно(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Использован платформенный метод РольДоступна(), т.к. роль
	// используется как маркер выполнения логики алгоритма.
	// Использование метода Пользователи.РолиДоступны() не подходит, т.к.
	// для полноправного пользователя логика не должна выполняться.
	Если РольДоступна("КР_ДобавлениеИзменениеТранспортныхСредствРозница") Тогда
		КР_ТранспортРозницы = Истина;
	КонецЕсли;
	
КонецПроцедуры // >> 25.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-880

#КонецОбласти

#КонецОбласти

#КонецЕсли