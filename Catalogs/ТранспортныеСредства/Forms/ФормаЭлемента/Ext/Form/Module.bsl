
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

	// << 26.08.2022, Мельников А.А., КРОК, Jira№ A2105505-ХХХ 
    КР_ПриСозданииНаСервереДополнительно();
	// >> 26.08.2022, Мельников А.А., КРОК, Jira№ A2105505-ХХХ 
	 
КонецПроцедуры

&НаКлиенте
Процедура  ПослеЗаписи(ПараметрыЗаписи)

	Оповестить("Запись_ТранспортноеСредство",,Объект.Ссылка);
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	СформироватьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура МаркаПриИзменении(Элемент)
	СформироватьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура ТипПриИзменении(Элемент)
	Если Не ЗначениеЗаполнено(Объект.Тип) Тогда
		Возврат;
	КонецЕсли;
	
	ТипПриИзмененииСервер(Объект.Тип);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаКлиенте
Процедура СформироватьНаименование()
	
	Объект.Наименование = СокрЛП(Объект.Код) + " " + СокрЛП(Объект.Марка);
	
КонецПроцедуры

&НаСервере
Процедура ТипПриИзмененииСервер(ТипТС)
	
	СтруктураРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТипТС, "ВместимостьВКубическихМетрах, ГрузоподъемностьВТоннах");
	ЗаполнитьЗначенияСвойств(Объект, СтруктураРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область КР_ДобавленныеПроцедурыИФункции

// << 25.08.2022, Мельников А.А., КРОК, Jira№ A2105505-325 
// Для добавления новых реквизтов на форму
&НаСервере
Процедура КР_ПриСозданииНаСервереДополнительно() 
	
	// << 25.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-880
	КР_ГруппаТипВладения = КР_МетодыМодификацииФорм.ВставитьГруппуФормы(
		ЭтотОбъект, "КР_ГруппаТипВладенияТС", ЭтотОбъект, Элементы.КодМарка);
	КР_ГруппаТипВладения.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	КР_ГруппаТипВладения.Отображение = ОтображениеОбычнойГруппы.Нет;
	КР_ГруппаТипВладения.ОтображатьЗаголовок = Ложь;
	
	Если Не РольДоступна("КР_ДобавлениеИзменениеТранспортныхСредствРозница") Тогда
		КР_ТранспортРозницыЭлемент = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(
			ЭтотОбъект, "Объект.КР_ТранспортРозницы", КР_ГруппаТипВладения);
	КонецЕсли;
	// >> 25.11.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-880
	
	// << 06.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325
	КР_ОбъемПоСкладуЭлемент = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(
		ЭтотОбъект, "Объект.КР_ОбъемПоДаннымСклада", Элементы.ГрузоподъемностьВместимость);
	КР_ОбъемПоСкладуЭлемент.КнопкаВыбора = Истина;
	
	КР_ОбъемПоСкладуДекорация = КР_МетодыМодификацииФорм.ВставитьДекорациюФормы(
		ЭтотОбъект, "КР_ОбъемПоДаннымСкладаДекорация", Элементы.ГрузоподъемностьВместимость);
	КР_ОбъемПоСкладуДекорация.Заголовок = НСтр("ru = 'м3'");
	// >> 06.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325
	
КонецПроцедуры      

#КонецОбласти
