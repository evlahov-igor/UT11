///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив из Строка
//
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ИспользоватьДляФормыОбъекта");
	Результат.Добавить("ИспользоватьДляФормыСписка");
	Результат.Добавить("Ответственный");
	Результат.Добавить("Публикация");
	
	Возврат Результат;
	
КонецФункции

// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Ссылка)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СохранениеДполнительныхОбработок

// #3181.. Фомин Д.Ю. 26.09.2023.
// При записи элемента справочника ВнешниеОбработки сохранять в каталог, указанный в константе злПутьХраненияВерсийВнешнихОбработок, данные из реквизита ХранилищеОбработки.
//
// Формат имени файла: ИмяОбъекта_ДатаВремя_УникальныйИдентификатор.Расширение, где
//		Код - код элемента в справочнике
//		ДатаВремя - текущая дата в формате yyyyMMddHHmmss
//		УникальныйИдентификатор - уникальный идентификатор ссылки элемента
//
// Расширение - определяется по значению реквизита Вид:
//		если ВидОбработки = Перечисления.ВидыДополнительныхОтчетовИОбработок.ДополнительныйОтчет, тогда "erf"
//		во всех остальных случая - "epf"
//
// Если записать файл версии не удалось, то вызывать отказ записи элемента.
// Если путь сохранения не указан, то это означает, что механизм выключен и сохранять версии не нужно.
Функция злЗаписатьВерсиюВнешнейОбработки(ДопОбработка, Вид, ИмяОбъекта, ХранилищеОбработки, ПутьХраненияВерсийВнешнихОбработок) Экспорт
	
	РезультатЗаписи = Новый Структура;
	РезультатЗаписи.Вставить("Результат", Истина);
	РезультатЗаписи.Вставить("Сообщение", "");
	
	Если ЗначениеЗаполнено(ПутьХраненияВерсийВнешнихОбработок) Тогда
		
		КаталогВерсийВнешнихОбработок = Новый Файл(ПутьХраненияВерсийВнешнихОбработок);
		
		Если КаталогВерсийВнешнихОбработок.Существует() Тогда	
			
			Если Вид = Перечисления.ВидыДополнительныхОтчетовИОбработок.ДополнительныйОтчет Тогда
				Расширение = ".erf";
			Иначе	
				Расширение = ".epf";
			КонецЕсли;	
			
			ДвоичныеДанные = ХранилищеОбработки.Получить();
			
			Если ЗначениеЗаполнено(ДвоичныеДанные) Тогда
				ИмяФайлаОбработки = КаталогВерсийВнешнихОбработок.ПолноеИмя + "\" + 
				ИмяОбъекта + "_" + Формат(ТекущаяДата(), "ДФ=""yyyyMMddHHmmss""") + "_" + 
				ДопОбработка.УникальныйИдентификатор() + Расширение;
				ДвоичныеДанные.Записать(ИмяФайлаОбработки);	
			КонецЕсли;	
			
			//Проверяем записался ли файл
			ФайлВнешнейОбработки = Новый Файл(ИмяФайлаОбработки);
			Если Не ФайлВнешнейОбработки.Существует() Тогда
				РезультатЗаписи.Вставить("Результат", Ложь);	
				РезультатЗаписи.Вставить("Сообщение", "Ошибка записи файла " + ИмяФайлаОбработки + ".");
			КонецЕсли;	
		Иначе	
			РезультатЗаписи.Вставить("Результат", Ложь);
			РезультатЗаписи.Вставить("Сообщение", "Не удалось сохранить новую версию обработки: каталог хранения версий не найден или недоступен для записи.");
		КонецЕсли;
		
	КонецЕсли;	

	Возврат РезультатЗаписи;

КонецФункции
// ..#3181

#КонецОбласти

#КонецЕсли
