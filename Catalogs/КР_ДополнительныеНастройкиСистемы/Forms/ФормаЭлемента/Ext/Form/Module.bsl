
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МенеджерОбъекта = Справочники.КР_ДополнительныеНастройкиСистемы;
	
	СтруктураНастройки = МенеджерОбъекта.ПолучитьОграничениеПолейФормыПредопределенногоЭлемента(
		Объект.ИмяПредопределенныхДанных);
	Если СтруктураНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	// << 28.12.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-970
	Элементы.Значение.Видимость = СтруктураНастройки.ДоступноЗначение
		И ДоступенВыбор(СтруктураНастройки, "Значение");
	Элементы.СписокЗначений.Видимость = СтруктураНастройки.ДоступенСписокЗначений
		И ДоступенВыбор(СтруктураНастройки, "СписокЗначений");
	// >> 28.12.2022 Маскаев П.Ю., КРОК, JIRA№ A2105505-970
	
	Если СтруктураНастройки.ДоступноЗначение Тогда 
		СтруктураСвойствЗначения = СтруктураНастройки.СтруктураСвойствЗначения; 
		Для Каждого КлючИЗначение Из СтруктураСвойствЗначения Цикл
			Если КлючИЗначение.Значение <> Неопределено Тогда
				Элементы.Значение[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если СтруктураНастройки.ДоступенСписокЗначений Тогда 
		СтруктураСвойствСписка = СтруктураНастройки.СтруктураСвойствСписка; 
		Для Каждого КлючИЗначение Из СтруктураСвойствСписка Цикл
			Если КлючИЗначение.Значение <> Неопределено Тогда
				Элементы.СписокЗначенийЗначение[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// << 13.01.2023 Маскаев П.Ю., КРОК, JIRA№ A2105505-970
&НаСервереБезКонтекста
Функция ДоступенВыбор(СтруктураНастройки, ИсточникВыбора = "Значение")
	
	ФлагДоступенВыбор = Ложь;
	МДСправочника = Метаданные.Справочники.КР_ДополнительныеНастройкиСистемы;
	
	Если ИсточникВыбора = "Значение" Тогда
		СтруктураСвойств = СтруктураНастройки.СтруктураСвойствЗначения;
		РазрешенныеТипы = МДСправочника.Реквизиты.Значение.Тип;
	ИначеЕсли ИсточникВыбора = "СписокЗначений" Тогда
		СтруктураСвойств = СтруктураНастройки.СтруктураСвойствСписка;
		РазрешенныеТипы = МДСправочника.ТабличныеЧасти.СписокЗначений.Реквизиты.Значение.Тип;
	Иначе
		Возврат ФлагДоступенВыбор;
	КонецЕсли;
	
	Если СтруктураСвойств = Неопределено Тогда
		ФлагДоступенВыбор = Истина;
	Иначе
		ТипыОграничения = СтруктураСвойств.ОграничениеТипа.Типы();
		Для Каждого ТипОграничения Из ТипыОграничения Цикл
			Если РазрешенныеТипы.СодержитТип(ТипОграничения) Тогда
				ФлагДоступенВыбор = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ФлагДоступенВыбор;
	
КонецФункции // >> 13.01.2023 Маскаев П.Ю., КРОК, JIRA№ A2105505-970

#КонецОбласти
