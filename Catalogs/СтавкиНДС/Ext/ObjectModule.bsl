#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") 
		И ДанныеЗаполнения.Свойство("ТипНалогообложенияНДС") Тогда 
		НоваяСтрока = ТипыНалогообложенияНДС.Добавить();
		НоваяСтрока.ТипНалогообложенияНДС = ДанныеЗаполнения.ТипНалогообложенияНДС;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") Тогда
		Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	БлокировкаДанных = Новый БлокировкаДанных();
	ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ОсновныеСтавкиНДС"); //ЭлементБлокировкиДанных
	ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
	БлокировкаДанных.Заблокировать();
	
	БлокировкаДанных = Новый БлокировкаДанных();
	ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("Справочник.СтавкиНДС"); //ЭлементБлокировкиДанных
	ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Разделяемый;
	БлокировкаДанных.Заблокировать();
	
	// Очистим записи по данной ставке
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОсновныеСтавкиНДС.Страна КАК Страна
	|ИЗ
	|	РегистрСведений.ОсновныеСтавкиНДС КАК ОсновныеСтавкиНДС
	|ГДЕ
	|	ОсновныеСтавкиНДС.СтавкаНДС = &Ссылка
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	&Страна
	|";
	Запрос.УстановитьПараметр("Страна", Страна);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	ВыборкаСтран = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаСтран.Следующий() Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СтавкиНДС.НачалоПериода КАК Период,
		|	СтавкиНДС.Страна КАК Страна,
		|	СтавкиНДС.Ссылка КАК СтавкаНДС
		|ИЗ
		|	Справочник.СтавкиНДС КАК СтавкиНДС
		|ГДЕ
		|	СтавкиНДС.Страна = &Страна
		|	И СтавкиНДС.Основная";
		Запрос.УстановитьПараметр("Страна", ВыборкаСтран.Страна);
		ВыборкаОсновныхСтавокНДС = Запрос.Выполнить().Выбрать();
		
		НаборЗаписей = РегистрыСведений.ОсновныеСтавкиНДС.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Страна.Установить(ВыборкаСтран.Страна);
		Пока ВыборкаОсновныхСтавокНДС.Следующий() Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаОсновныхСтавокНДС);
			Если Не ЗначениеЗаполнено(НоваяЗапись.Период) Тогда
				НоваяЗапись.Период = Дата("19800101");
			КонецЕсли;
		КонецЦикла;
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода) Тогда
		Если НачалоПериода >= КонецПериода Тогда
			ТекстОшибки = НСтр("ru = 'Дата окончания периода действия ставки НДС должна быть больше даты начала.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка, "КонецПериода",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если Ссылка = Справочники.СтавкиНДС.БезНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить(Метаданные().Реквизиты.Страна.Имя);
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриЧтенииПредставленийНаСервере() Экспорт
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриЧтенииПредставленийНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

#КонецОбласти

#КонецЕсли
