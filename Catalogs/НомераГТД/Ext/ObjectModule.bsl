#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Строка") Тогда
		
		Код = ДанныеЗаполнения;
		ПорядковыйНомерТовара = Справочники.НомераГТД.ПорядковыйНомерТовараИзНомераТаможеннойДекларации(Код);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Владелец") Тогда
			ИсключаемыеСвойстваПриЗаполнении = "Владелец";
		Иначе
			ИсключаемыеСвойстваПриЗаполнении = "";
		КонецЕсли;

		ЗаполнитьПорядковыйНомерТовараАвтоматически = Ложь;
		
		Если ДанныеЗаполнения.Свойство("Код")
			И ДанныеЗаполнения.Свойство("ЗаполнитьПорядковыйНомерТовараАвтоматически", ЗаполнитьПорядковыйНомерТовараАвтоматически)
			И ЗаполнитьПорядковыйНомерТовараАвтоматически Тогда
			
			ИсключаемыеСвойстваПриЗаполнении = ИсключаемыеСвойстваПриЗаполнении + ",ПорядковыйНомерТовара";
			ПорядковыйНомерТовара = Справочники.НомераГТД.ПорядковыйНомерТовараИзНомераТаможеннойДекларации(ДанныеЗаполнения.Код);
			
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения, , ИсключаемыеСвойстваПриЗаполнении);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ПометкаУдаления Тогда
		ПроверитьДублиЭлемента(Отказ);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ПометкаУдаления Тогда
		НовыйПорядковыйНомерТовара = Справочники.НомераГТД.ПорядковыйНомерТовараИзНомераТаможеннойДекларации(Код);
		Если НовыйПорядковыйНомерТовара <> ПорядковыйНомерТовара Тогда
			 ПорядковыйНомерТовара = НовыйПорядковыйНомерТовара;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Процедура ПроверитьДублиЭлемента(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НомераГТД.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НомераГТД КАК НомераГТД
	|ГДЕ
	|	НомераГТД.Ссылка <> &Ссылка
	|	И НЕ НомераГТД.ПометкаУдаления
	|	И НомераГТД.Код = &Код
	|	И НомераГТД.СтранаПроисхождения = &СтранаПроисхождения
	|	И НомераГТД.РНПТ = &РНПТ";
	
	Запрос.УстановитьПараметр("Ссылка",					Ссылка);
	Запрос.УстановитьПараметр("Код",					Код);
	Запрос.УстановитьПараметр("СтранаПроисхождения",	СтранаПроисхождения);
	Запрос.УстановитьПараметр("РНПТ",					РНПТ);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТекстСообщения = НСтр("ru = 'Аналогичный номер ГТД уже существует: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Ссылка);
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
