
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ИнициализироватьКомпоновщикНастроекДляОтборов();
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВосстановитьНастройки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.НастройкаОтбора = Новый ХранилищеЗначения(КомпоновщикНастроекДляОтборов.ПользовательскиеНастройки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	
	
КонецПроцедуры

#Область ОтборыСКД

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроекДляОтборов()
	
	СхемаКомпоновкиДанных = Справочники.КР_НастройкиОтборовСозданияЗаказовНаПеремещениеМеждуМагазинами.ПолучитьМакет("ОсновныеДанные");
	
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных);
	КомпоновщикНастроекДляОтборов.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроекДляОтборов.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КомпоновщикНастроекДляОтборов.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ВосстановитьНастройки()
	
	ХранилищеНастроек = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "НастройкаОтбора");
	НастройкаОтбора = ХранилищеНастроек.Получить();
	
	Если Не НастройкаОтбора = Неопределено Тогда
		КомпоновщикНастроекДляОтборов.ЗагрузитьПользовательскиеНастройки(НастройкаОтбора);
		КомпоновщикНастроекДляОтборов.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
