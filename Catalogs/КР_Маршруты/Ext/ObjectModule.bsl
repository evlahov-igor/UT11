///////////////////////////////////////////////////
//// Справочник "КР_Маршруты"
//// Создан: 25.08.2022, Мельников А.А.,  КРОК, Jira№A2105505-325
  
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
  
#Область ОбработчикиСобытий

Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ЗоныДоставки.Ссылка В ИЕРАРХИИ
		|				(ВЫБРАТЬ
		|					КР_ДополнительныеНастройкиСистемы.Значение КАК Значение
		|				ИЗ
		|					Справочник.КР_ДополнительныеНастройкиСистемы КАК КР_ДополнительныеНастройкиСистемы
		|				ГДЕ
		|					КР_ДополнительныеНастройкиСистемы.Ссылка = ЗНАЧЕНИЕ(Справочник.КР_ДополнительныеНастройкиСистемы.ЗоныДоставкиРегионы))
		|			ТОГДА ""6""
		|		КОГДА ЗоныДоставки.Ссылка В ИЕРАРХИИ
		|				(ВЫБРАТЬ
		|					КР_ДополнительныеНастройкиСистемы.Значение КАК Значение
		|				ИЗ
		|					Справочник.КР_ДополнительныеНастройкиСистемы КАК КР_ДополнительныеНастройкиСистемы
		|				ГДЕ
		|					КР_ДополнительныеНастройкиСистемы.Ссылка = ЗНАЧЕНИЕ(Справочник.КР_ДополнительныеНастройкиСистемы.ЗоныДоставкиСегменты))
		|			ТОГДА ""8""
		|		КОГДА ЗоныДоставки.Ссылка В ИЕРАРХИИ
		|				(ВЫБРАТЬ
		|					КР_ДополнительныеНастройкиСистемы.Значение КАК Значение
		|				ИЗ
		|					Справочник.КР_ДополнительныеНастройкиСистемы КАК КР_ДополнительныеНастройкиСистемы
		|				ГДЕ
		|					КР_ДополнительныеНастройкиСистемы.Ссылка = ЗНАЧЕНИЕ(Справочник.КР_ДополнительныеНастройкиСистемы.ЗоныДоставкиМаркетплейсы))
		|			ТОГДА ""9""
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК Префикс
		|ИЗ
		|	Справочник.ЗоныДоставки КАК ЗоныДоставки
		|ГДЕ
		|	ЗоныДоставки.Ссылка = &ЗонаДоставки";
	
	Запрос.УстановитьПараметр("ЗонаДоставки", ЗонаДоставки);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать(); 
	ВыборкаДетальныеЗаписи.Следующий();                  
	Префикс = ВыборкаДетальныеЗаписи.Префикс;		
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоНовый() И ПустаяСтрока(Код) Тогда
		УстановитьНовыйКод();
	КонецЕсли;

КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

// << 26.08.2022, Мельников А.А., КРОК, Jira№ A2105505-ХХХ 
// Формирует наимеование для данного объекта
Процедура СформироватьНаименование() Экспорт
	
	Если ПустаяСтрока(Код) Тогда
		УстановитьНовыйКод();
	КонецЕсли;
	
	Префикс = Лев(Код, 1);                                                         
	ЗначимыйКод = Прав(Код, СтрДлина(Код) - 1);
	ЗначимыйКод = СтроковыеФункцииКлиентСервер.УдалитьПовторяющиесяСимволы(ЗначимыйКод, "0");
	ДлиннаЗначимогоКода = 2;
	Если СтрДлина(ЗначимыйКод) < ДлиннаЗначимогоКода Тогда
		 ЗначимыйКод = "0" + ЗначимыйКод;
	КонецЕсли;
	ГородаСтрокой = "";
	Если ПунктыМаршрута.Количество() > 0 Тогда
		СписокГородов = ПунктыМаршрута.Выгрузить(, "Город").ВыгрузитьКолонку("Город");
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	БизнесРегионы.Наименование КАК Наименование
		|ИЗ
		|	Справочник.БизнесРегионы КАК БизнесРегионы
		|ГДЕ
		|	БизнесРегионы.Ссылка В(&СписокГородов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
		Запрос.УстановитьПараметр("СписокГородов", СписокГородов);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		ПервыйПроход = Истина;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			Если ПервыйПроход Тогда
				ПервыйПроход = Ложь;	
			Иначе
				ГородаСтрокой = ГородаСтрокой + " - ";
			КонецЕсли;  
			ГородаСтрокой = ГородаСтрокой + ВыборкаДетальныеЗаписи.Наименование;
		КонецЦикла; 		
	КонецЕсли;	
	
	Наименование = Префикс + ЗначимыйКод + " - " + ГородаСтрокой; 
	
КонецПроцедуры // >> 26.08.2022, Мельников А.А., КРОК, Jira№ A2105505-ХХХ 

#КонецОбласти

#КонецОбласти   

#КонецЕсли
			