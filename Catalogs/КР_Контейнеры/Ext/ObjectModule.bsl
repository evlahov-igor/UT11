
//////////////////////////////////////////////////////////////
//// Модуль объекта справочника "КР_Контейнеры"
//// Создан: 17.01.2023 Маскаев П.Ю., КРОК, JIRA№ A2105505-732
//// Разработка по ФДР С31.001, Разработка справочника "Сеты"

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьНаУникальность(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьНаУникальность(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КР_Контейнеры КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка <> &Ссылка
	|	И Таблица.Наименование = &Наименование";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ШаблонСообщения = НСтр("ru = 'Значение поля ""%1"" не уникально'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, "Контейнер");
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения, Выборка.Ссылка, , "Объект.Наименование", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
