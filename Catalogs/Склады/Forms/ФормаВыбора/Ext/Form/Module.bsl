
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ГруппаСкладов = Неопределено;
	Если Параметры.Свойство("ГруппаСкладов", ГруппаСкладов) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", ГруппаСкладов, ВидСравненияКомпоновкиДанных.ВИерархии,,Истина);
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;

	Если Параметры.Отбор.Свойство("ДатаНачалаОрдернойСхемыПриОтгрузке") Тогда
		ДатаНачалаОрдернойСхемыПриОтгрузке = Параметры.Отбор.ДатаНачалаОрдернойСхемыПриОтгрузке;
		Параметры.Отбор.Удалить("ДатаНачалаОрдернойСхемыПриОтгрузке");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
				"ДатаНачалаОрдернойСхемыПриОтгрузке",
				ДатаНачалаОрдернойСхемыПриОтгрузке,
				ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
		
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ДатаНачалаОрдернойСхемыПриПоступлении") Тогда
		ДатаНачалаОрдернойСхемыПриПоступлении = Параметры.Отбор.ДатаНачалаОрдернойСхемыПриПоступлении;
		Параметры.Отбор.Удалить("ДатаНачалаОрдернойСхемыПриПоступлении");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
				"ДатаНачалаОрдернойСхемыПриПоступлении",
				ДатаНачалаОрдернойСхемыПриПоступлении,
				ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
		
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач") Тогда
		ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач = Параметры.Отбор.ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач;
		Параметры.Отбор.Удалить("ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
				"ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач",
				ДатаНачалаОрдернойСхемыПриОтраженииИзлишковНедостач,
				ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
		
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("СкладОтветственногоХранения") Тогда
		СкладОтветственногоХранения = Параметры.Отбор.СкладОтветственногоХранения;
		Параметры.Отбор.Удалить("СкладОтветственногоХранения");
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
				"СкладОтветственногоХранения",
				СкладОтветственногоХранения,
				ВидСравненияКомпоновкиДанных.Равно);
		
	КонецЕсли;
	
	Если Параметры.Свойство("СсылкаНеВСписке") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Ссылка", Параметры.СсылкаНеВСписке, ВидСравненияКомпоновкиДанных.НеВСписке, "СсылкаНеВСписке", Истина);
	КонецЕсли;
	
	ВыборГруппыСкладов = Неопределено;
	
	ЗапретитьИзменятьСоставСтрок = Ложь;
	Если Не Параметры.Свойство("ВыборГруппыСкладов", ВыборГруппыСкладов) Или
		(Не ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовПродажи") И Параметры.Свойство("ДокументПродажи"))
		Или (Не ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовЗакупки") И Параметры.Свойство("ДокументЗакупки")) Тогда
		
		МассивОтбора = Новый Массив();
		МассивОтбора.Добавить(Перечисления.ВыборГруппыСкладов.Запретить);
		МассивОтбора.Добавить(Перечисления.ВыборГруппыСкладов.РазрешитьВЗаказах);
		МассивОтбора.Добавить(Перечисления.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных);
		ВыборГруппыСкладов = Новый ФиксированныйМассив(МассивОтбора);
		ЗапретитьИзменятьСоставСтрок = Истина;
		
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("ВыборГруппыСкладов", ВыборГруппыСкладов);
	
	Элементы.Список.ИзменятьСоставСтрок = 
		ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") И Не ЗапретитьИзменятьСоставСтрок;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// << 22.08.2022, Мельников А.А., КРОК, Jira№ A2105505-291 
	// Для модификации видимости/доступности реквизитов 
	КР_ПриСозданииНаСервереДополнительно(Отказ, СтандартнаяОбработка);
	// >> 22.08.2022, Мельников А.А., КРОК, Jira№ A2105505-291 
	
	// --> Евлахов Игорь Николаевич (Начало) 23.08.2023
	// Задача #3069
	злСобытияФормОценкаПроизводительности.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	// <-- Евлахов Игорь Николаевич (Конец) 23.08.2023
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// --> Евлахов Игорь Николаевич (Начало) 23.08.2023
	// Задача #3069
	злСобытияФормОценкаПроизводительностиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// <-- Евлахов Игорь Николаевич (Конец) 23.08.2023
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыборГруппыСкладов) И ЗначениеЗаполнено(Значение) И Элементы.Список.ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		ВыборЗапрещен = ТекущиеДанные.ЭтоГруппа;
		
		Если ВыборЗапрещен Тогда
			Если ТипЗнч(ВыборГруппыСкладов) = Тип("ФиксированныйМассив") Тогда
				Для Каждого ТекЭлемент Из ВыборГруппыСкладов Цикл
					Если ТекЭлемент = ТекущиеДанные.ВыборГруппы Тогда
						ВыборЗапрещен = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если ВыборГруппыСкладов = ТекущиеДанные.ВыборГруппы Тогда
					ВыборЗапрещен = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ВыборЗапрещен Тогда
			
			СтандартнаяОбработка = Ложь;
			Отказ = Истина;
			ТекстПредупреждения = НСтр("ru='Группу ""%ГруппаСкладов%"" нельзя выбирать в документ.'");
			ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%ГруппаСкладов%", ТекущиеДанные.Наименование);
			ПоказатьПредупреждение(Неопределено, ТекстПредупреждения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область КР_ДобавленныеПроцедурыИФункции

// << 22.08.2022, Мельников А.А., КРОК, Jira№ A2105505-291 
// Вызывается после всего типового кода Стандартного события ПриСозданииНаСервере, для модификации реквизитов   
// Входящие параметры:
// Отказ - Булево - параметр стандартного события
// СтандартнаяОбработка - Булево - параметр стандартного события
//
&НаСервере
Процедура КР_ПриСозданииНаСервереДополнительно(Отказ, СтандартнаяОбработка)
	
	// << 09.03.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1292
	// Рефакторинг вывода элементов на форму
	КР_ДобавитьЭлементыНаФорму();
	// >> 09.03.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1292
	
КонецПроцедуры // >> 22.08.2022, Мельников А.А., КРОК, Jira№ A2105505-291 

// << 09.03.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1292
&НаСервере
Процедура КР_ДобавитьЭлементыНаФорму()
	
	Если Не КР_НастроитьСписок() Тогда
		Возврат;
	КонецЕсли;
	
	// КР_Закрыт
	ЭлементФормы = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(ЭтотОбъект,
		"Список.КР_Закрыт", Элементы.Список); 
	
	// КР_НомерМагазина
	ЭлементФормы = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(ЭтотОбъект,
		"Список.КР_НомерМагазина", Элементы.Список); 
	ЭлементФормы.АвтоОтметкаНезаполненного = Ложь;
	
	// БизнесРегион
	ЭлементФормы = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(ЭтотОбъект,
		"Список.БизнесРегион", Элементы.Список); 
	
	// КР_СпецРаспределение
	ЭлементФормы = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(ЭтотОбъект,
		"Список.КР_СпецРаспределение", Элементы.Список);
	
	// КР_СтритРитейл
	ЭлементФормы = КР_МетодыМодификацииФорм.ВставитьЭлементФормы(ЭтотОбъект,
		"Список.КР_СтритРитейл", Элементы.Список);
	
КонецПроцедуры

&НаСервере
Функция КР_НастроитьСписок()
	
	ТекстЗапросаДинСписка = Список.ТекстЗапроса;
	ТекстПоиска = "СправочникСклады.Ссылка,";
	ТекстЗамены = "СправочникСклады.Ссылка,"
		+ Символы.ПС + Символы.Таб + "СправочникСклады.БизнесРегион,"
		+ Символы.ПС + Символы.Таб + "СправочникСклады.КР_НомерМагазина,"
		+ Символы.ПС + Символы.Таб + "СправочникСклады.КР_Закрыт,"
		+ Символы.ПС + Символы.Таб + "СправочникСклады.КР_СпецРаспределение,"
		+ Символы.ПС + Символы.Таб + "СправочникСклады.КР_СтритРитейл,";
		
	Если СтрНайти(ТекстЗапросаДинСписка, ТекстПоиска) Тогда
		ТекстЗапросаДинСписка = СтрЗаменить(ТекстЗапросаДинСписка, ТекстПоиска, ТекстЗамены);
	Иначе
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Текст запроса динамического списка был изменен ранее'"));
		Возврат Ложь;
	КонецЕсли;
	
	Список.ТекстЗапроса = ТекстЗапросаДинСписка;
	
	Возврат Истина;
	
КонецФункции // >> 09.03.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-1292

#КонецОбласти
