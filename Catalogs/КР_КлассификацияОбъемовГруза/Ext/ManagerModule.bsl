
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// Создан: 25.08.2022, Мельников А.А., КРОК, Jira№ A2105505-325
// Проверяет разрешено ли редактирование данного объекта
// Параметры:
//  ОбъемСсылка - СправочникСсылка.КР_КлассификацияОбъемовГруза - ссылка,
//										проверяемая на возможность редактирования
//  Отказ - Булево - флаг возводимый в истину, если нет доступа к редактированию данного документа.
//
Процедура ПроверитьРазрешениеРедактирования(ОбъемСсылка, Отказ) Экспорт
	
	Если Пользователи.ЭтоПолноправныйПользователь() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КР_ТарифыПеревозчиков.Объем КАК Объем
		|ИЗ
		|	РегистрСведений.КР_ТарифыПеревозчиков КАК КР_ТарифыПеревозчиков
		|ГДЕ
		|	КР_ТарифыПеревозчиков.Объем = &ОбъемСсылка";
	
	Запрос.УстановитьПараметр("ОбъемСсылка", ОбъемСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЕстьСсылкиНаОбъем = Не РезультатЗапроса.Пустой();
	
	Если ЕстьСсылкиНаОбъем Тогда
		ТекстОшибки = НСтр("ru='Редактирование запрещено, так как на данный объем имеются ссылки в тарифах.
								|Обратитесь к администратору.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры // >> 22.08.2022, Мельников А.А., КРОК, Jira№ A2105505-325

#КонецОбласти
	
#КонецЕсли