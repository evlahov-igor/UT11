
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)   
	
	Если Не Объект.Ссылка.Пустая() Тогда
		ЗапрещеноРедактировать = Ложь;
		Справочники.КР_КлассификацияОбъемовГруза.ПроверитьРазрешениеРедактирования(Объект.Ссылка, ЗапрещеноРедактировать);
		Если ЗапрещеноРедактировать Тогда
			ЭтотОбъект.ТолькоПросмотр = Истина;
		КонецЕсли;  
	КонецЕсли;

КонецПроцедуры  

// << 05.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("Ссылка"              , Объект.Ссылка);
	ПараметрыПроверки.Вставить("ТипВеличины"         , Объект.ТипВеличины);
	ПараметрыПроверки.Вставить("МинимальноеЗначение" , Объект.МинимальноеЗначение);
	ПараметрыПроверки.Вставить("МаксимальноеЗначение", Объект.МаксимальноеЗначение);
	
	Если Не ДанныеУникальны(ПараметрыПроверки) Тогда
		ТекстОшибки = НСтр("ru = 'Значение ""%1"" ""%2"" ""%3"" уже существует. Элемент не может быть записан.'");
		ТекстОшибки = СтрШаблон(
			ТекстОшибки,
			ПараметрыПроверки.ТипВеличины,
			ПараметрыПроверки.МинимальноеЗначение,
			ПараметрыПроверки.МаксимальноеЗначение);
		
		ПоказатьПредупреждение(, ТекстОшибки);
		
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры // >> 05.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325

#КонецОбласти

#Область СлужебныеПроцедурыФункции

// << 05.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325
&НаСервереБезКонтекста
Функция ДанныеУникальны(ПараметрыПроверки)
	
	// Контроль уникальности
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ 
	|	КР_КлассификацияОбъемовГруза.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КР_КлассификацияОбъемовГруза КАК КР_КлассификацияОбъемовГруза
	|ГДЕ
	|	КР_КлассификацияОбъемовГруза.Ссылка <> &Ссылка
	|	И КР_КлассификацияОбъемовГруза.МинимальноеЗначение = &МинимальноеЗначение
	|	И КР_КлассификацияОбъемовГруза.МаксимальноеЗначение = &МаксимальноеЗначение
	|	И КР_КлассификацияОбъемовГруза.ТипВеличины = &ТипВеличины";
	
	Запрос.УстановитьПараметр("Ссылка"              , ПараметрыПроверки.Ссылка);
	Запрос.УстановитьПараметр("ТипВеличины"         , ПараметрыПроверки.ТипВеличины);
	Запрос.УстановитьПараметр("МинимальноеЗначение" , ПараметрыПроверки.МинимальноеЗначение);
	Запрос.УстановитьПараметр("МаксимальноеЗначение", ПараметрыПроверки.МаксимальноеЗначение);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции // >> 05.10.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325

#КонецОбласти
