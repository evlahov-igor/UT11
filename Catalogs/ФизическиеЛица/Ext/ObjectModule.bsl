#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоГруппа Тогда
		Наименование = СокрЛП(Наименование);
		Пока Найти(Наименование,"  ") > 0 Цикл
			Наименование = СтрЗаменить(Наименование, "  ", " ");
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТекстСообщения = "";
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не ЭтоНовый() Тогда
		ЗначенияРеквизитовВИнформационнойБазе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Новый Структура("ИНН"));
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИНН) Тогда 
		Если (ЭтоНовый() ИЛИ ЗначенияРеквизитовВИнформационнойБазе.ИНН <> ИНН)
		 И НЕ РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь, ТекстСообщения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ИНН", , Отказ);
		КонецЕсли;
	Конецесли;
	
	Если НЕ УправлениеДоступом.ОграничиватьДоступНаУровнеЗаписей()
	 ИЛИ НЕ УправлениеДоступомСлужебный.ВидДоступаИспользуется("ГруппыФизическихЛиц") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГруппаДоступа");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область КР_ДобавленныеПроцедурыИФункции

#Область КР_ОбработчикиСобытий

// << 08.09.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	// << 16.09.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325
	// Рефакторинг по замечаниям код-ревью
	// Использован платформенный метод РольДоступна(), т.к. роль
	// используется как маркер выполнения логики алгоритма.
	// Использование метода Пользователи.РолиДоступны() не подходит, т.к.
	// для полноправного пользователя логика не должна выполняться.

	Если РольДоступна("КР_ДобавлениеИзменениеВодителей") Тогда
		КР_ЭтоВодитель = Истина;
	КонецЕсли;
	
	// << 09.01.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-325
	Если РольДоступна("КР_ДобавлениеИзменениеВодителейРозницы") Тогда
		КР_ЭтоВодитель = Истина;
		КР_ЭтоВодительРозницы = Истина;
	КонецЕсли;
	// >> 09.01.2023, Маскаев П.Ю., КРОК, Jira№ A2105505-325
	
КонецПроцедуры // >> 08.09.2022, Маскаев П.Ю., КРОК, Jira№ A2105505-325

#КонецОбласти

#КонецОбласти

#КонецЕсли