
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЭтаФорма.Параметры.Свойство("Владелец") Тогда		
		Объект.Владелец = ЭтаФорма.Параметры.Владелец;
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЗаполнитьУникальныйИД();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПроверитьВидимостьОграничений(Объект.ТипДанных);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДатаМобайл_ДополнительныеФормы.ИДСправочника
	|ИЗ
	|	Справочник.ДатаМобайл_ДополнительныеФормы КАК ДатаМобайл_ДополнительныеФормы
	|ГДЕ
	|	ДатаМобайл_ДополнительныеФормы.ПометкаУдаления = ЛОЖЬ
	|	И ДатаМобайл_ДополнительныеФормы.Наименование = &Наименование
	|	И ДатаМобайл_ДополнительныеФормы.ТипДанных ПОДОБНО &ТипДанных
	|	И ДатаМобайл_ДополнительныеФормы.Ссылка <> &Ссылка";
	
	Запрос.УстановитьПараметр("Наименование", Объект.Наименование);
	Запрос.УстановитьПараметр("ТипДанных", Объект.ТипДанных);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Сообщить("Такая запись уже создана" + Символы.ПС + "Пожалуйста, измените тип или название");
		Отказ = Истина;
	КонецЕсли;
				
	Для каждого СтрокаОграничения Из ТекущийОбъект.Ограничения Цикл
		Если Не ЗначениеЗаполнено(СтрокаОграничения.Значения) Тогда
			Сообщить("Не выбрано значение для строки ограничения");
			Отказ = Истина;		
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ТипДанныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СЗ = ТипДанныхНачалоВыбораНаСервере();
	ДанныеВыбора = СЗ;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТипДанныхНачалоВыбораНаСервере()
	СЗ = Новый СписокЗначений;
	СЗ.Добавить("Число", "Число");
	СЗ.Добавить("Текст", "Текст");
	СЗ.Добавить("Дата", "Дата");
	СЗ.Добавить("Булево", "Булево");
	СЗ.Добавить("Справочники", "Справочники");
	СЗ.Добавить("Перечисления", "Перечисления");
	СЗ.Добавить("ПланыВидовХарактеристик", "Планы видов характеристик");
	
	Возврат СЗ;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНазванияОбъектов(ТипОбъекта)
	СЗ = Новый СписокЗначений;
	Для каждого стр Из Метаданные[ТипОбъекта] Цикл
		Если Не стр = Метаданные.Справочники.ДатаМобайл_ДополнительныеФормы Тогда
			СЗ.Добавить(стр.Имя, стр.Имя);
		КонецЕсли;
	КонецЦикла;
	Возврат СЗ;
КонецФункции

&НаКлиенте
Функция ВыполнитьПослеЗакрытияВопроса(ВыбранныйЭлемент, ПараметрыОповещения) Экспорт
	
	Если Не ВыбранныйЭлемент = Неопределено Тогда
		Объект.Наименование = ВыбранныйЭлемент.Значение;
		Объект.ТипДанных = ВыбранныйЭлемент.Значение;
		ПроверитьВидимостьОграничений(ВыбранныйЭлемент.Значение);
	Иначе
		ПроверитьВидимостьОграничений("");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ТипДанныхОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Объект.Ограничения.Очистить();
	
	Если ВыбранноеЗначение = "Справочники" Или ВыбранноеЗначение = "Перечисления" Или ВыбранноеЗначение = "ПланыВидовХарактеристик" Тогда 
		СтандартнаяОбработка = Ложь;
		Оп = Новый ОписаниеОповещения("ВыполнитьПослеЗакрытияВопроса", ЭтотОбъект);
		ПоказатьВыборИзСписка(Оп, ПолучитьНазванияОбъектов(ВыбранноеЗначение), Элемент); 
	Иначе
		Объект.Наименование = ВыбранноеЗначение;
		ПроверитьВидимостьОграничений("");
	КонецЕсли;		
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУникальныйИД()
	
	Если Не ЗначениеЗаполнено(Объект.ИДСправочника) Тогда
		Объект.ИДСправочника = Новый УникальныйИдентификатор;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВидимостьОграничений(ТекущийТипДанных)
	
	Если Не ЗначениеЗаполнено(ТекущийТипДанных) 
		Или ТекущийТипДанных = "Текст" 
		Или ТекущийТипДанных = "Число" 
		Или ТекущийТипДанных = "Дата" 
		Или ТекущийТипДанных = "Булево" Тогда
		
		Элементы.Ограничения.Видимость = Ложь;
		
	Иначе
		Элементы.Ограничения.Видимость = Истина;
		
		УстановитьТипДанныхОграничений(ТекущийТипДанных);		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьТипДанныхОграничений(ТекущийТипДанных)
	
	Если Не Метаданные.Справочники.Найти(ТекущийТипДанных) = Неопределено Тогда
		ТипЭлемента = "СправочникСсылка." + ТекущийТипДанных;
	ИначеЕсли Не Метаданные.ПланыВидовХарактеристик.Найти(ТекущийТипДанных) = Неопределено Тогда
		ТипЭлемента = "ПланВидовХарактеристикСсылка." + ТекущийТипДанных;	
	ИначеЕсли Не Метаданные.Перечисления.Найти(ТекущийТипДанных) = Неопределено Тогда
		ТипЭлемента = "ПеречислениеСсылка." + ТекущийТипДанных;
	КонецЕсли;
	
	Массив = Новый Массив(); 
	Массив.Добавить(Тип(ТипЭлемента)); 
	ТекущееОграничениеТипов = Новый ОписаниеТипов(Массив);
	
	Элементы.ОграниченияЗначения.ОграничениеТипа = ТекущееОграничениеТипов; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОграниченияЗначенияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТипЭлемента = "СправочникСсылка.СкладскиеПомещения";		
	Массив = Новый Массив(); 
	Массив.Добавить(Тип(ТипЭлемента)); 
	ТекущееОграничениеТипов = Новый ОписаниеТипов(Массив);		
	
	Если Элементы.ОграниченияЗначения.ОграничениеТипа = ТекущееОграничениеТипов тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьВыборИзСписка(Новый ОписаниеОповещения("ЗакрытиеВыбораПомещения", ЭтотОбъект),ПолучитьСписокПомещений(), Элементы.Ограничения, 0);	
	КонецЕсли;
	
КонецПроцедуры

&Наклиенте
Процедура ЗакрытиеВыбораПомещения(Результат, ДопПараметры) Экспорт
	
	Попытка Элементы.Ограничения.ТекущиеДанные.Значения = Результат.Значение; Исключение КонецПопытки; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокПомещений() 
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	СкладскиеПомещения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СкладскиеПомещения КАК СкладскиеПомещения");
	
	СписокВозврата = Новый СписокЗначений;
	СписокВозврата.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Возврат СписокВозврата;
	
КонецФункции

#КонецОбласти