
#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ЗначенияСписка") Тогда
		СписокЗначенийИспр.Очистить();
		СписокЗначенийИспр.ЗагрузитьЗначения(Параметры.ЗначенияСписка);
	КонецЕсли;
	
	Если Параметры.Свойство("ИмяСпискаЗначений") Тогда
		ИмяСпискаЗначений = Параметры.ИмяСпискаЗначений;
	КонецЕсли; 
	
	Если Параметры.Свойство("ОписаниеТипов") Тогда
		СписокЗначенийИспр.ТипЗначения = Параметры.ОписаниеТипов;
	КонецЕсли;
	
	Если Параметры.Свойство("ИмяФормыВыбора") Тогда
		ИмяФормыВыбора = Параметры.ИмяФормыВыбора;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Оповестить("ОтредактированСписокЗначений", СписокЗначенийИспр.ВыгрузитьЗначения(), ИмяСпискаЗначений);
	ЭтаФорма.Закрыть(); 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	ЭтаФорма.Закрыть(); 
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначенийИспрПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыПодбора = Новый Структура; 
	ПараметрыПодбора.Вставить("МножественныйВыбор", Ложь);
	ПараметрыПодбора.Вставить("РежимВыбора", Истина);
	Подбор = Получитьформу(ИмяФормыВыбора, ПараметрыПодбора, ЭтаФорма);
	Подбор.ЗакрыватьПриВыборе = Истина;
	Подбор.Открыть(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораКлиент(ВыбранноеЗначение)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для каждого Значение Из ВыбранноеЗначение Цикл
			Если Не СписокЗначенийИспр.НайтиПоЗначению(Значение) = Неопределено Тогда Продолжить; КонецЕсли;
			СписокЗначенийИспр.Добавить(Значение);
		КонецЦикла; 
		
	Иначе
		Если Не СписокЗначенийИспр.НайтиПоЗначению(ВыбранноеЗначение) = Неопределено Тогда Возврат; КонецЕсли;
		СписокЗначенийИспр.Добавить(ВыбранноеЗначение); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбработкаВыбораКлиент(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначенийИспрОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработкаВыбораКлиент(ВыбранноеЗначение);  
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаписи(Команда)
	
	ПараметрыПодбора = Новый Структура; 
	ПараметрыПодбора.Вставить("МножественныйВыбор", Истина);
	ПараметрыПодбора.Вставить("РежимВыбора", Истина);
	Подбор = Получитьформу(ИмяФормыВыбора, ПараметрыПодбора, ЭтаФорма);
	Подбор.ЗакрыватьПриВыборе = Истина;
	Подбор.ТекущийЭлемент.РежимВыделения = РежимВыделенияТаблицы.Множественный;
	Подбор.Открыть(); 
	
КонецПроцедуры  

#КонецОбласти


