#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("СвойстваПодписи") Тогда
		ВызватьИсключение НСтр("ru = 'Обработка не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	ОбщегоНазначенияБЭД.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	СвойстваПодписи = Новый Структура;
	СвойстваДоверенности = Новый Структура;
	РезультатПроверки = Новый Структура;
	
	Параметры.Свойство("СвойстваПодписи", СвойстваПодписи);
	Параметры.Свойство("СвойстваДоверенности", СвойстваДоверенности);
	Параметры.Свойство("РезультатПроверки", РезультатПроверки);
	
	ПодписьОпределена = СвойстваПодписи <> Неопределено;  
			
	Если Не ПодписьОпределена Тогда 
	  
		СвойстваПодписи = Новый Структура;  
		СвойстваПодписи.Вставить("ДатаПроверкиПодписи", ТекущаяДатаСеанса());
		СвойстваПодписи.Вставить("ПодписьВерна", Ложь); 
		СвойстваПодписи.Вставить("Комментарий", "Подпись не определена"); 
		
	КонецЕсли;

	ОтобразитьПротоколНаФорме(СвойстваПодписи, СвойстваДоверенности, РезультатПроверки);
	
КонецПроцедуры 

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтобразитьПротоколНаФорме(СвойстваПодписи, СвойстваДоверенности, РезультатПроверки)
	
	ИмеетсяПротоколПроверки = Ложь;
	ПодписьВерна = СвойстваПодписи.ПодписьВерна;
	
	Если ЗначениеЗаполнено(РезультатПроверки) Тогда
		ПротоколПроверки = РезультатПроверки.ПротоколПроверки; 
		ИмеетсяПротоколПроверки = ПротоколПроверки <> Неопределено;
		Доверенность = РезультатПроверки.Доверенность; 
		ПодписьВерна = РезультатПроверки.ПодписьВерна;
	КонецЕсли;
	
	Если Доверенность = Неопределено Тогда 
		Элементы.ОткрытьМЧД.Видимость = Ложь;
	КонецЕсли;
	
	Если ИмеетсяПротоколПроверки Тогда
		ПроверкаПодписи = ПротоколПроверки.ПроверкаПодписи;
		ПодписьВернаОшибка = ПроверкаПодписи.ПодписьВернаОшибка;
		ДатаПроверкиПодписи = ПроверкаПодписи.ДатаПроверки;		
		
		ДоверенностьОпределена = ПротоколПроверки.ПроверкаДоверенности <> Неопределено;
		
	Иначе  
		ПроверкаПодписи = СвойстваПодписи; 
		ПодписьВернаОшибка = СвойстваПодписи.Комментарий; 
		ДатаПроверкиПодписи = СвойстваПодписи.ДатаПроверкиПодписи;
		
		ДоверенностьОпределена = СвойстваДоверенности <> Неопределено;
		
	КонецЕсли; 
	
	Если ПодписьВерна Тогда 
		Элементы.ЗаголовокПредставлениеПроверки.ЦветТекста = ЦветаСтиля.ЦветРазделаПанелиФункций;
		Элементы.ГруппаПроверкиШапка.ЦветФона = ЦветаСтиля.ЦветФонаПодсказки;	
	Иначе
		Элементы.ЗаголовокПредставлениеПроверки.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
		Элементы.ГруппаПроверкиШапка.ЦветФона = ЦветаСтиля.ЦветФонаПредупреждения;	
	КонецЕсли;
	
	Элементы.ЗаголовокДатаПроверки.Заголовок = НСтр("ru='Проверено '") 
	+ Формат(ДатаПроверкиПодписи, НСтр("ru='ДФ=''dd.MM.yyyy HH:mm'''"));
	
	Элементы.ГруппаПодписьВерна.Видимость = ПроверкаПодписи.ПодписьВерна;
	Элементы.ГруппаПодписьНеВерна.Видимость = Не ПроверкаПодписи.ПодписьВерна;
	ПредставлениеПодписьНеВернаПоле = НСтр("ru='" + СокрЛП(ПодписьВернаОшибка + "'"));
	
	Если ПроверкаПодписи.ПодписьВерна Тогда
		Элементы.ЗаголовокПредставлениеПроверки.Заголовок = НСтр("ru='Подпись верна '");
	Иначе  
		Элементы.ЗаголовокПредставлениеПроверки.Заголовок = НСтр("ru='Подпись неверна '"); 
	КонецЕсли;
	
	Если Не ИмеетсяПротоколПроверки И Не ДоверенностьОпределена Тогда
		
		Элементы.ДиагностикаМЧД.Видимость = Ложь;
		Возврат;
		
	ИначеЕсли Не ИмеетсяПротоколПроверки И ДоверенностьОпределена Тогда    
		
		Элементы.ГруппаМЧДДействительна.Видимость = Ложь;
		Элементы.ГруппаМЧДНеДействительна.Видимость = Истина;
		ПредставлениеМЧДНеДействительнаПоле = НСтр("ru='Доверенность не проверена'"); 
		
		Элементы.ГруппаМЧДПредставительСопоставлен.Видимость = Ложь;
		Элементы.ГруппаМЧДПредставительНеСопоставлен.Видимость = Истина; 
		ПредставлениеМЧДПредставительНеСопоставленПоле = НСтр("ru='Представитель не сопоставлен'");
		
		Элементы.ГруппаМЧДДоверительСопоставлен.Видимость = Ложь;
		Элементы.ГруппаМЧДДоверительНеСопоставлен.Видимость = Истина; 
		ПредставлениеМЧДДоверительНеСопоставленПоле = НСтр("ru='Доверитель не сопоставлен'");
		
		Элементы.ГруппаМЧДСрокДействияДействителен.Видимость = Ложь;
		Элементы.ГруппаМЧДСрокДействияНеДействителен.Видимость = Истина; 
		ПредставлениеМЧДСрокДействияНеДействителенПоле = НСтр("ru='Срок действия не определен'");  
		
		Элементы.ГруппаРезультатыПроверкаМЧДОператоромЭДО.Видимость = Ложь;	
		
		ДоверенностьДействует = Ложь;
		
	Иначе         
		
		ПроверкаДоверенности = ПротоколПроверки.ПроверкаДоверенности; 
		
		Элементы.ГруппаМЧДДействительна.Видимость = ПроверкаДоверенности.ДоверенностьДействительна;
		Элементы.ГруппаМЧДНеДействительна.Видимость = Не ПроверкаДоверенности.ДоверенностьДействительна; 
		ПредставлениеМЧДНеДействительнаПоле = 
		СокрЛП(ПроверкаДоверенности.ДоверенностьДействительнаОшибка);
		
		Элементы.ГруппаМЧДПредставительСопоставлен.Видимость = ПроверкаДоверенности.ПредставительСопоставлен;
		Элементы.ГруппаМЧДПредставительНеСопоставлен.Видимость = Не ПроверкаДоверенности.ПредставительСопоставлен; 
		ПредставлениеМЧДПредставительНеСопоставленПоле = 
		СокрЛП(ПроверкаДоверенности.ПредставительСопоставленОшибка);
		
		Элементы.ГруппаМЧДДоверительСопоставлен.Видимость = ПроверкаДоверенности.ДоверительСопоставлен;
		Элементы.ГруппаМЧДДоверительНеСопоставлен.Видимость = Не ПроверкаДоверенности.ДоверительСопоставлен; 
		ПредставлениеМЧДДоверительНеСопоставленПоле = СокрЛП(ПроверкаДоверенности.ДоверительСопоставленОшибка);
		
		Элементы.ГруппаМЧДСрокДействияДействителен.Видимость =
		ПроверкаДоверенности.ДатаПодписиИДоверенностиСоответствует;
		Элементы.ГруппаМЧДСрокДействияНеДействителен.Видимость = 
		Не ПроверкаДоверенности.ДатаПодписиИДоверенностиСоответствует; 
		ПредставлениеМЧДСрокДействияНеДействителенПоле = 
		СокрЛП(ПроверкаДоверенности.ДатаПодписиИДоверенностиСоответствуетОшибка);   
		
		ДоверенностьДействует = ПроверкаДоверенности.ДоверенностьДействительна
		И ПроверкаДоверенности.ПредставительСопоставлен
		И ПроверкаДоверенности.ДоверительСопоставлен
		И ПроверкаДоверенности.ДатаПодписиИДоверенностиСоответствует;
		
		ПроверкаОператором = ПротоколПроверки.ПроверкаОператором;
		Если ЗначениеЗаполнено(ПроверкаОператором) Тогда 
			Элементы.ГруппаМЧДПроверенаОператоромЭДО.Видимость = ПроверкаОператором.ДоверенностьДействительна;
			Элементы.ГруппаМЧДНеПроверенаОператоромЭДО.Видимость = Не ПроверкаОператором.ДоверенностьДействительна;  
			ПредставлениеМЧДНеПроверенаОператоромЭДОПоле = 
			СокрЛП(ПроверкаОператором.ДоверенностьДействительнаОшибка); 
			
			ДоверенностьДействует = ДоверенностьДействует И ПроверкаОператором.ДоверенностьДействительна;
			
		Иначе  
			Элементы.ГруппаРезультатыПроверкаМЧДОператоромЭДО.Видимость = Ложь;	
		КонецЕсли;
		
	КонецЕсли; 
	
	Если ДоверенностьДействует Тогда
		Элементы.ЗаголовокПредставлениеПроверки.Заголовок = Элементы.ЗаголовокПредставлениеПроверки.Заголовок
		+ Символы.ПС
		+ НСтр("ru='Доверенность действительна'"); 
	Иначе
		Элементы.ЗаголовокПредставлениеПроверки.Заголовок = Элементы.ЗаголовокПредставлениеПроверки.Заголовок
		+ Символы.ПС
		+ НСтр("ru='Доверенность недействительна'"); 
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьМЧДНажатие(Элемент) 
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ПоказатьЗначение( , Доверенность);         
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 