
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Организация = ИнтеграцияСЯндексМаркетСервер.ТекущиеДанныеАвторизацииОрганизация();
	 
	Если Организация <> Неопределено И ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда
		ДанныеАвторизации = ИнтеграцияСЯндексМаркетСервер.ТекущиеДанныеАвторизации(Организация);
		Если ДанныеАвторизации.Свойство("access_token") 
			И ДанныеАвторизации.Свойство("access_token_expires")
			И ДанныеАвторизации.Свойство("refresh_token") Тогда
			
				СрокЖизниТокена = ДанныеАвторизации.access_token_expires;
				Если ТипЗнч(СрокЖизниТокена) = Тип("Дата") Тогда
					Если СрокЖизниТокена > ТекущаяДата() Тогда
						Элементы.Обновить.Видимость = Ложь;
						Элементы.Подключить.Видимость = Ложь;
						Элементы.Отмена.КнопкаПоУмолчанию = Истина;
						Элементы.Очистить.Видимость = Истина;
					Иначе
						Элементы.Обновить.Видимость = Истина;
						Элементы.Подключить.Видимость = Ложь;
						Элементы.Обновить.КнопкаПоУмолчанию = Истина;
						Элементы.Очистить.Видимость = Ложь;
					КонецЕсли;
				КонецЕсли;				
		КонецЕсли;
	КонецЕсли;
	
	СтрокаИнформации = "Для организации "+ Организация+ " актуальна авторизация на Яндекс.Маркет до "+ СрокЖизниТокена;
	
	Элементы.ПодсказкаФормы.Заголовок = СтрокаИнформации;
	Элементы.ПодсказкаФормы.ЦветТекста = ЦветаСтиля.ЦветТекстаУспех;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьНастройкиНаСервере()  
	
	ИнтеграцияСЯндексМаркетСервер.УдалитьДанныеАвторизации(Организация);
	СтрокаИнформации = "Для организации "+ Организация+ " удалены настройки авторизации.";
	
	Элементы.ПодсказкаФормы.Заголовок = СтрокаИнформации;
	Элементы.ПодсказкаФормы.ЦветТекста = ЦветаСтиля.ЦветПустойГиперссылки;
	Элементы.Подключить.Видимость = Истина;
	Элементы.Подключить.КнопкаПоУмолчанию = Истина;
	Элементы.Очистить.Видимость = Ложь;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНастройкиЗавершение(Результат, Параметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьНастройкиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНастройки(Команда)
	
	Оповещение= Новый ОписаниеОповещения("ОчиститьНастройкиЗавершение", ЭтотОбъект);
	ТекстВопроса = "Настройки авторизации будут удалены без возможности восстановления. Для продолжения работы на площадке будет необходимо получить новые настройки.
	|Вы действительно хотите очистить настройки?";
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройки(Команда)
	
	ОткрытьФорму(
	"Обработка.ЯндексМаркетВитринаФулфилмент.Форма.ПодключениеКСервису",,,Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник) Экспорт

	Если ИмяСобытия = "ЗакрытьФормуОчисткиНастроек" Тогда
		ЭтаФорма.Закрыть();
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти



