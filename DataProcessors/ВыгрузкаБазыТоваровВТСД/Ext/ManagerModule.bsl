#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


// Функция возвращает пустую структуру настроек
// 
// Параметры: 
//  Нет
// 
// Возвращаемое значение: 
//  Структура - структура настроек.
Функция СтруктураНастроек() Экспорт
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ОбязательныеПоля",               Новый Массив);
	СтруктураНастроек.Вставить("КомпоновщикНастроек",            Неопределено);
	СтруктураНастроек.Вставить("ИмяМакетаСхемыКомпоновкиДанных", Неопределено);
	
	Возврат СтруктураНастроек;
	
КонецФункции

Функция УстановитьЗначениеПараметраСКД(КомпоновщикНастроек, ИмяПараметра, ЗначениеПараметра, ИспользоватьНеЗаполненный = Истина)
	
	ПараметрУстановлен = Ложь;
	
	ПараметрКомпоновкиДанных = Новый ПараметрКомпоновкиДанных(ИмяПараметра);
	ЗначениеПараметраКомпоновкиДанных = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрКомпоновкиДанных);
	Если ЗначениеПараметраКомпоновкиДанных <> Неопределено Тогда
		
		ЗначениеПараметраКомпоновкиДанных.Значение = ЗначениеПараметра;
		ЗначениеПараметраКомпоновкиДанных.Использование = ?(ИспользоватьНеЗаполненный, Истина, ЗначениеЗаполнено(ЗначениеПараметраКомпоновкиДанных.Значение));
		
		ПараметрУстановлен = Истина;
		
	КонецЕсли;
	
	Возврат ПараметрУстановлен;
	
КонецФункции

// Функция подготавливает структуру данных, необходимую для печати ценников и этикеток.
//
// Возвращаемое значение:
//  Структура - данные, необходимые для печати этикеток и ценников.
//
Функция ПодготовитьСтруктуруДанных(СтруктураНастроек) Экспорт
	
	////////////////////////////////////////////////////////////////////////////////
	// ПОДГОТОВКА СХЕМЫ КОМПОНОВКИ ДАННЫХ И КОМПОНОВЩИКА НАСТРОЕК СКД
	
	// Схема компоновки.
	СхемаКомпоновкиДанных = Обработки.ВыгрузкаБазыТоваровВТСД.ПолучитьМакет(СтруктураНастроек.ИмяМакетаСхемыКомпоновкиДанных);
	
	// Подготовка компоновщика макета компоновки данных.
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	Компоновщик.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	Компоновщик.Настройки.Отбор.Элементы.Очистить();
	
	// Отбор компоновщика настроек.
	Если СтруктураНастроек.КомпоновщикНастроек <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.СкопироватьЭлементы(Компоновщик.Настройки.Отбор, СтруктураНастроек.КомпоновщикНастроек.Настройки.Отбор);
	КонецЕсли;
	
	// Выбранные поля компоновщика настроек.
	Для Каждого ОбязательноеПоле Из СтруктураНастроек.ОбязательныеПоля Цикл
		ПолеСКД = КомпоновкаДанныхСервер.НайтиПолеСКДПоПолномуИмени(Компоновщик.Настройки.Выбор.ДоступныеПоляВыбора.Элементы, ОбязательноеПоле);
		Если ПолеСКД <> Неопределено Тогда
			ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Поле = ПолеСКД.Поле;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ПараметрДанных Из СтруктураНастроек.ПараметрыДанных Цикл
		УстановитьЗначениеПараметраСКД(Компоновщик, ПараметрДанных.Ключ, ПараметрДанных.Значение);
	КонецЦикла;
	
	ТекстЗапроса = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ЦеныНоменклатуры.Упаковка",
		"ЦеныНоменклатуры.Номенклатура"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ИсходныеДанныеПоследнийЗапрос.Упаковка",
		"ИсходныеДанныеПоследнийЗапрос.Номенклатура"));
	// << 20.03.2023 Федоров Д.Е., КРОК, JIRA№A2105505-955
	КР_ИзменитьТекстЗапросаПриЦенообразование25(ТекстЗапроса);
	// >> 20.03.2023 Федоров Д.Е., КРОК, JIRA№A2105505-955
	
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос = ТекстЗапроса;
	
	// Компоновка макета компоновки данных.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Компоновщик.Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	////////////////////////////////////////////////////////////////////////////////
	// ВЫПОЛНЕНИЕ ЗАПРОСА
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновкиДанных);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	Таблица = Новый ТаблицаЗначений();
	ПроцессорВывода.УстановитьОбъект(Таблица);
	ДанныеОтчета = ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат Таблица;
	
КонецФункции

#Область КР_ДополнительныеПроцедурыИФункции

// << 20.03.2023 Федоров Д.Е., КРОК, JIRA№A2105505-955
// Процедура изменяет запрос, в соответствии с использованием ценообразования "25"
// (Исправление типового функционала обработки выгрузки данных в ТСД,
// в будущем возможно будет исправлено при обновлении конфигурации на актуальную)
// 
// Параметры: 
//  ТекстЗапроса - Строка - Текст запроса для изменения
//  ПредставлениеРегистра - Имя представления регистра в запросе (РегистрСведений.ЦеныНоменклатуры КАК РегистрЦен)
//
Процедура КР_ИзменитьТекстЗапросаПриЦенообразование25(ТекстЗапроса, ПредставлениеРегистра = "ЦеныНоменклатуры") Экспорт
	
	ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25();
	Если Не ИспользуетсяЦенообразование25 Тогда
		Возврат;
	КонецЕсли;
	
	КР_УдалитьУслоныйОтборПоХарактеристике(ТекстЗапроса);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РегистрСведений.ЦеныНоменклатуры",
		"РегистрСведений.ЦеныНоменклатуры25");
	
	ШаблонЗаменыСвязи 		= ".Характеристика = %1.Характеристика";
	ШаблонНовойСтрокиСвязи 	= ".Характеристика.ХарактеристикаНоменклатурыДляЦенообразования = %1.ХарактеристикаЦО";
	СтрокаЗаменыСвязи 		= СтрШаблон(ШаблонЗаменыСвязи, ПредставлениеРегистра);
	НоваяСтрокаСвязи 		= СтрШаблон(ШаблонНовойСтрокиСвязи, ПредставлениеРегистра);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, СтрокаЗаменыСвязи, НоваяСтрокаСвязи);
	
КонецПроцедуры

// << 20.03.2023 Федоров Д.Е., КРОК, JIRA№A2105505-955
// Процедура изменяет запрос, в соответствии с использованием ценообразования "25"
// (Исправление типового функционала обработки выгрузки данных в ТСД,
// в будущем возможно будет исправлено при обновлении конфигурации на актуальную)
// Пример исходной cтроки:
//РегистрСведений.ЦеныНоменклатуры.СрезПоследних({&ТекущаяДата}, ВидЦены = &ВидЦены {(Номенклатура).* КАК Номенклатура, (Характеристика).* КАК Характеристика})
// Пример результата:
//РегистрСведений.ЦеныНоменклатуры.СрезПоследних({&ТекущаяДата}, ВидЦены = &ВидЦены {(Номенклатура).* КАК Номенклатура, (ХарактеристикаЦО).* КАК Характеристика})
//
// Параметры: 
//  ТекстЗапроса - Строка - Текст запроса для изменения
//
Процедура КР_УдалитьУслоныйОтборПоХарактеристике(ТекстЗапроса)
	
	СтрокаПоиска = "РегистрСведений.ЦеныНоменклатуры.СрезПоследних("; 
	ПозицияУсловия = СтрНайти(ТекстЗапроса, СтрокаПоиска);
	Если ПозицияУсловия = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоОткрытыхСкобок = 1;
	НомерПервогоСимволаОбхода = ПозицияУсловия + СтрДлина(СтрокаПоиска);
	НомерСимволаКонцаОтбораРегистра = 0;
	
	Для НомерСимвола = НомерПервогоСимволаОбхода По СтрДлина(ТекстЗапроса) Цикл
		
		ТекущийСимвол = Сред(ТекстЗапроса, НомерСимвола, 1);
		Если ТекущийСимвол = "(" Тогда
			КоличествоОткрытыхСкобок = КоличествоОткрытыхСкобок + 1;
		ИначеЕсли  ТекущийСимвол = ")" Тогда
			КоличествоОткрытыхСкобок = КоличествоОткрытыхСкобок - 1;
		КонецЕсли;
		Если КоличествоОткрытыхСкобок = 0 Тогда
			НомерСимволаКонцаОтбораРегистра = НомерСимвола;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НомерСимволаКонцаОтбораРегистра = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаЗаменыВЗапросе = Сред(ТекстЗапроса, ПозицияУсловия, НомерСимволаКонцаОтбораРегистра - ПозицияУсловия + 1);
	ИтоговаяСтрокаЗамены = СтрЗаменить(СтрокаЗаменыВЗапросе, ", (Характеристика).* КАК Характеристика", "");
	
	Если СтрокаЗаменыВЗапросе <> ИтоговаяСтрокаЗамены Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, СтрокаЗаменыВЗапросе, ИтоговаяСтрокаЗамены);
	КонецЕсли;
	
КонецПроцедуры // >> 20.03.2023 Федоров Д.Е., КРОК, JIRA№A2105505-955

#КонецОбласти

#КонецЕсли