
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИмяОбработки = РеквизитФормыВЗначение("Объект").Метаданные().ПолноеИмя();
	
	РольДоступнаПолныеПрава = Пользователи.ЭтоПолноправныйПользователь(,, Ложь);
	
	ПолучитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВидимостьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДекорацияПриемкаОписаниеГиперСсылкаНажатие(Элемент)
	
	ВнешняяОбработка = ОбработкаПриемки;
	
	Если ЗначениеЗаполнено(ВнешняяОбработка) Тогда
		ОбработатьЗапускВнешнейОбработки(ВнешняяОбработка);
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Необходимо выполнить настройку рабочего места'");
		ТекстСообщения = НСтр("ru = 'Для этого, необходимо обратиться к пользователю, обладающему полными правами.'");
		ПоказатьПредупреждение(, ТекстСообщения,, ТекстЗаголовка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОтгрузкаОписаниеГиперСсылкаНажатие(Элемент)
	
	ВнешняяОбработка = ОбработкаОтгрузки;
	
	Если ЗначениеЗаполнено(ВнешняяОбработка) Тогда
		ОбработатьЗапускВнешнейОбработки(ВнешняяОбработка);
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Необходимо выполнить настройку рабочего места'");
		ТекстСообщения = НСтр("ru = 'Для этого, необходимо обратиться к пользователю, обладающему полными правами.'");
		ПоказатьПредупреждение(, ТекстСообщения,, ТекстЗаголовка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИнвентаризацияОписаниеГиперСсылкаНажатие(Элемент)
	
	ВнешняяОбработка = ОбработкаИнвентаризации;
	
	Если ЗначениеЗаполнено(ВнешняяОбработка) Тогда
		ОбработатьЗапускВнешнейОбработки(ВнешняяОбработка);
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Необходимо выполнить настройку рабочего места'");
		ТекстСообщения = НСтр("ru = 'Для этого, необходимо обратиться к пользователю, обладающему полными правами.'");
		ПоказатьПредупреждение(, ТекстСообщения,, ТекстЗаголовка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПеремещениеОписаниеГиперСсылкаНажатие(Элемент)
	
	    
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВыходИзСистемыОписаниеГиперСсылкаНажатие(Элемент)
	
	ЗавершитьРаботуСистемы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаОписаниеГиперСсылкаНажатие(Элемент)
	
	Если ЗначениеЗаполнено(ПарольДоступаКФормеНастроек) Тогда
		ИмяОткрываемойФормы = ИмяОбработки + ".Форма.ФормаВводаПароля";
		Оповещение = Новый ОписаниеОповещения("ВводПароляЗавершение", ЭтотОбъект);
	Иначе
		ИмяОткрываемойФормы = ИмяОбработки + ".Форма.ФормаНастройки";
		Оповещение = Новый ОписаниеОповещения("НастройкаЗавершение", ЭтотОбъект);			
	КонецЕсли;		
	
	ОткрытьФорму(ИмяОткрываемойФормы,, 
					  ЭтотОбъект,
					  УникальныйИдентификатор,,, 
					  Оповещение, 
					  РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
				  КонецПроцедуры
				  
#КонецОбласти

#Область ПроцедурыОписанияОповещения

&НаКлиенте
Процедура ВводПароляЗавершение(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда				
		Если Результат Тогда
			ИмяОткрываемойФормы = ИмяОбработки + ".Форма.ФормаНастройки";
			
			Оповещение = Новый ОписаниеОповещения("НастройкаЗавершение", ЭтотОбъект);
			
			ОткрытьФорму(ИмяОткрываемойФормы,, 
						  ЭтотОбъект,
						  УникальныйИдентификатор,,, 
						  Оповещение, 
						  РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	        			
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаЗавершение(Результат, Параметры) Экспорт
	
	ПолучитьНастройки();
	ОбновитьВидимостьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьВидимостьЭлементов()
	
	Если НастройкаВыполнена Тогда		
		Элементы.СтраницаОписания.Видимость = Ложь;
		Элементы.СтраницаОсновная.Видимость = Истина; 
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОсновная;
		
		Если РольДоступнаПолныеПрава Тогда
			Элементы.ГруппаНастройка.Видимость = Истина;	
		Иначе
			Элементы.ГруппаНастройка.Видимость = Ложь;
		КонецЕсли;
	Иначе
		Элементы.СтраницаОписания.Видимость = Истина;
		Элементы.СтраницаОсновная.Видимость = Ложь; 
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОписания;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗапускВнешнейОбработки(ВнешняяОбработка)

	// Необходимо проверить публикацию
	Публикация = ПолучитьПубликациюВнешнейОбработки(ВнешняяОбработка);
	
	Используется = ПредопределенноеЗначение("Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.Используется");
	Отключена = ПредопределенноеЗначение("Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.Отключена");
	РежимОтладки = ПредопределенноеЗначение("Перечисление.ВариантыПубликацииДополнительныхОтчетовИОбработок.РежимОтладки");
	
	Если Публикация = Используется Тогда
		ВыполняемаяКоманда = Новый Структура();
		ВыполняемаяКоманда.Вставить("Идентификатор", "");
		ВыполняемаяКоманда.Вставить("Ссылка", ВнешняяОбработка);
		ВыполняемаяКоманда.Вставить("ЭтоОтчет", Ложь);
		
		ОбъектыНазначения = Новый Массив();
		
		ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьОткрытиеФормыОбработки(ВыполняемаяКоманда, Неопределено, ОбъектыНазначения);
	ИначеЕсли Публикация = Отключена Тогда
		ТекстЗаголовка = НСтр("ru = 'Публикация обработки ""Отключена"".'");
		ТекстСообщения = НСтр("ru = 'Обратитесь в отдел разработки.'");
		ПоказатьПредупреждение(, ТекстСообщения,, ТекстЗаголовка);
	ИначеЕсли Публикация = РежимОтладки Тогда
		ТекстЗаголовка = НСтр("ru = 'Публикация обработки ""Режим отладки"".'");
		ТекстСообщения = НСтр("ru = 'Обратитесь в отдел разработки.'");
		ПоказатьПредупреждение(, ТекстСообщения,, ТекстЗаголовка);
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПолучитьНастройки()
	
	НастройкаВыполнена = Истина;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Ключи = "ОбработкаИнвентаризации, ОбработкаОтгрузки, ОбработкаПриемки, ПарольДоступаКФормеНастроек";
		  
	ИмяНастройки = "НастройкаРабочееМестоСотрудникаМагазина";
	
	Настройки = КР_ОбщегоНазначениеСервер.ПрочитатьДанныеИзХранилищаНастроек(ИмяНастройки, Ключи);
	
	// Все настройки сохраняются по названию реквизитов на форме.
	Для каждого Настройка Из Настройки Цикл
		Если Настройка.Значение = Неопределено Тогда
			НастройкаВыполнена = Ложь;
		Иначе
			ЭтотОбъект[Настройка.Ключ] = Настройка.Значение;
		КонецЕсли;
	КонецЦикла; 
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПубликациюВнешнейОбработки(ВнешняяОбработка)
	
	Публикация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВнешняяОбработка, "Публикация");
	
	Возврат(Публикация);
	
КонецФункции

#КонецОбласти  
