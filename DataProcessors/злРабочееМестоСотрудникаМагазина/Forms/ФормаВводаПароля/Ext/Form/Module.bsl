
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СкрыватьПароль = Истина; 
	
	КартинкаОткрыть = БиблиотекаКартинок.ВидимостьПароляОткрыто;
	КартинкаЗакрыть = БиблиотекаКартинок.ВидимостьПароляЗакрыто;
	
	Элементы.Пароль.КартинкаКнопкиВыбора = КартинкаЗакрыть;
	
	ПолучитьНастройки();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
			
	ПодключитьОбработчикОжидания("ОбновитьРазмерыФормы", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ОткрытьЗакрытьВидимостьЭлемента("Пароль");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОк(Команда)
	
	Результат = ПроверитьПароль();
	
	Если Результат Тогда		
		Закрыть(Истина);		
	Иначе		
		 ПоказатьПредупреждение(,НСтр("ru = 'ПАРОЛЬ НЕ ВЕРНЫЙ'"));		
	КонецЕсли;		
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьРазмерыФормы()
	
	Элементы.ЗаголовокФормы.Видимость = НЕ Элементы.ЗаголовокФормы.Видимость;
	Элементы.ЗаголовокФормы.Видимость = НЕ Элементы.ЗаголовокФормы.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЗакрытьВидимостьЭлемента(ИмяЭлемента)
	
	ПоказатьПароль = Не ПоказатьПароль;
	
	ЭтотОбъект[ИмяЭлемента] = Элементы[ИмяЭлемента].ТекстРедактирования;
	
	Если ПоказатьПароль Тогда
		Элементы[ИмяЭлемента].КартинкаКнопкиВыбора = КартинкаОткрыть;
		Элементы[ИмяЭлемента].РежимПароля = Ложь;	
	
		Если СкрыватьПароль Тогда
			ПодключитьОбработчикОжидания("ПодключаемыйОтключитьВидимостьПароля", 2, Истина);
		КонецЕсли;
	Иначе
		Элементы[ИмяЭлемента].КартинкаКнопкиВыбора = КартинкаЗакрыть;
		Элементы[ИмяЭлемента].РежимПароля = Истина;	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключаемыйОтключитьВидимостьПароля()
	
	ИмяЭлемента = "Пароль";
	ЭлементФормы = Элементы[ИмяЭлемента];
	
	Если ЭлементФормы.РежимПароля <> Истина Тогда
		ЭтотОбъект[ИмяЭлемента] = ЭлементФормы.ТекстРедактирования;
		ПоказатьПароль = Ложь;
		ЭлементФормы.РежимПароля = Истина;
		ЭлементФормы.КартинкаКнопкиВыбора = КартинкаЗакрыть;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьПароль()
	
	Результат = Ложь;
	
	Если ЗначениеЗаполнено(ПарольДоступаКФормеНастроек) Тогда
		Если СокрЛП(Пароль) = СокрЛП(ПарольДоступаКФормеНастроек) Тогда	
			Результат = Истина;	
		КонецЕсли;		
	КонецЕсли;
	
	// Если пользовательский пароль не совпал, тогда
	// выполним проверку статичного пароля.
	Если Не Результат Тогда
		ПарольСтатичный = "159357";
		
		Если Пароль = ПарольСтатичный Тогда	
			Результат = Истина;	
		КонецЕсли; 	
	КонецЕсли;
	
	Возврат(Результат);
	
КонецФункции

&НаСервере
Процедура ПолучитьНастройки()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИмяНастройки = "НастройкаРабочееМестоСотрудникаМагазина";
	
    Настройка = КР_ОбщегоНазначениеСервер.ПрочитатьДанныеИзХранилищаНастроек(ИмяНастройки, "ПарольДоступаКФормеНастроек");
			
	Если Настройка <> Неопределено Тогда    					
    	ПарольДоступаКФормеНастроек = Настройка;    		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти






