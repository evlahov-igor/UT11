#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НайтиОшибки(Команда)
	НайтиОшибкиНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ИсправитьОшибки(Команда)
	ОчиститьСообщения();
	ИсправитьОшибкиНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НайтиОшибкиНаСервере()
	
	ТаблицаОшибокВременная = Новый ТаблицаЗначений();
	ТаблицаОшибокВременная.Колонки.Добавить("Номенклатура");
	ТаблицаОшибокВременная.Колонки.Добавить("Характеристика");
	ТаблицаОшибокВременная.Колонки.Добавить("Склад");
	ТаблицаОшибокВременная.Колонки.Добавить("Назначение");
	Пакет = РаспределениеЗапасов.ОшибкиПроверкиКорректностиЗаписейРегистраСведенийРаспределениеЗапасов(Истина, Истина, Неопределено);
	Индекс = 0;
	Для Счетчик = 2 По Пакет.Количество() Цикл
		Таблица = Пакет[Счетчик - 1].Выгрузить();
		Для Каждого Строка Из Таблица Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаОшибокВременная.Добавить(), Строка);
		КонецЦикла;
	КонецЦикла;
	ТаблицаОшибокВременная.Свернуть("Номенклатура,Характеристика,Склад,Назначение");
	ТаблицаОшибок.Загрузить(ТаблицаОшибокВременная);
	
КонецПроцедуры

&НаСервере
Процедура ИсправитьОшибкиНаСервере()
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.ЗаказНаОтгрузку КАК Ссылка
		|ИЗ
		|	РегистрСведений.РаспределениеЗапасов КАК Таблица
		|ГДЕ
		|	Таблица.Номенклатура = &Номенклатура
		|		И Таблица.Характеристика = &Характеристика
		|		И Таблица.Склад = &Склад
		|		И Таблица.Назначение = &Назначение
		|		И Таблица.ЗаказНаОтгрузку <> НЕОПРЕДЕЛЕНО";
	
	Для Каждого Идентификатор Из Элементы.ТаблицаОшибок.ВыделенныеСтроки Цикл
		
		СтрокаТаблицы = ТаблицаОшибок.НайтиПоИдентификатору(Идентификатор);
		
		ТранзакцияЗафиксирована = Истина;
		НачатьТранзакцию();
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных();
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.РаспределениеЗапасов");
			ЭлементБлокировки.УстановитьЗначение("Номенклатура", СтрокаТаблицы.Номенклатура);
			ЭлементБлокировки.УстановитьЗначение("Характеристика", СтрокаТаблицы.Характеристика);
			ЭлементБлокировки.УстановитьЗначение("Склад", СтрокаТаблицы.Склад);
			ЭлементБлокировки.УстановитьЗначение("Назначение", СтрокаТаблицы.Назначение);
			БлокировкаДанных.Заблокировать();
			
			Запрос.УстановитьПараметр("Номенклатура", СтрокаТаблицы.Номенклатура);
			Запрос.УстановитьПараметр("Характеристика", СтрокаТаблицы.Характеристика);
			Запрос.УстановитьПараметр("Склад", СтрокаТаблицы.Склад);
			Запрос.УстановитьПараметр("Назначение", СтрокаТаблицы.Назначение);
			Заказы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			
			Набор = РаспределениеЗапасов.ОбновлениеИБПоТовару(СтрокаТаблицы);
			Набор.Записать();
			
			ЗаказыПосле = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
			Для Каждого Ссылка Из ЗаказыПосле Цикл
				Заказы.Добавить(Ссылка);
			КонецЦикла;
			
			РаспределениеЗапасов.ИсключитьСсылкиТребующиеОтложенногоОбновления(Заказы);
			РегистрыСведений.СостоянияЗаказовКлиентов.ОтразитьСостояниеЗаказа(Заказы, Ложь, Ложь);
			РегистрыСведений.СостоянияВнутреннихЗаказов.ОтразитьСостояниеЗаказа(Заказы);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ТранзакцияЗафиксирована = Ложь;
			ТекстСообщения = НСтр("ru = 'Задание распределения запасов завершилось неудачно по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			СобытиеЖурнала = НСтр("ru = 'Распределение запасов'", ОбщегоНазначения.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(СобытиеЖурнала,
			                         УровеньЖурналаРегистрации.Ошибка,
			                         Метаданные.РегистрыСведений.РаспределениеЗапасов,
			                         ,
			                         ТекстСообщения);
			
		КонецПопытки;
		
		Если ТранзакцияЗафиксирована Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Обработан. Товар: %1, %2, Склад: %3, Назначение:%4'"),
				СтрокаТаблицы.Номенклатура,
				СтрокаТаблицы.Характеристика,
				СтрокаТаблицы.Склад,
				СтрокаТаблицы.Назначение);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
		Иначе
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка обработки. Товар: %1, %2, Склад: %3, Назначение:%4'"),
				СтрокаТаблицы.Номенклатура,
				СтрокаТаблицы.Характеристика,
				СтрокаТаблицы.Склад,
				СтрокаТаблицы.Назначение);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
