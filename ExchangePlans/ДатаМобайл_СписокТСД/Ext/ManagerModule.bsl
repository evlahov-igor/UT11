
Функция ПолучитьНастройкиТабличныхЧастейТСД(УзелПО, СтруктураНастроек) Экспорт
	
	СтруктураНастроекТСД = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
	"ВЫБРАТЬ
	|	ДатаМобайл_СписокТСДСклады.Склад КАК Склад
	|ПОМЕСТИТЬ Склады
	|ИЗ
	|	ПланОбмена.ДатаМобайл_СписокТСД.Склады КАК ДатаМобайл_СписокТСДСклады
	|ГДЕ
	|	ДатаМобайл_СписокТСДСклады.Ссылка = &УзелПО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатаМобайл_СписокТСДШаблоны.Шаблон КАК Шаблон
	|ПОМЕСТИТЬ Шаблоны
	|ИЗ
	|	ПланОбмена.ДатаМобайл_СписокТСД.Шаблоны КАК ДатаМобайл_СписокТСДШаблоны
	|ГДЕ
	|	ДатаМобайл_СписокТСДШаблоны.Ссылка = &УзелПО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатаМобайл_СписокТСДПомещения.Помещение КАК Помещение
	|ПОМЕСТИТЬ Помещения
	|ИЗ
	|	ПланОбмена.ДатаМобайл_СписокТСД.Помещения КАК ДатаМобайл_СписокТСДПомещения
	|ГДЕ
	|	ДатаМобайл_СписокТСДПомещения.Ссылка = &УзелПО";
	
	Запрос.УстановитьПараметр("УзелПО", УзелПО);
	
	МассивРезультатов = Запрос.ВыполнитьПакетСПромежуточнымиДанными();
	
	Если СтруктураНастроек.Свойство("Склады") Тогда
		
		Если МассивРезультатов[0].Пустой() Тогда	
			
			ВсеСклады = Истина; 
			СписокСкладов = Новый Массив;
			
		Иначе
			
			ВсеСклады = Ложь;
			СписокСкладов = МассивРезультатов[0].Выгрузить().ВыгрузитьКолонку("Склад");
			
		КонецЕсли;  
		
		СтруктураНастроекТСД.Вставить("ВсеСклады", ВсеСклады);
		СтруктураНастроекТСД.Вставить("СписокСкладов", СписокСкладов);
		
	КонецЕсли;	
	
	Если СтруктураНастроек.Свойство("Шаблоны") Тогда
	
		Если МассивРезультатов[1].Пустой() Тогда	
			
			ВсеШаблоны = Истина; 
			СписокШаблонов = Новый Массив;
			
		Иначе
			
			ВсеШаблоны = Ложь;
			СписокШаблонов = МассивРезультатов[1].Выгрузить().ВыгрузитьКолонку("Шаблон");
			
		КонецЕсли;  
		
		СтруктураНастроекТСД.Вставить("ВсеШаблоны", ВсеШаблоны);
		СтруктураНастроекТСД.Вставить("СписокШаблонов", СписокШаблонов);
		
	КонецЕсли;	 
	
	Если СтруктураНастроек.Свойство("Помещения") Тогда
	
		Если МассивРезультатов[2].Пустой() Тогда	
			
			ВсеПомещения = Истина; 
			СписокПомещений = Новый Массив;
			
		Иначе
			
			ВсеПомещения = Ложь;
			СписокПомещений = МассивРезультатов[2].Выгрузить().ВыгрузитьКолонку("Помещение");
			
		КонецЕсли;  
		
		СтруктураНастроекТСД.Вставить("ВсеПомещения", ВсеПомещения);
		СтруктураНастроекТСД.Вставить("СписокПомещений", СписокПомещений);
		
	КонецЕсли;	
	
	Возврат СтруктураНастроекТСД;
	
КонецФункции	 